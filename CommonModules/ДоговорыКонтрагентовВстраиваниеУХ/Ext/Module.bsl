
&ИзменениеИКонтроль("ПриЗаписиДоговорыКонтрагентов")
Процедура АЛФ_ПриЗаписиДоговорыКонтрагентов(Источник, Отказ)

	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;

	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ДополнительныеСвойства = Источник.ДополнительныеСвойства;

	// обновим записи в регистре "Связанные договоры".
	Если Источник.ДополнительныеСвойства.Свойство("ПараметрыСвязанныхДоговоровДляОбновления") 
		И НЕ Источник.ДополнительныеСвойства.Свойство("ЭтоСозданиеВстречногоДоговора") Тогда

		Для каждого Связь Из Источник.ДополнительныеСвойства.ПараметрыСвязанныхДоговоровДляОбновления Цикл

			Если Не ЗначениеЗаполнено(Связь.БазовыйДоговор) Тогда
				Связь.БазовыйДоговор = Источник.Ссылка;
			КонецЕсли;

			Если Не ЗначениеЗаполнено(Связь.ПодчиненныйДоговор) Тогда
				Связь.ПодчиненныйДоговор = Источник.Ссылка;
			КонецЕсли;

			РегистрыСведений.СвязанныеДоговоры.ЗаписатьСвязанныеДоговоры(Связь.БазовыйДоговор, Связь.ПодчиненныйДоговор, Связь.ВидСвязи, Отказ);			

		КонецЦикла;

	КонецЕсли;

	// Пакетно пометим на удаление все версии соглашения
	Если ДополнительныеСвойства.Свойство("УстановленнаяПометкаУдаления") И Не ДополнительныеСвойства.Свойство("ПометкаУдаленияВерсииСоглашения")  Тогда
		Если ДополнительныеСвойства.УстановленнаяПометкаУдаления И Не Источник.ПометкаУдаления Тогда
			// Сняли пометку удаления.   
			Если ОбщегоНазначения.СсылкаСуществует(Источник.ВерсияСоглашения) Тогда
#Вставка
			Если ЗначениеЗаполнено(Источник.ВерсияСоглашения) Тогда
#КонецВставки
				ДоговорОбъект = Источник.ВерсияСоглашения.ПолучитьОбъект();
				ДоговорОбъект.УстановитьПометкуУдаления(Ложь);  
#Вставка
			КонецЕсли;
#КонецВставки
			КонецЕсли;
		ИначеЕсли Источник.ПометкаУдаления И Не ДополнительныеСвойства.УстановленнаяПометкаУдаления Тогда
			// Установили пометку удаления.
			РаботаСДоговорамиКонтрагентовУХ.ПометитьНаУдалениеВерсииСоглашенияДоговора(Источник.Ссылка, Источник.ВидДоговораУХ);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
