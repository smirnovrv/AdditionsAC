
&ИзменениеИКонтроль("ЭтапыСогласованияДляТекущегоПользователя")
Функция АЛФ_ЭтапыСогласованияДляТекущегоПользователя(ОбъектСогласования, СписокЗадачВход)

	ТекПользователь = ОбщегоНазначенияУХ.ПолучитьЗначениеПеременной("глТекущийПользователь");
	РезультатФункции = ЭтапыСогласованияДляПользователя(ОбъектСогласования, ТекПользователь, , СписокЗадачВход);
#Вставка
	Если РезультатФункции.Количество() = 0 Тогда
		РезультатФункции = ЭтапыСогласованияДляПользователя(ОбъектСогласования, Справочники.Пользователи.ОтветственныйЗаБанковскийСчет, , СписокЗадачВход);
	КонецЕсли;
	Если РезультатФункции.Количество() = 0 Тогда
		РезультатФункции = ЭтапыСогласованияДляПользователя(ОбъектСогласования, Справочники.Пользователи.НайтиПоНаименованию("Бухгалтер", Истина), , СписокЗадачВход);
	КонецЕсли;
#КонецВставки
	Возврат РезультатФункции;

КонецФункции

&ИзменениеИКонтроль("ПользовательИзРеквизитаРасширеннаяАдресация")
Процедура АЛФ_ПользовательИзРеквизитаРасширеннаяАдресация(ДокументПроцессаВход, АдресацияВход, СтрокаОтветственныхВход, ТабОтветственныхИзм, СтрокаПредставленияИзм)
	Если ЗначениеЗаполнено(ДокументПроцессаВход) Тогда
		ОбъектСогласования = ДокументПроцессаВход.КлючевойОбъектПроцесса;
		Если ЗначениеЗаполнено(ОбъектСогласования) Тогда
			// Получим пользователя в реквизите.
			ИскомыйРеквизит = АдресацияВход.РеквизитПользователя;			
			ИскомыйРеквизит = СтрЗаменить(ИскомыйРеквизит, ".ДополнительныеРеквизиты.Значение", "");
			ИскомыйРеквизит = СтрЗаменить(ИскомыйРеквизит, ".ДополнительныеРеквизиты.Свойство", "");
			ИскомыйРеквизит = СтрЗаменить(ИскомыйРеквизит, ".ДополнительныеРеквизиты.Ссылка", "");
			ИскомыйРеквизит = СтрЗаменить(ИскомыйРеквизит, ".ДополнительныеРеквизиты.НомерСтроки", "");
			ИскомыйРеквизит = СтрЗаменить(ИскомыйРеквизит, ".ДополнительныеРеквизиты.ТекстоваяСтрока", "");

			СтруктураЗаменаДопРеквизитов = ОбщегоНазначенияСерверУХ.ЗаменитьТекстВСкобках(ИскомыйРеквизит, "[", "]");
			ИскомыйРеквизит = СтруктураЗаменаДопРеквизитов.СтрокаРезультат;
			СписокЗамен = СтруктураЗаменаДопРеквизитов.СписокЗамен;

			МассивРеквизитов = СтроковыеФункцииКлиентСерверУХ.РазложитьСтрокуВМассивПодстрок(ИскомыйРеквизит, ".");

			ЗначениеРеквизитаАдресации = ОбъектСогласования;
			РеквизитСуществует = Ложь;
			Для Каждого РеквизитАдресации Из МассивРеквизитов Цикл

				ПозицияСкобки1 = СтрНайти(РеквизитАдресации, "[");
				ПозицияСкобки2 = СтрНайти(РеквизитАдресации, "]");
				ДлинаИмениРеквизита = СтрДлина(РеквизитАдресации);

				Если ПозицияСкобки1 = 1 И ПозицияСкобки2 = ДлинаИмениРеквизита И ДлинаИмениРеквизита > 2 
					И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЗначениеРеквизитаАдресации, "ДополнительныеРеквизиты") Тогда

					УИДДопРеквизита = Сред(РеквизитАдресации, 2, ПозицияСкобки2 - 2);

					ЭлементЗамены = СтруктураЗаменаДопРеквизитов.СписокЗамен.НайтиПоЗначению(УИДДопРеквизита);
					ИмяДопРеквизита = ЭлементЗамены.Представление;

					Если Прав(ИмяДопРеквизита, 1) = ")" Тогда	
						ПозицияОткрывающейСкобки = СтрНайти(ИмяДопРеквизита, "(", НаправлениеПоиска.СКонца);

						Если ПозицияОткрывающейСкобки > 0 Тогда
							ИмяДопРеквизита = СокрЛП(Лев(ИмяДопРеквизита, ПозицияОткрывающейСкобки - 1));
						КонецЕсли;
					КонецЕсли;

					ДопРеквизитПВХ = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию(ИмяДопРеквизита, Истина);
					ДопРеквизитСтруктура = ЗначениеРеквизитаАдресации.ДополнительныеРеквизиты.Найти(ДопРеквизитПВХ, "Свойство");

					Если ДопРеквизитСтруктура <> Неопределено Тогда
						ЗначениеРеквизитаАдресации = ДопРеквизитСтруктура.Значение;
						РеквизитСуществует = Истина;
					ИначеЕсли НЕ ДопРеквизитПВХ.Пустая() Тогда
						ЗначениеРеквизитаАдресации = ДопРеквизитПВХ.ТипЗначения.ПривестиЗначение();
						РеквизитСуществует = Истина;
					Иначе
						РеквизитСуществует = Ложь;
						Прервать;	
					КонецЕсли;

				ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЗначениеРеквизитаАдресации, РеквизитАдресации) Тогда
					ЗначениеРеквизитаАдресации = ЗначениеРеквизитаАдресации[РеквизитАдресации];
					РеквизитСуществует = Истина;
				Иначе
					РеквизитСуществует = Ложь;
					Прервать;
				КонецЕсли;

			КонецЦикла;

			Если РеквизитСуществует Тогда
				НовыйОтветственный = ЗначениеРеквизитаАдресации;
				// Получим руководителя пользователя.
				ОрганизацияОбъекта = ПолучитьОрганизациюОбъекта(ОбъектСогласования);
				НовыйОтветственный = РуководительПользователя(НовыйОтветственный, АдресацияВход, ОрганизацияОбъекта);
				// Добавление готового варианта в таблицу.
				ДобавитьПользователяВТаблицуОтветсвенных(НовыйОтветственный, СтрокаОтветственныхВход, ТабОтветственныхИзм, СтрокаПредставленияИзм);
			Иначе
				ТекстСообщения = НСтр("ru = 'В объекте ""%Объект%"" не найден реквизит расширенной адресации ""%Реквизит%"". Операция пропущена.'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Объект%", Строка(ОбъектСогласования));
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Реквизит%", Строка(АдресацияВход.РеквизитПользователя));
				ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
			КонецЕсли;			
		Иначе
			ТекстСообщения = НСтр("ru = 'Объект согласования для расширенной адресации по процессу ""%Процесс%"" не задан. Операция пропущена.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Процесс%", Строка(ДокументПроцессаВход));
			ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
		КонецЕсли;
	Иначе
		// Пустой документ согласования. Пропускаем.
	КонецЕсли;
КонецПроцедуры

&ИзменениеИКонтроль("ОтветственныеПоОбъектуСогласования")
Функция АЛФ_ОтветственныеПоОбъектуСогласования(ОбъектСогласованияВход)
	РезультатФункции = Новый ТаблицаЗначений;
	Запрос = Новый Запрос;	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ЭкземплярПроцесса.Ссылка КАК ЭкземплярПроцесса,
	|	ЭкземплярПроцесса.КлючевойОбъектПроцесса КАК КлючевойОбъектПроцесса
	|ПОМЕСТИТЬ ВТ_ЭкземплярПроцесса
	|ИЗ
	|	Документ.ЭкземплярПроцесса КАК ЭкземплярПроцесса
	|ГДЕ
	|	ЭкземплярПроцесса.КлючевойОбъектПроцесса В(&КлючевойОбъектПроцесса)
	|	И НЕ ЭкземплярПроцесса.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВыполнениеПроцессов.ДокументПроцесса КАК ДокументПроцесса,
	|	ВыполнениеПроцессов.ЭтапПроцесса КАК ЭтапПроцесса,
	|	ЭкземплярПроцессаДополнительныеСогласующие.Пользователь КАК Пользователь,
	|	ЛОЖЬ КАК ДопСогласование,
	|	ВыполнениеПроцессов.ЭтапПроцесса.УтверждающиеИмеютПравоРедактирования КАК ПравоРедактирования,
	|	ВыполнениеПроцессов.Организация КАК Организация,
	|	ВыполнениеПроцессов.ПринялКИсполнению КАК ПринялКИсполнению
	|ПОМЕСТИТЬ ВТ_Пользователи
	|ИЗ
	|	Документ.ЭкземплярПроцесса.ДополнительныеСогласующие КАК ЭкземплярПроцессаДополнительныеСогласующие
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВыполнениеПроцессов КАК ВыполнениеПроцессов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЭкземплярПроцесса КАК ВТ_ЭкземплярПроцесса
	|			ПО ВыполнениеПроцессов.ДокументПроцесса = ВТ_ЭкземплярПроцесса.ЭкземплярПроцесса
	|		ПО ЭкземплярПроцессаДополнительныеСогласующие.Ссылка = ВыполнениеПроцессов.ДокументПроцесса
	|			И ЭкземплярПроцессаДополнительныеСогласующие.Этап = ВыполнениеПроцессов.ЭтапПроцесса
	|ГДЕ
	|	ВыполнениеПроцессов.СостояниеЭтапа = ЗНАЧЕНИЕ(Перечисление.СостоянияЭтаповУниверсальныхПроцессов.ВОбработке)
	|	И ВыполнениеПроцессов.АрхивнаяЗапись = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВизаДополнительныхСогласующих.ЭкземплярПроцесса,
	|	ВизаДополнительныхСогласующих.ЭтапПроцесса,
	|	ВизаДополнительныхСогласующих.Согласующий,
	|	ИСТИНА,
	|	ВизаДополнительныхСогласующих.ЭтапПроцесса.УтверждающиеИмеютПравоРедактирования,
	|	ВыполнениеПроцессов.Организация,
	|	ВыполнениеПроцессов.ПринялКИсполнению
	|ИЗ
	|	ВТ_ЭкземплярПроцесса КАК ВТ_ЭкземплярПроцесса
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВизаДополнительныхСогласующих КАК ВизаДополнительныхСогласующих
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВыполнениеПроцессов КАК ВыполнениеПроцессов
	|			ПО (ВыполнениеПроцессов.ДокументПроцесса = ВизаДополнительныхСогласующих.ЭкземплярПроцесса)
	|				И (ВыполнениеПроцессов.ЭтапПроцесса = ВизаДополнительныхСогласующих.ЭтапПроцесса)
	|				И (ВизаДополнительныхСогласующих.АрхивнаяЗапись = ЛОЖЬ)
	|				И (ВизаДополнительныхСогласующих.Виза = ЛОЖЬ)
	|		ПО ВТ_ЭкземплярПроцесса.ЭкземплярПроцесса = ВизаДополнительныхСогласующих.ЭкземплярПроцесса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВТ_Пользователи.ДокументПроцесса КАК ДокументПроцесса,
	|	ВТ_Пользователи.ЭтапПроцесса КАК ЭтапПроцесса,
	|	ВТ_Пользователи.Пользователь КАК Пользователь,
	|	ВТ_Пользователи.ДопСогласование КАК ДопСогласование,
	|	ВТ_Пользователи.ПравоРедактирования КАК ПравоРедактирования,
	|	ВТ_Пользователи.Организация КАК Организация,
	|	ЕСТЬNULL(ОтложеннаяОбработкаЭтаповПроцессовСрезПоследних.Задача, ЗНАЧЕНИЕ(Справочник.Задачи.ПустаяСсылка)) КАК Задача,
	|	ВТ_Пользователи.ПринялКИсполнению КАК ПринялКИсполнению,
	|	ОтложеннаяОбработкаЭтаповПроцессовСрезПоследних.Ответственный КАК Ответственный
	|ИЗ
	|	ВТ_Пользователи КАК ВТ_Пользователи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтложеннаяОбработкаЭтаповПроцессов.СрезПоследних КАК ОтложеннаяОбработкаЭтаповПроцессовСрезПоследних
	|		ПО ВТ_Пользователи.ДокументПроцесса = ОтложеннаяОбработкаЭтаповПроцессовСрезПоследних.ДокументПроцесса
	|			И ВТ_Пользователи.ЭтапПроцесса = ОтложеннаяОбработкаЭтаповПроцессовСрезПоследних.ЭтапПроцесса
	|			И (ОтложеннаяОбработкаЭтаповПроцессовСрезПоследних.ВидДействия В (ЗНАЧЕНИЕ(Перечисление.СобытияОтложеннойОбработкиПроцессов.ОбработкаЭтапаСогласования), ЗНАЧЕНИЕ(Перечисление.СобытияОтложеннойОбработкиПроцессов.ДополнительноеСогласованиеЭтапа)))
	|			И (ОтложеннаяОбработкаЭтаповПроцессовСрезПоследних.Выполнено = ЛОЖЬ)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_Пользователи.ДокументПроцесса";

	Запрос.УстановитьПараметр("КлючевойОбъектПроцесса", ОбъектСогласованияВход);
	РезультатЗапроса = Запрос.Выполнить();
	РезультатФункции = РезультатЗапроса.Выгрузить();

#Удаление
	СтрокиКУдалению = Новый Массив;
	МассивСтрокДляРасшифровки = Новый Массив();

	Для Каждого СтрокаОтветственные Из РезультатФункции Цикл

		ЭкземплярПроцесса = СтрокаОтветственные.ДокументПроцесса;
		ОрганизацияЭкземпляра = ЭкземплярПроцесса.Организация;
		ОрганизацияРоли = ОпределитьОрганизациюРоли(ОбъектСогласованияВход, ОрганизацияЭкземпляра);
		СтрокаПредставление = "";

		МассивСтрокДляРасшифровки.Очистить();
		МассивСтрокДляРасшифровки.Добавить(СтрокаОтветственные);
		ТаблицаДляРасшифровки = РезультатФункции.Скопировать(МассивСтрокДляРасшифровки);

		ТаблицаРасшифровки = РасшифроватьРолиОтветственных(ЭкземплярПроцесса, ТаблицаДляРасшифровки, ОрганизацияРоли, СтрокаПредставление);

		УдалятьСтроку = Истина;
		Для Каждого СтрокаРасшифровки Из ТаблицаРасшифровки Цикл
			Если СтрокаРасшифровки.Пользователь = СтрокаРасшифровки.Ответственный Тогда
				УдалятьСтроку = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;

		Если УдалятьСтроку Тогда
			СтрокиКУдалению.Добавить(СтрокаОтветственные);
		КонецЕсли;

	КонецЦикла;

	Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
		РезультатФункции.Удалить(УдаляемаяСтрока);
	КонецЦикла;
#КонецУдаления

	РезультатФункции.Свернуть("ДокументПроцесса, ЭтапПроцесса, Пользователь, ДопСогласование, ПравоРедактирования, Организация, Задача, ПринялКИсполнению");

	ПользовательБотаЗадачИОповещений = ТелеграмСерверУХ.ПолучитьПользователяБотаЗадачИОповещений();

	РезультатФункцииЭтапы = РезультатФункции.Скопировать(, "ЭтапПроцесса");
	РезультатФункцииЭтапы.Свернуть("ЭтапПроцесса");

	Для Каждого СтрокаЭтапа Из РезультатФункцииЭтапы Цикл

		СтрокаРезультата = РезультатФункции.Найти(СтрокаЭтапа.ЭтапПроцесса, "ЭтапПроцесса");

		Если СтрокаРезультата <> Неопределено Тогда
			НоваяСтрока = РезультатФункции.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРезультата);

			НоваяСтрока.Пользователь = ПользовательБотаЗадачИОповещений;
		КонецЕсли;

	КонецЦикла;

	Возврат РезультатФункции;
КонецФункции

&ИзменениеИКонтроль("НарисоватьЭтап")
Процедура АЛФ_НарисоватьЭтап(ТабличныйДокумент, СтрокаЭтапа, Уровни, СоответствиеРисунковИЭтапов, Ответственные, ОрганизацияВход, ВыбранныйЭтапВход, ЭкземплярПроцессаВход)

	Расшифровка = СтрокаЭтапа.Этап;

	ТекстОсновногоСообщения = Уровни.ТекстыЭтапов[Расшифровка];

#Вставка
	Если ЭкземплярПроцессаВход <> Неопределено И ТипЗнч(ЭкземплярПроцессаВход.КлючевойОбъектПроцесса) = Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств") И
		СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ЭтапСогласования Тогда
		МассивСлов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(СтрокаЭтапа.Наименование);
		ИндексСтр = МассивСлов[МассивСлов.Количество() - 1];
		Попытка
			ПользовательЭтапа = ЭкземплярПроцессаВход.КлючевойОбъектПроцесса.ИИТ_ПроектСогласования["ИИТ_Согласующий" + ИндексСтр];
			Если ПользовательЭтапа = Справочники.Пользователи.ОтветственныйЗаБанковскийСчет ИЛИ Строка(ПользовательЭтапа) = "Бухгалтер" Тогда
				ТекстОсновногоСообщения = "Согласование с: " + Строка(ЭкземплярПроцессаВход.КлючевойОбъектПроцесса.ИИТ_СчетОрганизации.Ответственный);
			Иначе
				ТекстОсновногоСообщения = "Согласование с: " + Строка(ПользовательЭтапа);
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЕсли;
#КонецВставки
	ШиринаЭтапа   = Уровни.Х[СтрокаЭтапа.УровеньХ].ШиринаЭтапа;	
	ВысотаЭтапа = Уровни.ВысотаЭтапов[Расшифровка];
	ВертикальнаяКоррекцияТекущая = ВысотаЭтапа - 40;

	ВысотаСтрокиТекста = 10;

	ВерхЭтапа = Уровни.У[СтрокаЭтапа.УровеньУ].ВерхЭтапа + Уровни.У[СтрокаЭтапа.УровеньУ].ВертикальнаяКоррекция;
	ЛевоЭтапа = Уровни.Х[СтрокаЭтапа.УровеньХ].ЛевоЭтапа + Уровни.Х[СтрокаЭтапа.УровеньХ].ГоризонтальнаяКоррекция;

	УникальныйИдентификатор = Новый УникальныйИдентификатор;
	Строка_УИ               = Строка(УникальныйИдентификатор);

	РисунокТень = ТабличныйДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Прямоугольник); //тень
	РисунокТень.Лево = ЛевоЭтапа ;
	РисунокТень.Верх = ВерхЭтапа ;//ВысотаЭтапа;
	РисунокТень.Ширина = ШиринаЭтапа + 0.5;
	РисунокТень.Высота = ВысотаЭтапа + 0.5;
	Если (ВыбранныйЭтапВход = Неопределено) ИЛИ (Расшифровка <> ВыбранныйЭтапВход) Тогда
		РисунокТень.ЦветФона = Новый Цвет(153,153, 153);
	Иначе
		РисунокТень.ЦветФона = Новый Цвет(255, 215, 0);			// Подсветка выбранного этапа.
	КонецЕсли;

	РисунокТень.ГраницаСверху = Ложь;
	РисунокТень.ГраницаСнизу  = Ложь;
	РисунокТень.ГраницаСправа = Ложь;
	РисунокТень.ГраницаСлева  = Ложь;
	РисунокТень.Узор = ТипУзораТабличногоДокумента.Сплошной;

	Рисунок = ТабличныйДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Прямоугольник);//Основа;
	Рисунок.Лево = ЛевоЭтапа;
	Рисунок.Верх = ВерхЭтапа;
	Рисунок.Ширина = ШиринаЭтапа;
	Рисунок.Высота = ВысотаЭтапа;
	// Определим цвет фона этапа.
	ЕстьЦветФона = Ложь;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаЭтапа, "ЦветФона") Тогда
		ЕстьЦветФона = (СтрокаЭтапа.ЦветФона <> Неопределено);
	Иначе
		ЕстьЦветФона = Ложь;
	КонецЕсли;
	Если ЕстьЦветФона Тогда
		Рисунок.ЦветФона = СтрокаЭтапа.ЦветФона;
	Иначе	
		// Изменим фон этапа в зависимости от его состояния.
		Если  СтрокаЭтапа.СостояниеЭтапа = NULL 
			ИЛИ СтрокаЭтапа.СостояниеЭтапа = Перечисления.СостоянияЭтаповУниверсальныхПроцессов.НеАктивен   Тогда
			Рисунок.ЦветФона = Новый Цвет(250,250,250);
		ИначеЕсли СтрокаЭтапа.СостояниеЭтапа = Перечисления.СостоянияЭтаповУниверсальныхПроцессов.Завершен Тогда
			Рисунок.ЦветФона = Новый Цвет(210, 250, 200);	
		ИначеЕсли СтрокаЭтапа.СостояниеЭтапа = Перечисления.СостоянияЭтаповУниверсальныхПроцессов.ВОбработке Тогда	
			Рисунок.ЦветФона = Новый Цвет(200, 230, 255);	
		ИначеЕсли СтрокаЭтапа.СостояниеЭтапа = Перечисления.СостоянияЭтаповУниверсальныхПроцессов.ЗавершенСОшибкой Тогда	
			Рисунок.ЦветФона = Новый Цвет(250, 200, 200);	
		КонецЕсли;
	КонецЕсли;

	Рисунок.ГраницаСверху = Ложь;
	Рисунок.ГраницаСнизу  = Ложь;
	Рисунок.ГраницаСправа = Ложь;
	Рисунок.ГраницаСлева  = Ложь;
	Рисунок.Узор = ТипУзораТабличногоДокумента.Сплошной;
	Рисунок.Имя  = "4_" + Строка_УИ;

	// Изменим цвет этапа, если по нему есть ошибки.
	//ЕстьОшибкиПоЭтапу = ПроверитьОшибкиЭтапа(Расшифровка);
	//Если ЕстьОшибкиПоЭтапу Тогда
	//	Рисунок.ГраницаСверху	 = Истина;
	//	Рисунок.ГраницаСнизу	 = Истина;
	//	Рисунок.ГраницаСправа	 = Истина;
	//	Рисунок.ГраницаСлева	 = Истина;
	//	Рисунок.ЦветЛинии = WebЦвета.Лосось;
	//Иначе	
	//	// Не меняем цвет этапа.
	//КонецЕсли;

	// Обновим значение кеша ошибок в этапе при необходимости.
	//Если ЕстьОшибкиПоЭтапу <> Расшифровка.НайденыОшибки Тогда
	//	Попытка
	//		ЭтапОбъект = Расшифровка.ПолучитьОбъект();
	//		ЭтапОбъект.НайденыОшибки = ЕстьОшибкиПоЭтапу;
	//		ЭтапОбъект.Записать();
	//	Исключение
	//		ТекстСообщения = НСтр("ru = 'Не удаось обновить кеш ошибок для этапа %ЭтапВход%: %ОписаниеОшибки%'");
	//		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ЭтапВход%", Строка(Расшифровка));
	//		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", ОписаниеОшибки());
	//		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
	//	КонецПопытки;
	//Иначе
	//	// Значение кеша актуально. Не изменяем этап.
	//КонецЕсли;

	//Рисуем заголовок с названием этапа	
	Если СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ЭтапСогласования Тогда
		Рисунок = НарисоватьТекст(ТабличныйДокумент, ТекстОсновногоСообщения, Расшифровка, ЛевоЭтапа, ВерхЭтапа + ВысотаСтрокиТекста, ШиринаЭтапа, ВысотаСтрокиТекста * 3 + ВертикальнаяКоррекцияТекущая);	
	ИначеЕсли  СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ЭтапПроцессаПодготовкиОтчетности Тогда   
		НакопленноеОтклонение =  ?(ЗначениеЗаполнено(СтрокаЭтапа.НакопленноеОтклонение), Окр(СтрокаЭтапа.НакопленноеОтклонение,2),0);

		Если  НакопленноеОтклонение>0 Тогда
			РисунокТень.ЦветФона = Новый Цвет(255,0,0);
		КонецЕсли;

		Рисунок = НарисоватьТекст(ТабличныйДокумент, ТекстОсновногоСообщения, Расшифровка, ЛевоЭтапа, ВерхЭтапа + 6, ШиринаЭтапа, ВысотаСтрокиТекста * 3 + ВертикальнаяКоррекцияТекущая);

		ВерхРисунка = Рисунок.Верх + Рисунок.Высота;

		ЛевоРисунка = ЛевоЭтапа;

		ЦветЛинии = Новый Цвет (0,0,0);

		Если СтрокаЭтапа.СостояниеЭтапа = Перечисления.СостоянияЭтаповУниверсальныхПроцессов.ВОбработке Тогда
			ОтобразитьСостояние("Выполняется", СтрокаЭтапа, ТабличныйДокумент, ЛевоРисунка, ВерхРисунка-3, ВысотаСтрокиТекста, ЦветЛинии,Расшифровка);
		КонецЕсли;
		Если СтрокаЭтапа.СостояниеЭтапа = Перечисления.СостоянияЭтаповУниверсальныхПроцессов.Завершен Тогда
			ОтобразитьСостояние("Утвержден", СтрокаЭтапа, ТабличныйДокумент, ЛевоРисунка, ВерхРисунка-3, ВысотаСтрокиТекста, ЦветЛинии,Расшифровка);
		КонецЕсли;	
	ИначеЕсли СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.РучнойПереход Тогда
		Рисунок = НарисоватьТекст(ТабличныйДокумент, ТекстОсновногоСообщения, Расшифровка, ЛевоЭтапа, ВерхЭтапа + ВысотаСтрокиТекста, ШиринаЭтапа, ВысотаСтрокиТекста * 3 + ВертикальнаяКоррекцияТекущая);		
	Иначе
		Рисунок = НарисоватьТекст(ТабличныйДокумент, ТекстОсновногоСообщения, Расшифровка, ЛевоЭтапа, ВерхЭтапа + ВысотаСтрокиТекста, ШиринаЭтапа, ВысотаСтрокиТекста * 3 + ВертикальнаяКоррекцияТекущая);
	КонецЕсли;

	Рисунок.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	Рисунок.Имя = "1_" + Строка_УИ;
	Рисунок.ГраницаСверху = Ложь;
	Рисунок.ГраницаСнизу  = Ложь;
	Рисунок.ГраницаСправа = Ложь;
	Рисунок.ГраницаСлева  = Ложь;
	Рисунок.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
	Рисунок.ВертикальноеПоложение   = ВертикальноеПоложение.Верх;
	Рисунок.Узор = ТипУзораТабличногоДокумента.БезУзора;

	Если СоответствиеРисунковИЭтапов <> Неопределено Тогда
		НоваяСтрока = СоответствиеРисунковИЭтапов.Добавить();
		НоваяСтрока.Этап = СтрокаЭтапа.Этап;
		НоваяСтрока.ИмяРисунка  = УникальныйИдентификатор;    
	КонецЕсли;	

	//Рисуем картинку типа этапа
	Рисунок = ТабличныйДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
	Рисунок.Лево   = ЛевоЭтапа;
	Рисунок.Верх   = ВерхЭтапа;
	Рисунок.Ширина = ШиринаЭтапа;

	Рисунок.ГраницаСверху = Ложь;
	Рисунок.ГраницаСнизу  = Ложь;
	Рисунок.ГраницаСправа = Ложь;
	Рисунок.ГраницаСлева  = Ложь;
	Рисунок.Узор = ТипУзораТабличногоДокумента.БезУзора;

	Рисунок.Высота = ВысотаСтрокиТекста-2;
	Рисунок.РазмерКартинки = РазмерКартинки.Пропорционально;
	Рисунок.Имя    = "2_" + Строка_УИ;

	Если СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ДочернийМаршрут Тогда
		Рисунок.Картинка = БиблиотекаКартинок.БизнесПроцесс;
	ИначеЕсли СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.УсловныйПереход Тогда
		Рисунок.Картинка = БиблиотекаКартинок.Вопрос32;
	ИначеЕсли СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ЭтапСогласования Тогда
		Рисунок.Картинка = БиблиотекаКартинок.ЗапланированнаяИдея;			
	ИначеЕсли СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.Оповещение Тогда
		Рисунок.Картинка = БиблиотекаКартинок.Оповещение;	
	ИначеЕсли СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.Автоутверждение Тогда
		Рисунок.Картинка = БиблиотекаКартинок.ЗеленаяГалка;
	ИначеЕсли СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.Автоотклонение Тогда
		Рисунок.Картинка = БиблиотекаКартинок.КрасныйКрест;
	ИначеЕсли СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ЭтапПроцессаПодготовкиОтчетности Тогда
		Рисунок.Высота = ВысотаСтрокиТекста-5;
		Рисунок.Картинка = БиблиотекаКартинок.ВидыОтчетов_32;
	ИначеЕсли СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ОбработкаУниверсальногоПроцесса Тогда
		Рисунок.Картинка = БиблиотекаКартинок.Шестеренки;
	ИначеЕсли СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ЦиклПока Тогда
		Рисунок.Картинка = БиблиотекаКартинок.ЦиклПока;
	ИначеЕсли СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ЭтапУниверсальногоЭкспорта Тогда
		Рисунок.Картинка = БиблиотекаКартинок.Процессы_ЭтапУниверсальногоЭкспорта;
	ИначеЕсли СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ЭтапУниверсальногоИмпорта Тогда
		Рисунок.Картинка = БиблиотекаКартинок.Процессы_ЭтапУниверсальногоИмпорта;
	ИначеЕсли СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.Пауза Тогда
		Рисунок.Картинка = БиблиотекаКартинок.Пауза;
	ИначеЕсли СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.РучнойПереход Тогда
		Рисунок.Картинка = БиблиотекаКартинок.РучнойПереход;
	КонецЕсли;

	Если СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ЭтапСогласования ИЛИ СтрокаЭтапа.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ЭтапПроцессаПодготовкиОтчетности Тогда

		Рисунок = НарисоватьТекст(ТабличныйДокумент, "", Расшифровка, ЛевоЭтапа, ВерхЭтапа + ВысотаСтрокиТекста * 2 + ВертикальнаяКоррекцияТекущая, ШиринаЭтапа, ВысотаСтрокиТекста * 2);

		Рисунок.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		Рисунок.ГраницаСверху = Ложь;
		Рисунок.ГраницаСнизу  = Ложь;
		Рисунок.ГраницаСправа = Ложь;
		Рисунок.ГраницаСлева  = Ложь;
		Рисунок.Узор = ТипУзораТабличногоДокумента.БезУзора;
		Рисунок.Имя = "3_" + УникальныйИдентификатор;

	КонецЕсли;

КонецПроцедуры

&ИзменениеИКонтроль("ИнициироватьСогласованиеЭтапа")
Процедура АЛФ_ИнициироватьСогласованиеЭтапа(ДокументПроцесса, Организация, ЭтапПроцесса, НужнаДальнейшаяОбработка, Отказ, ДополнительныеПараметры, Переинициировать)

	// Инициализация.
	ДополнительныеПараметры = ИнициализироватьДополнительныеПараметрыСогласования(ДополнительныеПараметры);
	ИдентификаторСообщенияРабочий = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ИдентификаторСообщения", "");
	// Убедимся, что этап ещё не запущен.
	ЭтапВыполняется = ПроверитьВыполнениеЭтапа(ДокументПроцесса, Организация, ЭтапПроцесса);
	ЭтапПроцессаПредставление = Строка(ЭтапПроцесса);

	Если ЭтапВыполняется И Не Переинициировать Тогда	
		Возврат;	// Этап уже запущен. Не запускаем повторно.
	Иначе
		// Проверка на запуск пройдена. Можно выполнять далее.
	КонецЕсли;

	Попытка
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВыполнениеПроцессовСрезПоследних.Период КАК Период,
		|	ВыполнениеПроцессовСрезПоследних.ЭтапПроцесса КАК ЭтапПроцесса,
		|	ВыполнениеПроцессовСрезПоследних.ДокументПроцесса КАК ДокументПроцесса,
		|	ВыполнениеПроцессовСрезПоследних.ПринялКИсполнению КАК ПринялКИсполнению
		|ИЗ
		|	РегистрСведений.ВыполнениеПроцессов.СрезПоследних КАК ВыполнениеПроцессовСрезПоследних
		|ГДЕ
		|	ВыполнениеПроцессовСрезПоследних.ЭтапПроцесса = &ЭтапПроцесса
		|	И ВыполнениеПроцессовСрезПоследних.ДокументПроцесса = &ДокументПроцесса";

		Запрос.УстановитьПараметр("ЭтапПроцесса", ЭтапПроцесса);
		Запрос.УстановитьПараметр("ДокументПроцесса", ДокументПроцесса);
		Выборка = Запрос.Выполнить().Выбрать();

		МенеджерЗаписи                      = РегистрыСведений.ВыполнениеПроцессов.СоздатьМенеджерЗаписи();

		Если Выборка.Следующий() Тогда

			ТекПользователь = ОбщегоНазначенияУХ.ПолучитьЗначениеПеременной("глТекущийПользователь");
			Если Выборка.ПринялКИсполнению <> Справочники.Пользователи.ПустаяСсылка() 
				И Выборка.ПринялКИсполнению <> ТекПользователь Тогда

				ТекстСообщения = СтрШаблон(НСтр("ru = 'При согласовании этапа %1 произошли ошибки: Этап принят на исполнение пользователем %2'"), Строка(ЭтапПроцесса), Строка(Выборка.ПринялКИсполнению));
				ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
				Отказ = Истина;

				Возврат;	
			КонецЕсли;

			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка, "ЭтапПроцесса, ДокументПроцесса, Период");
			МенеджерЗаписи.Прочитать();
		КонецЕсли;

		Если МенеджерЗаписи.Выбран() Тогда
			// Реквизиты заполнены.
		Иначе
			МенеджерЗаписи.ЭтапПроцесса    	 = ЭтапПроцесса;
			МенеджерЗаписи.ДокументПроцесса	 = ДокументПроцесса;

		КонецЕсли;

		МенеджерЗаписи.АрхивнаяЗапись		 = Ложь;
		МенеджерЗаписи.Период				 = ОбщегоНазначенияСерверУХ.ПолучитьОтметкуПоОбъекту(ДокументПроцесса);
		МенеджерЗаписи.Организация			 = Организация;

		// Получение количества неутверждённых этапов.
		ЧислоНеУтвержденныхЭтапов = ПолучитьКоличествоНеУтвержденныхЭтаповПредшественников(ЭтапПроцесса, ДокументПроцесса);
		ВсеПредшественникиУтверждены = (ЧислоНеУтвержденныхЭтапов = 0) ИЛИ ДокументПроцесса.ШаблонПроцесса.ПриОтклоненииЭтапаВыполнятьПроцессДоКонца;
		ЭтоЭтапДочернегоМаршрута = (ЭтапПроцесса.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ДочернийМаршрут);
		НуженЗапускДочернегоПроцесса = (ВсеПредшественникиУтверждены И ЭтоЭтапДочернегоМаршрута);

		// Установка состояния этапа.
		МенеджерЗаписи.СостояниеЭтапа = Перечисления.СостоянияЭтаповУниверсальныхПроцессов.ВОбработке;

		ОбъектСсылка = ДокументПроцесса.КлючевойОбъектПроцесса;
		Если ЭтапПроцесса.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ЭтапСогласования  Тогда
			ОтборЭтап = Новый Структура("Этап",ЭтапПроцесса);
			ТаблицаДопСогласующихВыгрузка = ДокументПроцесса.ДополнительныеСогласующие.Выгрузить(ОтборЭтап);
			ОрганизацияРоли = ОпределитьОрганизациюРоли(ОбъектСсылка, Организация);
			СтрокаПредставления = "";
			ОтветственныеТекущегоЭтапа = РасшифроватьРолиОтветственных(ДокументПроцесса, ТаблицаДопСогласующихВыгрузка, ОрганизацияРоли, СтрокаПредставления);
#Вставка
			//+ИИТ_Аксеньюшкин
			Для Каждого СтрокаТЗ Из ОтветственныеТекущегоЭтапа Цикл
				Если СтрокаТЗ.Пользователь = Справочники.Пользователи.ОтветственныйЗаБанковскийСчет ИЛИ СокрЛП(Строка(СтрокаТЗ.Пользователь)) = "Бухгалтер" Тогда
					Если Строка(ОбъектСсылка.ВидОперацииУХ) = "Перевод на счет другой организации" Тогда
						СтрокаТЗ.Пользователь = ОбъектСсылка.СчетКассаОрганизации.Ответственный;
					Иначе
						СтрокаТЗ.Пользователь = ОбъектСсылка.ИИТ_СчетОрганизации.Ответственный;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			//-ИИТ_Аксеньюшкин
#КонецВставки
			Если ОтветственныеТекущегоЭтапа.Количество() > 0 Тогда
				МодульРегламентныхЗаданийУХ.ДобавитьЗаданиеНаТаймаутЭтапа(ДокументПроцесса, ЭтапПроцесса);
				МодульРегламентныхЗаданийУХ.ДобавитьЗаданиеНаЭтап(ДокументПроцесса, ЭтапПроцесса, ОтветственныеТекущегоЭтапа, ДополнительныеПараметры);
			Иначе
				ОбработатьОшибкуМаршрутизации(ЭтапПроцесса, ДокументПроцесса, Организация);
			КонецЕсли;
			// Добавим ответстсвенных.
			Если ОтветственныеТекущегоЭтапа.Количество() = 1 Тогда
				ПерваяСтрока =  ОтветственныеТекущегоЭтапа[0];
				МенеджерЗаписи.Автор = ПерваяСтрока.Пользователь;
			Иначе
				// Нельзя определить ответственных однозначно.
			КонецЕсли;
			МенеджерЗаписи.АвторПредставление	 = СтрокаПредставления;
			МенеджерЗаписи.АрхивнаяЗапись		 = Ложь;
		ИначеЕсли  ЭтапПроцесса.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.РучнойПереход  Тогда
			ОтборЭтап = Новый Структура("Этап",ЭтапПроцесса);
			ТаблицаДопСогласующихВыгрузка = ДокументПроцесса.ДополнительныеСогласующие.Выгрузить(ОтборЭтап);
			ОрганизацияРоли = ОпределитьОрганизациюРоли(ОбъектСсылка, Организация);
			СтрокаПредставления = "";
			ОтветственныеТекущегоЭтапа = РасшифроватьРолиОтветственных(ДокументПроцесса, ТаблицаДопСогласующихВыгрузка, ОрганизацияРоли, СтрокаПредставления);
			Если ОтветственныеТекущегоЭтапа.Количество() > 0 Тогда
				МодульРегламентныхЗаданийУХ.ДобавитьЗаданиеНаТаймаутЭтапа(ДокументПроцесса, ЭтапПроцесса);
				МодульРегламентныхЗаданийУХ.ДобавитьЗаданиеНаЭтап(ДокументПроцесса, ЭтапПроцесса, ОтветственныеТекущегоЭтапа, ДополнительныеПараметры);
			Иначе
				ОбработатьОшибкуМаршрутизации(ЭтапПроцесса, ДокументПроцесса, Организация);
			КонецЕсли;
			// Добавим ответстсвенных.
			Если ОтветственныеТекущегоЭтапа.Количество() = 1 Тогда
				ПерваяСтрока =  ОтветственныеТекущегоЭтапа[0];
				МенеджерЗаписи.Автор = ПерваяСтрока.Пользователь;
			Иначе
				// Нельзя определить ответственных однозначно.
			КонецЕсли;
			МенеджерЗаписи.АвторПредставление	 = СтрокаПредставления;
			МенеджерЗаписи.АрхивнаяЗапись		 = Ложь;
		КонецЕсли;

		Если ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.НастраиваемыйОтчет") Тогда
			МенеджерЗаписи.ОписаниеВерсии = ВернутьТекущееОписаниеВерсииДляЭкземпляраОтчета(ОбъектСсылка);
		КонецЕсли;
		МенеджерЗаписи.ДатаНачала = ТекущаяДатаСеанса();
		МенеджерЗаписи.Записать(Истина);

		ОчиститьДополнительноеСогласованиеПоЭтапу(ЭтапПроцесса, ДокументПроцесса);

		Если НуженЗапускДочернегоПроцесса Тогда
			КлючевойРеквизитВложенногоПроцесса = ДокументПроцесса.ПараметрыПроцесса[0].ЗначениеПоУмолчанию; 

			НаШагНазад = Ложь;
			ДополнительныеПараметры.Свойство("НаШагНазад", НаШагНазад);
			НаШагНазад = ?(НаШагНазад = Неопределено, Ложь, НаШагНазад);

			ОтменитьДочерниеПроцессы(ДокументПроцесса, ЭтапПроцесса, НаШагНазад);

			ИнициализироватьПроцесс(Неопределено,ЭтапПроцесса.ДочернийМаршрут, Организация,КлючевойРеквизитВложенногоПроцесса, ДокументПроцесса, ЭтапПроцесса,,, НаШагНазад);
			НужнаДальнейшаяОбработка = Истина;
		ИначеЕсли ЭтапПроцесса.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.УсловныйПереход Тогда
			НужнаДальнейшаяОбработка = УтвердитьЭтапПроцесса(ДокументПроцесса, Организация, ЭтапПроцесса, "", Истина, , , , , , Отказ, ДополнительныеПараметры);
		ИначеЕсли ЭтапПроцесса.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.Автоотклонение Тогда
			ОбработкаЭтапаАвтоОтклонение(ДокументПроцесса, Организация, ЭтапПроцесса, ДополнительныеПараметры);
		ИначеЕсли ЭтапПроцесса.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.Автоутверждение Тогда			
			ОбработкаЭтапаАвтоУтверждение(ДокументПроцесса, Организация, ЭтапПроцесса, ДополнительныеПараметры);
		ИначеЕсли ЭтапПроцесса.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ОбработкаУниверсальногоПроцесса Тогда
			ОбработатьЭтапОбработка(ДокументПроцесса, Организация, ЭтапПроцесса, НужнаДальнейшаяОбработка, Отказ, ДополнительныеПараметры);
		ИначеЕсли ЭтапПроцесса.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ЭтапУниверсальногоЭкспорта Тогда
			СтруктураПараметров = Новый Структура;
			СтруктураПараметров.Вставить("ЭкземплярПроцесса", ДокументПроцесса);
			СтруктураПараметров.Вставить("ТекущийЭтап", ЭтапПроцесса);
			ФлагОшибки = УправлениеЭтапамиПроцесса.ОбработатьЭтапУниверсальногоЭкспорта(СтруктураПараметров);
			НужнаДальнейшаяОбработка = УтвердитьЭтапПроцесса(ДокументПроцесса, Организация, ЭтапПроцесса, "", Истина, , , , , , , ДополнительныеПараметры);	
		ИначеЕсли ЭтапПроцесса.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.Оповещение Тогда    
			УправлениеЭтапамиПроцесса.ОбработатьЭтапОповещения(ДокументПроцесса, ЭтапПроцесса);
			НужнаДальнейшаяОбработка = УтвердитьЭтапПроцесса(ДокументПроцесса, Организация, ЭтапПроцесса, "", Истина, , , , , , , ДополнительныеПараметры);	
		ИначеЕсли ЭтапПроцесса.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ЦиклПока Тогда    
			МодульРегламентныхЗаданийУХ.ДобавитьЗаданиеНаЭтап(ДокументПроцесса, ЭтапПроцесса,, ДополнительныеПараметры);
			МодульРегламентныхЗаданийУХ.ОбработкаЭтаповПроцессов(ЭтапПроцесса);
			НужнаДальнейшаяОбработка = Истина;
		ИначеЕсли ЭтапПроцесса.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.Пауза Тогда    
			МодульРегламентныхЗаданийУХ.ДобавитьЗаданиеНаТаймаутЭтапа(ДокументПроцесса, ЭтапПроцесса);
			НужнаДальнейшаяОбработка = Истина;		
		ИначеЕсли ЭтапПроцесса.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ЭтапСогласования Тогда    		
			// Выполним установку статуса объекта.
			СтатусСогласованияОбъекта = ЭтапПроцесса.СтатусСогласованияОбъекта;
			Если ЗначениеЗаполнено(СтатусСогласованияОбъекта) Тогда
				Результат = УправлениеПроцессамиСогласованияУХ.ПеревестиЗаявкуВПроизвольноеСостояние(ОбъектСсылка, СтатусСогласованияОбъекта, , , , ДокументПроцесса, , , , ИдентификаторСообщенияРабочий);	
				Если Не Результат Тогда
					Отказ = Истина;
				Иначе
					// Выполнено успешно.
					МассивПоследователей = МодульРегламентныхЗаданийУХ.ПолучитьМассивПоследователейЭтапаРекурсивно(ЭтапПроцесса, ДокументПроцесса, ДополнительныеПараметры);

					ОтменятьПоследующиеЭтапы = Ложь;

					Если МассивПоследователей.Количество() > 0 тогда
						ТаблицаСостоянийСледующегоЭтапа = ПолучитьСостояниеЭтапаПоЭкземпляруПроцесса(ДокументПроцесса, МассивПоследователей[0]);

						Если ТаблицаСостоянийСледующегоЭтапа <> Неопределено И ТаблицаСостоянийСледующегоЭтапа.Количество() > 0 Тогда
							ОтменятьПоследующиеЭтапы = (ТаблицаСостоянийСледующегоЭтапа[0].СостояниеЭтапа = Перечисления.СостоянияЭтаповУниверсальныхПроцессов.Завершен);
						КонецЕсли;

						Если МассивПоследователей.Найти(ЭтапПроцесса) <> Неопределено И ОтменятьПоследующиеЭтапы тогда
							ТекПользователь = ОбщегоНазначенияУХ.ПолучитьЗначениеПеременной("глТекущийПользователь");
							РезультатОтклонения = ОтклонитьЭтапСогласования(ДокументПроцесса, Организация, МассивПоследователей[0], "", ТекПользователь, Перечисления.СостоянияЭтаповУниверсальныхПроцессов.НеАктивен,, ДополнительныеПараметры);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			Иначе
				// Пустой статус на этом этапе. Не изменяем.
			КонецЕсли;
		ИначеЕсли ЭтапПроцесса.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.РучнойПереход Тогда
			// Выполним установку статуса объекта.
			СтатусСогласованияОбъекта = ЭтапПроцесса.СтатусСогласованияОбъекта;
			Если ЗначениеЗаполнено(СтатусСогласованияОбъекта) Тогда
				Результат = УправлениеПроцессамиСогласованияУХ.ПеревестиЗаявкуВПроизвольноеСостояние(ОбъектСсылка, СтатусСогласованияОбъекта, , , , ДокументПроцесса, , , , ИдентификаторСообщенияРабочий);
				Если Не Результат Тогда
					Отказ = Истина;
				Иначе
					// Выполнено успешно.
				КонецЕсли;
			Иначе
				// Пустой статус на этом этапе. Не изменяем.
			КонецЕсли;
		КонецЕсли;
	Исключение
		ТекстСообщения = НСтр("ru = 'При инициации этапа %Этап% произошли ошибки: %ОписаниеОшибки%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Этап%", ЭтапПроцессаПредставление);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", ОписаниеОшибки());
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения,,,, ДополнительныеПараметры.ИдентификаторФормыДляВыводаПротокола);
		Отказ = Истина;
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

&ИзменениеИКонтроль("УтвердитьЭтап")
Функция АЛФ_УтвердитьЭтап(Форма, ТекущийЭтап, Ссылка, ПользовательВход, ЗамещениеВход, ИдентификаторФормыДляВыводаПротокола, ОтключатьСообщенияТелеграм)

	// Инициализация.
	ОбъектСсылка=?(Форма=Неопределено,Ссылка,Форма.Объект.Ссылка);
	НачатьТранзакцию();
	ЕстьОшибки = Ложь;
	СтатусТранзакции = "";
	ТипЭтапаРучнойПереход = Перечисления.ТипыЭтаповУниверсальныхПроцессов.РучнойПереход;
	ТипЭтапаУсловныйПереход = Перечисления.ТипыЭтаповУниверсальныхПроцессов.УсловныйПереход;
	Если ПользовательВход = Неопределено Тогда
		ПользовательРабочий = ОбщегоНазначенияУХ.ПолучитьЗначениеПеременной("глТекущийПользователь");
	Иначе
		ПользовательРабочий = ПользовательВход;
	КонецЕсли;
	Если ТекущийЭтап.Свойство("КомментарийСистемы") Тогда
		КомментарийСистемы = ТекущийЭтап.КомментарийСистемы;
	Иначе
		КомментарийСистемы = "";
	КонецЕсли;
	ИдентификаторСообщения = Строка(Новый УникальныйИдентификатор);
	ДополнительныеПараметры = СформироватьДополнительныеПараметрыСогласования();
	ДополнительныеПараметры.Вставить("ИдентификаторСообщения", ИдентификаторСообщения);
	ДополнительныеПараметры.Вставить("ИдентификаторФормыДляВыводаПротокола", ИдентификаторФормыДляВыводаПротокола);

	АдресКэшаПерехода = ПоместитьВоВременноеХранилище("Неопределено");
	ДополнительныеПараметры.Вставить("АдресКэшаПерехода", АдресКэшаПерехода);
	СсылкаСтрокой = Строка(ОбъектСсылка);

	Попытка

		// Проверка контрольной суммы реквизитов, если необходимо.
		РеквизитыЭтапа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ТекущийЭтап.Этап,
		"Владелец.ИспользоватьПроверкуКонтрольнойСуммыПриУтверждении, Владелец.НастройкиПроверкиКонтрольнойСуммы");
		Если РеквизитыЭтапа.ВладелецИспользоватьПроверкуКонтрольнойСуммыПриУтверждении Тогда
#Удаление
			РезультатПроверки = РегистрыСведений.ДанныеДляПроверкиСогласования.ПроверитьКонтрольнуюСуммуДокумента(ОбъектСсылка, РеквизитыЭтапа.ВладелецНастройкиПроверкиКонтрольнойСуммы); 
			Если НЕ РезультатПроверки.КонтрольнаяСуммаСовпадает Тогда	
				ЕстьОшибки = Истина;
				ТекстСообщения = 
				НСтр("ru = 'Текущая версия объекта не соответствует отправленной на согласование.'")
				+ Символы.ПС
				+ РезультатПроверки.СообщениеОбОшибке;
				ВызватьИсключение ТекстСообщения;
			КонецЕсли;
#КонецУдаления
		КонецЕсли;

		// Запись статуса этапа.
		ВыполнитьПроцедурыПоСтатусам(ОбъектСсылка, ТекущийЭтап.Этап.СтатусСогласованияОбъекта, ТекущийЭтап.ДокументСогласования, ДополнительныеПараметры);
		// Переход по маршруту.
		Если ЗначениеЗаполнено(ТекущийЭтап.Организация) Тогда
			ТекОрганизация = ТекущийЭтап.Организация;
		Иначе
			ТекОрганизация = ПолучитьОрганизациюОбъекта(ОбъектСсылка);
		КонецЕсли;
		ВыбранныйЭтап = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ТекущийЭтап, "ВыбранныйЭтап", Неопределено);
		ЭтоЭтапПерехода = (ТекущийЭтап.Этап.ТипЭтапа = ТипЭтапаРучнойПереход) ИЛИ (ТекущийЭтап.Этап.ТипЭтапа = ТипЭтапаУсловныйПереход);

		Отказ = Ложь;
		Если (ЭтоЭтапПерехода) И (ЗначениеЗаполнено(ВыбранныйЭтап)) Тогда
			УтвердитьЭтапПроцесса(ТекущийЭтап.ДокументСогласования, ТекОрганизация, ТекущийЭтап.Этап, ТекущийЭтап.Комментарий, , ПользовательРабочий, ВыбранныйЭтап, ЗамещениеВход, КомментарийСистемы, , , ДополнительныеПараметры);
		Иначе
			УтвердитьЭтапПроцесса(ТекущийЭтап.ДокументСогласования, ТекОрганизация, ТекущийЭтап.Этап, ТекущийЭтап.Комментарий, , ПользовательРабочий, , ЗамещениеВход, КомментарийСистемы, , Отказ, ДополнительныеПараметры);
		КонецЕсли;

		Если Отказ Тогда
			ОтменитьТранзакцию();
			ОпределитьСостояние(Форма);

			Попытка
				УдалитьИзВременногоХранилища(АдресКэшаПерехода);
			Исключение
			КонецПопытки;

			Возврат "ОшибкаУтверждения";
		КонецЕсли;

		СтатусТранзакции = "ЭтапСогласован";
		Если ДокументПолностьюСогласован(ОбъектСсылка) Тогда
			СоответствиеСтатусов = ПолучитьСоответствиеСостоянийОбъекта(ОбъектСсылка);
			ТекущийСтатусОбъекта = УправлениеПроцессамиСогласованияУХ.ВернутьСтатусОбъекта(ОбъектСсылка);
			РезультатУтверждения = ПриУтвержденииВсехЭтаповМаршрутаСогласования(ОбъектСсылка);				
			Если Не РезультатУтверждения Тогда
				ЕстьОшибки = Истина;
			Иначе
				// Утверждение осуществлено успешно.
			КонецЕсли;

			ДокументПроцессаВход = ТекущийЭтап.ДокументСогласования;

			Если НЕ ЕстьОтклоненныеЭтапыПоПроцессуСогласования(ДокументПроцессаВход) Тогда

				СтатусТранзакции = "ОбъектСогласован";			
				Если ТекущийСтатусОбъекта <> СоответствиеСтатусов["Утвержден"] Тогда
					РезультатПеревода = УправлениеПроцессамиСогласованияУХ.ПеревестиЗаявкуВСостояниеУтверждена(ОбъектСсылка, Истина, ДокументПроцессаВход, ИдентификаторФормыДляВыводаПротокола, ДополнительныеПараметры);
					Если Не РезультатПеревода Тогда
						ЕстьОшибки = Истина;
					Иначе
						// Перевод осуществлён успешно.
					КонецЕсли;
				Иначе
					// Объект уже находится в этом статусе.
				КонецЕсли;

			ИначеЕсли ДокументПроцессаВход.ШаблонПроцесса.ПриОтклоненииЭтапаВыполнятьПроцессДоКонца Тогда
				// Есть отклонённые этапы. Отклоним документ.
				Если ЗначениеЗаполнено(ДокументПроцессаВход.ПроцессРодитель) Тогда
					ОтклонитьВложенныйПроцесс(ДокументПроцессаВход);
				Иначе	
					СтруктураПараметровЭтапа = Новый Структура;
					СтруктураПараметровЭтапа.Вставить("ДокументСогласования", ДокументПроцессаВход); 
					СтруктураПараметровЭтапа.Вставить("Комментарий", "");
					СтруктураПараметровЭтапа.Вставить("Организация", ТекОрганизация);
					СтруктураПараметровЭтапа.Вставить("Этап", ТекущийЭтап.Этап);
					ТекстКомментария = НСтр("ru = 'Документ возвращен на доработку'");
					ВернутьНаДоработку(ДокументПроцессаВход.КлючевойОбъектПроцесса, ТекстКомментария, , СтруктураПараметровЭтапа);
					ОстановитьПроцесс(ДокументПроцессаВход);
					УправлениеПроцессамиСогласованияУХ.ПеревестиЗаявкуВСостояниеОтклонена(ОбъектСсылка, , Истина, ДокументПроцессаВход);
				КонецЕсли;
			КонецЕсли;

		КонецЕсли;
		// Уведомление пользователя.
		Если ТекущийЭтап.Этап.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ОбработкаУниверсальногоПроцесса Тогда
			ВидСобытияЭтапСогласован = Справочники.ВидыСобытийОповещений.ОбработкаВыполнена;
		ИначеЕсли ТекущийЭтап.Этап.ТипЭтапа = Перечисления.ТипыЭтаповУниверсальныхПроцессов.ЭтапСогласования Тогда	
			ВидСобытияЭтапСогласован = Справочники.ВидыСобытийОповещений.ЭтапСогласован;
		Иначе	
			ВидСобытияЭтапСогласован = Справочники.ВидыСобытийОповещений.ЭтапСогласован;
		КонецЕсли;	
		ОбъектСогласования = ТекущийЭтап.ДокументСогласования.КлючевойОбъектПроцесса;
		МассивРассылки = Новый Массив;
		МассивРассылки.Добавить(ПользовательРабочий);		
		Если НЕ ДополнительныеПараметры.Свойство("ТекущийЭтап") Тогда
			ДополнительныеПараметры.Вставить("ТекущийЭтап", ТекущийЭтап.Этап);
		КонецЕсли;
		МодульУправленияОповещениямиУХ.ОповеститьПользователей(ВидСобытияЭтапСогласован, , ОбъектСогласования, МассивРассылки, ДополнительныеПараметры,, ТекущийЭтап.Этап);
		МодульУправленияОповещениямиУХ.СоздатьНапоминание(ВидСобытияЭтапСогласован, ОбъектСогласования, МассивРассылки, ТекущийЭтап.ДокументСогласования);		
	Исключение

		ЕстьОшибки = Истина;
		ТекстСообщения = НСтр("ru = 'Не удалось утвердить этап по %СсылкаНаОбъект%: %ОписаниеОшибки%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СсылкаНаОбъект%", СсылкаСтрокой);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", ОписаниеОшибки());
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения,,,, ИдентификаторФормыДляВыводаПротокола);

	КонецПопытки;

	Попытка
		УдалитьИзВременногоХранилища(АдресКэшаПерехода);
	Исключение
	КонецПопытки;

	// Обработка транзакции.
	Если ЕстьОшибки Тогда
		ОтменитьТранзакцию();
		ОпределитьСостояние(Форма);
		Возврат "ОшибкаУтверждения";
	Иначе
		ЗафиксироватьТранзакцию();	
		ОпределитьСостояние(Форма); 

		Если ТипЗнч(ОбъектСсылка)=Тип("ДокументСсылка.ЗаявкаНаИзменениеНСИ") Тогда

			ОбменДаннымиНСИУХ.ПередатьСтатусПриНеобходимости(ОбъектСсылка,Перечисления.СостоянияСогласования.Утверждена);

		КонецЕсли;

		Если ОтключатьСообщенияТелеграм тогда
			ТелеграмСерверУХ.ДеактивироватьСообщенияТелеграм();
		КонецЕсли;

		Возврат СтатусТранзакции;
	КонецЕсли;
КонецФункции


&ИзменениеИКонтроль("ЭтапыСогласованияДляПользователя")
Функция АЛФ_ЭтапыСогласованияДляПользователя(ОбъектСогласования, Пользователь, ДобавлятьЗаместителейВход, СписокЗадачВход)
	// Инициализация.
	ПустаяТаблица = Новый ТаблицаЗначений;
	ПустаяТаблица.Колонки.Добавить("ДокументПроцесса");
	ПустаяТаблица.Колонки.Добавить("ЭтапПроцесса");
	ПустаяТаблица.Колонки.Добавить("Пользователь");
	ПустаяТаблица.Колонки.Добавить("ДопСогласование");
	ПустаяТаблица.Колонки.Добавить("ПравоРедактирования");
	ПустаяТаблица.Колонки.Добавить("Организация");
	ПустаяТаблица.Колонки.Добавить("Задача");
	РезультатФункции = ПустаяТаблица;
	// Получение таблицы отвественных.
	ТаблицаОтветственных = ОтветственныеПоОбъектуСогласования(ОбъектСогласования);
#Вставка
	Если ТаблицаОтветственных.Количество() > 0 Тогда
		Если Найти(Строка(ТаблицаОтветственных[0].Пользователь), "Согласующий") <> 0 И ТипЗнч(ОбъектСогласования) = Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств")
			И ЗначениеЗаполнено(ОбъектСогласования.ИИТ_СчетОрганизации) И ОбъектСогласования.ИИТ_СчетОрганизации.Ответственный = Пользователь Тогда
			ТаблицаОтветственных[0].Пользователь = Пользователь;
		КонецЕсли;
	КонецЕсли;
#КонецВставки
	Если ТаблицаОтветственных.Количество() > 0 Тогда
		// Получение организации и экземпляра процесса.
		ПерваяСтрокаОтветственные = ТаблицаОтветственных[0];
		ЭкземплярПроцесса = ПерваяСтрокаОтветственные.ДокументПроцесса;
		ОрганизацияЭкземпляра = ЭкземплярПроцесса.Организация;
		ОрганизацияРоли = ОпределитьОрганизациюРоли(ОбъектСогласования, ОрганизацияЭкземпляра);
		// Расшифровка ролей и расширенного согласования.
		СтрокаПредставление = "";

		ТаблицаРасшифровка = РасшифроватьРолиОтветственных(ЭкземплярПроцесса, ТаблицаОтветственных, ОрганизацияРоли, СтрокаПредставление);

		// Добавление заместителей.
		СписокУтверждающих = Новый СписокЗначений();
		СписокУтверждающих.Добавить(Пользователь);
		Если ДобавлятьЗаместителейВход Тогда
			ПолучитьЗамещаемых(Пользователь, СписокУтверждающих);
		Иначе
			// Оставляем список.
		КонецЕсли;
		// Отбор полученной таблицы по списку утверждающих.
		РезультатФункции = ТаблицаРасшифровка.СкопироватьКолонки();
		Для Каждого ТекТаблицаРасшифровка Из ТаблицаРасшифровка Цикл
			НайденныйЭлемент = СписокУтверждающих.НайтиПоЗначению(ТекТаблицаРасшифровка.Пользователь);
			Если НайденныйЭлемент <> Неопределено Тогда
				НоваяСтрока = РезультатФункции.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекТаблицаРасшифровка);
			Иначе
				// Пропускаем строку.
			КонецЕсли;
		КонецЦикла;	
		// Отбор по задачам.
		РезультатФункции = ОтобратьЭтапыПоЗадачам(РезультатФункции, СписокЗадачВход);
	Иначе
		РезультатФункции = ПустаяТаблица;
	КонецЕсли;
	Возврат РезультатФункции;
КонецФункции

