
&ИзменениеИКонтроль("СоздатьДокументНаОснованииРаспознанного")
Функция АЛФ_СоздатьДокументНаОснованииРаспознанного(РаспознанныйДокумент, ТипСоздаваемогоДокумента, ПараметрыЗаполнения, РежимЗаписи)

	ЕстьПустаяНоменклатура = Ложь;
	Если ПараметрыЗаполнения.Свойство("Товары") Тогда
		Для Каждого СтрокаТаблицы Из ПараметрыЗаполнения.Товары Цикл
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда
				ЕстьПустаяНоменклатура = Истина;

				ЭтоСообщение = Новый СообщениеПользователю;
				ЭтоСообщение.Текст = НСтр("ru = 'Не заполнена колонка ""Номенклатура"" в строке '") + СтрокаТаблицы.ПорядокСтроки;
				ЭтоСообщение.Поле = СтрШаблон("ТаблицаДокумента[%1].Номенклатура", Формат(СтрокаТаблицы.ПорядокСтроки - 1, "ЧГ="));
				ЭтоСообщение.Сообщить();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если ПараметрыЗаполнения.Свойство("Услуги") Тогда
		Для Каждого СтрокаТаблицы Из ПараметрыЗаполнения.Услуги Цикл
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда
				ЕстьПустаяНоменклатура = Истина;

				ЭтоСообщение = Новый СообщениеПользователю;
				ЭтоСообщение.Текст = НСтр("ru = 'Не заполнена колонка ""Номенклатура"" в строке '") + СтрокаТаблицы.ПорядокСтроки;
				ЭтоСообщение.Поле = СтрШаблон("ТаблицаДокумента[%1].Номенклатура", Формат(СтрокаТаблицы.ПорядокСтроки - 1, "ЧГ="));
				ЭтоСообщение.Сообщить();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если ЕстьПустаяНоменклатура Тогда
		ВызватьИсключение НСтр("ru = 'Необходимо заполнить номенклатуру во всех строках'");
	КонецЕсли;

	СоздаваемыйДокумент = Документы[ТипСоздаваемогоДокумента].СоздатьДокумент();
	СоздаваемыйДокумент.Заполнить(ПараметрыЗаполнения);
	РаспознаваниеДокументовПереопределяемый.ПриСозданииДокументаНаОснованииРаспознанного(СоздаваемыйДокумент, РаспознанныйДокумент, ПараметрыЗаполнения);
	Если РежимЗаписи = Неопределено Тогда
		СоздаваемыйДокумент.Записать(РежимЗаписиДокумента.Запись);
	Иначе
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			Если Не СоздаваемыйДокумент.ПроверитьЗаполнение() Тогда
				ВызватьИсключение НСтр("ru = 'При проверке заполнения обнаружены ошибки'");
			КонецЕсли;
			РаспознаваниеДокументовПереопределяемый.ПриПроведенииДокументаНаОснованииРаспознанного(СоздаваемыйДокумент);
		КонецЕсли;
#Вставка
		СоздаваемыйДокумент.Дата = ТекущаяДата();
#КонецВставки
		СоздаваемыйДокумент.Записать(РежимЗаписи);
	КонецЕсли;

	РаспознаваниеДокументовПереопределяемый.ПослеСозданияДокументаНаОснованииРаспознанного(СоздаваемыйДокумент);

	РегистрыСведений.СвязанныеОбъектыРаспознаниеДокументов.ЗаписатьЗначения(СоздаваемыйДокумент.Ссылка, РаспознанныйДокумент, Ложь);

	Возврат СоздаваемыйДокумент.Ссылка;

КонецФункции
