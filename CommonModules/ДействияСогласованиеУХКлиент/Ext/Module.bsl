
&ИзменениеИКонтроль("ПослеВводаПричиныОтменыСогласования")
Процедура АЛФ_ПослеВводаПричиныОтменыСогласования(Значение, ДополнительныеПараметры)
	Если Значение <> Неопределено Тогда
		СсылкаНаОбъект = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "СсылкаНаОбъект", Неопределено);

		СправочникМетаданного = МодульУправленияОповещениямиУХ.ПолучитьТипОбъектаОповещенияПоСсылке(СсылкаНаОбъект);
		Если МодульУправленияПроцессамиУХ.ОбязателенКомментарий(СправочникМетаданного) И СокрЛП(Значение) = "" Тогда			
			СтруктураДополнительныеПараметры = Новый Структура;
			СтруктураДополнительныеПараметры.Вставить("СсылкаНаОбъект", СсылкаНаОбъект);
			СтруктураДополнительныеПараметры.Вставить("Форма", ДополнительныеПараметры.Форма);
			Оповещение = Новый ОписаниеОповещения("ПослеВводаПричиныОтменыСогласования", ЭтотОбъект, СтруктураДополнительныеПараметры);
			Подсказка = НСтр("ru = 'Причина отмены согласования'");
			ПоказатьВводСтроки(Оповещение, "", Подсказка, 0, Истина);

			ТекстСообщения = НСтр("ru = 'Укажите причину отмены согласования'");
			ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);

			Возврат;
		КонецЕсли;

		Если СсылкаНаОбъект <> Неопределено Тогда
			МодульУправленияПроцессамиУХ.ОтменитьСогласование(, СсылкаНаОбъект, , Значение);
			Оповестить("ОбъектОтклонен");
			Форма = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "Форма", Неопределено);
			Если Форма <> Неопределено Тогда
				Форма.Прочитать();
			Иначе
				// Форма не получена, не обновляем её.
			КонецЕсли;
#Вставка			
			Оповестить("ИИТ_ЗакрытьФормуПриСогласовании", Новый Структура("Заявка", СсылкаНаОбъект));
#КонецВставки
		Иначе
			ТекстСообщения = НСтр("ru = 'Не удалось определить объект согласования. Операция отменена.'");
			ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
		КонецЕсли;
	Иначе
		// Пользователь отказался. Ничего не делаем.
	КонецЕсли;
КонецПроцедуры

&ИзменениеИКонтроль("МаршрутСогласования")
Процедура АЛФ_МаршрутСогласования(Форма, Ссылка)

	Если НЕ Форма.Элементы.Найти("Список") = Неопределено
		И Форма.ИмяФормы <> "Документ.ВерсияСоглашенияКоммерческийДоговор.Форма.ФормаДокумента" Тогда

		Если Форма.Элементы.Список.ТекущаяСтрока=Неопределено Тогда

			Возврат

		Иначе
			Если Форма.Элементы.Список.ВыделенныеСтроки.Количество() = 1 Тогда
				ОбъектСсылка=Форма.Элементы.Список.ТекущаяСтрока;
				ОбъектСсылка = ПереопределениеОбъектовСогласования(ОбъектСсылка);
				Согласующий=МодульУправленияПроцессамиУХ.ПолучитьОтветственногоЗаТипОбъекта(ОбъектСсылка,,,,"Согласующий",Истина);
			Иначе
				ТекстСообщения = НСтр("ru = 'Требуется выбрать один объект согласования. Операция отменена.'");
				ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
				Возврат;
			КонецЕсли;
		КонецЕсли;

	Иначе

		ОбъектСсылка=Ссылка;
		Согласующий = МодульУправленияПроцессамиУХ.ПолучитьОтветственногоЗаТипОбъекта(Ссылка,,,,"Согласующий",Истина);		// Чтобы избежать проблемы неактуальных кэшей, будем получать согласующего на ходу.

	КонецЕсли;

	Если НЕ Согласующий = Неопределено Тогда
		Если Не ТипЗнч(Согласующий) = Тип("СправочникСсылка.ШаблоныУниверсальныхПроцессов") Тогда	
			Согласующий = ПредопределенноеЗначение("Справочник.ШаблоныУниверсальныхПроцессов.Автоутверждение");	
		КонецЕсли;	
	КонецЕсли;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СогласуемыйОбъект", ОбъектСсылка);
	ПараметрыФормы.Вставить("ШаблонПроцесса", Согласующий);
	ПараметрыФормы.Вставить("Режим", "УправлениеСогласованием");
#Удаление
	ОткрытьФорму("Обработка.КонсольУправленияПроцессом.Форма.Форма", ПараметрыФормы);
#КонецУдаления
#Вставка
	ОткрытьФорму("Обработка.КонсольУправленияПроцессом.Форма.ИИТ_ФормаСпискаСогласующих", ПараметрыФормы);
#КонецВставки

КонецПроцедуры
