
&ИзменениеИКонтроль("ОбновитьРолиПользователей")
Процедура АЛФ_ОбновитьРолиПользователей(Знач ОписаниеПользователей, Знач ПарольПользователяСервиса, ЕстьИзменения)

	Если НЕ ПользователиСлужебный.ЗапретРедактированияРолей() Тогда
		// Роли устанавливаются механизмами подсистем Пользователи и ВнешниеПользователи.
		Возврат;
	КонецЕсли;

	Если ОписаниеПользователей = Неопределено Тогда
		МассивПользователей = Неопределено;
		Пользователи.НайтиНеоднозначныхПользователейИБ(Неопределено);

	ИначеЕсли ТипЗнч(ОписаниеПользователей) = Тип("Массив") Тогда
		МассивПользователей = ОписаниеПользователей;
		Если МассивПользователей.Количество() = 0 Тогда
			Возврат;
		ИначеЕсли МассивПользователей.Количество() = 1 Тогда
			Пользователи.НайтиНеоднозначныхПользователейИБ(МассивПользователей[0]);
		Иначе
			Пользователи.НайтиНеоднозначныхПользователейИБ(Неопределено);
		КонецЕсли;

	ИначеЕсли ТипЗнч(ОписаниеПользователей) = Тип("Тип") Тогда
		МассивПользователей = ОписаниеПользователей;
		Пользователи.НайтиНеоднозначныхПользователейИБ(Неопределено);
	Иначе
		МассивПользователей = Новый Массив;
		МассивПользователей.Добавить(ОписаниеПользователей);
		Пользователи.НайтиНеоднозначныхПользователейИБ(ОписаниеПользователей);
	КонецЕсли;

	УстановитьПривилегированныйРежим(Истина);

	ТекущиеСвойстваПользователей = ТекущиеСвойстваПользователей(МассивПользователей);

	// Параметры проверки в цикле.
	ВсеРоли                             = ПользователиСлужебный.ВсеРоли().Соответствие;
	ИдентификаторыПользователейИБ       = ТекущиеСвойстваПользователей.ИдентификаторыПользователейИБ;
	НовыеРолиПользователей              = ТекущиеСвойстваПользователей.РолиПользователей;
	ИдентификаторыРолей                 = ТекущиеСвойстваПользователей.ИдентификаторыРолей;
	ИменаРолей                          = ТекущиеСвойстваПользователей.ИменаРолей;
	ОбязательныеРолиАдминистратора      = ТекущиеСвойстваПользователей.ОбязательныеРолиАдминистратора;
	ДополнительныеРолиАдминистратора    = ТекущиеСвойстваПользователей.ДополнительныеРолиАдминистратора;
	Администраторы                      = ТекущиеСвойстваПользователей.Администраторы;
	РазделениеВключено                  = ОбщегоНазначения.РазделениеВключено();
	НеобходимоОбновлениеИБ              = ОбновлениеИнформационнойБазы.НеобходимоОбновлениеИнформационнойБазы();
	ИдентификаторТекущегоПользователяИБ = ПользователиИнформационнойБазы.ТекущийПользователь().УникальныйИдентификатор;

	// Будущий итог после цикла.
	НовыеАдминистраторыИБ     = Новый Соответствие;
	ОбновляемыеПользователиИБ = Новый Соответствие;
	НекорректныеРоли          = НовыеНекорректныеРоли(НовыеРолиПользователей);

	Для Каждого ОписаниеПользователя Из ИдентификаторыПользователейИБ Цикл

		ТекущийПользователь         = ОписаниеПользователя.Пользователь;
		ИдентификаторПользователяИБ = ОписаниеПользователя.ИдентификаторПользователяИБ;
		НовыйАдминистраторИБ        = Ложь;

		// Поиск пользователя ИБ.
		Если ТипЗнч(ИдентификаторПользователяИБ) = Тип("УникальныйИдентификатор") Тогда
			ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
			ИдентификаторПользователяИБ);
		Иначе
			ПользовательИБ = Неопределено;
		КонецЕсли;

		Если ПользовательИБ = Неопределено
			Или Не ЗначениеЗаполнено(ПользовательИБ.Имя) Тогда
			Продолжить;
		КонецЕсли;

		Если НеобходимоОбновлениеИБ
			И ИдентификаторПользователяИБ = ИдентификаторТекущегоПользователяИБ Тогда
			Продолжить;
		КонецЕсли;

		Отказ = Ложь;
		ИнтеграцияПодсистемБСП.ПриОбновленииРолейПользователяИБ(ИдентификаторПользователяИБ, Отказ);
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;

		Отбор = Новый Структура("Пользователь", ТекущийПользователь);
		НовыеРоли = НовыеРолиПользователей.Скопировать(
		НовыеРолиПользователей.НайтиСтроки(Отбор), "Роль, РольСсылка");

		НовыеРоли.Индексы.Добавить("РольСсылка");

#Удаление
		Если Администраторы[ТекущийПользователь] <> Неопределено Тогда
			ТекущиеНовыеРоли = НовыеРоли;
			НовыеРоли = ТекущиеНовыеРоли.Скопировать(Новый Массив);
			Для Каждого КлючИЗначение Из ОбязательныеРолиАдминистратора Цикл
				НовыеРоли.Добавить().РольСсылка = КлючИЗначение.Значение;
			КонецЦикла;
			Для Каждого КлючИЗначение Из ДополнительныеРолиАдминистратора Цикл
				Если ТекущиеНовыеРоли.Найти(КлючИЗначение.Значение, "РольСсылка") = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				НовыеРоли.Добавить().РольСсылка = КлючИЗначение.Значение;
			КонецЦикла;
		КонецЕсли;
#КонецУдаления		

		НедоступныеРоли = ПользователиСлужебный.НедоступныеРолиПоТипуПользователя(
		ТипЗнч(ТекущийПользователь) = Тип("СправочникСсылка.ВнешниеПользователи"));

		// Проверка старых ролей.
		РолиДляДобавления = Новый Соответствие;
		РолиДляУдаления   = Новый Соответствие;

		Для Каждого Роль Из ПользовательИБ.Роли Цикл
			ИмяРоли = Роль.Имя;
			РольСсылка = ИдентификаторыРолей.Получить(ИмяРоли);
			Если РольСсылка = Неопределено Тогда
				КлючРоли = Справочники.ИдентификаторыОбъектовМетаданных.КлючОбъектаМетаданныхРоль(Роль);
				РольСсылка = ИдентификаторыРолей.Получить(КлючРоли);
			КонецЕсли;
			Строка = НовыеРоли.Найти(РольСсылка, "РольСсылка");
			Если Строка = Неопределено Тогда
				РолиДляУдаления.Вставить(ИмяРоли, Роль);
			Иначе
				Если РазделениеВключено И НедоступныеРоли.Получить(ИмяРоли) <> Неопределено Тогда
					Строка.Роль = ИмяРоли;
					ДобавитьНекорректнуюРоль(НекорректныеРоли, Строка, ТекущийПользователь, Ложь);
					РолиДляУдаления.Вставить(ИмяРоли, Роль);
				КонецЕсли;
				НовыеРоли.Удалить(Строка);
			КонецЕсли;
		КонецЦикла;

		// Проверка новых ролей.
		Для Каждого Строка Из НовыеРоли Цикл
			Строка.Роль = ИменаРолей.Получить(Строка.РольСсылка);

			Если ВсеРоли.Получить(Строка.Роль) = Неопределено Тогда
				ДобавитьНекорректнуюРоль(НекорректныеРоли, Строка, ТекущийПользователь, Истина);
				Продолжить;
			КонецЕсли;

			Если НедоступныеРоли.Получить(Строка.Роль) <> Неопределено Тогда
				ДобавитьНекорректнуюРоль(НекорректныеРоли, Строка, ТекущийПользователь, Ложь);
				Продолжить;
			КонецЕсли;

			РолиДляДобавления.Вставить(Строка.Роль, Истина);

			Если Строка.Роль = "АдминистраторСистемы" Тогда
				НовыйАдминистраторИБ = Истина;
			КонецЕсли;
		КонецЦикла;

		// Завершение обработки текущего пользователя.
		Если РолиДляДобавления.Количество() = 0
			И РолиДляУдаления.Количество()   = 0 Тогда
			Продолжить;
		КонецЕсли;

		ИзмененияРолей = Новый Структура;
		ИзмененияРолей.Вставить("ПользовательСсылка", ТекущийПользователь);
		ИзмененияРолей.Вставить("ПользовательИБ",     ПользовательИБ);
		ИзмененияРолей.Вставить("РолиДляДобавления",  РолиДляДобавления);
		ИзмененияРолей.Вставить("РолиДляУдаления",    РолиДляУдаления);

		Если НовыйАдминистраторИБ Тогда
			НовыеАдминистраторыИБ.Вставить(ТекущийПользователь, ИзмененияРолей);
		Иначе
			ОбновляемыеПользователиИБ.Вставить(ТекущийПользователь, ИзмененияРолей);
		КонецЕсли;

		ЕстьИзменения = Истина;
	КонецЦикла;

	ЗарегистрироватьНекорректныеРоли(НекорректныеРоли);

	// Добавление новых администраторов.
	Если НовыеАдминистраторыИБ.Количество() > 0 Тогда
		ОбновитьРолиПользователейИБ(НовыеАдминистраторыИБ, ПарольПользователяСервиса);
	КонецЕсли;

	// Удаление старых администраторов и обновление остальных пользователей.
	Если ОбновляемыеПользователиИБ.Количество() > 0 Тогда
		ОбновитьРолиПользователейИБ(ОбновляемыеПользователиИБ, ПарольПользователяСервиса);
	КонецЕсли;

	ОтключитьУВсехРасширенийФлажокИспользоватьОсновныеРолиДляВсехПользователей();

КонецПроцедуры
