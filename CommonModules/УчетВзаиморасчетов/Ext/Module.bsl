
&ИзменениеИКонтроль("СчетаРасчетовПоОтгрузке")
Функция АЛФ_СчетаРасчетовПоОтгрузке(Реквизиты)

	СчетаРасчетов = Новый Структура("СчетРасчетов,СчетАвансов");
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();

	Параметры = Новый Структура();
	Параметры.Вставить("РасчетыВУсловныхЕдиницах", Ложь);
	Параметры.Вставить("ВалютаВзаиморасчетов", ВалютаРегламентированногоУчета);

	ЗаполнитьЗначенияСвойств(Параметры, Реквизиты);
#Вставка
	Если Параметры.РасчетыВУсловныхЕдиницах = Null Тогда 
		Параметры.Вставить("РасчетыВУсловныхЕдиницах", Ложь);
	КонецЕсли;
	Если  Параметры.ВалютаВзаиморасчетов = Null Тогда
		Параметры.Вставить("ВалютаВзаиморасчетов", ВалютаРегламентированногоУчета);
	КонецЕсли;
#КонецВставки 
	Если Параметры.РасчетыВУсловныхЕдиницах Тогда
		СчетаРасчетов.СчетРасчетов = ПланыСчетов.Хозрасчетный.НДСРасчетыПоОтгрузкеУЕ;
		СчетаРасчетов.СчетАвансов  = ПланыСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымВСчетОтгрузкиУЕ;
	ИначеЕсли Параметры.ВалютаВзаиморасчетов <> ВалютаРегламентированногоУчета Тогда
		СчетаРасчетов.СчетРасчетов = ПланыСчетов.Хозрасчетный.НДСРасчетыПоОтгрузкеВал;
		СчетаРасчетов.СчетАвансов  = ПланыСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымВСчетОтгрузкиВал;
	Иначе
		СчетаРасчетов.СчетРасчетов = ПланыСчетов.Хозрасчетный.НДСРасчетыПоОтгрузкеРуб;
		СчетаРасчетов.СчетАвансов  = ПланыСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымВСчетОтгрузкиРуб;
	КонецЕсли;

	Возврат СчетаРасчетов;

КонецФункции

//&ИзменениеИКонтроль("ДобавитьСтрокиПогашенияЗадолженности")
Процедура АЛФ_ДобавитьСтрокиПогашенияЗадолженности(ТаблицаВзаиморасчетов, НераспределеннаяСумма, НераспределеннаяСуммаБУ, НераспределеннаяСуммаПлатежа, КратностьВзаиморасчетов, ОстаткиЗадолженности, СтрокаПлатежа, Реквизиты, Отказ)
	#Область УХ_Интеграция
	//Процедура ДобавитьСтрокиПогашенияЗадолженности(ТаблицаВзаиморасчетов, НераспределеннаяСумма, НераспределеннаяСуммаБУ, КратностьВзаиморасчетов, ОстаткиЗадолженности, СтрокаПлатежа, Реквизиты, Отказ)
	#КонецОбласти

#Вставка     
	//+ИИТ_Аксеньюшкин
	КратностьВзаиморасчетов = ?(ЗначениеЗаполнено(КратностьВзаиморасчетов), КратностьВзаиморасчетов, 1);
	СтрокаПлатежа.РасчетыВУсловныхЕдиницахВалюта = ?(ЗначениеЗаполнено(СтрокаПлатежа.РасчетыВУсловныхЕдиницахВалюта), СтрокаПлатежа.РасчетыВУсловныхЕдиницахВалюта, Ложь); 
	СтрокаПлатежа.РасчетыВУсловныхЕдиницах = ?(ЗначениеЗаполнено(СтрокаПлатежа.РасчетыВУсловныхЕдиницах), СтрокаПлатежа.РасчетыВУсловныхЕдиницах, Ложь); 
	//-ИИТ_Аксеньюшкин
#КонецВставки
	Если СтрокаПлатежа.СпособПогашенияЗадолженности = Перечисления.СпособыПогашенияЗадолженности.НеПогашать Тогда
		Возврат;
	КонецЕсли;

	РасчетыВВалюте           = СтрокаПлатежа.ВалютаВзаиморасчетов <> Константы.ВалютаРегламентированногоУчета.Получить();
	РасчетыВУсловныхЕдиницах = СтрокаПлатежа.РасчетыВУсловныхЕдиницах;  
	#Область УХ_Встраивание
	РасчетыВУсловныхЕдиницах = РасчетыВУсловныхЕдиницах 
	ИЛИ СтрокаПлатежа.РасчетыВУсловныхЕдиницахВалюта
	ИЛИ Не СтрокаПлатежа.ВалютаВзаиморасчетов = Реквизиты.ВалютаДокумента;
	#КонецОбласти

	Регистратор              = Реквизиты.Регистратор;

	ВедетсяУчетПоПодразделениям = БухгалтерскийУчетПереопределяемый.ВестиУчетПоПодразделениям();

	НаСчетеВедетсяУчетПоДокументам = БухгалтерскийУчет.НаСчетеВедетсяУчетПоДокументамРасчетов(СтрокаПлатежа.СчетРасчетов);

	ОстаткиЗадолженностиСОтбором = ОстаткиЗадолженности.СкопироватьКолонки();
	Отбор = Новый Структура("Контрагент, ДоговорКонтрагента");
	Если НаСчетеВедетсяУчетПоДокументам
		И СтрокаПлатежа.СпособПогашенияЗадолженности = Перечисления.СпособыПогашенияЗадолженности.ПоДокументу Тогда
		Отбор.Вставить("ДокументРасчетов");
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(Отбор, СтрокаПлатежа);
	МассивСтрокОтбор = ОстаткиЗадолженности.НайтиСтроки(Отбор);
	Для Каждого СтрокаТаблицы Из МассивСтрокОтбор Цикл
		Если ЭтоСчетРасчетовПоОтгрузке(СтрокаТаблицы.СчетРасчетов)
			ИЛИ СтрокаТаблицы.СчетРасчетов = СтрокаПлатежа.СчетРасчетов Тогда
			СтрокаСОтбором = ОстаткиЗадолженностиСОтбором.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаСОтбором, СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;

	Если НаСчетеВедетсяУчетПоДокументам Тогда
		ОстаткиЗадолженностиСОтбором.Сортировать("ДатаДокументаРасчетов, ДокументРасчетов", Новый СравнениеЗначений);
	Иначе
		ОстаткиЗадолженностиСОтбором.Сортировать("НомерСтроки");
	КонецЕсли;

	// С 01.01.2015 в налоговом учете упразднены "Суммовые разницы" для активов и обязательств, выраженных в
	// иностранной валюте, оплата по которым предусмотренна в рублях (у.е.) и принятых к учету после 01.01.2015.
	// Для этих объектов учета с 01.01.2015 действует порядок переоценки, аналогичный порядку переоценки активов и обязательств,
	// выраженных в иностранной валюте и оплата которых предусмотрена также в иностранной валюте.
	// Для активов и обязательств, принятых к учету до 01.01.2015 действует старый порядок.

	ДатаОтменыСуммовыхРазниц = '20150101';
	Для каждого СтрокаОстатка Из ОстаткиЗадолженностиСОтбором Цикл

		Если НераспределеннаяСумма <= 0 Тогда
			Прервать;
		КонецЕсли;

		ПогашеннаяСумма = Мин(НераспределеннаяСумма, СтрокаОстатка.СуммаВзаиморасчетов);
		Если ПогашеннаяСумма <= 0 Тогда
			Продолжить;
		КонецЕсли;

		Если ВедетсяУчетПоПодразделениям И Не НаСчетеВедетсяУчетПоДокументам
			И СтрокаПлатежа.СпособПогашенияЗадолженности = Перечисления.СпособыПогашенияЗадолженности.ПоДокументу
			И ЗначениеЗаполнено(СтрокаПлатежа.ДокументРасчетов) Тогда

			МетаданныеДокументаРасчетов = СтрокаПлатежа.ДокументРасчетов.Метаданные();
			Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ПодразделениеОрганизации", МетаданныеДокументаРасчетов) Тогда
				ПодразделениеДокументаРасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				СтрокаПлатежа.ДокументРасчетов, "ПодразделениеОрганизации");
				Если СтрокаОстатка.Подразделение <> ПодразделениеДокументаРасчетов Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;

		КонецЕсли;

		ЭтоПолноеПогашение = СтрокаОстатка.СуммаВзаиморасчетов = ПогашеннаяСумма;

		СтрокаВзаиморасчетов = ТаблицаВзаиморасчетов.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаВзаиморасчетов, СтрокаПлатежа);
		ЗаполнитьЗначенияСвойств(СтрокаВзаиморасчетов, СтрокаОстатка);

		Если Не НаСчетеВедетсяУчетПоДокументам
			И СтрокаПлатежа.СпособПогашенияЗадолженности = Перечисления.СпособыПогашенияЗадолженности.ПоДокументу Тогда
			СтрокаВзаиморасчетов.ДокументРасчетов = СтрокаПлатежа.ДокументРасчетов;

			// Получим оплачиваемый платежной картой документ.
			Если ТипЗнч(СтрокаПлатежа.ДокументРасчетов) = Тип("ДокументСсылка.ОплатаПлатежнойКартой") Тогда
				РеквизитыОплатыПлатежнойКартой =
				ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаПлатежа.ДокументРасчетов, "ВидОперации, РасшифровкаПлатежа");
				РасшифровкаПлатежаОплатыПлатежнойКартой = РеквизитыОплатыПлатежнойКартой.РасшифровкаПлатежа.Выгрузить();
				МассивСделок = ОбщегоНазначения.ВыгрузитьКолонку(РасшифровкаПлатежаОплатыПлатежнойКартой, "Сделка", Истина);
				Если РеквизитыОплатыПлатежнойКартой.ВидОперации = Перечисления.ВидыОперацийОплатаПлатежнойКартой.ОплатаПокупателя
					И МассивСделок.Количество() > 0 И ЗначениеЗаполнено(МассивСделок[0]) Тогда
					СтрокаВзаиморасчетов.ДокументРасчетов = МассивСделок[0];
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		КурсВзаиморасчетов = ?(НераспределеннаяСумма = 0, 0, (НераспределеннаяСуммаБУ / НераспределеннаяСумма) * КратностьВзаиморасчетов);
		// Автоматически рассчитываемый в авансовом отчете курс не нужно округлять.
		Если ТипЗнч(Регистратор) <> Тип("ДокументСсылка.АвансовыйОтчет") Тогда
			КурсВзаиморасчетов = Окр(КурсВзаиморасчетов, 4);
		КонецЕсли;

		КоэффициентРуб = ?(НЕ РасчетыВВалюте, 1,
		?(СтрокаОстатка.СуммаВзаиморасчетов = 0, 0, СтрокаПлатежа.СуммаРуб / СтрокаПлатежа.СуммаВзаиморасчетов));

		СтрокаВзаиморасчетов.СуммаВзаиморасчетов = ПогашеннаяСумма;
		СтрокаВзаиморасчетов.СуммаРуб            = Окр(ПогашеннаяСумма * КоэффициентРуб, 2);

		#Область УХ_Интеграция
		КоэффициентПлатежаРуб = ?(НЕ (РасчетыВВалюте И (РасчетыВУсловныхЕдиницах)), 1,
		?(СтрокаОстатка.СуммаВзаиморасчетов = 0, 0, СтрокаПлатежа.СуммаПлатежа / СтрокаПлатежа.СуммаВзаиморасчетов));

		ПогашеннаяСуммаПлатежа = Мин(НераспределеннаяСуммаПлатежа, Окр(СтрокаОстатка.СуммаВзаиморасчетов * КоэффициентПлатежаРуб, 2));

		СтрокаВзаиморасчетов.СуммаПлатежа = ПогашеннаяСуммаПлатежа; 
		#КонецОбласти

		Если РасчетыВВалюте Тогда

			// Погашение долга в валюте производится в БУ и НУ по курсу тек.документа
			// Погашение долга в условных единицах производятся в БУ по курсу тек.документа, в НУ формируется суммовая разница,
			// 	для ПБУ 18 образуется постоянная разница, равная суммовой.

			// Возвраты в валюте производятся в БУ по курсу остатка, в НУ по курсу тек.документа, для ПБУ 18 образуется постоянная разница.
			// Возвраты в условных единицах производятся в БУ и НУ только по курсу остатка,
			// 	если курс остатка БУ отличается от курса тек.документа - сообщается об ошибке и документ возврата не проводится.

			Если РасчетыВУсловныхЕдиницах И (ПогашеннаяСумма = НераспределеннаяСумма) Тогда
				СуммаПоКурсуДокумента = НераспределеннаяСуммаБУ;
				// Погашаемая сумма (в условных единицах) в этом случае - расчетная, без округления до 2 знаков
				// Это помогает обойти ошибки округления при расчете сумм по курсу остатка
				ПогашаемаяСуммаВзаиморасчетов = НераспределеннаяСуммаБУ * КратностьВзаиморасчетов / КурсВзаиморасчетов;
			Иначе
				СуммаПоКурсуДокумента = Окр(ПогашеннаяСумма * КурсВзаиморасчетов / КратностьВзаиморасчетов, 2);
				ПогашаемаяСуммаВзаиморасчетов = ПогашеннаяСумма;
			КонецЕсли;

			КурсОстаткаБУ = Окр(?(СтрокаОстатка.СуммаВзаиморасчетов = 0, 0, (СтрокаОстатка.СуммаБУ / СтрокаОстатка.СуммаВзаиморасчетов) * КратностьВзаиморасчетов), 4);
			КурсОстаткаНУ = Окр(?(СтрокаОстатка.СуммаВзаиморасчетов = 0, 0, (СтрокаОстатка.СуммаНУ / СтрокаОстатка.СуммаВзаиморасчетов) * КратностьВзаиморасчетов), 4);

			СуммаБУпоКурсуОстатка = Окр(?(КратностьВзаиморасчетов = 0, 0, ПогашеннаяСумма * КурсОстаткаБУ / КратностьВзаиморасчетов), 2);
			СуммаНУпоКурсуОстатка = Окр(?(КратностьВзаиморасчетов = 0, 0, ПогашеннаяСумма * КурсОстаткаНУ / КратностьВзаиморасчетов), 2);

			Если Реквизиты.ЭтоВозврат Тогда

				// Возвраты предоплаты в валюте и в условных единицах производятся в БУ и НУ по курсу предоплаты
				СтрокаВзаиморасчетов.СуммаБУ = СуммаБУпоКурсуОстатка;
				СтрокаВзаиморасчетов.СуммаНУ = СуммаБУпоКурсуОстатка;

				Если РасчетыВУсловныхЕдиницах Тогда

					Если ЭтоПолноеПогашение Тогда
						КурсОстаткаОтличаетсяОтКурсаДокумента = КурсОстаткаБУ <> КурсВзаиморасчетов;
					Иначе
						КурсОстаткаОтличаетсяОтКурсаДокумента = СуммаПоКурсуДокумента <> СуммаБУпоКурсуОстатка;
					КонецЕсли;
					#Область УХ_Интеграция
					Если НЕ ЭтоПолноеПогашение Тогда
						КурсОстаткаОтличаетсяОтКурсаДокумента = Ложь;									
					КонецЕсли;
					#КонецОбласти

					Если КурсОстаткаОтличаетсяОтКурсаДокумента Тогда
						ТекстОшибки = НСтр("ru = 'Рублевая сумма возврата по договору в условных единицах отличается от суммы по курсу остатка долга.
						|	Договор: ""%1"", документ расчетов: ""%2"":
						|	Сумма возврата: %3 %4, по курсу остатка: %5 руб.
						|	Возврат по документу: %6 руб.
						|	Возвраты по договору в условных единицах должны производиться по курсу остатка долга!'");
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ТекстОшибки,
						СтрокаОстатка.ДоговорКонтрагента,
						СтрокаОстатка.ДокументРасчетов,
						ПогашеннаяСумма,
						СтрокаПлатежа.ВалютаВзаиморасчетов,
						СуммаБУпоКурсуОстатка,
						СуммаПоКурсуДокумента);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Регистратор, , , Отказ);
					КонецЕсли;

					//Скорректируем суммы в проводках из-за ошибок округления
					//Если мы зачитываем весь остаток по строке, то и суммы НУ в проводку должны пойти все,
					//т.к. этот счет и договор вообще не переоценивается по НУ, 
					//а вот в БУ должны пойти именно те суммы, которые указаны в документа
					Если ЭтоПолноеПогашение Тогда

						СтрокаВзаиморасчетов.СуммаБУ = СуммаПоКурсуДокумента;
						СтрокаВзаиморасчетов.СуммаНУ = СтрокаОстатка.СуммаНУ;

					КонецЕсли;

				Иначе
					// При расчетах в валюте дополнительно передается отдельная сумма
					// для "обратной" переоценки в НУ возвращаемой суммы до курса поступления предоплаты
					СтрокаВзаиморасчетов.Разница = СуммаПоКурсуДокумента - СуммаБУпоКурсуОстатка;

				КонецЕсли;

			Иначе

				// Погашение долга в валюте и в условных единицах производятся в БУ и НУ по курсу тек.документа.
				СтрокаВзаиморасчетов.СуммаБУ = СуммаПоКурсуДокумента;
				// При расчетах в у.е дополнительно передается отдельная сумма для формирования в НУ суммовой разницы
				Если РасчетыВУсловныхЕдиницах и (СтрокаОстатка.ДатаДокументаРасчетов < ДатаОтменыСуммовыхРазниц) Тогда
					СтрокаВзаиморасчетов.СуммаНУ = СуммаНУпоКурсуОстатка;
					СтрокаВзаиморасчетов.Разница = СуммаПоКурсуДокумента - СуммаНУпоКурсуОстатка;
				Иначе
					СтрокаВзаиморасчетов.СуммаНУ = СуммаПоКурсуДокумента;
				КонецЕсли;
				Если ЭтоСчетРасчетовПоОтгрузке(СтрокаОстатка.СчетРасчетов) Тогда
					СтрокаВзаиморасчетов.СчетАвансов_Отгрузка = СтрокаПлатежа.СчетАвансов;
					Если ЭтоПолноеПогашение Тогда
						СтрокаВзаиморасчетов.СуммаБУ_Отгрузка = СтрокаОстатка.СуммаБУ;
					Иначе
						СтрокаВзаиморасчетов.СуммаБУ_Отгрузка = СуммаБУпоКурсуОстатка;
					КонецЕсли;
				КонецЕсли;

			КонецЕсли;

		Иначе
			// Расчеты в рублях
			СтрокаВзаиморасчетов.СуммаБУ = ПогашеннаяСумма;
			СтрокаВзаиморасчетов.СуммаНУ = ПогашеннаяСумма;

			Если ЭтоСчетРасчетовПоОтгрузке(СтрокаОстатка.СчетРасчетов) Тогда
				СтрокаВзаиморасчетов.СчетАвансов_Отгрузка = СтрокаПлатежа.СчетАвансов;
				СтрокаВзаиморасчетов.СуммаБУ_Отгрузка = ПогашеннаяСумма;
			КонецЕсли;

		КонецЕсли;

		Если Реквизиты.УчитыватьЗадолженностьУСН ИЛИ Реквизиты.УчитыватьЗадолженностьУСНПатент Тогда

			// Ограничим сумму по счетам УСн, чтобы не получилось, что по счетам УСН
			// сумма прошла больше, чем по балансовому счету.

			ВсегоСуммаВзаиморасчетовПоСчетамУСНПоСтроке = СтрокаОстатка.СуммаВзаиморасчетовЕНВД
			+ СтрокаОстатка.СуммаВзаиморасчетовПатент
			+ СтрокаОстатка.СуммаВзаиморасчетовКомитента
			+ СтрокаОстатка.СуммаВзаиморасчетовТорговыйСбор;

			Если ВсегоСуммаВзаиморасчетовПоСчетамУСНПоСтроке > 0 Тогда

				МаксимальнаяСуммаВзаиморасчетовДляУСН = Мин(ВсегоСуммаВзаиморасчетовПоСчетамУСНПоСтроке, СтрокаОстатка.СуммаВзаиморасчетов);

				СуммаВзаиморасчетовЕНВД      =
				МаксимальнаяСуммаВзаиморасчетовДляУСН * СтрокаОстатка.СуммаВзаиморасчетовЕНВД / ВсегоСуммаВзаиморасчетовПоСчетамУСНПоСтроке;
				СуммаВзаиморасчетовПатент    =
				МаксимальнаяСуммаВзаиморасчетовДляУСН * СтрокаОстатка.СуммаВзаиморасчетовПатент / ВсегоСуммаВзаиморасчетовПоСчетамУСНПоСтроке;
				СуммаВзаиморасчетовКомитента =
				МаксимальнаяСуммаВзаиморасчетовДляУСН * СтрокаОстатка.СуммаВзаиморасчетовКомитента / ВсегоСуммаВзаиморасчетовПоСчетамУСНПоСтроке;
				СуммаВзаиморасчетовТорговыйСбор    =
				МаксимальнаяСуммаВзаиморасчетовДляУСН * СтрокаОстатка.СуммаВзаиморасчетовТорговыйСбор / ВсегоСуммаВзаиморасчетовПоСчетамУСНПоСтроке;

				СтрокаВзаиморасчетов.СуммаВзаиморасчетовЕНВД = Окр(СуммаВзаиморасчетовЕНВД
				* СтрокаВзаиморасчетов.СуммаВзаиморасчетов / СтрокаОстатка.СуммаВзаиморасчетов, 2);

				СтрокаВзаиморасчетов.СуммаВзаиморасчетовПатент = Окр(СуммаВзаиморасчетовПатент
				* СтрокаВзаиморасчетов.СуммаВзаиморасчетов / СтрокаОстатка.СуммаВзаиморасчетов, 2);

				СтрокаВзаиморасчетов.СуммаВзаиморасчетовКомитента = Окр(СуммаВзаиморасчетовКомитента
				* СтрокаВзаиморасчетов.СуммаВзаиморасчетов / СтрокаОстатка.СуммаВзаиморасчетов, 2);

				СтрокаВзаиморасчетов.СуммаВзаиморасчетовТорговыйСбор = Окр(СуммаВзаиморасчетовТорговыйСбор
				* СтрокаВзаиморасчетов.СуммаВзаиморасчетов / СтрокаОстатка.СуммаВзаиморасчетов, 2);

				// Рублевые суммы расчетов по доходам, облагаемым в особом порядке, рассчитываем по курсу документа оплаты.

				Если РасчетыВВалюте И (СтрокаОстатка.СуммаВзаиморасчетовЕНВД <> 0) Тогда
					СтрокаВзаиморасчетов.СуммаБУ_ЕНВД = Окр(СтрокаВзаиморасчетов.СуммаВзаиморасчетовЕНВД * КоэффициентРуб, 2);
				Иначе
					СтрокаВзаиморасчетов.СуммаБУ_ЕНВД = СтрокаВзаиморасчетов.СуммаВзаиморасчетовЕНВД;
				КонецЕсли;

				Если РасчетыВВалюте И (СтрокаОстатка.СуммаВзаиморасчетовПатент <> 0) Тогда
					СтрокаВзаиморасчетов.СуммаБУ_Патент = Окр(СтрокаВзаиморасчетов.СуммаВзаиморасчетовПатент * КоэффициентРуб, 2);
				Иначе
					СтрокаВзаиморасчетов.СуммаБУ_Патент = СтрокаВзаиморасчетов.СуммаВзаиморасчетовПатент;
				КонецЕсли;

				Если РасчетыВВалюте И (СтрокаОстатка.СуммаВзаиморасчетовКомитента <> 0) Тогда
					СтрокаВзаиморасчетов.СуммаБУ_Комитента = Окр(СтрокаВзаиморасчетов.СуммаВзаиморасчетовКомитента * КоэффициентРуб, 2);
				Иначе
					СтрокаВзаиморасчетов.СуммаБУ_Комитента = СтрокаВзаиморасчетов.СуммаВзаиморасчетовКомитента;
				КонецЕсли;

				Если РасчетыВВалюте И (СтрокаОстатка.СуммаВзаиморасчетовТорговыйСбор <> 0) Тогда
					СтрокаВзаиморасчетов.СуммаБУ_ТорговыйСбор = Окр(СтрокаВзаиморасчетов.СуммаВзаиморасчетовТорговыйСбор * КоэффициентРуб, 2);
				Иначе
					СтрокаВзаиморасчетов.СуммаБУ_ТорговыйСбор = СтрокаВзаиморасчетов.СуммаВзаиморасчетовТорговыйСбор;
				КонецЕсли;

			КонецЕсли;

		КонецЕсли;

		НераспределеннаяСумма   = НераспределеннаяСумма   - СтрокаВзаиморасчетов.СуммаВзаиморасчетов;
		НераспределеннаяСуммаБУ = НераспределеннаяСуммаБУ - СтрокаВзаиморасчетов.СуммаБУ;

		#Область УХ_Интеграция
		НераспределеннаяСуммаПлатежа = НераспределеннаяСуммаПлатежа - СтрокаВзаиморасчетов.СуммаПлатежа;
		#КонецОбласти

		// Остатки уменьшаем в строке основной таблицы остатков.
		СтрокаОстаткаБезОтбора = ОстаткиЗадолженности.Найти(СтрокаОстатка.НомерСтроки, "НомерСтроки");

		СтрокаОстаткаБезОтбора.СуммаВзаиморасчетов = СтрокаОстаткаБезОтбора.СуммаВзаиморасчетов
		- СтрокаВзаиморасчетов.СуммаВзаиморасчетов;

		СтрокаОстаткаБезОтбора.СуммаБУ = СтрокаОстаткаБезОтбора.СуммаБУ - СтрокаВзаиморасчетов.СуммаБУ;
		СтрокаОстаткаБезОтбора.СуммаНУ = СтрокаОстаткаБезОтбора.СуммаНУ - СтрокаВзаиморасчетов.СуммаНУ;

		Если Реквизиты.УчитыватьЗадолженностьУСН ИЛИ Реквизиты.УчитыватьЗадолженностьУСНПатент Тогда

			СтрокаОстаткаБезОтбора.СуммаВзаиморасчетовЕНВД = СтрокаОстаткаБезОтбора.СуммаВзаиморасчетовЕНВД
			- СтрокаВзаиморасчетов.СуммаВзаиморасчетовЕНВД;
			СтрокаОстаткаБезОтбора.СуммаВзаиморасчетовПатент = СтрокаОстаткаБезОтбора.СуммаВзаиморасчетовПатент
			- СтрокаВзаиморасчетов.СуммаВзаиморасчетовПатент;
			СтрокаОстаткаБезОтбора.СуммаВзаиморасчетовКомитента = СтрокаОстаткаБезОтбора.СуммаВзаиморасчетовКомитента
			- СтрокаВзаиморасчетов.СуммаВзаиморасчетовКомитента;
			СтрокаОстаткаБезОтбора.СуммаВзаиморасчетовТорговыйСбор = СтрокаОстаткаБезОтбора.СуммаВзаиморасчетовТорговыйСбор
			- СтрокаВзаиморасчетов.СуммаВзаиморасчетовТорговыйСбор;

			// Доли оставшихся в остатках сумм - соотношение "стало/было".
			КоэффициентОстаткаЕНВД = ?(СтрокаОстатка.СуммаВзаиморасчетовЕНВД = 0, 0,
			СтрокаОстаткаБезОтбора.СуммаВзаиморасчетовЕНВД / СтрокаОстатка.СуммаВзаиморасчетовЕНВД);
			КоэффициентОстаткаПатент = ?(СтрокаОстатка.СуммаВзаиморасчетовПатент = 0, 0,
			СтрокаОстаткаБезОтбора.СуммаВзаиморасчетовПатент / СтрокаОстатка.СуммаВзаиморасчетовПатент);
			КоэффициентОстаткаКомитента = ?(СтрокаОстатка.СуммаВзаиморасчетовКомитента = 0, 0,
			СтрокаОстаткаБезОтбора.СуммаВзаиморасчетовКомитента / СтрокаОстатка.СуммаВзаиморасчетовКомитента);
			КоэффициентОстаткаТорговыйСбор = ?(СтрокаОстатка.СуммаВзаиморасчетовТорговыйСбор = 0, 0,
			СтрокаОстаткаБезОтбора.СуммаВзаиморасчетовТорговыйСбор / СтрокаОстатка.СуммаВзаиморасчетовТорговыйСбор);

			// Рублевые суммы расчетов по особым доходам рассчитываем по долям оставшихся валютных сумм.
			СтрокаОстаткаБезОтбора.СуммаБУ_ЕНВД = Окр(СтрокаОстаткаБезОтбора.СуммаБУ_ЕНВД * КоэффициентОстаткаЕНВД, 2);
			СтрокаОстаткаБезОтбора.СуммаБУ_Патент = Окр(СтрокаОстаткаБезОтбора.СуммаБУ_Патент * КоэффициентОстаткаПатент, 2);
			СтрокаОстаткаБезОтбора.СуммаБУ_Комитента = Окр(СтрокаОстаткаБезОтбора.СуммаБУ_Комитента * КоэффициентОстаткаКомитента, 2);
			СтрокаОстаткаБезОтбора.СуммаБУ_ТорговыйСбор = Окр(СтрокаОстаткаБезОтбора.СуммаБУ_ТорговыйСбор * КоэффициентОстаткаТорговыйСбор, 2);

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры
