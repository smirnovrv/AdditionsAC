
Функция ПолучитьДанныеАналитикиПоОбъектамСтроительства(ДоговорСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТабТекущегоДокумента.ЭтапСтроительства КАК ЭтапСтроительства,
	|	ТабТекущегоДокумента.ОбъектСтроительства КАК ОбъектСтроительства,
	|	ТабТекущегоДокумента.Сумма КАК Сумма,
	|	ТабТекущегоДокумента.Бюджет КАК Бюджет,
	|	ТабТекущегоДокумента.НеОбновлятьСтроку КАК НеОбновлятьСтроку
	|ПОМЕСТИТЬ ВТ_Документа
	|ИЗ
	|	&ТабТекущегоДокумента КАК ТабТекущегоДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ЕСТЬNULL(СписаниеСРасчетногоСчетаРасшифровкаПлатежа.СуммаПлатежа, 0)) КАК Сумма,
	|	СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Проект КАК ЭтапСтроительства
	|ПОМЕСТИТЬ ВТ_Оплаты
	|ИЗ
	|	Документ.СписаниеСРасчетногоСчета.РасшифровкаПлатежа КАК СписаниеСРасчетногоСчетаРасшифровкаПлатежа
	|ГДЕ
	|	СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка.Проведен
	|	И СписаниеСРасчетногоСчетаРасшифровкаПлатежа.ДокументПланирования.ДоговорКонтрагента = &ДоговорКонтрагента
	|	И НЕ СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Проект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Проект КАК ЭтапСтроительства,
	|	СУММА(ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Сумма) КАК СуммаЗаявок
	|ПОМЕСТИТЬ ВТ_Заявки
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств.ДвиженияОперации КАК ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации
	|ГДЕ
	|	ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Ссылка.Проведен
	|	И НЕ ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
	|	И ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Ссылка.ДоговорКонтрагента = &ДоговорКонтрагента
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Проект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Документа.ЭтапСтроительства КАК ЭтапСтроительства,
	|	ВТ_Документа.ОбъектСтроительства КАК ОбъектСтроительства,
	|	ВТ_Документа.Бюджет КАК Бюджет,
	|	ВТ_Документа.НеОбновлятьСтроку КАК НеОбновлятьСтроку,
	|	ВТ_Оплаты.Сумма КАК Сумма,
	|	ВТ_Заявки.СуммаЗаявок КАК СуммаЗаказов
	|ИЗ
	|	ВТ_Документа КАК ВТ_Документа
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Оплаты КАК ВТ_Оплаты
	|		ПО ВТ_Документа.ЭтапСтроительства = ВТ_Оплаты.ЭтапСтроительства
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Заявки КАК ВТ_Заявки
	|		ПО ВТ_Документа.ЭтапСтроительства = ВТ_Заявки.ЭтапСтроительства";
	
	Запрос.УстановитьПараметр("ДоговорКонтрагента"	, ДоговорСсылка); 
	Запрос.УстановитьПараметр("ТабТекущегоДокумента", ДоговорСсылка.ИИТ_АналитикаПоОбъектамСтроительства.Выгрузить());
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаРезультата; 
	
КонецФункции
