
&ИзменениеИКонтроль("ДоговорыКонтрагентовОбработкаПолученияФормы")
Процедура АЛФ_ДоговорыКонтрагентовОбработкаПолученияФормы(Источник, ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)

	Если ВидФормы <> "ФормаОбъекта" Тогда
		Возврат;
	КонецЕсли;

	СтандартнаяОбработка = Ложь;

	ВидДоговораУХ = Неопределено;

	Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда

		ТекущаяВерсияСоглашения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Ключ, "ВерсияСоглашения");

		Если ЗначениеЗаполнено(ТекущаяВерсияСоглашения) Тогда
			// Подобрали соответствующую версию соглашения.
			// Придется открыть служебную форму-прослойку, а уже из нее открывать нужный документ.
			// В противном случае можем получить проблемы с проверкой уникальности.
#Вставка
			//+ИИТ_Аксеньюшкин
			СтандартнаяОбработка = Истина;
			Возврат;
			//-ИИТ_Аксеньюшкин
#КонецВставки
			ВыбраннаяФорма = "ФормаОткрытияВерсииСоглашения";
			Параметры.Вставить("ВерсияСоглашения", ТекущаяВерсияСоглашения);
			Возврат;
		Иначе
			// открываем стандартную форму договора с гиперссылкой создания версии соглашения
			СтандартнаяОбработка = Истина;
			Возврат; 
		КонецЕсли;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ВидДоговораУХ) И Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		ПараметрыЗначенияКопирования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.ЗначениеКопирования, "ВидДоговораУХ,ВерсияСоглашения");
		ВидДоговораУХ = ПараметрыЗначенияКопирования.ВидДоговораУХ;
		// Подменим значение копирования.
		Параметры.ЗначениеКопирования = ПараметрыЗначенияКопирования.ВерсияСоглашения;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ВидДоговораУХ) И Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("ВидДоговораУХ") Тогда
		ВидДоговораУХ = Параметры.Отбор.ВидДоговораУХ;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ВидДоговораУХ) Тогда
		Если Параметры.Свойство("ЗначенияЗаполнения") 
			И ТипЗнч(Параметры.ЗначенияЗаполнения) = Тип("Структура") Тогда

			Если Параметры.ЗначенияЗаполнения.Свойство("ВстречныйДоговор") 
				И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.ВстречныйДоговор) Тогда			
				ВидДоговораУХ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ЗначенияЗаполнения.ВстречныйДоговор, "ВидДоговораУХ");
			ИначеЕсли Параметры.ЗначенияЗаполнения.Свойство("БазовыйДоговор") 
				И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.БазовыйДоговор) Тогда				
				ВидДоговораУХ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ЗначенияЗаполнения.БазовыйДоговор, "ВидДоговораУХ");
			ИначеЕсли Параметры.ЗначенияЗаполнения.Свойство("ВидДоговораУХ") Тогда
				ВидДоговораУХ = Параметры.ЗначенияЗаполнения.ВидДоговораУХ;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	ИмяСправочника = "СправочникСсылка.ВидыДоговоровКонтрагентовУХ";
	Если Не ЗначениеЗаполнено(ВидДоговораУХ) ИЛИ ТипЗнч(ВидДоговораУХ) <> Тип(ИмяСправочника) Тогда

		ВыбраннаяФорма = "ФормаВыбораВидаДоговора";
		// Добавим фильтр по виду справочника.
		Если Не Параметры.Свойство("Отбор") Тогда
			Параметры.Вставить("Отбор", Новый Структура);
		КонецЕсли;

		Параметры.Отбор.Вставить(
		"ТипСправочникаДоговора",
		Источник.ПустаяСсылка().Метаданные().Имя);

	Иначе

		МодульУправлениеДоговорамиУХВызовСервераПовтИсп = ОбщегоНазначения.ОбщийМодуль("УправлениеДоговорамиУХВызовСервераПовтИсп");//для БМ, НМ
		ИмяДокумента = МодульУправлениеДоговорамиУХВызовСервераПовтИсп.ПолучитьИмяДокументаПоВидуДоговора(ВидДоговораУХ);
		Если ЗначениеЗаполнено(ИмяДокумента) Тогда
			ВыбраннаяФорма = Метаданные.Документы[ИмяДокумента].ОсновнаяФормаОбъекта;
		Иначе
			ВызватьИсключение НСтр("ru = 'Невозможно определить тип версии соглашения.'");
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры
