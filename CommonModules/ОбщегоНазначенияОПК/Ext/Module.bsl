
&ИзменениеИКонтроль("ВыгрузитьРезультатыЗапроса")
Функция АЛФ_ВыгрузитьРезультатыЗапроса(Запрос, ТекстыЗапроса, ОбходРезультата, ДобавитьРазделитель, УничтожитьСозданныеВременныеТаблицы)

	Таблицы = Новый Структура;

	// Инициализация варианта обхода результата запроса.
	Если ОбходРезультата = Неопределено Тогда
		ОбходРезультата = ОбходРезультатаЗапроса.Прямой;
	КонецЕсли;

	ТекстУничтоженияВременныхТаблиц = "";

	ТекстИтоговогоЗапроса = "";

	// Формирование текст запроса.
	Для Каждого ТекстЗапроса Из ТекстыЗапроса Цикл
		Если ЗначениеЗаполнено(ТекстЗапроса.Представление) Тогда
			ТекстИтоговогоЗапроса = ТекстИтоговогоЗапроса 
			+ ?(ТекстИтоговогоЗапроса <> "", Символы.ПС, "")
			+ "// " + ТекстЗапроса.Представление + Символы.ПС;
		КонецЕсли; 
		ТекстИтоговогоЗапроса = ТекстИтоговогоЗапроса + ТекстЗапроса.Значение;

		Если ДобавитьРазделитель Тогда
			ТекстИтоговогоЗапроса = ТекстИтоговогоЗапроса + ТекстРазделителяЗапросовПакета();
		КонецЕсли;

		Если УничтожитьСозданныеВременныеТаблицы
			И ЗначениеЗаполнено(ТекстЗапроса.Представление) Тогда
			Если СтрНайти(ВРег(ТекстЗапроса.Значение), "ПОМЕСТИТЬ") <> 0 Тогда
				ТекстУничтоженияВременныхТаблиц = ТекстУничтоженияВременныхТаблиц + "
				|УНИЧТОЖИТЬ " + ТекстЗапроса.Представление;
				ТекстУничтоженияВременныхТаблиц = ТекстУничтоженияВременныхТаблиц + ТекстРазделителяЗапросовПакета();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	// Выполнение запроса.

	Если ЗначениеЗаполнено(ТекстУничтоженияВременныхТаблиц) Тогда
		ТекстИтоговогоЗапроса = ТекстИтоговогоЗапроса + ТекстУничтоженияВременныхТаблиц;
	КонецЕсли;

	Запрос.Текст = ТекстИтоговогоЗапроса;
#Вставка
	Запрос.УстановитьПараметр("ТолькоПланыПоПоставке", Ложь);
#КонецВставки
	Результат = Запрос.ВыполнитьПакет();

	// Помещение результатов запроса в таблицы
	Для Каждого ТекстЗапроса Из ТекстыЗапроса Цикл

		ИмяТаблицы = ТекстЗапроса.Представление;

		Если Не ПустаяСтрока(ИмяТаблицы) Тогда

			Индекс = ТекстыЗапроса.Индекс(ТекстЗапроса);
			Таблицы.Вставить(ИмяТаблицы, Результат[Индекс].Выгрузить(ОбходРезультата));

		КонецЕсли;

	КонецЦикла;

	Возврат Таблицы;

КонецФункции
