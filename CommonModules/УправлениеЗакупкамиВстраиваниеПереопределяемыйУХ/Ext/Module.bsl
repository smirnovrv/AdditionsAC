
&ИзменениеИКонтроль("ОбработкаУдаленияПроведенияДокументаИсполненияДоговора")
Процедура АЛФ_ОбработкаУдаленияПроведенияДокументаИсполненияДоговора(Источник, Отказ)
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	РегистрыСведений.ИсполнениеДоговоровСПоставщиками.ОчиститьПоДокументу(
	Источник.Ссылка);
	РегистрыСведений.ПоступлениеНоменклатуры.ОчиститьПоДокументу(
	Источник.Ссылка);    
#Удаление
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ЗаказПоставщику", Источник.Метаданные()) Тогда
		Если ЗначениеЗаполнено(Источник.ЗаказПоставщику) Тогда
			РегистрыСведений.СостоянияЗаказовПоставщикам.ОтразитьСостояниеЗаказа(Источник.ЗаказПоставщику, Отказ);   
		КонецЕсли;	
	КонецЕсли;
#КонецУдаления
КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаПроведенияПоступлениеТоваровУслуг")
Процедура АЛФ_ОбработкаПроведенияПоступлениеТоваровУслуг(ПТУОбъект, Отказ, РежимПроведения)

	Если ПТУОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если Справочники.ЭтапыРаздельнойЗакупки.ЭтоОрдер(ПТУОбъект.ЭтапРаздельнойЗакупки) Тогда
		Возврат;
	КонецЕсли;	
#Удаление
	ПоступлениеТоваровУслугЗаполнитьДвиженияПланПоставокПоДоговорам(
	ПТУОбъект, 
	Отказ, 
	РежимПроведения);  
#КонецУдаления
	#Область УХ_Встраивание
	Если ЗначениеЗаполнено(ПТУОбъект.ЗаказПоставщику) Тогда  
		ПТУОбъект.Движения.Записать();
		РегистрыСведений.СостоянияЗаказовПоставщикам.ОтразитьСостояниеЗаказа(ПТУОбъект.ЗаказПоставщику, Отказ);   
	КонецЕсли;	
	#КонецОбласти


КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаПроведенияЗаказПоставщику")
Процедура АЛФ_ОбработкаПроведенияЗаказПоставщику(Источник, Отказ, РежимПроведения)

	Если НЕ ЗначениеЗаполнено(Источник.ДатаПоступления) Тогда
		ТекстСообщения = НСтр("ru = 'Необходимо заполнить поле ""Общая дата поступления""'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		Возврат;
	КонецЕсли;   

	//ТипСтатьи = ПланированиеПотребностей.ИспользуемыеВидыБюджетовЗакупок(Источник.Дата);	
	//Если ПланированиеПотребностей.ИспользуемыеВидыБюджетовЗакупок(Источник.Дата) <> Неопределено Тогда
	ВидБюджетаБДДС = ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыБюджетов.БюджетДвиженияДенежныхСредств");
	ВидБюджетаБДР = ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыБюджетов.БюджетДоходовИРасходов");	

	БДДСОбязательна = КонтрольУХВызовСервера.ОбязательностьЗаполненияСтатейПриПланированииЗакупок(ВидБюджетаБДДС,Источник.ДатаПоступления);	
	БДРОбязательна = КонтрольУХВызовСервера.ОбязательностьЗаполненияСтатейПриПланированииЗакупок(ВидБюджетаБДР,Источник.ДатаПоступления);

	ПроверятьСоответсвиеСтатьямЗаявки = БДРОбязательна И ЗначениеЗаполнено(Источник.ДокументОснование) И 
	ТипЗнч(Источник.ДокументОснование) = Тип("ДокументСсылка.ЗаявкаНаРасход");
	Если ПроверятьСоответсвиеСтатьямЗаявки Тогда
		МассивСтатейЗаявкиНРасход = ПолучитьМассивСтатейЗаявкиНРасход(Источник.ДокументОснование); 		
	КонецЕсли;								

	Для Каждого Строка ИЗ Источник.Товары Цикл

		//Если НЕ ЗначениеЗаполнено(Строка.СтатьяБюджета) Тогда
		//	ТекстСообщения = НСтр("ru = 'Необходимо заполнить поле ""Статья бюджета"" в таблице по номенклатуре'");
		//	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		//	Возврат;
		//КонецЕсли;  
#Удаление
		Если НЕ ЗначениеЗаполнено(Строка.СтатьяБюджета) И БДДСОбязательна ТОгда
			ТекстСообщения = НСтр("ru = 'Необходимо заполнить поле ""Статья бюджета"" в таблице по номенклатуре'");		
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			Отказ = Истина;    
			Прервать;
		КонецЕсли;	
		Если НЕ ЗначениеЗаполнено(Строка.СтатьяБюджетаБДР) И БДРОбязательна ТОгда
			ТекстСообщения = НСтр("ru = 'Необходимо заполнить поле ""Статья бюджета БДР"" в таблице по номенклатуре'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			Отказ = Истина;
			Прервать;
		КонецЕсли;			
		Если НЕ ЗначениеЗаполнено(Строка.ЦФО) Тогда
			ТекстСообщения = НСтр("ru = 'Необходимо заполнить поле ""ЦФО"" в таблице по номенклатуре'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			Возврат;
		КонецЕсли;	

		Если ПроверятьСоответсвиеСтатьямЗаявки И ЗначениеЗаполнено(Строка.СтатьяБюджетаБДР) И
			МассивСтатейЗаявкиНРасход.Найти(Строка.СтатьяБюджетаБДР) = Неопределено Тогда
			ТекстСообщения = НСтр("ru = 'Статья бюджета БДР ""%1"" в таблице по номенклатуре не соответствует статьям БДР из документа-основания'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Строка.СтатьяБюджетаБДР);

			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			Возврат;
		КонецЕсли;	
 #КонецУдаления
	КонецЦикла;   
	//КонецЕсли;

	ТаблицаРасчетыСКонтрагентамиФакт = ПолучитьТаблицуРасчетыСКонтрагентамиФакт(Источник.ДоговорКонтрагента, Источник.Дата, Источник.Ссылка, Источник.СуммаЗачетаОплаты);

	Если ЗначениеЗаполнено(Источник.СуммаЗачетаОплаты) Тогда

		Если ТаблицаРасчетыСКонтрагентамиФакт.Количество() = 0 Тогда 
			ТекстСообщения = НСтр("ru = 'Сумма к зачету аванса превышает доступную'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,Источник,"СуммаЗачетаОплаты","Объект",Отказ);
			Возврат;
		Иначе
			Для каждого СтрокаТаблицы Из ТаблицаРасчетыСКонтрагентамиФакт Цикл

				Если СтрокаТаблицы.Превышение < 0 Тогда
					ТекстСообщения = НСтр("ru = 'Сумма к зачету аванса превышает доступную'");
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,Источник,"СуммаЗачетаОплаты","Объект",Отказ);
					Возврат;
				КонецЕсли;

			КонецЦикла;
		КонецЕсли; 

	КонецЕсли;

	ДоговорКонтрагента = ПолучитьДоговор(Источник);

	ПриобретениеФинансовыхУслуг = Ложь;
	ПроведениеСерверОПК.ИнициализироватьДополнительныеСвойстваДляПроведения(Источник.Ссылка, Источник.ДополнительныеСвойства, РежимПроведения);
	Источник.ДополнительныеСвойства.Вставить("ЭтоНовый",Источник.ЭтоНовый());
	Источник.ДополнительныеСвойства.Вставить("РежимЗаписи", РежимПроведения);

	ИнициализироватьДанныеЗаказа(Источник.Ссылка, Источник.ДополнительныеСвойства, , ПриобретениеФинансовыхУслуг);

	ПроведениеСерверОПК.ПодготовитьНаборыЗаписейКПроведению(Источник);
	Если ПриобретениеФинансовыхУслуг Тогда
		ОтразитьОперацииЗаказа(Источник.ДополнительныеСвойства, Источник.Движения, Отказ);
	Иначе
		ПланированиеПотребностей.ОтразитьДвиженияПланирования(Источник.ДополнительныеСвойства, Источник.Движения, Отказ);
	КонецЕсли; 

	Если Источник.ДополнительныеСвойства.ЭтоРасчетыПоЗаказам Тогда 
		РаботаСДоговорамиКонтрагентовУХ.ОтразитьВерсииРасчетов(Источник.ДополнительныеСвойства, Источник.Движения, Отказ);
		РаботаСДоговорамиКонтрагентовУХ.ОтразитьРасчетыСКонтрагентамиГрафики(Источник.ДополнительныеСвойства, Источник.Движения, Отказ);   
		РаботаСДоговорамиКонтрагентовУХ.ОтразитьОперацииБюджетов(Источник.ДополнительныеСвойства, Источник.Движения, Отказ);
	КонецЕсли;

	ТаблицаПодтвержденныеОбъемыЗакупок = ПодтверждениеЗакупокУХ.ПолучитьТаблицуПодтвержденныеОбъемыЗакупок(Источник.Ссылка);
	Если НЕ Отказ И ТаблицаПодтвержденныеОбъемыЗакупок.Количество()  Тогда
		Источник.Движения.ПодтвержденныеОбъемыЗакупок.Записывать	= Истина;
		Источник.Движения.ПодтвержденныеОбъемыЗакупок.Загрузить(ТаблицаПодтвержденныеОбъемыЗакупок);
	КонецЕсли;

	ТаблицаРасчетовСПоставщикамиПоЗаказам = ПолучитьТаблицуРасчетыСПоставщикамиПоЗаказам(Источник.Ссылка);
	Если НЕ Отказ И ТаблицаРасчетовСПоставщикамиПоЗаказам.Количество()  Тогда
		Источник.Движения.РасчетыСПоставщикамиПоЗаказам.Записывать	= Истина;
		Источник.Движения.РасчетыСПоставщикамиПоЗаказам.Загрузить(ТаблицаРасчетовСПоставщикамиПоЗаказам);
	КонецЕсли;  

	Если НЕ Отказ И ТаблицаРасчетыСКонтрагентамиФакт.Количество()  Тогда
		Источник.Движения.РасчетыСКонтрагентамиФакт.Записывать	= Истина;
		Источник.Движения.РасчетыСКонтрагентамиФакт.Загрузить(ТаблицаРасчетыСКонтрагентамиФакт);
	КонецЕсли;  

	//
	ПланированиеПотребностей.СформироватьСписокРегистровДляКонтроля(Источник);
	// 
	ПроведениеСерверОПК.ЗаписатьНаборыЗаписей(Источник);
	ПроведениеСерверОПК.ВыполнитьКонтрольРезультатовПроведения(Источник, Отказ);  
	ПроведениеСерверОПК.ОчиститьДополнительныеСвойстваДляПроведения(Источник.ДополнительныеСвойства);
	РегистрыСведений.СостоянияЗаказовПоставщикам.ОтразитьСостояниеЗаказа(Источник, Отказ);

	// Проверим график 
	ЭтоРасчетыПоЗаказам = (ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ПорядокРасчетов")
	= Перечисления.ПорядокРасчетов.ПоЗаказамНакладным);

	Если ЭтоРасчетыПоЗаказам И  Источник.ЕстьГрафик Тогда
		Если Источник.ДополнительныеСвойства.Свойство("АктуальнаяВерсияГрафикаРасчетов")  Тогда
			АктуальнаяВерсияГрафикаРасчетов = Источник.ДополнительныеСвойства.АктуальнаяВерсияГрафикаРасчетов;	
		Иначе
			АктуальнаяВерсияГрафикаРасчетов = ДоговорыКонтрагентовФормыУХВызовСервера.АктуальнаяВерсияГрафикаРасчетовПоЗаказу(Источник.Ссылка);	
		КонецЕсли;

		Если Не Источник.ДополнительныеСвойства.Свойство("ЗаписьБезПроверки") Тогда

			Если АктуальнаяВерсияГрафикаРасчетов = Неопределено Тогда 
				ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'График расчетов не обнаружен. Создайте график перед проведением '"), , , , Отказ);
				Возврат;
			КонецЕсли;

			Если Не РезультатКонтроляГрафика(АктуальнаяВерсияГрафикаРасчетов, Источник.СуммаДокумента) Тогда
				Отказ = Истина;
				Возврат;		
			КонецЕсли;   

		КонецЕсли;	

		ГрафикОбъект = АктуальнаяВерсияГрафикаРасчетов.ПолучитьОбъект();
		ГрафикОбъект.ДополнительныеСвойства.Вставить("РазрешитьПроведение", Истина);
		ГрафикОбъект.Записать(РежимЗаписиДокумента.Проведение);  

	ИначеЕсли  Источник.ЕстьГрафик Тогда
		Документы.ГрафикРасчетовСПокупателемПоставщиком.ОтменитьПроведениеГрафиковПоЗаказу(Источник.Ссылка, ДоговорКонтрагента, Источник.ПометкаУдаления);
	КонецЕсли;
	// автоматическое создание ПП
	ГрафикиОплатыУХ.ОбработкаПроведенияЗаказ(Источник, Отказ, РежимПроведения);
КонецПроцедуры




