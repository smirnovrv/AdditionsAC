
&НаСервере
Процедура АЛФ_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)

	//Переопределение запроса
	ТекстЗапроса = "ВЫБРАТЬ
			|	ЖурналДокументовЖурналОпераций.Ссылка КАК Ссылка,
			|	ЖурналДокументовЖурналОпераций.Дата КАК Дата,
			|	ЕСТЬNULL(ЖурналДокументовЖурналОпераций.Ссылка.МежотчетныйПериод, ЛОЖЬ) КАК МежотчетныйПериод,
			|	ЖурналДокументовЖурналОпераций.ПометкаУдаления КАК ПометкаУдаления,
			|	ЖурналДокументовЖурналОпераций.Номер КАК Номер,
			|	ЖурналДокументовЖурналОпераций.Проведен КАК Проведен,
			|	ЖурналДокументовЖурналОпераций.Организация КАК Организация,
			|	ЖурналДокументовЖурналОпераций.Информация КАК Информация,
			|	ЖурналДокументовЖурналОпераций.Тип КАК Тип,
			|	ВЫБОР
			|		КОГДА ЖурналДокументовЖурналОпераций.СостояниеРеглОперации ЕСТЬ NULL
			|			ТОГДА ВЫБОР
			|					КОГДА ЖурналДокументовЖурналОпераций.Тип = ТИП(Документ.ОперацияБух)
			|						ТОГДА ВЫБОР
			|								КОГДА ЖурналДокументовЖурналОпераций.ПометкаУдаления = ИСТИНА
			|									ТОГДА 2
			|								ИНАЧЕ 1
			|							КОНЕЦ
			|					ИНАЧЕ ВЫБОР
			|							КОГДА ЖурналДокументовЖурналОпераций.РучнаяКорректировка = ИСТИНА
			|								ТОГДА ВЫБОР
			|										КОГДА ЖурналДокументовЖурналОпераций.ПометкаУдаления = ИСТИНА
			|											ТОГДА 10
			|										КОГДА ЖурналДокументовЖурналОпераций.Проведен = ЛОЖЬ
			|											ТОГДА 9
			|										ИНАЧЕ 8
			|									КОНЕЦ
			|							ИНАЧЕ ВЫБОР
			|									КОГДА ЖурналДокументовЖурналОпераций.ПометкаУдаления = ИСТИНА
			|										ТОГДА 2
			|									КОГДА ЖурналДокументовЖурналОпераций.Проведен = ИСТИНА
			|										ТОГДА 1
			|									ИНАЧЕ 0
			|								КОНЕЦ
			|						КОНЕЦ
			|				КОНЕЦ
			|		ИНАЧЕ ВЫБОР
			|				КОГДА ЖурналДокументовЖурналОпераций.ПометкаУдаления
			|					ТОГДА 2
			|				КОГДА ЖурналДокументовЖурналОпераций.СостояниеРеглОперации = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийРегламентныхОпераций.НеВыполнено)
			|						ИЛИ ЖурналДокументовЖурналОпераций.СостояниеРеглОперации = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийРегламентныхОпераций.Выполняется)
			|					ТОГДА 0
			|				КОГДА ЖурналДокументовЖурналОпераций.СостояниеРеглОперации = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийРегламентныхОпераций.ВыполненоСошибками)
			|					ТОГДА 6
			|				КОГДА ЖурналДокументовЖурналОпераций.СостояниеРеглОперации = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийРегламентныхОпераций.Выполнено)
			|						И ЖурналДокументовЖурналОпераций.РучнаяКорректировка
			|					ТОГДА 5
			|				КОГДА ЖурналДокументовЖурналОпераций.СостояниеРеглОперации = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийРегламентныхОпераций.Пропущено)
			|					ТОГДА 7
			|				ИНАЧЕ 4
			|			КОНЕЦ
			|	КОНЕЦ КАК СостояниеДокумента,
			|	ЖурналДокументовЖурналОпераций.СуммаДокумента КАК СуммаДокумента,
			|	ЖурналДокументовЖурналОпераций.ВалютаДокумента КАК ВалютаДокумента,
			|	ЖурналДокументовЖурналОпераций.РучнаяКорректировка КАК РучнаяКорректировка,
			|	ЖурналДокументовЖурналОпераций.ВидОперации КАК ВидОперации,
			|	ЖурналДокументовЖурналОпераций.ДоговорКонтрагента КАК ДоговорКонтрагента,
			|	ЖурналДокументовЖурналОпераций.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
			|	ЖурналДокументовЖурналОпераций.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
			|	ЖурналДокументовЖурналОпераций.Ответственный КАК Ответственный,
			|	ЖурналДокументовЖурналОпераций.Комментарий КАК Комментарий,  
			//Добавка
			|	ВЫБОР КОГДА ЖурналДокументовЖурналОпераций.Ссылка ССЫЛКА Документ.ПриходныйКассовыйОрдер ТОГДА ЖурналДокументовЖурналОпераций.Ссылка.ИИТ_ОбъектСтроительства 
			|		ИНАЧЕ ЖурналДокументовЖурналОпераций.ИИТ_ОбъектСтроительства КОНЕЦ КАК ИИТ_ОбъектСтроительства,
			|	ЖурналДокументовЖурналОпераций.ИИТ_ЭтапСтроительства КАК ИИТ_ЭтапСтроительства,
			|	ВЫБОР КОГДА ЖурналДокументовЖурналОпераций.Ссылка ССЫЛКА Документ.ПриходныйКассовыйОрдер И 
			|	ЖурналДокументовЖурналОпераций.Ссылка.Контрагент.НаименованиеПолное <> ЖурналДокументовЖурналОпераций.Ссылка.ПринятоОт И
			|	ЖурналДокументовЖурналОпераций.Ссылка.Контрагент.Наименование <> ЖурналДокументовЖурналОпераций.Ссылка.ПринятоОт ТОГДА ЖурналДокументовЖурналОпераций.Ссылка.ПринятоОт ИНАЧЕ """" КОНЕЦ КАК МОЛ,
            //Конец добавки
			|	ВЫБОР
			|		КОГДА НЕ ЖурналДокументовЖурналОпераций.СостояниеРеглОперации ЕСТЬ NULL
			|			ТОГДА ЖурналДокументовЖурналОпераций.СостояниеРеглОперации
			|		КОГДА ЖурналДокументовЖурналОпераций.Тип = ТИП(Документ.СчетНаОплатуПокупателю)
			|				ИЛИ ЖурналДокументовЖурналОпераций.Тип = ТИП(Документ.СчетНаОплатуПоставщика)
			|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(СтатусыДокументов.Статус, &СтатусОплатыПоУмолчанию) КАК Перечисление.СтатусОплатыСчета)
			|		КОГДА ЖурналДокументовЖурналОпераций.Тип = ТИП(Документ.РеализацияТоваровУслуг)
			|				ИЛИ ЖурналДокументовЖурналОпераций.Тип = ТИП(Документ.АктОбОказанииПроизводственныхУслуг)
			|				ИЛИ ЖурналДокументовЖурналОпераций.Тип = ТИП(Документ.АктСверкиВзаиморасчетов)
			|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(СтатусыДокументов.Статус, &СтатусРеализацииПоУмолчанию) КАК Перечисление.СтатусыДокументовРеализации)
			|		КОГДА ЖурналДокументовЖурналОпераций.Тип = ТИП(Документ.ПоступлениеТоваровУслуг)
			|				ИЛИ ЖурналДокументовЖурналОпераций.Тип = ТИП(Документ.ПоступлениеДопРасходов)
			|				ИЛИ ЖурналДокументовЖурналОпераций.Тип = ТИП(Документ.СчетФактураПолученный)
			|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(СтатусыДокументов.Статус, &СтатусПоступленияПоУмолчанию) КАК Перечисление.СтатусыДокументовПоступления)
			|		КОГДА ЖурналДокументовЖурналОпераций.Тип = ТИП(Документ.ВыплатыСамозанятым)
			|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(СтатусыДокументов.Статус, &СтатусВыплатыСамозанятымПоУмолчанию) КАК Перечисление.СтатусыДокументовВыплатыСамозанятым)
			|		КОГДА ЖурналДокументовЖурналОпераций.Тип = ТИП(Документ.ОтражениеЗарплатыВБухучете)
			|			ТОГДА ВЫБОР
			|					КОГДА ВЫРАЗИТЬ(ЖурналДокументовЖурналОпераций.Ссылка КАК Документ.ОтражениеЗарплатыВБухучете).ЗарплатаОтраженаВБухучете
			|						ТОГДА &ТекстОтраженВБухучете
			|					ИНАЧЕ &ТекстНеОтраженВБухучете
			|				КОНЕЦ
			|		КОГДА ЖурналДокументовЖурналОпераций.Тип = ТИП(Документ.ПлатежноеПоручение)
			|				ИЛИ ЖурналДокументовЖурналОпераций.Тип = ТИП(Документ.ПлатежноеТребование)
			|			ТОГДА СостоянияБанковскихДокументов.Состояние
			|		КОГДА ЖурналДокументовЖурналОпераций.Тип = ТИП(Документ.КоммерческоеПредложение)
			|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(СтатусыДокументов.Статус, &СтатусКоммерческогоПредложенияПоУмолчанию) КАК Перечисление.СтатусыКоммерческогоПредложения)
			|	КОНЕЦ КАК Состояние
			|ИЗ
			|	ЖурналДокументов.ЖурналОпераций КАК ЖурналДокументовЖурналОпераций
			|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументов КАК СтатусыДокументов
			|		ПО ЖурналДокументовЖурналОпераций.Организация = СтатусыДокументов.Организация
			|			И ЖурналДокументовЖурналОпераций.Ссылка = СтатусыДокументов.Документ}
			|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияБанковскихДокументов КАК СостоянияБанковскихДокументов
			|		ПО ЖурналДокументовЖурналОпераций.Ссылка = СостоянияБанковскихДокументов.СсылкаНаОбъект}
			|{ГДЕ
			|	(ЖурналДокументовЖурналОпераций.Ссылка В
			|			(ВЫБРАТЬ
			|				ДокументыПоКонтрагенту.Ссылка
			|			ИЗ
			|				КритерийОтбора.ДокументыПоКонтрагенту(&ОтборКонтрагент) КАК ДокументыПоКонтрагенту)),
			|	(ЖурналДокументовЖурналОпераций.Ссылка В
			|			(ВЫБРАТЬ
			|				ДокументыПоДоговоруКонтрагента.Ссылка
			|			ИЗ
			|				КритерийОтбора.ДокументыПоДоговоруКонтрагента(&ОтборДоговорКонтрагента) КАК ДокументыПоДоговоруКонтрагента)),
			|	(ЖурналДокументовЖурналОпераций.Ссылка В (&ОтборСсылки)),
			|	(ЖурналДокументовЖурналОпераций.Тип В (&ОтборТипыДокументов)),
			|	(НЕ ЖурналДокументовЖурналОпераций.Тип В (&ТипыИсключаемыхДокументов))}";
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ОсновнаяТаблица = "ЖурналДокументов.ЖурналОпераций";
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список,
		СвойстваСписка);     
		
	//Поля списка
	ЭлементСпискаФормы = Элементы.Добавить("МОЛ", Тип("ПолеФормы"), Элементы.Список);
	ЭлементСпискаФормы.Заголовок = "МОЛ"; 
	ЭлементСпискаФормы.Вид = ВидПоляФормы.ПолеНадписи; 
	ЭлементСпискаФормы.ПутьКДанным = "Список.МОЛ";   
	
	ЭлементСпискаФормы = Элементы.Добавить("ИИТ_ОбъектСтроительства", Тип("ПолеФормы"), Элементы.Список);
	ЭлементСпискаФормы.Заголовок = "Объект строительства"; 
	ЭлементСпискаФормы.Вид = ВидПоляФормы.ПолеНадписи; 
	ЭлементСпискаФормы.ПутьКДанным = "Список.ИИТ_ОбъектСтроительства";   

	ЭлементСпискаФормы = Элементы.Добавить("ИИТ_ЭтапСтроительства", Тип("ПолеФормы"), Элементы.Список);
	ЭлементСпискаФормы.Заголовок = "Этап строительства"; 
	ЭлементСпискаФормы.Вид = ВидПоляФормы.ПолеНадписи; 
	ЭлементСпискаФормы.ПутьКДанным = "Список.ИИТ_ЭтапСтроительства";   

КонецПроцедуры
