#Область Перенос_доработок

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ПроставитьЗаявки(Команда)
	
	МассивДокументов = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из Элементы.Список.ВыделенныеСтроки Цикл
		МассивДокументов.Добавить(ВыделеннаяСтрока);
	КонецЦикла;
	
	ОткрытьФорму("ЖурналДокументов.Деньги.Форма.ФормаПростановкиЗаявки", Новый Структура("ВыделенныеДокументы", МассивДокументов));

КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура АЛФ_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)

	//Переопределение запроса
	ТекстЗапроса = "ВЫБРАТЬ
				|	ВЫБОР
				|		КОГДА ЖурналДокументовБанковскиеВыписки.Ссылка ССЫЛКА Документ.СписаниеСРасчетногоСчета
				|			ТОГДА ВЫРАЗИТЬ(ЖурналДокументовБанковскиеВыписки.Ссылка КАК Документ.СписаниеСРасчетногоСчета)
				|		ИНАЧЕ ВЫРАЗИТЬ(ЖурналДокументовБанковскиеВыписки.Ссылка КАК Документ.ПоступлениеНаРасчетныйСчет)
				|	КОНЕЦ КАК Ссылка,
				|	ЖурналДокументовБанковскиеВыписки.Дата КАК Дата,
				|	НАЧАЛОПЕРИОДА(ЖурналДокументовБанковскиеВыписки.Дата, ДЕНЬ) КАК ДатаОтбора,
				|	ЖурналДокументовБанковскиеВыписки.ПометкаУдаления КАК ПометкаУдаления,
				|	ЖурналДокументовБанковскиеВыписки.Номер КАК Номер,
				|	ЖурналДокументовБанковскиеВыписки.Проведен КАК Проведен,
				|	ЖурналДокументовБанковскиеВыписки.Организация КАК Организация,
				|	ЖурналДокументовБанковскиеВыписки.БанковскийСчет КАК БанковскийСчет,
				|	БанковскиеСчетаИнтеграцииСБанком.БанковскийСчет ЕСТЬ НЕ NULL  КАК СчетВРежимеИнтеграции,
				|	ЖурналДокументовБанковскиеВыписки.Контрагент КАК Контрагент,
				|	ЖурналДокументовБанковскиеВыписки.Доход КАК Поступление,
				|	ЖурналДокументовБанковскиеВыписки.Расход КАК Списание,
				|	ЖурналДокументовБанковскиеВыписки.Валюта КАК Валюта,
				|	ЖурналДокументовБанковскиеВыписки.ВидОперации КАК ВидОперации,
				|	ЖурналДокументовБанковскиеВыписки.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
				|	ЖурналДокументовБанковскиеВыписки.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
				|	ЖурналДокументовБанковскиеВыписки.РучнаяКорректировка КАК РучнаяКорректировка,
				|	ЖурналДокументовБанковскиеВыписки.НазначениеПлатежа КАК НазначениеПлатежа,
				|	ЖурналДокументовБанковскиеВыписки.Ответственный КАК Ответственный,
				|	ЖурналДокументовБанковскиеВыписки.Комментарий КАК Комментарий,
				|	ЖурналДокументовБанковскиеВыписки.Тип КАК Тип,
				|	ВЫБОР
				|		КОГДА ЖурналДокументовБанковскиеВыписки.РучнаяКорректировка = ИСТИНА
				|			ТОГДА ВЫБОР
				|					КОГДА ЖурналДокументовБанковскиеВыписки.ПометкаУдаления = ИСТИНА
				|						ТОГДА 10
				|					КОГДА ЖурналДокументовБанковскиеВыписки.Проведен = ЛОЖЬ
				|						ТОГДА 9
				|					ИНАЧЕ 8
				|				КОНЕЦ
				|		ИНАЧЕ ВЫБОР
				|				КОГДА ЖурналДокументовБанковскиеВыписки.ПометкаУдаления = ИСТИНА
				|					ТОГДА 2
				|				КОГДА ЖурналДокументовБанковскиеВыписки.Проведен = ИСТИНА
				|					ТОГДА 1
				|				ИНАЧЕ 0
				|			КОНЕЦ
				|	КОНЕЦ КАК СостояниеДокумента,
				|	НаличиеФайлов.ЕстьФайлы КАК ЕстьФайлы,
				|	НЕ ПредупрежденияПриЗагрузкеВыписки.Документ ЕСТЬ NULL КАК ЕстьПредупреждения,
				|	ЧекиНПД.НомерЧека КАК НомерЧека,
				|	ЧекиНПД.Состояние КАК Состояние,
				|	ЧекиНПД.ПроизведенВозвратПоЧеку КАК ПроизведенВозвратПоЧеку,
				|	ЧекиНПД.ДатаАннулированияЧека КАК ДатаАннулированияЧека,
				|	ВЫБОР
				|		КОГДА ЧекиНПД.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияЧековНПД.ОжидаетОтправкиВФНС)
				|			ТОГДА ИСТИНА
				|		ИНАЧЕ ЛОЖЬ
				|	КОНЕЦ КАК ЧекНПДОжидаетОтправкиВФНС,
				|	ЖурналДокументовБанковскиеВыписки.Ссылка.ИИТ_ЗаявкиПроставлены КАК ЗаявкиПроставлены
				|ИЗ
				|	ЖурналДокументов.Деньги КАК ЖурналДокументовБанковскиеВыписки
				|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаличиеФайлов КАК НаличиеФайлов
				|		ПО ЖурналДокументовБанковскиеВыписки.Ссылка = НаличиеФайлов.ОбъектСФайлами}
				|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредупрежденияПриЗагрузкеВыписки КАК ПредупрежденияПриЗагрузкеВыписки
				|		ПО ЖурналДокументовБанковскиеВыписки.Ссылка = ПредупрежденияПриЗагрузкеВыписки.Документ
				|			И ЖурналДокументовБанковскиеВыписки.Организация = ПредупрежденияПриЗагрузкеВыписки.Организация}
				|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЧекиНПД КАК ЧекиНПД
				|		ПО ЖурналДокументовБанковскиеВыписки.Ссылка = ЧекиНПД.ДокументОснование
				|			И ЖурналДокументовБанковскиеВыписки.Организация = ЧекиНПД.Организация}
				|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БанковскиеСчетаИнтеграцииСБанком КАК БанковскиеСчетаИнтеграцииСБанком
				|		ПО ЖурналДокументовБанковскиеВыписки.БанковскийСчет = БанковскиеСчетаИнтеграцииСБанком.БанковскийСчет}
				|ГДЕ
				|	(ЖурналДокументовБанковскиеВыписки.Ссылка ССЫЛКА Документ.ПоступлениеНаРасчетныйСчет
				|			ИЛИ ЖурналДокументовБанковскиеВыписки.Ссылка ССЫЛКА Документ.СписаниеСРасчетногоСчета)";
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ОсновнаяТаблица = "ЖурналДокументов.Деньги";
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список,
		СвойстваСписка);     
		
	//Команды
	КомандаФормы = Команды.Добавить("ПроставитьЗаявки");  //На форме отсутствует
	КомандаФормы.Заголовок = "ПроставитьЗаявки";
	КомандаФормы.Действие  = "ПроставитьЗаявки";
		
	КнопкаКоманднойПанели = Элементы.Вставить("ФормаПроставитьЗаявки", Тип("КнопкаФормы"), Элементы.ГруппаГлобальныеКоманды, Элементы.ОбщаяКомандаСтруктураПодчиненности);
	КнопкаКоманднойПанели.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	КнопкаКоманднойПанели.Заголовок = "Проставить заявки";
	КнопкаКоманднойПанели.ИмяКоманды  = "ПроставитьЗаявки"; 

	//Поля списка
	ЭлементСпискаФормы = Элементы.Вставить("ЗаявкиПроставлены", Тип("ПолеФормы"), Элементы.Список, Элементы.Дата);
	ЭлементСпискаФормы.Заголовок = "Заявки проставлены"; 
	ЭлементСпискаФормы.Вид = ВидПоляФормы.ПолеФлажка; 
	ЭлементСпискаФормы.ОтображатьВШапке = Ложь;
	ЭлементСпискаФормы.КартинкаШапки = БиблиотекаКартинок.ЗавершитьРедактированиеМакета;
	ЭлементСпискаФормы.ПутьКДанным = "Список.ЗаявкиПроставлены";   
	
КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ОбработкаОповещения")
Процедура АЛФ_ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		ОсновнаяОрганизация = Параметр;
		Если ОсновнаяОрганизация <> ОтборОрганизация Тогда
			ОтборОрганизация                 = ОсновнаяОрганизация;
			УстановитьСчетОрганизации(ОтборБанковскийСчет, ОтборОрганизация, ИнтеграцияСБанкамиПодключена);
			ОтборОрганизацияИспользование    = ЗначениеЗаполнено(ОтборОрганизация);
			ОтборБанковскийСчетИспользование = ЗначениеЗаполнено(ОтборБанковскийСчет);

			УстановитьВосстановленныеОтборы(Истина);
			ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);

		КонецЕсли;

		Если ИтогиВключены Тогда
			ВсегдаОбновлять = Истина;
			ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИтогиОбработчик", 0.2, Истина);
		КонецЕсли;


	ИначеЕсли ИмяСобытия = "ИзменениеВыписки" Тогда
		Если ИтогиВключены Тогда
			ВсегдаОбновлять = Истина;
			ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИтогиОбработчик", 0.2, Истина);
		КонецЕсли;

		ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
		Оповестить("ПоказатьИнформациюОПравеПримененияСпецрежима",
		?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено),
		ЭтотОбъект);

	ИначеЕсли ИмяСобытия = "ПоказатьИнформациюОПравеПримененияСпецрежима" Тогда

		Если Источник <> ЭтотОбъект И (Не ОтборОрганизацияИспользование Или Параметр = ОтборОрганизация) Тогда
			ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
		КонецЕсли;

	ИначеЕсли ИмяСобытия = "ИзмененБанковскийСчет" И Параметр.Владелец = ОтборОрганизация Тогда

		ИзменениеСоставаБанковскихСчетовОрганизации();
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);

	ИначеЕсли ИмяСобытия = "ИзмененоПредупреждениеПриЗагрузкеВыписки" Тогда

		ПоказатьБаннерПредупрежденийПриЗагрузкеИУстановитьОтбор();
		Элементы.Список.Обновить();

	ИначеЕсли (ИмяСобытия = "ИзмененоСостояниеИнтеграцииСПлатформойСамозанятые"
		Или ИмяСобытия = "ИнтеграцияАУСН_ИзмененоСостояние"
		Или ИмяСобытия = "ИзменениеУчетнойПолитики") Тогда

		ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
		ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);

	ИначеЕсли ИмяСобытия = ЧекиНПДКлиент.ИмяСобытияИзменениеСтатусаОфлайнЧека()
		Или ИмяСобытия = ЧекиНПДКлиент.ИмяСобытияОбновленияСведенийОСформированныхЧеках() Тогда

		Элементы.Список.Обновить();

	ИначеЕсли ИмяСобытия = ИнтеграцияАУСНКлиент.ИмяСобытияОбменССервисом() Тогда

		ПодключитьОбработчикОжидания("Подключаемый_ВыполнитьОбновлениеИнформацииИБаннеровАвтозагрузки", 0.1, Истина);

		Если Источник = ЭтотОбъект Тогда
			ПодключитьОбработчикОжидания("Подключаемый_ПоказатьУведомленияАУСНОтФНС", 0.1, Истина);
		КонецЕсли;

	ИначеЕсли ИмяСобытия = ИнтеграцияАУСНКлиент.ИмяСобытияИзмененияВОшибкахОтправки() Тогда

		Элементы.ГруппаСостояниеАвтозагрузкиОшибка.Видимость = Ложь;
		ПодключитьОбработчикОжидания("Подключаемый_ВыполнитьОбновлениеИнформацииИБаннеровАвтозагрузки", 5, Истина);

#Вставка
	//+ИИТ_Аксеньюшкин
	ИначеЕсли ИмяСобытия = "ОбновитьЖурналБанковскойВыписки" Тогда
		Элементы.Список.Обновить();
	//-ИИТ_Аксеньюшкин
#КонецВставки
	КонецЕсли;

	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения

	ПрисоединенныеФайлыБПКлиент.ОбновитьСписокПослеДобавленияФайла(ЭтотОбъект, ИмяСобытия, Параметр, Источник);

	Если ИмяСобытия = "ИзмененаНастройкаОбмена" Тогда

		ВидимостьЭлементовДиректБанк = ЭлектронноеВзаимодействиеБПВызовСервера.ВидимостьЭлементовДиректБанк(
		ОтборОрганизация, ОтборБанковскийСчет);

		ПоказатьБаннерДиректБанк(ЭтотОбъект);

	КонецЕсли;

КонецПроцедуры


