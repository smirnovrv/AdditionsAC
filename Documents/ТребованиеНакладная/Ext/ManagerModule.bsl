
#Область Перенос_доработок

//ИИТ_Аксеньюшкин
// Формирует табличный документ по унифицированной форме М-11
// 
// Возвращаемое значение
//  Табличный документ - унифицированная форма М-11
//
Функция ПечатьМ29(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСверху			= 10;
	ТабличныйДокумент.ПолеСнизу				= 10;
	ТабличныйДокумент.ПолеСправа			= 0;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_М29";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ТребованиеНакладная.М29");
	
	Для Каждого Объект Из МассивОбъектов Цикл

		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьШапка.Параметры.Склад = Объект.Склад;
		ОбластьШапка.Параметры.ОбъектСтроительства = Объект.ИИТ_ОбъектОтправитель;
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		Н = 1;
		
		Для Каждого СтрокаТоваров Из Объект.Материалы Цикл
			ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
			ОбластьСтрока.Параметры.Номер = Н;
			ОбластьСтрока.Параметры.Код = СтрокаТоваров.НоменклатураБухгалтерская.Код;
			ОбластьСтрока.Параметры.Номенклатура = СтрокаТоваров.НоменклатураБухгалтерская;			
			ОбластьСтрока.Параметры.ЕдИзм = СтрокаТоваров.ЕдиницаИзмерения;
			ОбластьСтрока.Параметры.Колво1 = СтрокаТоваров.Количество;
			ОбластьСтрока.Параметры.Колво2 = СтрокаТоваров.Количество;
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			Н = Н + 1;
		КонецЦикла;
		
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции
	
#КонецОбласти

&ИзменениеИКонтроль("Печать")
Процедура АЛФ_Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)

	// Проверяем, нужно ли для макета ПКО формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТребованиеНакладная") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТребованиеНакладная", "Требование накладная",
		ПечатьТребованиеНакладная(МассивОбъектов, ОбъектыПечати), , "Документ.ТребованиеНакладная.ПФ_MXL_ТребованиеНакладная");
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М11") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "М11", "М11 (Требование-накладная)",
		ПечатьМ11(МассивОбъектов, ОбъектыПечати), , "ОбщийМакет.ПФ_MXL_М11");
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктНаСписаниеМатериалов") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктНаСписаниеМатериалов", "Акт на списание материалов",
		ПечатьАктНаСписаниеМатериалов(МассивОбъектов, ОбъектыПечати, ПараметрыПечати), , "Документ.ТребованиеНакладная.ПФ_MXL_АктНаСписаниеМатериалов");
#Вставка
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М29") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "М29", "Требование-накладная М-29", 
			ПечатьМ29(МассивОбъектов, ОбъектыПечати), , "Документ.ТребованиеНакладная.М29");
#КонецВставки
	КонецЕсли;

	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	

КонецПроцедуры

&ИзменениеИКонтроль("ДобавитьКомандыПечати")
Процедура АЛФ_ДобавитьКомандыПечати(КомандыПечати)

	// Требование-накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТребованиеНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Требование-накладная'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 10;

	// Требование-накладная (М-11)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "М11";
	КомандаПечати.Представление = НСтр("ru = 'Требование-накладная (М-11)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 20;

	// Акт на списание материалов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктНаСписаниеМатериалов";
	КомандаПечати.Представление = НСтр("ru = 'Акт на списание материалов'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 30;

	// Принятие расходов УСН
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор       = "РасходыУСН";
	КомандаПечати.Представление       = НСтр("ru = 'Признание расходов УСН'");
	КомандаПечати.Обработчик          = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРасходовУСН";
	КомандаПечати.СписокФорм          = "ФормаДокумента";
	КомандаПечати.ФункциональныеОпции = "ПрименяетсяУСНДоходыМинусРасходы";
	КомандаПечати.Порядок             = 40;
	УчетУСН.ДобавитьУсловиеВидимостиПечатиРасходыУСН(КомандаПечати);
   #Вставка 
	//+ИИТ_Аксеньюшкин
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "М29";
	КомандаПечати.Представление = НСтр("ru = 'Требование-накладная (М-29)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 50;
	//-ИИТ_Аксеньюшкин
#КонецВставки
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Требование-накладная""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;

КонецПроцедуры



