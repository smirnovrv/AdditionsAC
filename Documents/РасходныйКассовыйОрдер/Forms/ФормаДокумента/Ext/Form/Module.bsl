
&НаСервере
&ИзменениеИКонтроль("ПриСозданииНаСервере")
Процедура АЛФ_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

#Вставка
	//Команды формы
	КомандаФормы = Команды.Добавить("Отказать");
	КомандаФормы.Заголовок = "Отказать";
	КомандаФормы.Действие  = "Отказать";
	
	КомандаФормы = Команды.Добавить("Согласовать");
	КомандаФормы.Заголовок = "Согласовать";
	КомандаФормы.Действие  = "Согласовать";

	ГруппаСогласование = Элементы.Добавить("ГруппаСогласование", Тип("ГруппаФормы"));
	ГруппаСогласование.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСогласование.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно;
	ГруппаСогласование.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСогласование.ОтображатьЗаголовок = Ложь;  	
    ГруппаСогласование.Видимость = Ложь;
	
	КнопкаКоманднойПанели = Элементы.Добавить("Отказать", Тип("КнопкаФормы"), ГруппаСогласование);
	КнопкаКоманднойПанели.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	КнопкаКоманднойПанели.Заголовок = "Отказать";
	КнопкаКоманднойПанели.ИмяКоманды  = "Отказать"; 
	
	КнопкаКоманднойПанели = Элементы.Добавить("Согласовать", Тип("КнопкаФормы"), ГруппаСогласование);
	КнопкаКоманднойПанели.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	КнопкаКоманднойПанели.Заголовок = "Согласовать";
	КнопкаКоманднойПанели.ИмяКоманды  = "Согласовать"; 
	
#КонецВставки
	РасходныйКассовыйОрдерФормы.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

	#Область УХ_Казначейство
	ДенежныеСредстваВстраиваниеФормыУХ.СоздатьЭлементВидОперацииУХ(ЭтотОбъект);
	УстановитьПараметрыВыбора();
	ЗарегистрироватьСтатьиБюджетаИАналитики();
	// ограничения видов операций 
	ЗаявкиНаОперацииКлиентСервер.УстановитьОграничениеВидыОпераций(ЭтотОбъект, "ВидОперацииУХ");
	#КонецОбласти
#Вставка
	//+ИИТ_Аксеньюшкин
	Если Параметры.Свойство("ОткрытьДляСогласования") Тогда
		Элементы.ГруппаПодвал.ТолькоПросмотр = Истина;
		Элементы.ГруппаОтражениеВУСН.ТолькоПросмотр = Истина;
		Элементы.ГруппаСтраницы.ТолькоПросмотр = Истина;
		Элементы.ГруппаШапка.ТолькоПросмотр = Истина;
		Элементы.ПредупреждениеОПечатиЧека.ТолькоПросмотр = Истина;
		Элементы.ПредупреждениеОЗаполненииРеквизитовПлатежаНПД.ТолькоПросмотр = Истина;
		Элементы.ПредупреждениеОЗаполненииРеквизитовСчета.ТолькоПросмотр = Истина;
		Элементы.ГруппаСогласование.Видимость = Истина;
	КонецЕсли;
	//-ИИТ_Аксеньюшкин
#КонецВставки

КонецПроцедуры

&НаСервере
Процедура АЛФ_ПослеЗаписиНаСервереПосле(ТекущийОбъект, ПараметрыЗаписи)

	//+ИИТ_Аксеньюшкин
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРКО.ВыдачаПодотчетномуЛицу") Тогда
		НоваяЗапись = РегистрыСведений.ИИТ_УведомленияОВозвратеСогласования.СоздатьМенеджерЗаписи();
		НоваяЗапись.Заявка = Объект.Ссылка;
		НоваяЗапись.Ответственный = Справочники.Пользователи.НайтиПоРеквизиту("ФизическоеЛицо", Объект.Контрагент);
		НоваяЗапись.ДатаСоздания = ТекущаяДата();
		НоваяЗапись.Записать(Истина);
	КонецЕсли;
	//-ИИТ_Аксеньюшкин
	
КонецПроцедуры

#Область Перенос_доработок

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура Отказать(Команда)
	УстановитьСогласование(Ложь);
	Закрыть();
КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура Согласовать(Команда)
	УстановитьСогласование(Истина);
	Закрыть();
КонецПроцедуры	 

//ИИТ_Аксеньюшкин
Процедура УстановитьСогласование(Согласовать)

	Объект.ИИТ_ПодтвержденаПередачаПодотчетнику = Согласовать;
	Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));

	НаборЗаписей = РегистрыСведений.ИИТ_УведомленияОВозвратеСогласования.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Заявка.Установить(Объект.Ссылка);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	НаборЗаписей.Записать(Истина);

КонецПроцедуры  

#КонецОбласти