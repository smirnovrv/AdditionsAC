#Область Перенос_доработок

//ИИТ_Аксеньюшкин
Функция ДокументУтвержден(ДокументПланирования)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрСостоянийОбъектовСрезПоследних.СостояниеОбъекта КАК СостояниеОбъекта
	|ИЗ
	|	РегистрСведений.РегистрСостоянийОбъектов.СрезПоследних(, Объект = &Заявка) КАК РегистрСостоянийОбъектовСрезПоследних";
	Запрос.УстановитьПараметр("Заявка", ДокументПланирования);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат (Выборка.СостояниеОбъекта = Перечисления.СостоянияСогласования.Утверждена ИЛИ Выборка.СостояниеОбъекта = Справочники.СостоянияСогласования.Утверждено);
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

//ИИТ_Аксеньюшкин
Функция ЕстьЗаписиРезервов(Заявка)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИИТ_РезервыДенежныхСредствОбороты.Заявка КАК Заявка,
	|	СУММА(ИИТ_РезервыДенежныхСредствОбороты.СуммаПриход) КАК СуммаПриход
	|ИЗ
	|	РегистрНакопления.ИИТ_РезервыДенежныхСредств.Обороты(, , , Заявка = &Заявка) КАК ИИТ_РезервыДенежныхСредствОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ИИТ_РезервыДенежныхСредствОбороты.Заявка
	|
	|ИМЕЮЩИЕ
	|	СУММА(ИИТ_РезервыДенежныхСредствОбороты.СуммаПриход) > 0";
	Запрос.УстановитьПараметр("Заявка", Заявка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

//ИИТ_Аксеньюшкин
Функция ПолучитьОбъектСтроительстваМОРС(Проект)
	
	ИИТ_ПроектМОРС_ = Справочники.Проекты.ПустаяСсылка();

	Если Проект.Проект Тогда
		ИИТ_ПроектМОРС_ = Проект;
	ИначеЕсли Проект.Родитель.Проект Тогда
		ИИТ_ПроектМОРС_ = Проект.Родитель;
	ИначеЕсли Проект.Родитель.Родитель.Проект Тогда
		ИИТ_ПроектМОРС_ = Проект.Родитель.Родитель;
	ИначеЕсли Проект.Родитель.Родитель.Родитель.Проект Тогда
		ИИТ_ПроектМОРС_ = Проект.Родитель.Родитель.Родитель;
	КонецЕсли;
	
	Возврат ИИТ_ПроектМОРС_;

КонецФункции

//ИИТ_Аксеньюшкин
Функция ПолучитьОсновнойДоговор(Организация, Контрагент)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Владелец = &Контрагент
	|	И ДоговорыКонтрагентов.Организация = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДоговорыКонтрагентов.Номер УБЫВ";
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

//ИИТ_Аксеньюшкин
Функция ПолучитьЭтапПроцесса(Заявка)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЭкземплярПроцесса.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЭкземплярПроцесса КАК ЭкземплярПроцесса
	|ГДЕ
	|	ЭкземплярПроцесса.КлючевойОбъектПроцесса = &Заявка
	|	И НЕ ЭкземплярПроцесса.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭкземплярПроцесса.Дата";
	Запрос.УстановитьПараметр("Заявка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Документы.ЭкземплярПроцесса.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

//ИИТ_Аксеньюшкин
Функция СтатусОплатыУжеУстановлен(ДокументПланирования)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрСостоянийОбъектовСрезПоследних.СостояниеОбъекта КАК СостояниеОбъекта
	|ИЗ
	|	РегистрСведений.РегистрСостоянийОбъектов.СрезПоследних(, Объект = &Заявка) КАК РегистрСостоянийОбъектовСрезПоследних";
	Запрос.УстановитьПараметр("Заявка", ДокументПланирования);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат (Выборка.СостояниеОбъекта = Перечисления.СостоянияСогласования.ИИТ_ОплаченоБезСогласования ИЛИ 
				Выборка.СостояниеОбъекта = Перечисления.СостоянияСогласования.УдалитьОплачено);
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

//ИИТ_Аксеньюшкин
Функция СчетРезерва(Заявка)
	
	//Проблема, что когда перепроводят заявку, СчетРезерва = Пусто, 
	//когда провести заявку повторно, СчетРезерва = Заполнено
	//нужно вернуть этот код, с ним нет проблемы 
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(ИИТ_РезервыДенежныхСредствОбороты.СуммаПриход) КАК СуммаПриход,
	|	ИИТ_РезервыДенежныхСредствОбороты.БанковскийСчет КАК БанковскийСчет
	|ИЗ
	|	РегистрНакопления.ИИТ_РезервыДенежныхСредств.Обороты(, , , Заявка = &Заявка) КАК ИИТ_РезервыДенежныхСредствОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ИИТ_РезервыДенежныхСредствОбороты.БанковскийСчет
	|
	|ИМЕЮЩИЕ
	|	СУММА(ИИТ_РезервыДенежныхСредствОбороты.СуммаПриход) > 0";
	Запрос.УстановитьПараметр("Заявка", Заявка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.БанковскийСчет;
	Иначе
		Возврат Справочники.БанковскиеСчета.ПустаяСсылка();
	КонецЕсли;
	
	//Этот код работате через раз
	//Запрос = Новый Запрос;
	//Запрос.Текст =
	//"ВЫБРАТЬ
	//|	ИИТ_РезервыДенежныхСредствОстатки.БанковскийСчет КАК БанковскийСчет,
	//|	СУММА(ИИТ_РезервыДенежныхСредствОстатки.СуммаОстаток) КАК СуммаОстаток
	//|ИЗ
	//|	РегистрНакопления.ИИТ_РезервыДенежныхСредств.Остатки(, Заявка = &Заявка) КАК ИИТ_РезервыДенежныхСредствОстатки
	//|ГДЕ
	//|	ИИТ_РезервыДенежныхСредствОстатки.СуммаОстаток > 0
	//|
	//|СГРУППИРОВАТЬ ПО
	//|	ИИТ_РезервыДенежныхСредствОстатки.БанковскийСчет";
	//Запрос.УстановитьПараметр("Заявка", Заявка);
	//
	//Выборка = Запрос.Выполнить().Выбрать();
	//Если Выборка.Следующий() Тогда
	//	Возврат Выборка.БанковскийСчет;
	//Иначе
	//	Возврат Справочники.БанковскиеСчета.ПустаяСсылка();
	//КонецЕсли;

КонецФункции

//ИИТ_Аксеньюшкин
Процедура ЗакрытьТекущиеЗадачиПоЗаявке(Заявка)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	*
	|ИЗ
	|	РегистрСведений.ОтложеннаяОбработкаЭтаповПроцессов КАК ОтложеннаяОбработкаЭтаповПроцессов
	|ГДЕ
	|	ОтложеннаяОбработкаЭтаповПроцессов.ДокументПроцесса.КлючевойОбъектПроцесса = &Заявка";
	Запрос.УстановитьПараметр("Заявка", Заявка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Выполнено Тогда
			Продолжить;
		КонецЕсли;

		НаборЗаписей = РегистрыСведений.ОтложеннаяОбработкаЭтаповПроцессов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
		НаборЗаписей.Отбор.ВидДействия.Установить(Выборка.ВидДействия);
		НаборЗаписей.Отбор.ДокументПроцесса.Установить(Выборка.ДокументПроцесса);
		НаборЗаписей.Отбор.ЭтапПроцесса.Установить(Выборка.ЭтапПроцесса);
		НаборЗаписей.Отбор.Ответственный.Установить(Выборка.Ответственный);
		НаборЗаписей.Отбор.ТипКлючевогоОбъекта.Установить(Выборка.ТипКлючевогоОбъекта);
		НаборЗаписей.Отбор.Выполнено.Установить(Выборка.Выполнено);
		НаборЗаписей.Отбор.Задача.Установить(Выборка.Задача);
		НаборЗаписей.Прочитать();
		НаборЗаписей.Очистить();
		НаборЗаписей.Записать();

		НоваяЗаписьОтложеннаяОбработка = РегистрыСведений.ОтложеннаяОбработкаЭтаповПроцессов.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(НоваяЗаписьОтложеннаяОбработка, Выборка);
		НоваяЗаписьОтложеннаяОбработка.ТекстКомментария = "Принудительное выполнение. Оплата заявки без утверждения.";
		НоваяЗаписьОтложеннаяОбработка.Выполнено = Истина;
		НоваяЗаписьОтложеннаяОбработка.Записать(Истина);
			
		НоваяЗаписьИсторииСогласования = РегистрыСведений.ИсторияВыполненияПроцессов.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(НоваяЗаписьИсторииСогласования, Выборка);
		НоваяЗаписьИсторииСогласования.Период = ТекущаяДата();
		НоваяЗаписьИсторииСогласования.ИдентификаторСообщения = Строка(Новый УникальныйИдентификатор);
		НоваяЗаписьИсторииСогласования.Пользователь = Выборка.Ответственный;
		НоваяЗаписьИсторииСогласования.Событие = Перечисления.ВидыОповещенийПользователейУХ.ИИТ_ЗавершенДосрочно;
		НоваяЗаписьИсторииСогласования.Комментарий = "Принудительное выполнение. Оплата заявки без утверждения.";
		НоваяЗаписьИсторииСогласования.Записать(Истина);

		НаборЗаписей.Записать(Истина);
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

&После("ПриКопировании")
Процедура АЛФ_ПриКопировании(ОбъектКопирования)

	//+ИИТ_Аксеньюшкин
	Для Каждого СтрокаДвижения Из РасшифровкаПлатежа Цикл
		СтрокаДвижения.ДокументПланирования = Документы.ЗаявкаНаРасходованиеДенежныхСредств.ПустаяСсылка();
	КонецЦикла;
	
	ДокументПланирования = Документы.ЗаявкаНаРасходованиеДенежныхСредств.ПустаяСсылка();
	//-ИИТ_Аксеньюшкин

КонецПроцедуры

&Перед("ПередЗаписью")
Процедура АЛФ_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	//+ИИТ_Аксеньюшкин
	ИИТ_ЗаявкиПроставлены = Истина;
	Для Каждого СтрокаРасшифровки Из РасшифровкаПлатежа Цикл
		Если Не ЗначениеЗаполнено(СтрокаРасшифровки.ДокументПланирования) Тогда
			ИИТ_ЗаявкиПроставлены = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если РасшифровкаПлатежа.Количество() = 0 И Не ЗначениеЗаполнено(ДокументПланирования) Тогда
		ИИТ_ЗаявкиПроставлены = Ложь;
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП И ПеречислениеЗаработнойПлаты.Количество() = 0 Тогда
		НоваяСтрока = ПеречислениеЗаработнойПлаты.Добавить();
		НоваяСтрока.СуммаПлатежа = СуммаДокумента;
	КонецЕсли;
	
	Для Каждого СтрокаРасшифровки Из РасшифровкаПлатежа Цикл
		Если Не ЗначениеЗаполнено(СтрокаРасшифровки.ДоговорКонтрагента) Тогда
			СтрокаРасшифровки.ДоговорКонтрагента = ПолучитьОсновнойДоговор(Организация, Контрагент);
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаРасшифровки.СтавкаНДС) = Ложь И СтрокаРасшифровки.СуммаНДС = 0 Тогда 
			СтрокаРасшифровки.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;	
		КонецЕсли;	
	КонецЦикла;
	//-ИИТ_Аксеньюшкин

КонецПроцедуры


&После("ПриЗаписи")
Процедура АЛФ_ПриЗаписи(Отказ)
	Если РасшифровкаПлатежа.Количество() = 0  Тогда
		//Если ЗначениеЗаполнено(ДокументПланирования) Тогда 
		//	ДокументОбъект = ДокументПланирования.ПолучитьОбъект(); 
		//	Попытка
		//		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение); 
		//	Исключение
		//		ДокументОбъект.Записать();	
		//	КонецПопытки;
		//КонецЕсли;
	ИначеЕсли РасшифровкаПлатежа.Количество() > 0 Тогда 			
	//	Для Каждого СтрТЧ Из РасшифровкаПлатежа Цикл 		
	//		Если ЗначениеЗаполнено(СтрТЧ.ДокументПланирования) Тогда 
	//			ДокументОбъект = СтрТЧ.ДокументПланирования.ПолучитьОбъект(); 
	//			Попытка
	//				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение); 
	//			Исключение
	//				ДокументОбъект.Записать();	
	//			КонецПопытки;
	//		КонецЕсли;	
	//	КонецЦикла;
	КонецЕсли;
КонецПроцедуры


&ИзменениеИКонтроль("ОбработкаПроведения")
Процедура АЛФ_ОбработкаПроведения(Отказ, Режим)

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
   #Вставка
	//+ИИТ_Аксеньюшкин
	//ПРОВЕРЯЕМ ЗАПОЛНЕННОСТЬ КОЛОНКИ СУММА
	Для Каждого СтрокаРасшифровки Из РасшифровкаПлатежа Цикл
		Если Не ЗначениеЗаполнено(СтрокаРасшифровки.СуммаПлатежа) Тогда
			Сообщить("Не заполнена колонка ""Сумма"" в строке № " + Строка(СтрокаРасшифровки.НомерСтроки));
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЦикла;

	Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") И Контрагент.ПометкаУдаления Тогда
		Отказ = Истина;
		Сообщить("Контрагент, указанный в документе, помечен на удаление, проведение документа отменено.");
	КонецЕсли;
	//-ИИТ_Аксеньюшкин
#КонецВставки
	ПараметрыПроведения = Документы.СписаниеСРасчетногоСчета.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ

	ТаблицаВзаиморасчеты  = УчетВзаиморасчетов.ПодготовитьТаблицуВзаиморасчетовПогашениеЗадолженности(
	ПараметрыПроведения.РасшифровкаПлатежа, ПараметрыПроведения.Реквизиты, Отказ);

	ТаблицаСуммовыхРазниц = УчетНДС.ПодготовитьТаблицуСуммовыхРазниц(ТаблицаВзаиморасчеты,
	ПараметрыПроведения.Реквизиты, Отказ);

	// Структура таблиц для отражения в налоговом учете УСН
	ТаблицаВыплатаЗарплаты         = УчетЗарплаты.ПолучитьТаблицуВыплатыЗарплатыДляУСН(ПараметрыПроведения.ПеречислениеЗарплаты);
	ТаблицаВыплатаДепонентов       = УчетЗарплаты.ПолучитьТаблицуВыплатыЗарплатыДляУСН(ПараметрыПроведения.ПеречислениеДепонентов);
	ТаблицаВыплатаДоговоровПодряда = УчетЗарплаты.ПолучитьТаблицуВыплатыЗарплатыДляУСН(ПараметрыПроведения.ОплатаПоДоговорамПодряда);
	ТаблицаПеречислениеПоИсполнительномуЛисту = УчетЗарплаты.ПолучитьТаблицуВыплатыПоИСполнительномуЛистуДляУСН(ПараметрыПроведения.Реквизиты,
	ПараметрыПроведения.ПеречислениеПоИсполнительномуЛистуРаботника);

	СтруктураТаблицУСН = Новый Структура;
	СтруктураТаблицУСН.Вставить("ТаблицаРасчетов",                ТаблицаВзаиморасчеты);
	СтруктураТаблицУСН.Вставить("ТаблицаВыплатаЗарплаты",         ТаблицаВыплатаЗарплаты);
	СтруктураТаблицУСН.Вставить("ТаблицаПеречислениеНДФЛ",        ПараметрыПроведения.ПеречислениеНДФЛ);
	СтруктураТаблицУСН.Вставить("ТаблицаВыплатаДепонентов",       ТаблицаВыплатаДепонентов);
	СтруктураТаблицУСН.Вставить("ТаблицаВыплатаДоговоровПодряда", ТаблицаВыплатаДоговоровПодряда);
	СтруктураТаблицУСН.Вставить("ТаблицаВыплатаСамозанятым",      ПараметрыПроведения.ВыплатыСамозанятым);
	СтруктураТаблицУСН.Вставить("ТаблицаПеречислениеПоИсполнительномуЛисту", ТаблицаПеречислениеПоИсполнительномуЛисту);
	СтруктураТаблицУСН.Вставить("ТаблицаУплатыПоЕдиномуНалоговомуСчету", Неопределено);

	ТаблицаУплатаТорговогоСбораУСН = УчетУСН.ПодготовитьТаблицуУплатыТорговогоСбораДляРаздела5КУДиР(
	ПараметрыПроведения.РасшифровкаПлатежаПрочее, ПараметрыПроведения.Реквизиты);

	// Разметка операций Автоматизированной УСН
	ТаблицаРазметкиАУСН = РазметкаОперацийАУСН.ПодготовитьТаблицуСписаниеДенежныхСредств(
	ПараметрыПроведения.РеквизитыАУСН,
	ТаблицаВзаиморасчеты,
	ПараметрыПроведения.ПеречислениеЗарплаты,
	ПараметрыПроведения.РазметкаАУСНБанка);

	// Учет доходов и расходов ИП
	ТаблицыОплатыПоставщикуИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицыОплатыПоставщику(
	ТаблицаВзаиморасчеты,
	ПараметрыПроведения.Реквизиты);

	ТаблицаОплатыОСиНМА = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуОплатыОСиНМА(
	ТаблицыОплатыПоставщикуИП, 
	ПараметрыПроведения.Реквизиты);

	ТаблицаУслугИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуПоступленияМПЗ(
	ПараметрыПроведения.ПоступлениеМПЗИПТаблицаУслуг,
	ПараметрыПроведения.ПоступлениеМПЗИПРеквизиты);

	СтруктураТаблицМПЗ = Новый Структура("ТаблицаУслуг", ТаблицаУслугИП);

	ТаблицыУплатаНалогов = Новый Структура;
	ТаблицыУплатаНалогов.Вставить("ПеречислениеНалогов",                   ПараметрыПроведения.РасшифровкаПлатежаПрочее);
	ТаблицыУплатаНалогов.Вставить("ПеречислениеНДФЛ",                      ПараметрыПроведения.ПеречислениеНДФЛ);
	ТаблицыУплатаНалогов.Вставить("ПеречислениеНалогаИП",                  ПараметрыПроведения.ПеречислениеНалогаИПТаблица);
	ТаблицыУплатаНалогов.Вставить("ТаблицаУплатыПоЕдиномуНалоговомуСчету", Неопределено);

	ТаблицыУплатыВзносовФОТИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицыУплатыВзносовФОТ(
	ТаблицыУплатаНалогов, ПараметрыПроведения.Реквизиты);

	ТаблицыВыплатыЗарплатыИП   = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицыВыплатыЗарплаты(
	ПараметрыПроведения.ПеречислениеЗарплаты, ПараметрыПроведения.Реквизиты);

	ТаблицыВыплатыДепонентовИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицыВыплатыЗарплаты(
	ПараметрыПроведения.ПеречислениеДепонентов, ПараметрыПроведения.Реквизиты);

	ТаблицыВыплатыПоДоговорамПодрядаИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицыВыплатыЗарплаты(
	ПараметрыПроведения.ОплатаПоДоговорамПодряда, ПараметрыПроведения.Реквизиты);

	ТаблицаУдержанийИП = УчетЗарплаты.ПолучитьТаблицуПрочихУдержанийИП(ПараметрыПроведения.РасшифровкаПлатежаПрочее,
	ПараметрыПроведения.ПеречислениеПоИсполнительномуЛистуРаботникаИП);
	ТаблицыПрочихУдержанийИП   = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицыПеречислениеПрочихУдержаний(
	ТаблицаУдержанийИП, ПараметрыПроведения.Реквизиты);

	ТаблицыВыплатаДивидендовФизическомуЛицу = УчетЗарплаты.ПодготовитьТаблицуВыплатаДивидендов(
	ПараметрыПроведения.ВыплатаДивидендов);

	Если Не ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		ТаблицаСтатусовСчетов = СтатусыДокументов.ПодготовитьТаблицуСтатусовОплатыСчетов(
		ПараметрыПроведения.ОплатаСчетов, ПараметрыПроведения.Реквизиты);
	КонецЕсли;

	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ

	УчетВзаиморасчетов.СформироватьДвиженияПогашениеЗадолженности(ТаблицаВзаиморасчеты,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	ВыплатыСамозанятым.СформироватьДвиженияВыплатыСамозанятым(ПараметрыПроведения.ВыплатыСамозанятым,
	ПараметрыПроведения.РеквизитыВыплатыСамозанятым, Движения, Отказ);

	ВыплатыСамозанятым.СформироватьДвиженияЗадолженностиСамозанятыхПоРеестру(ПараметрыПроведения.ВыплатыСамозанятым,
	ПараметрыПроведения.РеквизитыВыплатыСамозанятым, Движения, Отказ);
	ВыплатыСамозанятым.УстановитьСостояниеДокументаПриОплате(
	ПараметрыПроведения.РеквизитыВыплатыСамозанятым, Движения, Отказ);

	НачалоПростогоУчета = ЕдиныйНалоговыйСчет.НачалоПростогоУчета();
	Если Дата < НачалоПростогоУчета Тогда
		ЕдиныйНалоговыйСчет.ЗарегистрироватьЗачислениеНаЕдиныйНалоговыйСчет(ПараметрыПроведения.Реквизиты,
		ПараметрыПроведения.РасшифровкаПлатежаПрочее, Движения, Отказ);
	КонецЕсли;

	УчетДоходовРасходов.СформироватьДвиженияСуммовыеРазницыРасчетыВУЕ(ТаблицаСуммовыхРазниц,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);
#Удаление
	УчетЗарплаты.СформироватьДвиженияПеречислениеВыплатаЗарплаты(ЭтотОбъект, ПараметрыПроведения.ПеречислениеЗарплаты,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);
#КонецУдаления
	УчетЗарплаты.СформироватьДвиженияОплатаПоДоговорамПодряда(ЭтотОбъект, ПараметрыПроведения.ОплатаПоДоговорамПодряда,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетЗарплаты.СформироватьДвиженияВыплатаДепонентов(ПараметрыПроведения.ПеречислениеДепонентов,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УставныйКапитал.СформироватьДвиженияВыплатаДивидендов(ПараметрыПроведения.ВыплатаДивидендов,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетЗарплаты.СформироватьДвиженияВыплатаДивидендов(ТаблицыВыплатаДивидендовФизическомуЛицу,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДенежныхСредств.СформироватьДвиженияПрочееСписание(ПараметрыПроведения.РасшифровкаПлатежаПрочее,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетЗарплаты.СформироватьДвиженияПоНалогамИВзносамСФОТ(ПараметрыПроведения.Реквизиты,
	ТаблицыУплатаНалогов, Движения, Отказ);

	УчетЗарплаты.СформироватьДвиженияПеречислениеПоИсполнительномуЛистуРаботника(ПараметрыПроведения.ПеречислениеПоИсполнительномуЛистуРаботника,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетНДС.СформироватьДвиженияКурсовыеРазницыНалоговыйАгент(
	ПараметрыПроведения.Реквизиты, ТаблицаВзаиморасчеты, Движения, Отказ);

	УчетНДСРаздельный.СформироватьДвиженияКурсовыеРазницыНалоговыйАгент(
	ПараметрыПроведения.Реквизиты, ТаблицаВзаиморасчеты, Движения, Отказ);

	НалоговыйУчетУСН.СформироватьДвиженияУСН(ЭтотОбъект, СтруктураТаблицУСН);

	УчетУСН.СформироватьДвиженияКУДиРРаздел5(ТаблицаУплатаТорговогоСбораУСН,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	РазметкаОперацийАУСН.СформироватьДвиженияРазметки(ПараметрыПроведения.РеквизитыАУСН,
	ТаблицаРазметкиАУСН, Движения);

	СтатусыДокументов.СформироватьДвиженияОплатаСчетов(
	ПараметрыПроведения.ОплатаСчетов, ПараметрыПроведения.Реквизиты, Движения, Отказ);

	// Учет доходов и расходов ИП
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияОплатаПоставщику(
	ТаблицыОплатыПоставщикуИП, ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияВыплатыСамозанятым(
	ПараметрыПроведения.ВыплатыСамозанятымИП,
	ПараметрыПроведения.РеквизитыВыплатыСамозанятым, Движения, Отказ);

	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияПоступлениеМПЗ(СтруктураТаблицМПЗ,
	ПараметрыПроведения.ПоступлениеМПЗИПТаблицаВзаиморасчетов,,
	ПараметрыПроведения.ПоступлениеМПЗИПРеквизиты, Движения, Отказ);

	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияУплатаНДФЛ(
	ТаблицыУплатаНалогов,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияУплатаВзносовФОТ(
	ТаблицыУплатыВзносовФОТИП,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияВыплатаЗарплаты(
	ТаблицыВыплатыЗарплатыИП.СписокВыплат, ТаблицыВыплатыЗарплатыИП.СписокПереплат,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияВыплатаЗарплаты(
	ТаблицыВыплатыПоДоговорамПодрядаИП.СписокВыплат, ТаблицыВыплатыПоДоговорамПодрядаИП.СписокПереплат,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияВыплатаЗарплаты(
	ТаблицыВыплатыДепонентовИП.СписокВыплат, ТаблицыВыплатыДепонентовИП.СписокПереплат,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияПеречислениеПрочихУдержаний(
	ТаблицыПрочихУдержанийИП.СписокОплаченныхРасчетов, ТаблицыПрочихУдержанийИП.СписокОплаченныхУдержаний,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовИРасходовПредпринимателя.РегистрацияСведенийОбОплатеОСиНМА(
	ТаблицаОплатыОСиНМА, ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияРасходыНаУплатуПроцентовПоЗаймам(
	ПараметрыПроведения.РасходыНаУплатуПроцентовПоЗаймам,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	// ПЕРЕОЦЕНКА ВАЛЮТНЫХ ОСТАТКОВ
	ТаблицаПереоценка = УчетДоходовРасходов.ПодготовитьТаблицуПереоценкиДвиженийДокумента(
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовРасходов.СформироватьДвиженияПереоценкаВалютныхОстатков(ТаблицаПереоценка,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовРасходов.СформироватьДвиженияРасчетПереоценкиВалютныхСредств(ТаблицаПереоценка,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетУСН.СформироватьДвиженияПереоценкаВалютныхОстатков(ТаблицаПереоценка,
	ПараметрыПроведения.Реквизиты, Движения, Отказ);

	Если НЕ ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		СтатусыДокументов.СформироватьДвиженияСтатусовДокументов(
		ТаблицаСтатусовСчетов, ПараметрыПроведения.Реквизиты);
	КонецЕсли;

	// Отложенные расчеты с контрагентами.
	УчетВзаиморасчетовОтложенноеПроведение.ЗарегистрироватьОтложенныеРасчетыСКонтрагентами(
	ЭтотОбъект, Отказ, ПараметрыПроведения.РасчетыСКонтрагентамиОтложенноеПроведение);

	// Регистрация в последовательности
	РаботаСПоследовательностями.ЗарегистрироватьОтложенныеРасчетыВПоследовательности(
	ЭтотОбъект, Отказ, ПараметрыПроведения.РасчетыСКонтрагентамиОтложенноеПроведение);

	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);

	ТаблицаРасчетаНДФЛАУСН = УчетАУСН.ПодготовитьТаблицуРасчетНДФЛ(ПараметрыПроведения.ПеречислениеЗарплаты,
	ПараметрыПроведения.ОплатаПоДоговорамПодряда, ПараметрыПроведения.Реквизиты);
	УчетАУСН.СформироватьДвиженияНДФЛ(ПараметрыПроведения.РеквизитыРасчетаНДФЛАУСН,
	ТаблицаРасчетаНДФЛАУСН, Движения);
	
	#Вставка
	//+ИИТ_Аксеньюшкин
	Если РасшифровкаПлатежа.Количество() > 0 Тогда

		Для Каждого СтрокаРасшифровки ИЗ РасшифровкаПлатежа Цикл

			Если ЗначениеЗаполнено(СтрокаРасшифровки.ДокументПланирования) И СтрокаРасшифровки.ДокументПланирования.ИИТ_ОтразитьБДДС Тогда
				//В таком случае движений не делаем, т.к. их сделала заявка
				Продолжить;
			КонецЕсли;

			Движение = Движения.ИИТ_ДенежныеСредстваОбъектовСтроительства.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.БанковскийСчет = СчетОрганизации;
			Движение.ЭтапСтроительства = СтрокаРасшифровки.Проект;
			Движение.ОбъектСтроительства = ПолучитьОбъектСтроительстваМОРС(СтрокаРасшифровки.Проект);
			Движение.СуммаБДДС = СтрокаРасшифровки.СуммаПлатежа;
			Движение.СтатьяДДС = ?(ЗначениеЗаполнено(СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств), СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств, СтатьяДвиженияДенежныхСредств);
			Если Движение.СтатьяДДС.Депозит Тогда
				Движение.Депозит = -СтрокаРасшифровки.СуммаПлатежа;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(СтрокаРасшифровки.ДокументПланирования) Тогда
				Движение.Сумма = СтрокаРасшифровки.СуммаПлатежа;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаРасшифровки.ДокументПланирования) И ЕстьЗаписиРезервов(СтрокаРасшифровки.ДокументПланирования) Тогда
				Движение = Движения.ИИТ_РезервыДенежныхСредств.ДобавитьРасход();
				Движение.Период = Дата;
				//Движение.БанковскийСчет = СчетОрганизации;
				Движение.БанковскийСчет = СчетРезерва(СтрокаРасшифровки.ДокументПланирования);
				Движение.ОбъектСтроительства = ПолучитьОбъектСтроительстваМОРС(СтрокаРасшифровки.Проект);
				Движение.Заявка = СтрокаРасшифровки.ДокументПланирования;
				Движение.Сумма = СтрокаРасшифровки.СуммаПлатежа;
				Движение.СтатьяДДС = ?(ЗначениеЗаполнено(СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств), СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств, СтатьяДвиженияДенежныхСредств);
			КонецЕсли;

		КонецЦикла;
		
	Иначе
		
		Если ЗначениеЗаполнено(ДокументПланирования) И ДокументПланирования.ИИТ_ОтразитьБДДС Тогда
			
			//В таком случае движений не делаем, т.к. их сделала заявка
			
		Иначе

			Движение = Движения.ИИТ_ДенежныеСредстваОбъектовСтроительства.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.БанковскийСчет = СчетОрганизации;
			Движение.ЭтапСтроительства = Проект;
			Движение.ОбъектСтроительства = ПолучитьОбъектСтроительстваМОРС(Проект);
			Движение.СуммаБДДС = СуммаДокумента;
			Движение.СтатьяДДС = СтатьяДвиженияДенежныхСредств;
			Если Движение.СтатьяДДС.Депозит Тогда
				Движение.Депозит = -СуммаДокумента;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДокументПланирования) Тогда
				Движение.Сумма = СуммаДокумента;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДокументПланирования) И ЕстьЗаписиРезервов(ДокументПланирования) Тогда
				Движение = Движения.ИИТ_РезервыДенежныхСредств.ДобавитьРасход();
				Движение.Период = Дата;
				Движение.БанковскийСчет = СчетОрганизации;
				Движение.ОбъектСтроительства = ПолучитьОбъектСтроительстваМОРС(Проект);
				Движение.СтатьяДДС = СтатьяДвиженияДенежныхСредств;
				Движение.Заявка = ДокументПланирования;
				Движение.Сумма = СуммаДокумента;
			КонецЕсли;

		КонецЕсли;
		
	КонецЕсли;
	
	Движения.ИИТ_ДенежныеСредстваОбъектовСтроительства.Записать();
	Движения.ИИТ_РезервыДенежныхСредств.Записать();
	
	ЗаявкаНаРасходДС = Неопределено;
	Если ЗначениеЗаполнено(ДокументПланирования) Тогда
		ЗаявкаНаРасходДС = ДокументПланирования;
	ИначеЕсли РасшифровкаПлатежа.Количество() > 0 И ЗначениеЗаполнено(РасшифровкаПлатежа[0].ДокументПланирования) Тогда
		ЗаявкаНаРасходДС = РасшифровкаПлатежа[0].ДокументПланирования;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗаявкаНаРасходДС) И ЗаявкаНаРасходДС.Контрагент <> Контрагент Тогда

		Для Каждого СтрокаДвижений ИЗ Движения.Хозрасчетный Цикл
			Если Лев(СтрокаДвижений.СчетДт.Код, 2) = "60" Тогда
				СтрокаДвижений.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты] = ЗаявкаНаРасходДС.Контрагент;
				СтрокаДвижений.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры] = ЗаявкаНаРасходДС.ДоговорКонтрагента;
			КонецЕсли;
		КонецЦикла;
		
		Движения.Хозрасчетный.Записать();
		
	КонецЕсли;
	
	Если Найти(НазначениеПлатежа, "патент") > 0 Тогда

		МассивСлов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(НазначениеПлатежа);
		ПатентСерия = МассивСлов[6];
		ПатентНомер = МассивСлов[7];
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ФизическиеЛица.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|ГДЕ
		|	ФизическиеЛица.ПатентСерия = &ПатентСерия
		|	И ФизическиеЛица.ПатентНомер = &ПатентНомер";
		Запрос.УстановитьПараметр("ПатентСерия", ПатентСерия);
		Запрос.УстановитьПараметр("ПатентНомер", ПатентНомер);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			НачальнаяДатаОплаченоДо = ИИТ_ВспомогательныеФункцииСервер.ПолучитьНачальнуюДатуОплаченоДоПатента(Выборка.Ссылка);
			Если Дата > НачальнаяДатаОплаченоДо Тогда

				Движение = РегистрыСведений.ИИТ_ОплатыПатентов.СоздатьМенеджерЗаписи();
				Движение.ФизическоеЛицо = Выборка.Ссылка;
				Движение.ДокументОплаты = Ссылка;
				Движение.ДатаОплаты = ДобавитьМесяц(ИИТ_ВспомогательныеФункцииСервер.ПолучитьДатуОплаты(Выборка.Ссылка, Ссылка), 1);
				Движение.Записать(Истина);
				
			КонецЕсли;

		КонецЕсли;
		
	КонецЕсли;
	
	//ТЕПЕРЬ ЗАКРЫВАЕМ ЗАДАЧИ ПО ЗАЯВКЕ
	Для Каждого СтрокаРасшифровки Из РасшифровкаПлатежа Цикл

		Если ЗначениеЗаполнено(СтрокаРасшифровки.ДокументПланирования) И Проведен И Не СтатусОплатыУжеУстановлен(СтрокаРасшифровки.ДокументПланирования) Тогда

			НоваяЗапись = РегистрыСведений.РегистрСостоянийОбъектов.СоздатьМенеджерЗаписи();
			НоваяЗапись.Объект = СтрокаРасшифровки.ДокументПланирования;
			Если ДокументУтвержден(СтрокаРасшифровки.ДокументПланирования) Тогда
				НоваяЗапись.СостояниеОбъекта = Перечисления.СостоянияСогласования.УдалитьОплачено;
			Иначе
				НоваяЗапись.СостояниеОбъекта = Перечисления.СостоянияСогласования.ИИТ_ОплаченоБезСогласования;
				ЗакрытьТекущиеЗадачиПоЗаявке(СтрокаРасшифровки.ДокументПланирования);
			КонецЕсли;
			НоваяЗапись.Период = ТекущаяДата();
			НоваяЗапись.Записать(Истина);
			
			ЭтапПроцессаСсылка = ПолучитьЭтапПроцесса(СтрокаРасшифровки.ДокументПланирования);
			Если ЗначениеЗаполнено(ЭтапПроцессаСсылка) Тогда
				НаборЗаписей = РегистрыСведений.ЗадачиУниверсальныхПроцессов.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.СвязанныйОбъект.Установить(ЭтапПроцессаСсылка);
				НаборЗаписей.Прочитать();
				НаборЗаписей.Очистить();
				НаборЗаписей.Записать(Истина);
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;
	
	Если ЗначениеЗаполнено(ДокументПланирования) И Проведен И Не СтатусОплатыУжеУстановлен(ДокументПланирования) Тогда
		
		НоваяЗапись = РегистрыСведений.РегистрСостоянийОбъектов.СоздатьМенеджерЗаписи();
		НоваяЗапись.Объект = ДокументПланирования;
		Если ДокументУтвержден(ДокументПланирования) Тогда
			НоваяЗапись.СостояниеОбъекта = Перечисления.СостоянияСогласования.УдалитьОплачено;
		Иначе
			НоваяЗапись.СостояниеОбъекта = Перечисления.СостоянияСогласования.ИИТ_ОплаченоБезСогласования;
			ЗакрытьТекущиеЗадачиПоЗаявке(ДокументПланирования);
		КонецЕсли;
		НоваяЗапись.Период = ТекущаяДата();
		НоваяЗапись.Записать(Истина);
		
		ЭтапПроцессаСсылка = ПолучитьЭтапПроцесса(ДокументПланирования);
		Если ЗначениеЗаполнено(ЭтапПроцессаСсылка) Тогда
			НаборЗаписей = РегистрыСведений.ЗадачиУниверсальныхПроцессов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.СвязанныйОбъект.Установить(ЭтапПроцессаСсылка);
			НаборЗаписей.Прочитать();
			НаборЗаписей.Очистить();
			НаборЗаписей.Записать(Истина);
		КонецЕсли;
		
	КонецЕсли;
	//-ИИТ_Аксеньюшкин
	
	Движения.АС_БюджетРасходов.Записывать = Истина;
	Для Каждого СтрокаРасшифровки Из РасшифровкаПлатежа Цикл	    
		
		//уберем ограничения
		//Если СтрНайти(Ссылка.НазначениеПлатежа, "Пополнение р/с") > 0
		//	Или СтрНайти(Ссылка.НазначениеПлатежа, "Пополнение расчетного счета") > 0
		//	Или СтрНайти(Ссылка.НазначениеПлатежа, "Перевод средств") > 0  
		//	Или СтрНайти(Ссылка.НазначениеПлатежа, "Возврат ошибочно") > 0 
		//	Или СтрНайти(Ссылка.НазначениеПлатежа, "Возмещение ранее") > 0 Тогда
		//	Продолжить;	
		//КонецЕсли;
		
		ТекДоговор	= Ссылка.ДоговорКонтрагента;
		
		Если НЕ ЗначениеЗаполнено(ТекДоговор) Тогда
			Если ЗначениеЗаполнено(СтрокаРасшифровки.ДокументПланирования) Тогда 
				ТекДоговор = ДокументПланирования.ДоговорКонтрагента;
			КонецЕсли;	
		КонецЕсли;
	
		Если НЕ ЗначениеЗаполнено(ТекДоговор) Тогда   
			Если ЗначениеЗаполнено(Ссылка.ДокументПланирования) Тогда 
				Если ЗначениеЗаполнено(Ссылка.ДокументПланирования.ДоговорКонтрагента) Тогда  
					ТекДоговор = Ссылка.ДокументПланирования.ДоговорКонтрагента;
				КонецЕсли;
			КонецЕсли;	
		КонецЕсли;
		 		
		Если НЕ ЗначениеЗаполнено(ТекДоговор) Тогда 
			//Продолжить;
		КонецЕсли;
		
		ТекЭтап 				= СтрокаРасшифровки.Проект; 
		ТекОбъектСтроительства 	= ИИТ_ВспомогательныеФункцииСервер.МаксимальныйУровеньСправочника(ТекЭтап);
		
		Если НЕ ЗначениеЗаполнено(ТекОбъектСтроительства) Тогда  
			Если ЗначениеЗаполнено(Ссылка.Проект) Тогда
				ТекЭтап 				= Ссылка.Проект;
				ТекОбъектСтроительства 	= ИИТ_ВспомогательныеФункцииСервер.МаксимальныйУровеньСправочника(ТекЭтап); 	
			КонецЕсли; 
			Если НЕ ЗначениеЗаполнено(ТекОбъектСтроительства) Тогда 
				//Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Движение = Движения.АС_БюджетРасходов.ДобавитьРасход(); 
		Движение.ОбъектСтроительства 	= ТекОбъектСтроительства;
		Движение.Период                 = Дата;
		Движение.Регистратор            = Ссылка;
		Движение.Этап                   = ТекЭтап;
		Движение.Сумма           		= СтрокаРасшифровки.СуммаПлатежа;
		Движение.Договор                = ТекДоговор;
		
	КонецЦикла;

#КонецВставки
КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаПроверкиЗаполнения")
Процедура АЛФ_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	МассивНепроверяемыхРеквизитов = Новый Массив;

	// Отключаем проверку реквизитов шапки.

	Если НЕ Справочники.БанковскиеСчета.ИспользуетсяНесколькоБанковскихСчетовОрганизации(Организация) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СчетОрганизации");
		ПроверкаРеквизитовОрганизации.ОбработкаПроверкиЗаполнения(Организация, СчетОрганизации, Ложь, Отказ);
	КонецЕсли;

	Если НЕ Документы.СписаниеСРасчетногоСчета.КонтрагентДолженБытьЗаполнен(ВидОперации) Тогда

		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");

	КонецЕсли;

	Если ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПереводНаДругойСчет Тогда

		ВалютаСчетаОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетОрганизации, "ВалютаДенежныхСредств");
		ВалютаСчетаКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетКонтрагента, "ВалютаДенежныхСредств");

		Если ВалютаСчетаОрганизации <> ВалютаСчетаКонтрагента Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Валюта счета получателя ""%1"" не совпадает с валютой документа ""%2""'"), ВалютаСчетаКонтрагента, ВалютаСчетаОрганизации);
			ТекстОшибкиЗаполнения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Корректность", НСтр("ru = 'Счет получателя'"),,, ТекстСообщения);

			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиЗаполнения, ЭтотОбъект, "СчетКонтрагента", "Объект", Отказ);
		КонецЕсли;

	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.Депозит Тогда

		ВалютаСчетаОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетОрганизации, "ВалютаДенежныхСредств");
		ВалютаСчетаКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетКонтрагента, "ВалютаДенежныхСредств");
		Если ЗначениеЗаполнено(ВалютаСчетаКонтрагента) И ВалютаСчетаОрганизации <> ВалютаСчетаКонтрагента Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Валюта счета депозита ""%1"" не совпадает с валютой документа ""%2""'"),
			ВалютаСчетаКонтрагента,
			ВалютаСчетаОрганизации);
			ТекстОшибкиЗаполнения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Корректность", НСтр("ru = 'Счет депозита'"),,, ТекстСообщения);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибкиЗаполнения, ЭтотОбъект, "СчетКонтрагента", "Объект", Отказ);
		КонецЕсли;

		МассивНепроверяемыхРеквизитов.Добавить("СчетКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаРасчетовСКонтрагентом");

	Иначе

		МассивНепроверяемыхРеквизитов.Добавить("СчетКонтрагента");

	КонецЕсли;

	Если ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога Тогда

		Если Не Справочники.ВидыНалоговИПлатежейВБюджет.ОрганизацияМожетУплачиватьНалог(Налог, Организация) Тогда

			ДопустимыйВидОрганизации = ?(ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация),
			НСтр("ru = 'физическими лицами'"),
			НСтр("ru = 'организациями'"));

			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Выбранный налог (взнос) уплачивается %1'"), ДопустимыйВидОрганизации);

			ТекстОшибкиЗаполнения  = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Корректность", НСтр("ru = 'Налог'"),,, ТекстСообщения);

			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиЗаполнения, ЭтотОбъект, "Налог", "Объект", Отказ);

		КонецЕсли;

		Если ЗначениеЗаполнено(Налог)
			И ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоНДФЛ(Налог.ВидНалога)
			И ПеречислениеНДФЛ.Количество() > 0 Тогда

			УчетЗарплатыИКадровСредствамиБухгалтерии = Константы.УчетЗарплатыИКадровСредствамиБухгалтерии.Получить();
			Если ПеречислениеНДФЛ.Итог("Сумма") <> СуммаДокумента Тогда
				Если УчетЗарплатыИКадровСредствамиБухгалтерии Тогда
					ШаблонСообщения = НСтр("ru = 'Не совпадают сумма документа и ее расшифровка по сотрудникам'");
				Иначе
					ШаблонСообщения = НСтр("ru = 'Не совпадают сумма документа и ее расшифровка'");
				КонецЕсли;
				ТекстСообщения  = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Корректность", НСтр("ru = 'Сумма документа'"),,, ШаблонСообщения); 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, "НадписьСуммаДокумента",, Отказ);
			КонецЕсли;

			Если НЕ УчетЗарплатыИКадровСредствамиБухгалтерии Тогда
				МассивНепроверяемыхРеквизитов.Добавить("ПеречислениеНДФЛ.ФизическоеЛицо");
				МассивНепроверяемыхРеквизитов.Добавить("ПеречислениеНДФЛ.Сумма");
			КонецЕсли;
		КонецЕсли;

		Если СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.НДСНалоговогоАгента
			И НалоговыйАгентНДС.Количество() = 0 Тогда

			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Список", "Заполнение",,, НСтр("ru = 'Расшифровка платежа'"),); 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения, Ссылка, "ПорядокОтраженияБУ",, Отказ);

		КонецЕсли;

		Если ЗначениеЗаполнено(НалоговыйПериод) Тогда
			КорректныйПериод = ОбщегоНазначенияБПСобытия.КорректныйПериодВводаДокументов();
			Если НалоговыйПериод < КорректныйПериод.НачалоКорректногоПериода
				Или НалоговыйПериод > КорректныйПериод.КонецКорректногоПериода Тогда
				ТекстСообщения  = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность", НСтр("ru = 'Период'"));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "НалоговыйПериод", "Объект", Отказ);
			КонецЕсли;
		КонецЕсли;

		Если СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ВидНалоговогоОбязательства");
		КонецЕсли;

	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("Налог");
		МассивНепроверяемыхРеквизитов.Добавить("ВидНалоговогоОбязательства");
	КонецЕсли;

	Если ВидОперации <> Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалогаЗаТретьихЛиц Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Налогоплательщик");
	КонецЕсли;

	// Заполнение регистрации проверяется при необходимости только в форме документа.
	МассивНепроверяемыхРеквизитов.Добавить("РегистрацияВНалоговомОргане");

	Если ВидОперации <> Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВозвратЗайма
		ИЛИ ВидОперации <> Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВозвратКредита Тогда
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.ВидПлатежаПоКредитамЗаймам");
	КонецЕсли;

	УчетБезДокументовВозможен = УчетКассовымМетодом.УчетБезЗакрывающихДокументовВозможен(Организация, Дата);
	Если Не УчетБезДокументовВозможен И БезЗакрывающихДокументов Тогда
		ТекстСообщения = НСтр("ru = 'При выбранных настройках Учет без закрывающих документов невозможен'");
		ТекстОшибкиЗаполнения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
		"Поле", "Корректность", НСтр("ru = 'Расчеты'"),,, ТекстСообщения);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибкиЗаполнения, , "РасчетыПриОплате", , Отказ);
	КонецЕсли;

	СообщенияПроверки = Документы.СписаниеСРасчетногоСчета.ПодготовитьСообщенияПроверкиЗаполненияРасшифровкаПлатежа(
	ЭтотОбъект,, Отказ, ПроверяемыеРеквизиты, Ложь); // Счета учета ТЧ РасшифровкиПлатежа проверяются ниже вместе с другими реквизитам документа.
	Документы.СписаниеСРасчетногоСчета.ОбработкаПроверкиЗаполненияПеречислениеЗаработнойПлаты(
	ЭтотОбъект,, Отказ, ПроверяемыеРеквизиты);
	Документы.СписаниеСРасчетногоСчета.ОбработкаПроверкиЗаполненияПеречислениеДепонентов(ЭтотОбъект,, Отказ, ПроверяемыеРеквизиты);

	Если ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВыплатыСамозанятым И Не РучнаяКорректировка Тогда
		Если ЗначениеЗаполнено(РеестрВыплатСамозанятым) Тогда
			ЮрФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ЮридическоеФизическоеЛицо");
			Если ЮрФизЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
				СуммаЗадолженности = РегистрыНакопления.ЗадолженностьСамозанятыхПоРеестру.СуммаЗадолженностиПоСамозанятому(
				Ссылка, РеестрВыплатСамозанятым, Контрагент, СчетКонтрагента);
				Если СуммаЗадолженности = 0 Тогда
					ТекстСообщения = НСтр("ru = 'Самозанятому уже была выплата по этому реестру'");
					ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Поле", "Корректность", НСтр("ru = 'Реестр выплат'"), , , ТекстСообщения);
					ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения, ЭтотОбъект, "РеестрВыплатСамозанятым", "Объект", Отказ);
				ИначеЕсли СуммаЗадолженности - СуммаДокумента < 0 Тогда
					ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Задолженность самозанятого по этому реестру %1 руб.'"), СуммаЗадолженности);
					ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Поле", "Корректность", НСтр("ru = 'Сумма документа'"), , , ТекстСообщения);
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "СуммаДокумента", "Объект", Отказ);
				КонецЕсли;
			Иначе
				СуммаЗадолженности = РегистрыНакопления.ЗадолженностьСамозанятыхПоРеестру.СуммаЗадолженностиПоРеестру(
				Ссылка, РеестрВыплатСамозанятым);
				Если СуммаЗадолженности < СуммаДокумента Тогда
					ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'Сумма не соответствует реестру выплат самозанятым, размер задолженности %1 руб.'"), СуммаЗадолженности);
					ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Поле", "Корректность", НСтр("ru = 'Сумма документа'"), , , ТекстСообщения);
					ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения, ЭтотОбъект, "СуммаДокумента", "Объект", Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаРасчетовСКонтрагентом");
	ИначеЕсли ВидОперации <> Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВыплатыСамозанятым
		Или РучнаяКорректировка Тогда
		МассивНепроверяемыхРеквизитов.Добавить("РеестрВыплатСамозанятым");
	КонецЕсли;

	// Отключаем проверку реквизитов, связанных с перечислением заработной платы.
	// Если установлен флаг ручная корректировка - то заполненность полей по заработной плате не проверяем.

	Если (ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику
		Или ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП
		Или ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда
		Или ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда)
		И НЕ РучнаяКорректировка Тогда

		ПроверятьСоответствиеСуммыДокументаИВедомости = Документы.СписаниеСРасчетногоСчета.ПроверятьСоответствиеСуммыДокументаИВедомостиПоЗарплате()
		Или (ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда
		И ЗначениеЗаполнено(ПлатежнаяВедомость))
		Или ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда;

		Если ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП
			Или ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда
			Или ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ПлатежнаяВедомость");
			Если ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП
				И Не ПроверятьСоответствиеСуммыДокументаИВедомости Тогда
				МассивНепроверяемыхРеквизитов.Добавить("ПеречислениеЗаработнойПлаты.Ведомость");
			КонецЕсли;
		Иначе
			МассивНепроверяемыхРеквизитов.Добавить("ПеречислениеЗаработнойПлаты");
			МассивНепроверяемыхРеквизитов.Добавить("ПеречислениеЗаработнойПлаты.Ведомость");
			МассивНепроверяемыхРеквизитов.Добавить("ПеречислениеЗаработнойПлаты.СуммаПлатежа");
			Если НЕ ПроверятьСоответствиеСуммыДокументаИВедомости Тогда
				МассивНепроверяемыхРеквизитов.Добавить("ПлатежнаяВедомость");
			КонецЕсли;
		КонецЕсли;

	Иначе

		ПроверятьСоответствиеСуммыДокументаИВедомости = Ложь;
		МассивНепроверяемыхРеквизитов.Добавить("ПеречислениеЗаработнойПлаты");
		МассивНепроверяемыхРеквизитов.Добавить("ПеречислениеЗаработнойПлаты.Ведомость");
		МассивНепроверяемыхРеквизитов.Добавить("ПеречислениеЗаработнойПлаты.СуммаКВыплате");
		МассивНепроверяемыхРеквизитов.Добавить("ПлатежнаяВедомость");

	КонецЕсли;

	// Проверка соответствия суммы документа расшифровке платежа.

	Если Документы.СписаниеСРасчетногоСчета.ЕстьРасшифровкаПлатежа(ВидОперации) Тогда

		Если РасшифровкаПлатежа.Итог("СуммаПлатежа") <> СуммаДокумента Тогда
			ТекстСообщения = НСтр("ru = 'Не совпадают сумма документа и ее расшифровка'");
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Корректность", НСтр("ru = 'Сумма документа'"),,, ТекстСообщения);
			Если РасшифровкаПлатежа.Количество() > 1 Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, "НадписьСуммаДокумента",, Отказ);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "СуммаДокумента", "Объект", Отказ);
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;

	// Проверка соответствия суммы документа и ведомостей на выплату зарплаты.

	Если (ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП
		Или ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда)
		И ПроверятьСоответствиеСуммыДокументаИВедомости Тогда

		Ведомости = ОбщегоНазначения.ВыгрузитьКолонку(ПеречислениеЗаработнойПлаты, "Ведомость", Истина);
		Если Ведомости.Количество() > 0 Тогда
			ТаблицаВедомостей = УчетЗарплаты.ПолучитьДанныеВедомостейДляОплатыТаблично(Ссылка, Ведомости);
			Если ПеречислениеЗаработнойПлаты.Итог("СуммаПлатежа") <> СуммаДокумента
				Или ТаблицаВедомостей.Итог("СуммаКВыплате") + ТаблицаВедомостей.Итог("КомпенсацияЗаЗадержкуЗарплаты") <> СуммаДокумента Тогда
				ШаблонСообщения = НСтр("ru = 'Не совпадают сумма документа и общая сумма по платежным ведомостям'");
				ТекстСообщения  = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Корректность", НСтр("ru = 'Сумма документа'"),,, ШаблонСообщения);
				Если ПеречислениеЗаработнойПлаты.Количество() > 1 Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, "НадписьСуммаДокумента",, Отказ);
				Иначе
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "СуммаДокумента", "Объект", Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;

	// Проверка соответствия суммы документа и ведомости на выплату зарплаты.

	Если ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику
		ИЛИ ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда Тогда

		Если ПроверятьСоответствиеСуммыДокументаИВедомости Тогда
			Если ЗначениеЗаполнено(ПлатежнаяВедомость) И ЗначениеЗаполнено(Контрагент) Тогда

				Ведомости = Новый Массив;
				Ведомости.Добавить(ПлатежнаяВедомость);

				ТаблицаНеоплаченныхВедомостей = УчетЗарплаты.ПолучитьДанныеВедомостейДляОплатыТаблично(Ссылка, Ведомости, Контрагент, Ложь);
				ТаблицаОплаченныхВедомостей   = УчетЗарплаты.ПолучитьДанныеВедомостейДляОплатыТаблично(Ссылка, Ведомости, Контрагент, Истина);

				Если ТаблицаНеоплаченныхВедомостей.Количество() = 0 И ТаблицаОплаченныхВедомостей.Количество() = 0 Тогда

					Если ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда Тогда
						СтрокаУточнениеДохода = НСтр("ru = 'о выплате по договору ГПХ'");
					Иначе
						СтрокаУточнениеДохода = НСтр("ru = 'о заработной плате'");
					КонецЕсли;

					ШаблонСообщения = НСтр("ru = 'В выбранной ведомости отсутствуют сведения %1 сотрудника %2.'");
					ШаблонСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, СтрокаУточнениеДохода, Контрагент);
					ТекстСообщения  = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Поле", "Корректность", НСтр("ru = 'Ведомость'"),,, ШаблонСообщения);
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ПлатежнаяВедомость", "Объект", Отказ);

				Иначе

					Если ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда Тогда
						СтрокаУточнениеДохода = НСтр("ru = 'сумма дохода по договору ГПХ'");
					Иначе
						СтрокаУточнениеДохода = НСтр("ru = 'заработная плата'");
					КонецЕсли;

					Если ТаблицаОплаченныхВедомостей.Количество() <> 0 Тогда
						ШаблонСообщения = НСтр("ru = 'По текущей ведомости %1 сотрудника %2 выплачена ранее.'");
						ШаблонСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, СтрокаУточнениеДохода, Контрагент);

						ТекстСообщения  = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
						"Поле", "Корректность", НСтр("ru = 'Ведомость'"),,, ШаблонСообщения);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ПлатежнаяВедомость", "Объект", Отказ);
					Иначе
						СуммаПоВедомости = ТаблицаНеоплаченныхВедомостей.Итог("СуммаКВыплате") + ТаблицаНеоплаченныхВедомостей.Итог("КомпенсацияЗаЗадержкуЗарплаты");

						Если СуммаПоВедомости <> СуммаДокумента Тогда
							ШаблонСообщения = НСтр("ru = 'Не совпадают сумма документа (%1 руб.) и сумма по платежной ведомости (%2 руб.).'");
							ШаблонСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, СуммаДокумента, СуммаПоВедомости);

							ТекстСообщения  = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
							"Поле", "Корректность", НСтр("ru = 'Сумма документа'"),,, ШаблонСообщения);
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "СуммаДокумента", "Объект", Отказ);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;

			КонецЕсли;

		Иначе
			МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		КонецЕсли;

	КонецЕсли;

	Если ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеДепонентов
		И (ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеСводно")
		И НЕ ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме")
		ИЛИ НЕ ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии")) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
	КонецЕсли;

	Если ВидОперации <> Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеПоИсполнительномуЛистуРаботника Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Сотрудник");
		МассивНепроверяемыхРеквизитов.Добавить("ИсполнительныйЛист");
	ИначеЕсли ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ИсполнительныйЛист");
	КонецЕсли;

	// При УСН или АУСН проверяется заполнение полей для отражения расходов

	ПроверятьОтражениеВУСН = УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Организация, Дата)
	Или УчетнаяПолитика.ПрименяетсяАУСН(Организация, Дата);

	Если ПроверятьОтражениеВУСН И ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПрочееСписание Тогда
		ПроверяемыеРеквизиты.Добавить("ОтражениеВУСН");
		// Сообщение о сумме расходов УСН выведем явно, поскольку синоним реквизита отличается от заголовка поля формы.
		Если ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются И Графа7_УСН = 0 Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Сумма расходов'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Графа7_УСН", "Объект", Отказ);
		КонецЕсли;
	КонецЕсли;

	#Область УХ_Казначейство 
	Если Не ПлатежСКонвертацией И ВалютаДокумента = СчетОрганизации.ВалютаДенежныхСредств Тогда  
		МассивНепроверяемыхРеквизитов.Добавить("КурсНаДатуОперацииСВалютой");
		МассивНепроверяемыхРеквизитов.Добавить("СуммаДокументаВВалютеОтправителя");
	КонецЕсли;
	#КонецОбласти


	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

	СчетаУчетаВДокументах.ПроверитьЗаполнение(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, , СообщенияПроверки, Ложь);

	Документы.СписаниеСРасчетногоСчета.СообщитьРезультатПроверки(
	ЭтотОбъект,
	Отказ,
	СообщенияПроверки,
	Метаданные.Документы.СписаниеСРасчетногоСчета.ТабличныеЧасти.РасшифровкаПлатежа);

	// Для отдельных видов операций некоторые счета проверяются вне зависимости от настроек пользователя,
	// кроме режима Интеграции с банком
	Если ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПрочееСписание
		И Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
		ПроверяемыеРеквизиты.Добавить("СчетУчетаРасчетовСКонтрагентом");
	КонецЕсли;

	// Переопределим представление сообщения об ошибке для случая,
	// когда имя и синоним реквизита не соответствуют его роли.
	Сообщили = Новый Массив;
	Если ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику
		Или ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП Тогда
		Если ПроверяемыеРеквизиты.Найти("СчетУчетаРасчетовСКонтрагентом") <> Неопределено
			И НЕ ЗначениеЗаполнено(СчетУчетаРасчетовСКонтрагентом) Тогда

			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Счет затрат'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "СчетУчетаРасчетовСКонтрагентом", "Объект", Отказ);
			Сообщили.Добавить("СчетУчетаРасчетовСКонтрагентом");

		КонецЕсли;
	КонецЕсли;
	#Вставка
	МассивНепроверяемыхРеквизитов.Добавить("ПлатежнаяВедомость");
	Попытка
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ПлатежнаяВедомость"));
	Исключение
	КонецПопытки;
	#КонецВставки
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, Сообщили);

КонецПроцедуры