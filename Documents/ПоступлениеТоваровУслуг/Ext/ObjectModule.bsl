
&После("ОбработкаПроведения")
Процедура АЛФ_ОбработкаПроведения(Отказ, РежимПроведения) 
	 
	НаборЗаписей2 = РегистрыСведений.АС_ДвижениеМатериалов2_6.СоздатьНаборЗаписей();
	НаборЗаписей2.Отбор.ДокументРегистратор.Установить(Ссылка);
	НаборЗаписей2.Прочитать(); 
	НаборЗаписей2.Очистить();
	
	Для Каждого СтрокаРаспределения Из СопоставленныеТовары Цикл

		Характеристика = ИИТ_ВспомогательныеФункцииСервер.ПолучитьХарактеристикуНоменклатуры_ПВД(СтрокаРаспределения);
		
		СтрЗапись = НаборЗаписей2.Добавить();
		
		СтрЗапись.НоменклатураСнабжения		    = СтрокаРаспределения.Номенклатура;
		СтрЗапись.Номенклатура 					= СтрокаРаспределения.НоменклатураБух;
		СтрЗапись.ХарактеристикаНоменклатуры  	= Характеристика;
		СтрЗапись.НомерСтрокиТЧ 				= СтрокаРаспределения.НомерСтроки;
		СтрЗапись.ДокументРегистратор 			= Ссылка;
		СтрЗапись.КоличествоПоступление 		= СтрокаРаспределения.Количество; 
		СтрЗапись.СуммаСНДСПоступление	 		= СтрокаРаспределения.Сумма;
		
		Отбор = Новый Структура;
		Отбор.Вставить("Номенклатура", СтрокаРаспределения.НоменклатураБух);
		
		тСтрокаТЧТовары = Товары.Найти(СтрокаРаспределения.НоменклатураБух,"Номенклатура");
		
		Если Не тСтрокаТЧТовары = Неопределено Тогда 
			СтрЗапись.СтавкаНДСПоступление 		= тСтрокаТЧТовары.СтавкаНДС; 
			СтрЗапись.СуммаНДСПоступление 		= тСтрокаТЧТовары.СуммаНДС;
			СтрЗапись.ЕдИзмПоступление 			= СтрокаРаспределения.ЕдИзм; 			
			СтрЗапись.СуммаБезНДСПоступление 	= тСтрокаТЧТовары.Сумма;				 
		КонецЕсли;
		
		СтрЗапись.Этап 							= Ссылка.ЭтапСтроительства;		
		СтрЗапись.ЕдиницаИзмерения              = СтрЗапись.ЕдИзмПоступление;
		СтрЗапись.Склад 						= Склад;
		СтрЗапись.МОЛ 							= МОЛ;
	КонецЦикла;
	
	НаборЗаписей2.Записать();
		
	//+ИИТ_Аксеньюшкин	
	Для Каждого СтрокаРаспределения Из СопоставленныеТовары Цикл

		Движение = Движения.ИИТ_ЗаказыПоставщику.ДобавитьРасход();
		Движение.Период = Дата;
		ЗаполнитьЗначенияСвойств(Движение, СтрокаРаспределения);
		Движение.Сумма = СтрокаРаспределения.СуммаБух;

	КонецЦикла;
	
	Движения.ИИТ_ЗаказыПоставщику.Записать();

	//Создаем уведомление для снабжения
	Для Каждого СтрокаТоваров Из Товары Цикл
		Движение = Движения.ИИТ_ОстаткиТТНПоступления.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Номенклатура = СтрокаТоваров.Номенклатура;
		Движение.Поступление = Ссылка;
		Движение.Количество = СтрокаТоваров.Количество;
	КонецЦикла;
	
	Движения.ИИТ_ОстаткиТТНПоступления.Записать();
	
	Для Каждого СтрокаСопост Из СопоставленныеТовары Цикл
		
		Если ЗначениеЗаполнено(СтрокаСопост.Номенклатура.ВидНоменклатуры) И СтрокаСопост.Номенклатура.ВидНоменклатуры.ИИТ_ОСИИнструмент Тогда
			Продолжить;
		КонецЕсли;

		Движение = Движения.ИИТ_ОстаткиТМЦ.ДобавитьПриход();
		Движение.Период 		= Дата;
		Движение.Номенклатура 	= СтрокаСопост.Номенклатура;//ИИТ_ВспомогательныеФункцииСервер.ПолучитьПлоскуюНоменклатуру(СтрокаСопост.Номенклатура, СтрокаСопост.Конфигурация, СтрокаСопост.Цвет, СтрокаСопост.Размер);
		Движение.Конфигурация 	= СтрокаСопост.Конфигурация;
		Движение.Размер 		= СтрокаСопост.Размер;
		Движение.Цвет 			= СтрокаСопост.Цвет;
		Движение.БухгалтерскаяНоменклатура = СтрокаСопост.НоменклатураБух;
		Движение.Склад = Склад;
		
		Если ЗначениеЗаполнено(Движение.Номенклатура) И Не ЗначениеЗаполнено(Движение.Номенклатура.ЕдиницаИзмерения) Тогда
			НомОбъект = Движение.Номенклатура.ПолучитьОбъект();
			НомОбъект.ЕдиницаИзмерения = СтрокаСопост.ЕдИзм;
			НомОбъект.ОбменДанными.Загрузка = Истина;
			НомОбъект.Записать();
		КонецЕсли;

		//Проставляем объект строительства - ищем его из данных заявки, иначе берем из шапки документа поступления
		Если ЗначениеЗаполнено(СтрокаСопост.ЗаказПоставщику) И СтрокаСопост.ЗаказПоставщику.Товары.Количество() > 0 Тогда
			Заявка = СтрокаСопост.ЗаказПоставщику.Товары[0].ИИТ_ЗаявкаНаЗакуп;
			Движение.ОбъектСтроительства = Заявка.ОбъектСтроительства;
		Иначе
			Движение.ОбъектСтроительства = ОбъектСтроительства;
		КонецЕсли;
		
		//Проставляем МОЛа - берем его из табличной части Товары
		НайденныеСтроки = Товары.НайтиСтроки(Новый Структура("Номенклатура", СтрокаСопост.НоменклатураБух));
		Если НайденныеСтроки.Количество() > 0 Тогда
			Движение.МОЛ = НайденныеСтроки[0].ПринимающийМОЛ;
		КонецЕсли;

		Движение.Количество = СтрокаСопост.Количество;
		Движение.Сумма = СтрокаСопост.Сумма;

	КонецЦикла;
	
	Движения.ИИТ_ОстаткиТМЦ.Записать();
	
	//ПРОВЕРЯЕМ, ВСЕ ЛИ ПОЗИЦИИ СОПОСТАВЛЕНЫ
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровУслугТовары.Количество КАК Количество
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.Ссылка = &ДокПоступления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугСопоставленныеТовары.НоменклатураБух,
	|	-ПоступлениеТоваровУслугСопоставленныеТовары.КоличествоБух
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.СопоставленныеТовары КАК ПоступлениеТоваровУслугСопоставленныеТовары
	|ГДЕ
	|	ПоступлениеТоваровУслугСопоставленныеТовары.Ссылка = &ДокПоступления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Номенклатура КАК Номенклатура,
	|	СУММА(ВТ.Количество) КАК Количество
	|ИЗ
	|	ВТ КАК ВТ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВТ.Количество) <> 0";
	Запрос.УстановитьПараметр("ДокПоступления", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ПозицииСопоставлены = (Выборка.Количество() = 0);
	
	НаборЗаписей = РегистрыСведений.ИИТ_УведомленияПоПоступлениям.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДокументПоступления.Установить(Ссылка);
	НаборЗаписей.Прочитать();	
	Для Каждого СтрокаНабора Из НаборЗаписей Цикл
		СтрокаНабора.Выполнена = ПозицииСопоставлены;
	КонецЦикла;
	НаборЗаписей.Записать(Истина);
	
	Для Каждого СтрокаТоваров Из СопоставленныеТовары Цикл
		Если СтрокаТоваров.Номенклатура.ВидНоменклатуры.ИИТ_УчетПоИнвентарнымНомерам
			ИЛИ СтрокаТоваров.Номенклатура.ВидНоменклатуры = Справочники.ВидыНоменклатуры.НайтиПоНаименованию("КРУ ОС без инвентарного номера (Материалы многократного использования)")
			ИЛИ СтрокаТоваров.Номенклатура.ВидНоменклатуры = Справочники.ВидыНоменклатуры.НайтиПоНаименованию("ИТ без инвентарного номера") Тогда
			Движение = Движения.ИИТ_ОстаткиОприходованияОС.ДобавитьПриход();
			Движение.Период = Дата;
			Движение.ДокументПоступления = Ссылка;
			Движение.Номенклатура = СтрокаТоваров.Номенклатура;
			Движение.Конфигурация = СтрокаТоваров.Конфигурация;
			Движение.Размер = СтрокаТоваров.Размер;
			Движение.Цвет = СтрокаТоваров.Цвет;
			Движение.Количество = СтрокаТоваров.Количество;
			Движение.Сумма = СтрокаТоваров.Сумма;
		КонецЕсли
	КонецЦикла;
	
	Движения.ИИТ_ОстаткиОприходованияОС.Записать(Истина);
	//-ИИТ_Аксеньюшкин
	
КонецПроцедуры

&После("ПриЗаписи")
Процедура АЛФ_ПриЗаписи(Отказ)
	//+ИИТ_Аксеньюшкин
	Если ПометкаУдаления Тогда

		НаборЗаписей = РегистрыСведений.ИИТ_УведомленияПоПоступлениям.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ДокументПоступления.Установить(Ссылка);
		НаборЗаписей.Прочитать();
		НаборЗаписей.Очистить();
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
	Запись = РегистрыСведений.ИИТ_ОчередьОбменаСБухУПД.СоздатьМенеджерЗаписи();
	Запись.УПД = Ссылка;
	Запись.Записать(Истина);
	//-ИИТ_Аксеньюшкин
КонецПроцедуры

&ИзменениеИКонтроль("ПередЗаписью")
Процедура АЛФ_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	#Вставка     
	Если Не ЗначениеЗаполнено(ДоговорКонтрагента.ВалютаВзаиморасчетов) Тогда
		ДоговорОбъект = ДоговорКонтрагента.ПолучитьОбъект();
		ДоговорОбъект.ВалютаВзаиморасчетов = Справочники.Валюты.НайтиПоКоду("643");
		ДоговорОбъект.ОбменДанными.Загрузка = Истина;
		ДоговорОбъект.Записать();
	КонецЕсли;
	#КонецВставки
	
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	РаботаСДоговорамиКонтрагентовБП.ЗаполнитьДоговорПередЗаписью(ЭтотОбъект);
	ВидДоговораКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора");

	ЭтоКомиссия = ЗначениеЗаполнено(ДоговорКонтрагента)
	И ВидДоговораКонтрагента = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;
	ВозможнаКомиссияПоЗакупке = ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия 
	И НЕ ЭтоКомиссия
	И ПолучитьФункциональнуюОпцию("ОсуществляетсяЗакупкаТоваровУслугДляКомитентов");
	ЭтоВвозИзЕАЭС = Справочники.Контрагенты.КонтрагентРезидентТаможенногоСоюза(Контрагент);
	НеИспользуемыеТабличныеЧасти = Документы.ПоступлениеТоваровУслуг.НеИспользуемыеТабличныеЧасти(
	ВидОперации,
	ЭтоКомиссия,
	ВозможнаКомиссияПоЗакупке);

	ОбщегоНазначенияБП.ОчиститьНеиспользуемыеТабличныеЧасти(ЭтотОбъект, НеИспользуемыеТабличныеЧасти);

	СпособОценкиТоваровВРознице = УчетнаяПолитика.СпособОценкиТоваровВРознице(Организация, Дата);

	ДанныеОТипеСклада = Неопределено;

	//Поступление на несколько складов
	Если Не ПолучитьФункциональнуюОпцию("ПоступлениеНаНесколькоСкладов") И СкладВСписке Тогда
		СкладВСписке = Ложь;
	КонецЕсли;
	ПредставлениеСклада = "";
	Если СкладВСписке Тогда
		Для Каждого Строка Из Товары Цикл
			Если УчетТоваров.РозницаВПродажныхЦенах(СпособОценкиТоваровВРознице, Строка.Склад, ЭтоКомиссия, ДанныеОТипеСклада, ВидОперации) Тогда
				Если ДанныеОТипеСклада = Перечисления.ТипыСкладов.РозничныйМагазин Тогда
					Строка.СчетУчета = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ;
				Иначе
					Строка.СчетУчета = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ;
				КонецЕсли;
				Строка.СчетУчетаНДС = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
			КонецЕсли;
		КонецЦикла;

		Склады = ОбщегоНазначения.ВыгрузитьКолонку(Товары, "Склад", Истина);
		Если Склады.Количество() > 0 Тогда
			ПредставлениеСклада = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склады[0], "Наименование"); 
			КоличествоСкладов 	= Склады.Количество() - 1;
			ПредставлениеСклада = ?(КоличествоСкладов > 0, ПредставлениеСклада + " и еще " + КоличествоСкладов, ПредставлениеСклада);
		КонецЕсли;
	Иначе
		Если УчетТоваров.РозницаВПродажныхЦенах(СпособОценкиТоваровВРознице, Склад, ЭтоКомиссия, ДанныеОТипеСклада, ВидОперации) Тогда
			Для Каждого Строка Из Товары Цикл
				Если ДанныеОТипеСклада = Перечисления.ТипыСкладов.РозничныйМагазин Тогда
					Строка.СчетУчета    = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ;
					Строка.СчетУчетаНДС = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
				Иначе
					Строка.СчетУчета = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ;
					Строка.СчетУчетаНДС = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	//Конец Поступление на несколько складов

	Если ЭтоКомиссия И ПолучитьФункциональнуюОпцию("ОсуществляетсяЗакупкаТоваровУслугДляКомитентов") Тогда
		Для Каждого Строка Из Товары Цикл
			Строка.Контрагент = Справочники.Контрагенты.ПустаяСсылка();
			Строка.ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
			Строка.СчетРасчетов = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
		КонецЦикла;
	КонецЕсли;

	Если УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Организация, Дата) Тогда
		НДСВключенВСтоимость = Ложь;
	КонецЕсли;

	// При групповом перепроведении реквизиты документов не меняются,
	// поэтому обновление связанных данных выполнять не требуется.
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	СчетаУчетаВДокументах.ЗаполнитьПередЗаписью(ЭтотОбъект, РежимЗаписи);

	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
	СуммаДокумента = УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ЭтотОбъект);

	ДоговорУказан = ЗначениеЗаполнено(ДоговорКонтрагента);
	Если ДоговорУказан Тогда
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ДоговорКонтрагента, "ВидДоговора, УчетАгентскогоНДС, ВидАгентскогоДоговора, НалогообложениеКонтрагента");
		ЭтоКомиссия       = РеквизитыДоговора.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;
		УчетАгентскогоНДС = РеквизитыДоговора.УчетАгентскогоНДС;
		НДСИсчисляетсяНалоговымАгентом = 
		РеквизитыДоговора.ВидАгентскогоДоговора = Перечисления.ВидыАгентскихДоговоров.РеализацияТоваров
		И БухгалтерскийУчетПереопределяемый.ИспользуетсяОбратноеНачислениеНДС();
		ЭтоДоговорССамозанятым =
		РеквизитыДоговора.НалогообложениеКонтрагента = Перечисления.СистемыНалогообложения.НалогНаПрофессиональныйДоход;
	Иначе
		ЭтоКомиссия       = Ложь;
		УчетАгентскогоНДС = Ложь;
		НДСИсчисляетсяНалоговымАгентом = Ложь;
		ЭтоДоговорССамозанятым = Ложь;
	КонецЕсли;

	ТребуетсяСчетФактура = Не ЭтоДоговорССамозанятым И Не УчетАгентскогоНДС И Не ЭтоКомиссия
	И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку
	Или НДСИсчисляетсяНалоговымАгентом;

	ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("ПометкаУдаления", ЭтотОбъект, "СчетФактураПолученный");
	Если Не ТребуетсяСчетФактура Тогда 	
		ПараметрыДействия.СостояниеФлага = Истина;
	КонецЕсли;
	УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ);

	Документы.КорректировкаПоступления.ОбновитьРеквизитыСвязанныхДокументовКорректировки(ЭтотОбъект, Отказ);

	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
		И ВедетсяУчетПредметовАренды
		Или ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга Тогда
		Для Каждого Строка Из Услуги Цикл
			Строка.Количество = 0;
			Строка.Цена = Строка.Сумма;
			Строка.ПодразделениеЗатрат = ПодразделениеОрганизации;
			Строка.Субконто1 = Контрагент;
			Строка.Субконто2 = ДоговорКонтрагента;
			Строка.СчетЗатратНУ = Строка.СчетЗатрат;
			Строка.СубконтоНУ1 = Контрагент;
			Строка.СубконтоНУ2 = ДоговорКонтрагента;
		КонецЦикла;
	КонецЕсли;

	ИнтеграцияГИСМБП.УстановитьПризнакЕстьМаркируемаяПродукцияГИСМ(ЭтотОбъект);

	УчетОС.УстановитьАмортизационнуюГруппаОС(
	ОсновныеСредства.Выгрузить( ,"ОсновноеСредство, СрокПолезногоИспользования"));

	Если (ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПриобретениеЗемельныхУчастков)
		И БухгалтерскийУчетПереопределяемый.ВестиУчетПоПодразделениям() Тогда
		ПодразделениеОрганизации = МестонахождениеОС;
	КонецЕсли;

	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги 
		И Справочники.Склады.ИспользуетсяНесколькоСкладов() Тогда

		Склад = Справочники.Склады.ПустаяСсылка();
	КонецЕсли; 

	ПрослеживаемыйТовар = Товары.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	ПрослеживаемоеОборудование = Оборудование.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	ПрослеживаемоеОС = ОсновныеСредства.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	ЕстьПрослеживаемыйТовар = ПрослеживаемыйТовар.Количество() <> 0
	ИЛИ ПрослеживаемоеОборудование.Количество() <> 0
	Или ПрослеживаемоеОС.Количество() <> 0;
	Если НЕ ЕстьПрослеживаемыйТовар 
		ИЛИ ЭтоВвозИзЕАЭС Тогда
		СведенияПрослеживаемости.Очистить();
	КонецЕсли;
	// Дополнительно очистим номера ТД для прослеживаемого товара.
	Если НЕ ЭтоВвозИзЕАЭС Тогда
		Для Каждого СтрокаСТоваром ИЗ ПрослеживаемыйТовар Цикл
			СтрокаСТоваром.НомерГТД = Неопределено;
		КонецЦикла;
		Для Каждого СтрокаСОборудованием ИЗ ПрослеживаемоеОборудование Цикл
			СтрокаСОборудованием.НомерГТД = Неопределено;
		КонецЦикла;
	КонецЕсли;

	ИменаТабличныхЧастей = Новый Массив;
	ИменаТабличныхЧастей.Добавить("Товары");
	ИменаТабличныхЧастей.Добавить("Услуги");
	ИменаТабличныхЧастей.Добавить("АгентскиеУслуги");
	ИменаТабличныхЧастей.Добавить("Оборудование");
	ИменаТабличныхЧастей.Добавить("ОсновныеСредства");
	ОбщегоНазначенияБПКлиентСервер.ЗаполнитьИдентификаторыСтрок(ЭтотОбъект, ИменаТабличныхЧастей);

	ВедетсяУчетПрослеживаемыхТоваров = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
	И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Дата);

	Если ВедетсяУчетПрослеживаемыхТоваров Тогда
		ИменаТабличныхЧастей = Новый Массив;
		ИменаТабличныхЧастей.Добавить("Товары");
		ИменаТабличныхЧастей.Добавить("Оборудование");
		ОбщегоНазначенияБП.ОчиститьЛишниеДанныеПоПрослеживаемости(ЭтотОбъект, ИменаТабличныхЧастей);
	КонецЕсли;

	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства Тогда

		МассивОС = ОсновныеСредства.ВыгрузитьКолонку("ОсновноеСредство");

		Если ЗначениеЗаполнено(МассивОС) Тогда
			ЕдиницыУчета = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивОС, "ЕдиницаУчета");
			Для Каждого СтрокаТаблицы Из ОсновныеСредства Цикл
				Если Не СтрокаТаблицы.ПрименяетсяИнвестиционныйВычет Тогда
					Продолжить;
				КонецЕсли;
				ЕдиницаУчета = ЕдиницыУчета[СтрокаТаблицы.ОсновноеСредство];
				Если ЕдиницаУчета <> Перечисления.ЕдиницыУчетаОС.ИнвентарныйОбъект Тогда
					СтрокаТаблицы.ПрименяетсяИнвестиционныйВычет = Ложь;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	#Вставка  
	Если ЗначениеЗаполнено(ОбъектСтроительства) Тогда
		ИИТ_ПроектСогласования = ОбъектСтроительства;
	КонецЕсли;
	
	Если НЕ НДСВключенВСтоимость Тогда
		СуммаДокумента = Товары.Итог("Сумма") + Товары.Итог("СуммаНДС");
	Иначе
		СуммаДокумента = Товары.Итог("Сумма");
	КонецЕсли;
	
	ИИТ_НоменклатураСтрокой = "";
	Для Каждого СтрокаТоваров Из Товары Цикл
		ИИТ_НоменклатураСтрокой = ИИТ_НоменклатураСтрокой + ?(ЗначениеЗаполнено(ИИТ_НоменклатураСтрокой), ", ", "") + Строка(СтрокаТоваров.Номенклатура);
	КонецЦикла;
	
	ИИТ_СопоставленныеТоварыСтрокой = "";
	Для Каждого СтрокаТоваров Из СопоставленныеТовары Цикл
		ИИТ_СопоставленныеТоварыСтрокой = ИИТ_СопоставленныеТоварыСтрокой + ?(ЗначениеЗаполнено(ИИТ_СопоставленныеТоварыСтрокой), ", ", "") + Строка(СтрокаТоваров.Номенклатура);
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ИИТ_ДатаСопоставленияТоваров) И СопоставленныеТовары.Количество() > 0 Тогда
		ИИТ_ДатаСопоставленияТоваров = ТекущаяДата();
		ИИТ_ОператорСопоставления = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	#КонецВставки
КонецПроцедуры




