
&НаСервере
Процедура АЛФ_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)

	//Переопределение запроса
	ТекстЗапроса = " ВЫБРАТЬ
					|	ВЫБОР
					|		КОГДА ДокументПоступлениеТоваровУслуг.РучнаяКорректировка = ИСТИНА
					|			ТОГДА ВЫБОР
					|					КОГДА ДокументПоступлениеТоваровУслуг.ПометкаУдаления = ИСТИНА
					|						ТОГДА 10
					|					КОГДА ДокументПоступлениеТоваровУслуг.Проведен = ЛОЖЬ
					|						ТОГДА 9
					|					ИНАЧЕ 8
					|				КОНЕЦ
					|		ИНАЧЕ ВЫБОР
					|				КОГДА ДокументПоступлениеТоваровУслуг.ПометкаУдаления = ИСТИНА
					|					ТОГДА 2
					|				КОГДА ДокументПоступлениеТоваровУслуг.Проведен = ИСТИНА
					|					ТОГДА 1
					|				ИНАЧЕ 0
					|			КОНЕЦ
					|	КОНЕЦ КАК СостояниеДокумента,
					|	ДокументПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
					|	ДокументПоступлениеТоваровУслуг.ПометкаУдаления КАК ПометкаУдаления,
					|	ДокументПоступлениеТоваровУслуг.Дата КАК Дата,
					|	ДокументПоступлениеТоваровУслуг.Номер КАК Номер,
					|	ДокументПоступлениеТоваровУслуг.Проведен КАК Проведен,
					|	ДокументПоступлениеТоваровУслуг.ВидОперации КАК ВидОперации,
					|	ДокументПоступлениеТоваровУслуг.Организация КАК Организация,
					|	ДокументПоступлениеТоваровУслуг.Склад КАК Склад,
					|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
					|	ДокументПоступлениеТоваровУслуг.Контрагент КАК Контрагент,
					|	ДокументПоступлениеТоваровУслуг.ДоговорКонтрагента КАК ДоговорКонтрагента,
					|	ДокументПоступлениеТоваровУслуг.СпособЗачетаАвансов КАК СпособЗачетаАвансов,
					|	ДокументПоступлениеТоваровУслуг.ВалютаДокумента КАК ВалютаДокумента,
					|	ДокументПоступлениеТоваровУслуг.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
					|	ДокументПоступлениеТоваровУслуг.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
					|	ДокументПоступлениеТоваровУслуг.Грузоотправитель КАК Грузоотправитель,
					|	ДокументПоступлениеТоваровУслуг.Грузополучатель КАК Грузополучатель,
					|	ДокументПоступлениеТоваровУслуг.Ответственный КАК Ответственный,
					|	ДокументПоступлениеТоваровУслуг.Комментарий КАК Комментарий,
					|	ДокументПоступлениеТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
					|	ДокументПоступлениеТоваровУслуг.ТипЦен КАК ТипЦен,
					|	ДокументПоступлениеТоваровУслуг.РучнаяКорректировка КАК РучнаяКорректировка,
					|	СостоянияЭД.СостояниеЭДО КАК СостояниеЭДО,
					|	ВЫБОР
					|		КОГДА СтатусыДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДокументовПоступления.ОригиналПолучен)
					|			ТОГДА ИСТИНА
					|		ИНАЧЕ ЛОЖЬ
					|	КОНЕЦ КАК Оригинал,
					|	ВЫБОР
					|		КОГДА ДокументыСОшибкамиПроверкиКонтрагентов.Документ ЕСТЬ NULL
					|			ТОГДА ЛОЖЬ
					|		ИНАЧЕ ИСТИНА
					|	КОНЕЦ КАК СодержитНекорректныхКонтрагентов,
					|	ВЫБОР
					|		КОГДА НаличиеФайлов.ЕстьФайлы ЕСТЬ NULL
					|			ТОГДА 1
					|		КОГДА НаличиеФайлов.ЕстьФайлы
					|			ТОГДА 0
					|		ИНАЧЕ 1
					|	КОНЕЦ КАК ЕстьФайлы,
					|	ЕСТЬNULL(СтатусыДокументов.СтатусСчетаФактуры, ЗНАЧЕНИЕ(Перечисление.СтатусыСчетаФактуры.ПустаяСсылка)) КАК СчетФактура,
					|	ВЫБОР
					|		КОГДА СтатусыДокументов.СтатусОригиналаСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.СтатусыДокументовПоступления.ОригиналПолучен)
					|			ТОГДА ИСТИНА
					|		ИНАЧЕ ЛОЖЬ
					|	КОНЕЦ КАК ОригиналСчетаФактуры,
					|	ВЫБОР
					|		КОГДА ИИТ_УведомленияПоПоступлениям.Выполнена ЕСТЬ NULL
					|			ТОГДА ""Создан""
					|		ИНАЧЕ ВЫБОР
					|				КОГДА НЕ ИИТ_УведомленияПоПоступлениям.Выполнена
					|					ТОГДА ""Передан снабженцу""
					|				ИНАЧЕ ""Выполнен""
					|			КОНЕЦ
					|	КОНЕЦ КАК СтатусСопоставления,
					|	ДокументПоступлениеТоваровУслуг.ИИТ_ПроверенКРУ КАК ПроверенКРУ,
					|	ЕСТЬNULL(ДокументыСОстаткамиПоОС.КоличествоОстаток, 0) > 0 КАК ТребуетсяВводОприходованияОС,
					|	ДокументПоступлениеТоваровУслуг.ИИТ_НоменклатураСтрокой КАК НоменклатураСтрокой,
					|	ДокументПоступлениеТоваровУслуг.ИИТ_ПровереноЭДО КАК ПровереноЭДО,
					|	ДокументПоступлениеТоваровУслуг.ИИТ_СопоставленныеТоварыСтрокой КАК СопоставленныеТоварыСтрокой,
					|	ЗаказыПоставщикам.ЗаказПоставщику КАК ЗаказПоставщику,
					|	ЗаказыПоставщикам.ЗаказПоставщику.Номер КАК Заказы,
					|	ИИТ_СтатусыПроведенияУПДВБухгалтерии.Проведен КАК ПроведенПоБухгалтерии
					|ИЗ
					|	Документ.ПоступлениеТоваровУслуг КАК ДокументПоступлениеТоваровУслуг
					|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументов КАК СтатусыДокументов
					|		ПО ДокументПоступлениеТоваровУслуг.Организация = СтатусыДокументов.Организация
					|			И ДокументПоступлениеТоваровУслуг.Ссылка = СтатусыДокументов.Документ}
					|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияПоОбъектамУчетаЭДО КАК СостоянияЭД
					|		ПО ДокументПоступлениеТоваровУслуг.Ссылка = СостоянияЭД.СсылкаНаОбъект}
					|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДокументыСОшибкамиПроверкиКонтрагентов КАК ДокументыСОшибкамиПроверкиКонтрагентов
					|		ПО ДокументПоступлениеТоваровУслуг.Ссылка = ДокументыСОшибкамиПроверкиКонтрагентов.Документ
					|			И (&ИспользованиеПроверкиВозможно)}
					|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаличиеФайлов КАК НаличиеФайлов
					|		ПО ДокументПоступлениеТоваровУслуг.Ссылка = НаличиеФайлов.ОбъектСФайлами}
					|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИИТ_УведомленияПоПоступлениям КАК ИИТ_УведомленияПоПоступлениям
					|		ПО ДокументПоступлениеТоваровУслуг.Ссылка = ИИТ_УведомленияПоПоступлениям.ДокументПоступления
					|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
					|			ИИТ_ОстаткиОприходованияОСОстатки.ДокументПоступления КАК ДокументПоступления,
					|			СУММА(ИИТ_ОстаткиОприходованияОСОстатки.КоличествоОстаток) КАК КоличествоОстаток
					|		ИЗ
					|			РегистрНакопления.ИИТ_ОстаткиОприходованияОС.Остатки КАК ИИТ_ОстаткиОприходованияОСОстатки
					|		
					|		СГРУППИРОВАТЬ ПО
					|			ИИТ_ОстаткиОприходованияОСОстатки.ДокументПоступления
					|		
					|		ИМЕЮЩИЕ
					|			СУММА(ИИТ_ОстаткиОприходованияОСОстатки.КоличествоОстаток) <> 0) КАК ДокументыСОстаткамиПоОС
					|		ПО ДокументПоступлениеТоваровУслуг.Ссылка = ДокументыСОстаткамиПоОС.ДокументПоступления
					|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
					|			ПоступлениеТоваровУслугСопоставленныеТовары.Ссылка КАК Ссылка,
					|			МАКСИМУМ(ПоступлениеТоваровУслугСопоставленныеТовары.ЗаказПоставщику) КАК ЗаказПоставщику
					|		ИЗ
					|			Документ.ПоступлениеТоваровУслуг.СопоставленныеТовары КАК ПоступлениеТоваровУслугСопоставленныеТовары
					|		
					|		СГРУППИРОВАТЬ ПО
					|			ПоступлениеТоваровУслугСопоставленныеТовары.Ссылка) КАК ЗаказыПоставщикам
					|		ПО ДокументПоступлениеТоваровУслуг.Ссылка = ЗаказыПоставщикам.Ссылка
					|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИИТ_СтатусыПроведенияУПДВБухгалтерии КАК ИИТ_СтатусыПроведенияУПДВБухгалтерии
					|		ПО ДокументПоступлениеТоваровУслуг.Ссылка = ИИТ_СтатусыПроведенияУПДВБухгалтерии.УПД";	  
					
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ОсновнаяТаблица = "Документ.ПоступлениеТоваровУслуг";
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список,
		СвойстваСписка);     
		
   //Поля ТЧ
	ЭлементСпискаФормы = Элементы.Вставить("ПровереноЭДО", Тип("ПолеФормы"), Элементы.Список, Элементы.ЕстьФайлы);
	ЭлементСпискаФормы.Заголовок = "Проверено ЭДО"; 
	ЭлементСпискаФормы.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементСпискаФормы.ПутьКДанным = "Список.ПровереноЭДО";
	
	ЭлементСпискаФормы = Элементы.Вставить("ПроверенКРУ", Тип("ПолеФормы"), Элементы.Список, Элементы.Номер);
	ЭлементСпискаФормы.Заголовок = "Проверен КРУ"; 
	ЭлементСпискаФормы.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементСпискаФормы.ПутьКДанным = "Список.ПроверенКРУ";
		
	ЭлементСпискаФормы = Элементы.Вставить("СтатусСопоставления", Тип("ПолеФормы"), Элементы.Список, Элементы.Ссылка);
	ЭлементСпискаФормы.Заголовок = "Статус сопоставления"; 
	ЭлементСпискаФормы.Вид = ВидПоляФормы.ПолеНадписи;
	ЭлементСпискаФормы.ПутьКДанным = "Список.СтатусСопоставления";

	ЭлементСпискаФормы = Элементы.Вставить("НоменклатураСтрокой", Тип("ПолеФормы"), Элементы.Список, Элементы.Ссылка);
	ЭлементСпискаФормы.Заголовок = "Номенклатура строкой"; 
	ЭлементСпискаФормы.Вид = ВидПоляФормы.ПолеНадписи;
	ЭлементСпискаФормы.ПутьКДанным = "Список.НоменклатураСтрокой";  
	
	ЭлементСпискаФормы = Элементы.Добавить("СопоставленныеТоварыСтрокой", Тип("ПолеФормы"), Элементы.Список);
	ЭлементСпискаФормы.Заголовок = "Сопоставленные товары"; 
	ЭлементСпискаФормы.Вид = ВидПоляФормы.ПолеНадписи;
	ЭлементСпискаФормы.ПутьКДанным = "Список.СопоставленныеТоварыСтрокой";  
	
	ЭлементСпискаФормы = Элементы.Добавить("ТребуетсяВводОприходованияОС", Тип("ПолеФормы"), Элементы.Список);
	ЭлементСпискаФормы.Заголовок = "Требуется ввод оприходования ОС"; 
	ЭлементСпискаФормы.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементСпискаФормы.ПутьКДанным = "Список.ТребуетсяВводОприходованияОС";  

	ЭлементСпискаФормы = Элементы.Добавить("Заказы", Тип("ПолеФормы"), Элементы.Список);
	ЭлементСпискаФормы.Заголовок = "Заказы"; 
	ЭлементСпискаФормы.Вид = ВидПоляФормы.ПолеНадписи;
	ЭлементСпискаФормы.ПутьКДанным = "Список.Заказы";  

	ЭлементСпискаФормы = Элементы.Добавить("ЗаказПоставщику", Тип("ПолеФормы"), Элементы.Список);
	ЭлементСпискаФормы.Заголовок = "Заказ поставщику"; 
	ЭлементСпискаФормы.Вид = ВидПоляФормы.ПолеНадписи;
	ЭлементСпискаФормы.ПутьКДанным = "Список.ЗаказПоставщику";  

	ЭлементСпискаФормы = Элементы.Добавить("ПроведенПоБухгалтерии", Тип("ПолеФормы"), Элементы.Список);
	ЭлементСпискаФормы.Заголовок = "Проведен по бухгалтерии"; 
	ЭлементСпискаФормы.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементСпискаФормы.ПутьКДанным = "Список.ПроведенПоБухгалтерии";  

КонецПроцедуры

&НаКлиенте
Процедура АЛФ_ПриОткрытииПосле(Отказ)

	//Условное оформление
	ЭлементыУсловногоОформления = Список.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление.Элементы;	
	ЭлементУсловногоОформления = ЭлементыУсловногоОформления.Добавить();
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(204, 255, 255));
	
	ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных" ));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТребуетсяВводОприходованияОС"); 
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно; 
	ЭлементОтбора.ПравоеЗначение = Истина;  
	ЭлементОтбора.Использование = Истина;   

	ЭлементУсловногоОформления.Использование = Истина;
	
КонецПроцедуры
