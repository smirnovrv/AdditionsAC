
&НаСервере
Процедура АЛФ_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)

	//Вариант масштаба установлен на форме
	
	//Команды
	КомандаФормы = Команды.Добавить("Подбор");  //На форме отсутствует
	КомандаФормы.Заголовок = "Подбор";
	КомандаФормы.Действие  = "Подбор";
	
	КомандаФормы = Команды.Добавить("Сопоставить");
	КомандаФормы.Заголовок = "Сопоставить";
	КомандаФормы.Действие  = "Сопоставить";

	КомандаФормы = Команды.Добавить("ИИТ_ВСнабжение");
	КомандаФормы.Заголовок = "В снабжение";
	КомандаФормы.Действие  = "ИИТ_ВСнабжение";  

	КомандаФормы = Команды.Добавить("ВверхОтТекущей");
	КомандаФормы.Заголовок = "Вверх от текущей";
	КомандаФормы.Действие  = "ВверхОтТекущей";  

	КомандаФормы = Команды.Добавить("ВнизОтТекущей");
	КомандаФормы.Заголовок = "Вниз от текущей";
	КомандаФормы.Действие  = "ВнизОтТекущей";  
	
	//Элементы формы
	Элементы.СчетНаОплатуПоставщика.Видимость = Ложь;
	
	ПолеВвода_АЛФ = Элементы.Вставить("ИИТ_СчетВложенныйФайл", Тип("ПолеФормы"), Элементы.ГруппаШапкаЛевая, Элементы.ЗаказПоставщику);
	ПолеВвода_АЛФ.Заголовок = "Счет на оплату";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ИИТ_СчетВложенныйФайл"; 
	ПолеВвода_АЛФ.УстановитьДействие("НачалоВыбора", "ИИТ_СчетВложенныйФайлНачалоВыбора");
	
	ПолеВвода_АЛФ = Элементы.Добавить("ОбъектСтроительства", Тип("ПолеФормы"), Элементы.ГруппаШапкаЛевая);
	ПолеВвода_АЛФ.Заголовок = "Объект строительства";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ОбъектСтроительства";
	
	ПолеВвода_АЛФ = Элементы.Добавить("ЭтапСтроительства", Тип("ПолеФормы"), Элементы.ГруппаШапкаЛевая);
	ПолеВвода_АЛФ.Заголовок = "Этап строительства";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ЭтапСтроительства";
	ПолеВвода_АЛФ.УстановитьДействие("НачалоВыбора", "ЭтапСтроительстваНачалоВыбора");
	ПолеВвода_АЛФ.УстановитьДействие("АвтоПодбор", "ЭтапСтроительстваАвтоПодбор");
	
	ПолеВвода_АЛФ = Элементы.Добавить("ИИТ_ПровереноЭДО", Тип("ПолеФормы"), Элементы.ГруппаОригиналы);
	ПолеВвода_АЛФ.Заголовок = "Проверено ЭДО";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеФлажка;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ИИТ_ПровереноЭДО";
	ПолеВвода_АЛФ.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	
	ГруппаОрганизацияПроверенКРУ = Элементы.Вставить("ГруппаОрганизацияПроверенКРУ", Тип("ГруппаФормы"), Элементы.ГруппаШапкаПравая, Элементы.Организация);
	ГруппаОрганизацияПроверенКРУ.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаОрганизацияПроверенКРУ.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаОрганизацияПроверенКРУ.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаОрганизацияПроверенКРУ.ОтображатьЗаголовок = Ложь;  
	
	Элементы.Переместить(Элементы.Организация, ГруппаОрганизацияПроверенКРУ);
	
	ПолеВвода_АЛФ = Элементы.Добавить("ИИТ_ПроверенКРУ", Тип("ПолеФормы"), ГруппаОрганизацияПроверенКРУ);
	ПолеВвода_АЛФ.Заголовок = "КРУ МОЛ установлен";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеФлажка;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ИИТ_ПроверенКРУ";
	ПолеВвода_АЛФ.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	
	//У реквизита формы установлен реквизит "Использовать", иначе не доступен
	ПолеВвода_АЛФ = Элементы.Вставить("МОЛ", Тип("ПолеФормы"), Элементы.ГруппаШапкаПравая, Элементы.ПодразделениеОрганизации);
	ПолеВвода_АЛФ.Заголовок = "Принимающий МОЛ";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.МОЛ";
	ПолеВвода_АЛФ.УстановитьДействие("ПриИзменении", "МОЛПриИзменении");
	
	ИИТ_СнабженецУведомлениеГруппа = Элементы.Вставить("ИИТ_СнабженецУведомлениеГруппа", Тип("ГруппаФормы"), Элементы.ГруппаШапкаПравая, Элементы.ПодразделениеОрганизации);
	ИИТ_СнабженецУведомлениеГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ИИТ_СнабженецУведомлениеГруппа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ИИТ_СнабженецУведомлениеГруппа.Отображение = ОтображениеОбычнойГруппы.Нет;
	ИИТ_СнабженецУведомлениеГруппа.ОтображатьЗаголовок = Ложь;  

	ПолеВвода_АЛФ = Элементы.Добавить("ИИТ_СнабженецУведомление", Тип("ПолеФормы"), ИИТ_СнабженецУведомлениеГруппа);
	ПолеВвода_АЛФ.Заголовок = "Снабженец уведомление";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ИИТ_СнабженецУведомление";

	КнопкаФормы = Элементы.Добавить("ИИТ_ВСнабжение", Тип("КнопкаФормы"), Элементы.ИИТ_СнабженецУведомлениеГруппа);
	КнопкаФормы.Заголовок = "В снабжение";
	КнопкаФормы.ИмяКоманды = "ИИТ_ВСнабжение";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;  
    КнопкаФормы.Картинка = БиблиотекаКартинок.МетаданныеПакетыXDTO;
	КнопкаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;
	
	Элементы.ГруппаТовары.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	Элементы.Товары.УстановитьДействие("ПриАктивизацииСтроки", "ТоварыПриАктивизацииСтроки");
	
	КнопкаКоманднойПанели = Элементы.Добавить("ТоварыСопоставить", Тип("КнопкаФормы"), Элементы.ТоварыКнопкиКоманднойПанели);
	КнопкаКоманднойПанели.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	КнопкаКоманднойПанели.Заголовок = "Сопоставить";
	КнопкаКоманднойПанели.ИмяКоманды  = "Сопоставить"; 
	КнопкаКоманднойПанели.ЦветФона = Новый Цвет(255, 204, 0);
	КнопкаКоманднойПанели.Шрифт = Новый Шрифт(КнопкаКоманднойПанели.Шрифт,,, Истина);
	
	КнопкаКоманднойПанели = Элементы.Добавить("ТоварыВверхОтТекущей", Тип("КнопкаФормы"), Элементы.ТоварыКнопкиКоманднойПанели);
	КнопкаКоманднойПанели.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	КнопкаКоманднойПанели.Заголовок = "Вверх от текущей";
	КнопкаКоманднойПанели.ИмяКоманды  = "ВверхОтТекущей"; 
	КнопкаКоманднойПанели.Картинка = БиблиотекаКартинок.Предыдущий;
	КнопкаКоманднойПанели.Отображение = ОтображениеКнопки.Картинка;
	
	КнопкаКоманднойПанели = Элементы.Добавить("ТоварыВнизОтТекущей", Тип("КнопкаФормы"), Элементы.ТоварыКнопкиКоманднойПанели);
	КнопкаКоманднойПанели.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	КнопкаКоманднойПанели.Заголовок = "Вниз от текущей";
	КнопкаКоманднойПанели.ИмяКоманды  = "ВнизОтТекущей"; 
	КнопкаКоманднойПанели.Картинка = БиблиотекаКартинок.Следующий;
	КнопкаКоманднойПанели.Отображение = ОтображениеКнопки.Картинка;

	Элементы.ТоварыНомерСтроки.ФиксацияВТаблице	= ФиксацияВТаблице.Нет;
	Элементы.ТоварыНоменклатураКод.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	
	ГруппаКолонок_1 = Элементы.Вставить("ТоварыГруппа1", Тип("ГруппаФормы"), Элементы.Товары, Элементы.ТоварыНоменклатураАртикул); 
	ГруппаКолонок_1.Вид = ВидГруппыФормы.ГруппаКолонок;
	ГруппаКолонок_1.Группировка = ГруппировкаКолонок.Вертикальная;
	ГруппаКолонок_1.ОтображатьВШапке = Ложь; 
	
	ЭлементТаблицыФормы = Элементы.Добавить("ТоварыНоменклатураВидНоменклатуры", Тип("ПолеФормы"), ГруппаКолонок_1);
	ЭлементТаблицыФормы.Заголовок = "Вид номенклатуры"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.Номенклатура.ВидНоменклатуры";
	ЭлементТаблицыФормы.Видимость = Ложь;
	
	ЭлементТаблицыФормы = Элементы.Добавить("ТоварыНомПлоскаяВидНоменклатуры", Тип("ПолеФормы"), ГруппаКолонок_1);
	ЭлементТаблицыФормы.Заголовок = "Вид номенклатуры"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.НомПлоская.ВидНоменклатуры";
	
	Элементы.ТоварыГруппаНоменклатура.ОтображатьВШапке = Ложь;
	Элементы.ТоварыГруппаНоменклатура.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	Элементы.ТоварыГруппаНоменклатура.Группировка = ГруппировкаКолонок.Вертикальная;
	Элементы.Переместить(Элементы.ТоварыГруппаНоменклатура, Элементы.Товары, Элементы.ТоварыНоменклатураАртикул);	
	
	Элементы.ТоварыНоменклатура.ОтображатьВШапке = Истина;
	Элементы.ТоварыНоменклатура.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	Элементы.ТоварыНоменклатура.Заголовок = "Номенклатура бух.";
	
	ЭлементТаблицыФормы = Элементы.Добавить("ТоварыНомПлоская", Тип("ПолеФормы"), Элементы.ТоварыГруппаНоменклатура);
	ЭлементТаблицыФормы.Заголовок = "Номенклатура снабж."; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.НомПлоская"; 
	
	Элементы.ТоварыПрослеживаемыйТовар.Видимость = Ложь;
	
	Элементы.Переместить(Элементы.ТоварыГруппаКоличествоЕдиница, Элементы.Товары, Элементы.ТоварыУХ_Характеристика);
	
	ЭлементТаблицыФормы = Элементы.Вставить("ТоварыКоличествоВЗаказе", Тип("ПолеФормы"), Элементы.ТоварыГруппаКоличество, Элементы.ТоварыКоличествоОтклонение);
	ЭлементТаблицыФормы.Заголовок = "Количество в заказе"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.КоличествоВЗаказе";
	
	ЭлементТаблицыФормы = Элементы.Вставить("ТоварыКоличествоОсталосьПринять", Тип("ПолеФормы"), Элементы.ТоварыГруппаКоличество, Элементы.ТоварыКоличествоОтклонение);
	ЭлементТаблицыФормы.Заголовок = "Осталось принять"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.КоличествоОсталосьПринять";
 	ЭлементТаблицыФормы.Видимость = Ложь;
	
	Элементы.ТоварыНоменклатураАртикул.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	Элементы.Переместить(Элементы.ТоварыНоменклатураАртикул, Элементы.Товары, Элементы.ГруппаТоварыЗаголовкиСтрок);
	Элементы.ГруппаТоварыЗаголовкиСтрок.Видимость = Ложь;
	
	ГруппаКолонок_3 = Элементы.Вставить("ТоварыГруппа3", Тип("ГруппаФормы"), Элементы.Товары, Элементы.ТоварыУХ_Характеристика); 
	ГруппаКолонок_3.Вид = ВидГруппыФормы.ГруппаКолонок;
	ГруппаКолонок_3.Группировка = ГруппировкаКолонок.Вертикальная;
	ГруппаКолонок_3.ОтображатьВШапке = Ложь;  

	ЭлементТаблицыФормы = Элементы.Добавить("ТоварыИнвентарныйНомер", Тип("ПолеФормы"), ГруппаКолонок_3);
	ЭлементТаблицыФормы.Заголовок = "Инвентарный номер"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.ИнвентарныйНомер";
	
	ЭлементТаблицыФормы = Элементы.Добавить("ТоварыСерийныйНомер", Тип("ПолеФормы"), ГруппаКолонок_3);
	ЭлементТаблицыФормы.Заголовок = "Серийный номер"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.СерийныйНомер";

	ГруппаКолонок_2 = Элементы.Вставить("ТоварыГруппа2", Тип("ГруппаФормы"), Элементы.Товары, Элементы.ТоварыКиЗ_ГИСМ); 
	ГруппаКолонок_2.Вид = ВидГруппыФормы.ГруппаКолонок;
	ГруппаКолонок_2.Группировка = ГруппировкаКолонок.Вертикальная;
	ГруппаКолонок_2.ОтображатьВШапке = Ложь;  
	
	Элементы.Переместить(Элементы.ТоварыЕдиницаИзмерения, ГруппаКолонок_2);
    //Пользовательская видимость по умолчанию установлена на форме
	Элементы.ТоварыЕдиницаИзмерения.Заголовок = "Ед. изм.";
	Элементы.ТоварыЕдиницаИзмерения.Ширина = 0;
	Элементы.ТоварыЕдиницаИзмерения.УстановитьДействие("ПриИзменении", "");
	
	Элементы.ТоварыУХ_Характеристика.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	Элементы.ТоварыУХ_Характеристика.Видимость = Ложь;
	Элементы.ТоварыУХ_НоменклатураПоставщика.Видимость = Ложь;

	ЭлементТаблицыФормы = Элементы.Добавить("ТоварыПринимающийМОЛ", Тип("ПолеФормы"), Элементы.Товары);
	ЭлементТаблицыФормы.Заголовок = "Принимающий МОЛ"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.ПринимающийМОЛ";

КонецПроцедуры

&НаСервере
Процедура АЛФ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)   
	
	//+ИИТ_Аксеньюшкин
	ПроставитьДопКОличество();
	ИИТ_ПроверитьБлокировкуКнопкиВСнабжение();
	
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		Если Не ЗначениеЗаполнено(СтрокаТоваров.ИдентификаторСтроки) Тогда
			СтрокаТоваров.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИИТ_ЗаказыПоставщикуОстатки.Номенклатура КАК Номенклатура,
	|	ИИТ_ЗаказыПоставщикуОстатки.Конфигурация КАК Конфигурация,
	|	ИИТ_ЗаказыПоставщикуОстатки.Цвет КАК Цвет,
	|	ИИТ_ЗаказыПоставщикуОстатки.Размер КАК Размер,
	|	ИИТ_ЗаказыПоставщикуОстатки.КоличествоОстаток КАК Количество,
	|	ИИТ_ЗаказыПоставщикуОстатки.ЗаказПоставщику КАК Заказ
	|ИЗ
	|	РегистрНакопления.ИИТ_ЗаказыПоставщику.Остатки(, ЗаказПоставщику.Контрагент = &Контрагент) КАК ИИТ_ЗаказыПоставщикуОстатки";
	Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	
	ИИТ_ВариантыСоответствия.Загрузить(Запрос.Выполнить().Выгрузить());

	//Устанавливаем выбор для снабженцев
	//МассивСнабженцев = ОтветственныеСнабженцы();
	//Если ЗначениеЗаполнено(Объект.ИИТ_СнабженецУведомление) И МассивСнабженцев.Найти(Объект.ИИТ_СнабженецУведомление) = Неопределено Тогда
	//	МассивСнабженцев.Добавить(Объект.ИИТ_СнабженецУведомление);
	//КонецЕсли;
	//Элементы.ИИТ_СнабженецУведомление.СписокВыбора.ЗагрузитьЗначения(МассивСнабженцев);
	//Если МассивСнабженцев.Количество() = 1 И Не ЗначениеЗаполнено(Объект.ИИТ_СнабженецУведомление) Тогда
	//	Объект.ИИТ_СнабженецУведомление = МассивСнабженцев[0];
	//КонецЕсли;
	
	//Устанавливаем выбор для МОЛов
	МассивПринимающихМОЛов = ПринимающиеМОЛы();
	Если ЗначениеЗаполнено(Объект.МОЛ) И МассивПринимающихМОЛов.Найти(Объект.МОЛ) = Неопределено Тогда
		МассивПринимающихМОЛов.Добавить(Объект.МОЛ);
	КонецЕсли;
	//Элементы.МОЛ.СписокВыбора.ЗагрузитьЗначения(МассивПринимающихМОЛов);
	//Если МассивПринимающихМОЛов.Количество() = 1 И Не ЗначениеЗаполнено(Объект.МОЛ) Тогда
	//	Объект.МОЛ = МассивПринимающихМОЛов[0];
	//КонецЕсли;
	
	Элементы.ИИТ_ПроверенКРУ.Доступность = РольДоступна("ИИТ_Кладовщик") ИЛИ РольДоступна("ПолныеПрава");
	
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		НайденныеСтроки = Объект.СопоставленныеТовары.НайтиСтроки(Новый Структура("НоменклатураБух", СтрокаТоваров.Номенклатура));
		Если НайденныеСтроки.Количество() > 0 Тогда
			СтрокаТоваров.НомПлоская = НайденныеСтроки[0].Номенклатура;
			СтрокаТоваров.КоличествоВЗаказе = НайденныеСтроки[0].Количество;
		КонецЕсли;
	КонецЦикла;
	
	Если РольДоступна("ПолныеПрава") Тогда
		Элементы.МОЛ.РежимВыбораИзСписка = Ложь;
	КонецЕсли;
	//-ИИТ_Аксеньюшкин 
	
КонецПроцедуры

&НаКлиенте
Процедура АЛФ_КонтрагентПриИзмененииПосле(Элемент)
	
	//+ИИТ_Аксеньюшкин
	//МассивСнабженцев = ОтветственныеСнабженцы();
	//Элементы.ИИТ_СнабженецУведомление.СписокВыбора.ЗагрузитьЗначения(МассивСнабженцев);
	//Если МассивСнабженцев.Количество() = 1 Тогда
	//	Объект.ИИТ_СнабженецУведомление = МассивСнабженцев[0];
	//Иначе
	//	Объект.ИИТ_СнабженецУведомление = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
	//КонецЕсли;
	
	МассивПринимающихМОЛов = ПринимающиеМОЛы();
	//Элементы.МОЛ.СписокВыбора.ЗагрузитьЗначения(МассивПринимающихМОЛов);
	Если МассивПринимающихМОЛов.Количество() = 1 Тогда
		//Объект.МОЛ = МассивПринимающихМОЛов[0];
	Иначе
		Объект.МОЛ = ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка");
	КонецЕсли;
	//-ИИТ_Аксеньюшкин
	
КонецПроцедуры

&НаКлиенте
Процедура АЛФ_ОбработкаОповещенияПосле(ИмяСобытия, Параметр, Источник)
	
	//+ИИТ_Аксеньюшкин
	Если ИмяСобытия = "ПростановкаСчета" Тогда
		Объект.ИИТ_СчетВложенныйФайл = Параметр.Счет;
		Если ЗначениеЗаполнено(Объект.ИИТ_СчетВложенныйФайл) Тогда
			Объект.ИИТ_СнабженецУведомление = Параметр.Ответственный;
		КонецЕсли;
	КонецЕсли;
	//-ИИТ_Аксеньюшкин

КонецПроцедуры

&НаСервере
Процедура АЛФ_ПослеЗаписиНаСервереПосле(ТекущийОбъект, ПараметрыЗаписи)
	
	//+ИИТ_Аксеньюшкин
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		НайденныеСтроки = Объект.СопоставленныеТовары.НайтиСтроки(Новый Структура("НоменклатураБух", СтрокаТоваров.Номенклатура));
		Если НайденныеСтроки.Количество() > 0 Тогда
			СтрокаТоваров.НомПлоская = НайденныеСтроки[0].Номенклатура;
			СтрокаТоваров.КоличествоВЗаказе = НайденныеСтроки[0].Количество;
		КонецЕсли;
	КонецЦикла;
	//-ИИТ_Аксеньюшкин
	
КонецПроцедуры

&НаСервере
Процедура АЛФ_ПриЗаписиНаСервереПосле(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	//+ИИТ_Аксеньюшкин
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение И НЕ РольДоступна("ПолныеПрава") Тогда
		Если Не ЗначениеЗаполнено(Объект.ОбъектСтроительства) Тогда
			Сообщить("Для проведения документа необходимо заполнить объект строительства.");
			Отказ = Истина;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Объект.ЭтапСтроительства) Тогда
			Сообщить("Для проведения документа необходимо заполнить этап строительства.");
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	//-ИИТ_Аксеньюшкин
	
КонецПроцедуры

&НаКлиенте
Процедура АЛФ_ТоварыПриОкончанииРедактированияПосле(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	//+ИИТ_Аксеньюшкин
	ПроставитьДопКОличество();
	//-ИИТ_Аксеньюшкин

КонецПроцедуры  

#Область Перенос_доработок

//ИИТ_Аксеньюшкин
Функция ОтветственныеСнабженцы()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИИТ_ЗаказыПоставщикуОстатки.ЗаказПоставщику.Ответственный КАК Ответственный
	|ИЗ
	|	РегистрНакопления.ИИТ_ЗаказыПоставщику.Остатки(, ЗаказПоставщику.Контрагент = &Контрагент) КАК ИИТ_ЗаказыПоставщикуОстатки";
	Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ответственный");

КонецФункции

//ИИТ_Аксеньюшкин
&НаСервереБезКонтекста
Функция ПолучитьКоличествоПоЗаказу(Заказ, Номенклатура)
	
	//Запрос = Новый Запрос;
	//Запрос.Текст =
	//"ВЫБРАТЬ
	//|	ЗаказыПоставщикамУХОстатки.КоличествоОстаток КАК КоличествоОстаток
	//|ИЗ
	//|	РегистрНакопления.ЗаказыПоставщикамУХ.Остатки(
	//|			,
	//|			ЗаказПоставщику = &Заказ
	//|				И Номенклатура = &Номенклатура) КАК ЗаказыПоставщикамУХОстатки";
	//Запрос.УстановитьПараметр("Заказ", Заказ);
	//Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	//
	//Выборка = Запрос.Выполнить().Выбрать();
	//Если Выборка.Следующий() Тогда
	//	Возврат ?(ЗначениеЗаполнено(Выборка.КоличествоОстаток), Выборка.КоличествоОстаток, 0);
	//Иначе
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказПоставщикуТовары.Количество КАК КоличествоОстаток
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	|ГДЕ
	|	ЗаказПоставщикуТовары.Ссылка = &Заказ
	|	И ЗаказПоставщикуТовары.Номенклатура = &Номенклатура";
	Запрос.УстановитьПараметр("Заказ", Заказ);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат ?(ЗначениеЗаполнено(Выборка.КоличествоОстаток), Выборка.КоличествоОстаток, 0);
	Иначе
		Возврат 0;
	КонецЕсли;
	//КонецЕсли;

КонецФункции

//ИИТ_Аксеньюшкин
&НаСервере
Функция ПолучитьПараметрыЭлементов()
	
	ПараметрыЭлементов = ФормыУХ.ПолучитьПараметрыЭлементовПоУмолчанию();
	ПараметрыЭлементов.ГруппаВ.ОтображатьЗаголовок  = Ложь;
	ПараметрыЭлементов.ГруппаГ.ОтображатьЗаголовок  = Ложь;
	
	Возврат ПараметрыЭлементов;
	
КонецФункции

//ИИТ_Аксеньюшкин
Функция ПринимающиеМОЛы()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаказПоставщикуТовары.ИИТ_ЗаявкаНаЗакуп.МОЛПолучателя КАК Ответственный
	|ИЗ
	|	РегистрНакопления.ИИТ_ЗаказыПоставщику.Остатки(, ЗаказПоставщику.Контрагент = &Контрагент) КАК ИИТ_ЗаказыПоставщикуОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	|		ПО ИИТ_ЗаказыПоставщикуОстатки.ЗаказПоставщику = ЗаказПоставщикуТовары.Ссылка";
	Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ответственный");

КонецФункции

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ВверхОтТекущей(Команда)

	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКолонки = СтрЗаменить(Элементы.Товары.ТекущийЭлемент.Имя, "Товары", "");
	
	Для Н = 0 По ТекСтрока.НомерСтроки - 1 Цикл
		СтрокаТЗ = Объект.Товары[Н];
		СтрокаТЗ[ИмяКолонки] = ТекСтрока[ИмяКолонки];
	КонецЦикла;

КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ВнизОтТекущей(Команда)

	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКолонки = СтрЗаменить(Элементы.Товары.ТекущийЭлемент.Имя, "Товары", "");
	
	Для Н = ТекСтрока.НомерСтроки - 1 По Объект.Товары.Количество() - 1 Цикл
		СтрокаТЗ = Объект.Товары[Н];
		СтрокаТЗ[ИмяКолонки] = ТекСтрока[ИмяКолонки];
	КонецЦикла;

КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ИИТ_ВСнабжение(Команда)

	//Если Не ЗначениеЗаполнено(Объект.ИИТ_СнабженецУведомление) Тогда
	//	Сообщение = Новый СообщениеПользователю;
	//	Сообщение.Текст = "Укажите сотрудника снабжения для уведомления.";
	//	Сообщение.Поле = "Объект.ИИТ_СнабженецУведомление";
	//	Сообщение.Сообщить();		
	//	Возврат;
	//КонецЕсли;
	
	ИИТ_ВСнабжениеНаСервере();
	ИИТ_ПроверитьБлокировкуКнопкиВСнабжение();

КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаСервере
Процедура ИИТ_ВСнабжениеНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИИТ_УведомленияПоПоступлениям.ДокументПоступления КАК ДокументПоступления
	|ИЗ
	|	РегистрСведений.ИИТ_УведомленияПоПоступлениям КАК ИИТ_УведомленияПоПоступлениям
	|ГДЕ
	|	ИИТ_УведомленияПоПоступлениям.ДокументПоступления = &ДокПоступления";
	Запрос.УстановитьПараметр("ДокПоступления", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Сообщить("По данному документу уже отправлено уведомление!");
		Возврат;
	КонецЕсли;
	
	Новаязапись = РегистрыСведений.ИИТ_УведомленияПоПоступлениям.СоздатьМенеджерЗаписи();
	Новаязапись.ДокументПоступления = Объект.Ссылка;
	Новаязапись.ДатаПостановкиЗадачи = ТекущаяДата();
	Новаязапись.СодержаниеУведомления = "Требуется выполнить сопоставление позиций документа.";
	Новаязапись.Записать(Истина);
	
	НоваяЗапись = РегистрыСведений.ИИТ_ЛогПередачиСнабженцу.СоздатьМенеджерЗаписи();
	НоваяЗапись.Период = ТекущаяДата();
	НоваяЗапись.ДокументУПД = Объект.Ссылка;
	НоваяЗапись.КомуПередано = Объект.ИИТ_СнабженецУведомление;
	НоваяЗапись.Пользователь = ПараметрыСеанса.ТекущийПользователь;
	НоваяЗапись.Записать(Истина);
	
КонецПроцедуры

//ИИТ_Аксеньюшкин
Процедура ИИТ_ПроверитьБлокировкуКнопкиВСнабжение()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИИТ_УведомленияПоПоступлениям.ДокументПоступления КАК ДокументПоступления
	|ИЗ
	|	РегистрСведений.ИИТ_УведомленияПоПоступлениям КАК ИИТ_УведомленияПоПоступлениям
	|ГДЕ
	|	ИИТ_УведомленияПоПоступлениям.ДокументПоступления = &ДокПоступления";
	Запрос.УстановитьПараметр("ДокПоступления", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	//Элементы.ИИТ_ВСнабжение.Доступность = (Выборка.Количество() = 0) И (РольДоступна("ИИТ_Оператор") ИЛИ РольДоступна("ПолныеПрава"));
	//Элементы.ИИТ_СнабженецУведомление.Доступность = (Выборка.Количество() = 0) И (РольДоступна("ИИТ_Оператор") ИЛИ РольДоступна("ПолныеПрава"));
	
КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ИИТ_РаспределениеПоЗаказамЗаказПоставщикуНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;

	СтандартнаяОбработка = Ложь;
	ФормаВыбора = ПолучитьФорму("Документ.ЗаказПоставщику.Форма.ФормаВыбора",Новый Структура("Контрагент,Номенклатура", Объект.Контрагент, ТекСтрока.НоменклатураПлоская),Элемент);
	ФормаВыбора.Открыть();
	
КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ИИТ_РаспределениеПоЗаказамПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекСтрокаТовары = Элементы.Товары.ТекущиеДанные;
	
	Если НоваяСтрока Тогда
		ТекСтрокаРаспределение = Элементы.ИИТ_РаспределениеПоЗаказам.ТекущиеДанные;
		Если ТекСтрокаРаспределение <> Неопределено И ТекСтрокаТовары <> Неопределено Тогда
			ТекСтрокаРаспределение.ИдентификаторСтрокиТовары = ТекСтрокаТовары.ИдентификаторСтроки;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры  

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ИИТ_СчетВложенныйФайлНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	фиксНастройки = Новый НастройкиКомпоновкиДанных;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КонтрагентДляСчета", Объект.Контрагент);

	ОткрытьФорму("Справочник.Файлы.Форма.ФормаВыбораСчета", ПараметрыФормы, Элементы.ИИТ_СчетВложенныйФайл);	
	
КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура МОЛПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.МОЛ) Тогда
		Для Каждого СтрокаТоваров Из Объект.Товары Цикл
			СтрокаТоваров.ПринимающийМОЛ = Объект.МОЛ;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура Подбор(Команда)

	ТекСтрокаТовары = Элементы.Товары.ТекущиеДанные;
	ТекСтрокаВариантыПодбора = Элементы.ИИТ_ВариантыСоответствия.ТекущиеДанные;
	
	ТекСтрокаРаспределение = Объект.ИИТ_РаспределениеПоЗаказам.Добавить();
	ТекСтрокаРаспределение.ИдентификаторСтрокиТовары = ТекСтрокаТовары.ИдентификаторСтроки;
	ТекСтрокаРаспределение.ЗаказПоставщику = ТекСтрокаВариантыПодбора.Заказ;
	ТекСтрокаРаспределение.Количество = ТекСтрокаВариантыПодбора.Количество;

	ТекСтрокаТовары.Конфигурация = ТекСтрокаВариантыПодбора.Конфигурация;
	ТекСтрокаТовары.Цвет = ТекСтрокаВариантыПодбора.Цвет;
	ТекСтрокаТовары.Размер = ТекСтрокаВариантыПодбора.Размер;
	ТекСтрокаТовары.НоменклатураПлоская = ТекСтрокаВариантыПодбора.Номенклатура;

КонецПроцедуры

//ИИТ_Аксеньюшкин
Процедура ПроставитьДопКОличество()
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		СтрокаТоваров.КоличествоВЗаказе = ПолучитьКоличествоПоЗаказу(Объект.ЗаказПоставщику, СтрокаТоваров.Номенклатура);
		СтрокаТоваров.КоличествоОсталосьПринять = СтрокаТоваров.КоличествоВЗаказе - СтрокаТоваров.Количество;
	КонецЦикла;
КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура Сопоставить(Команда)
	
	Форма = ПолучитьФорму("Обработка.ИИТ_СопоставлениеУПДИЗаказПоставщику.Форма.Форма", Новый Структура("УПД", Объект.Ссылка));
	Форма.Открыть();
	ЭтаФорма.Закрыть();

КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ТоварыКоличествоБухПриИзменении(Элемент)
	ТекСТрока = Элементы.Товары.ТекущиеДанные;
	ТекСТрока.ЦенаБух = ТекСТрока.Сумма / ТекСТрока.КоличествоБух;
КонецПроцедуры  

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ЭтапСтроительстваНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(Объект.ОбъектСтроительства) Тогда

		СтандартнаяОбработка = Ложь;

		фиксНастройки = Новый НастройкиКомпоновкиДанных;
		 
		эОтбор = фиксНастройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		эОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Ссылка");
		эОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии;
		эОтбор.ПравоеЗначение = Объект.ОбъектСтроительства;
		эОтбор.Использование = Истина;
		
		эОтбор.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		
		эОтбор = фиксНастройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		эОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Проект");
		эОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		эОтбор.ПравоеЗначение = Ложь;
		эОтбор.Использование = Истина;
		 
		эОтбор.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		 
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ФиксированныеНастройки", фиксНастройки);
		ПараметрыФормы.Вставить("ОтображениеСписка", ОтображениеТаблицы.Список);
		 
		ОткрытьФорму("Справочник.Проекты.Форма.ФормаВыбора", ПараметрыФормы, Элементы.ЭтапСтроительства);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти
