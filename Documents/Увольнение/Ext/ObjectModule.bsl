
&После("ОбработкаПроведения")
Процедура АЛФ_ОбработкаПроведения(Отказ, РежимПроведения)

	//+ИИТ_Аксеньюшкин
	Движение = Движения.ИИТ_БригадыФизическихЛиц.Добавить();
	Движение.Период = Дата;
	Движение.Физлицо = Сотрудник.ФизическоеЛицо;
	Движение.Бригада = Справочники.ИИТ_Бригады.ПустаяСсылка();
	Движения.ИИТ_БригадыФизическихЛиц.Записать();
	
	Движение = Движения.ИИТ_ОбъектыСтроительстваСотрудников.Добавить();
	Движение.Период = Дата;
	Движение.Сотрудник = Сотрудник;
	Движение.ОбъектСтроительства = Справочники.Проекты.ПустаяСсылка();
	Движения.ИИТ_ОбъектыСтроительстваСотрудников.Записать();
	
	ИИТ_ВспомогательныеФункцииСервер.УстановитьСтатусФЛ(Сотрудник.ФизическоеЛицо, Дата);
	//-ИИТ_Аксеньюшкин
	
КонецПроцедуры


&ИзменениеИКонтроль("ПолучитьДанныеДляПроведения")
Функция АЛФ_ПолучитьДанныеДляПроведения()

	ДанныеДляПроведения = РасчетЗарплаты.СоздатьДанныеДляПроведенияНачисленияЗарплаты();

	РасчетЗарплаты.ЗаполнитьНачисления(ДанныеДляПроведения, Ссылка, , "ДатаНачала");
	РасчетЗарплаты.ЗаполнитьУдержания(ДанныеДляПроведения, Ссылка, , "ДатаНачала");
	РасчетЗарплаты.ЗаполнитьСписокФизическихЛиц(ДанныеДляПроведения, Ссылка);
	РасчетЗарплаты.ЗаполнитьДанныеНДФЛ(ДанныеДляПроведения, Ссылка);
	РасчетЗарплаты.ЗаполнитьДанныеКорректировкиВыплаты(ДанныеДляПроведения, Ссылка);
	РасчетЗарплаты.ЗаполнитьДанныеСтраховыхВзносов(ДанныеДляПроведения, Ссылка);

	ОтражениеЗарплатыВБухучете.СоздатьВТНачисленияСДаннымиЕНВД(Организация, ПериодРегистрации,
	ДанныеДляПроведения.МенеджерВременныхТаблиц, ДанныеДляПроведения.НачисленияПоСотрудникам);

	ДатаСобытия = КонецДня(ДатаУвольнения) + 1;
    #Вставка
	 //Для случаев, когда увольнение и прием в один день
	 ДатаСобытия = КонецДня(ДатаУвольнения);
	#КонецВставки
	Запрос = Новый Запрос;

	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

	УсловияОтбора = Новый Массив;
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(УсловияОтбора, "Регистратор", "<>", Ссылка);

	ПоляОтбораПериодическихДанных = Новый Структура;
	ПоляОтбораПериодическихДанных.Вставить("КадроваяИсторияСотрудников", УсловияОтбора);

	КадровыйУчет.СоздатьНаДатуВТКадровыеДанныеСотрудников(
	Запрос.МенеджерВременныхТаблиц,
	Ложь,
	Сотрудник,
	"ГоловнаяОрганизация,Организация,Подразделение,Должность,ВидЗанятости,ФизическоеЛицо,КоличествоСтавок",
	ДатаСобытия,
	ПоляОтбораПериодическихДанных);

	Запрос.УстановитьПараметр("ДатаСобытия", ДатаСобытия);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	&ДатаСобытия КАК ДатаСобытия,
	|	КадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
	|	КадровыеДанныеСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	КадровыеДанныеСотрудников.Организация КАК Организация,
	|	КадровыеДанныеСотрудников.Подразделение КАК Подразделение,
	|	КадровыеДанныеСотрудников.Должность КАК Должность,
	|	КадровыеДанныеСотрудников.ВидЗанятости КАК ВидЗанятости,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение) КАК ВидСобытия,
	|	КадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
	|	КадровыеДанныеСотрудников.КоличествоСтавок КАК КоличествоСтавок
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников";

	РезультатыЗапроса = Запрос.ВыполнитьПакет();

	// Первый набор данных для проведения - таблица для формирования кадровых движений, истории графиков, авансов.
	ДанныеДляПроведения.Вставить("КадровыеДвижения", РезультатыЗапроса[0].Выгрузить());

	ДанныеДляПроведения.Вставить("МероприятияТрудовойДеятельности",
	Документы.Увольнение.ДанныеДляПроведенияМероприятияТрудовойДеятельности(Ссылка).Получить(Ссылка));

	Возврат ДанныеДляПроведения;

КонецФункции

