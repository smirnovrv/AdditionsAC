
&НаСервере
Процедура АЛФ_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)

	//Элементы формы
	КомандаФормы = Команды.Добавить("ЗаполнитьПоОстаткамМОЛа");
	КомандаФормы.Заголовок = "Заполнить по остаткам МОЛа";
	КомандаФормы.Действие  = "Заполнить по остаткам МОЛа";
	
	КнопкаКоманднойПанели = Элементы.Добавить("ТоварыЗаполнитьПоОстаткамМОЛа", Тип("КнопкаФормы"), Элементы.ГруппаТоварыЗаполнить);
	КнопкаКоманднойПанели.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	КнопкаКоманднойПанели.Заголовок = "Заполнить по остаткам МОЛа";
	КнопкаКоманднойПанели.ИмяКоманды  = "ЗаполнитьПоОстаткамМОЛа"; 

	//Основные
	ПолеВвода_АЛФ = Элементы.Вставить("ОбъектСтроительства", Тип("ПолеФормы"), Элементы.ГруппаОсновныеРеквизиты, Элементы.Склад);
	ПолеВвода_АЛФ.Заголовок = "Объект строительства";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ОбъектСтроительства"; 

	ПолеВвода_АЛФ = Элементы.Вставить("МОЛ", Тип("ПолеФормы"), Элементы.ГруппаОсновныеРеквизиты, Элементы.БанковскийСчетОрганизации);
	ПолеВвода_АЛФ.Заголовок = "МОЛ";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.МОЛ"; 

	ПолеВвода_АЛФ = Элементы.Вставить("ВидНоменклатуры", Тип("ПолеФормы"), Элементы.ГруппаОсновныеРеквизиты, Элементы.БанковскийСчетОрганизации);
	ПолеВвода_АЛФ.Заголовок = "Вид номенклатуры";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ВидНоменклатуры"; 

	//ТЧ
	ПолеВвода_АЛФ = Элементы.Вставить("ТоварыПлоскаяНоменклатура", Тип("ПолеФормы"), Элементы.ТоварыГруппаНоменклатура, Элементы.ТоварыГруппаНоменклатураСКартинкой);
	ПолеВвода_АЛФ.Заголовок = "Плоская номенклатура";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.Товары.ПлоскаяНоменклатура"; 
	
	Элементы.ТоварыНоменклатура.Заголовок =  "Бухгалтерская номенклатура";
	
	ПолеВвода_АЛФ = Элементы.Вставить("ТоварыИнвентарныйНомер", Тип("ПолеФормы"), Элементы.Товары, Элементы.ТоварыСтранаПроисхождения);
	ПолеВвода_АЛФ.Заголовок = "Инвентарный номер";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.Товары.ИнвентарныйНомер"; 

	ПолеВвода_АЛФ = Элементы.Вставить("ТоварыСерийныйНомер", Тип("ПолеФормы"), Элементы.Товары, Элементы.ТоварыСтранаПроисхождения);
	ПолеВвода_АЛФ.Заголовок = "Серийный номер";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.Товары.СерийныйНомер"; 
	
КонецПроцедуры

#Область Перенос_доработок

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ЗаполнитьПоОстаткамМОЛа(Команда)
	ЗаполнитьПоОстаткамМОЛаНаСервере();
КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаСервере
Процедура ЗаполнитьПоОстаткамМОЛаНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИИТ_ОстаткиТМЦОстатки.Номенклатура КАК ПлоскаяНоменклатура,
	|	ИИТ_ОстаткиТМЦОстатки.КоличествоОстаток КАК Количество,
	|	ИИТ_ОстаткиТМЦОстатки.СуммаОстаток КАК Себестоимость,
	|	ИИТ_ОстаткиТМЦОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	1 КАК Коэффициент,
	|	&Счет КАК СчетУчета,
	|	&Счет КАК НовыйСчетУчета,
	|	&СчетРасходов КАК СчетРасходов,
	|	&СчетДоходов КАК СчетДоходов,
	|	&СчетУчетаНДСПоРеализации КАК СчетУчетаНДСПоРеализации,
	|	"""" КАК ИнвентарныйНомер,
	|	"""" КАК СерийныйНомер,
	|	ИИТ_ОстаткиТМЦОстатки.БухгалтерскаяНоменклатура КАК Номенклатура
	|ИЗ
	|	РегистрНакопления.ИИТ_ОстаткиТМЦ.Остатки(
	|			&Дата,
	|			МОЛ = &МОЛ
	|				И Склад = &Склад
	|				И Номенклатура.ВидНоменклатуры = &ВидНоменклатуры) КАК ИИТ_ОстаткиТМЦОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.ОсновноеСредство,
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.КоличествоОстаток,
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.СтоимостьОстаток,
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.ОсновноеСредство.ЕдиницаИзмерения,
	|	1,
	|	&Счет,
	|	&Счет,
	|	&СчетРасходов,
	|	&СчетДоходов,
	|	&СчетУчетаНДСПоРеализации,
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.ИнвентарныйНомер,
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.СерийныйНомер,
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.БухгалтерскаяНоменклатура
	|ИЗ
	|	РегистрНакопления.ИИТ_ОстаткиОсновныхСредствИИнструмента.Остатки(
	|			&Дата,
	|			МОЛ = &МОЛ
	|				И Склад = &Склад
	|				И ОсновноеСредство.ВидНоменклатуры = &ВидНоменклатуры) КАК ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки";
	Запрос.УстановитьПараметр("Дата", КонецДня(Объект.Дата));
	Запрос.УстановитьПараметр("Склад", Объект.Склад);
	Запрос.УстановитьПараметр("МОЛ", Объект.МОЛ);
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.Хозрасчетный.ТоварыНаСкладах);
	Запрос.УстановитьПараметр("СчетРасходов", ПланыСчетов.Хозрасчетный.СебестоимостьПродажНеЕНВД);
	Запрос.УстановитьПараметр("СчетДоходов", ПланыСчетов.Хозрасчетный.ВыручкаНеЕНВД);
	Запрос.УстановитьПараметр("СчетУчетаНДСПоРеализации", ПланыСчетов.Хозрасчетный.Продажи_НДС);
	Запрос.УстановитьПараметр("ВидНоменклатуры", Объект.ВидНоменклатуры);
	
	Объект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры

#КонецОбласти