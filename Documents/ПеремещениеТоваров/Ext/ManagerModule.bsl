
#Область Перенос_доработок

//ИИТ_Аксеньюшкин
// Формирует табличный документ по унифицированной форме М-11
// 
// Возвращаемое значение
//  Табличный документ - унифицированная форма М-11
//
Функция ПечатьПеремещениеТоваровМ11(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСверху			= 10;
	ТабличныйДокумент.ПолеСнизу				= 10;
	ТабличныйДокумент.ПолеСправа			= 0;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ПеремещениеТоваров_М11";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПеремещениеТоваров.НакладнаяМ11");
	
	Для Каждого Объект Из МассивОбъектов Цикл

		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьШапка.Параметры.Номер = Объект.Номер;
		ОбластьШапка.Параметры.Организация = Объект.Организация.НаименованиеПолное;
		ОбластьШапка.Параметры.Дата = Объект.Дата;
		ОбластьШапка.Параметры.ОбъектОтправитель = Объект.ОбъектОтправитель;
		ОбластьШапка.Параметры.ОбъектПолучатель = Объект.ОбъектПолучатель;
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		Для Каждого СтрокаТоваров Из Объект.Товары Цикл
			ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
			ОбластьСтрока.Параметры.Наименование = СтрокаТоваров.Номенклатура.Наименование;
			ОбластьСтрока.Параметры.Код = СтрокаТоваров.Номенклатура.Код;
			ОбластьСтрока.Параметры.КодЕдИзм = СтрокаТоваров.Номенклатура.ЕдиницаИзмерения.Код;
			ОбластьСтрока.Параметры.ЕдИзм = СтрокаТоваров.Номенклатура.ЕдиницаИзмерения;
			ОбластьСтрока.Параметры.Количество = СтрокаТоваров.Количество;
			ОбластьСтрока.Параметры.КоличествоПринятое = СтрокаТоваров.КоличествоПринятое;
			Если ЗначениеЗаполнено(СтрокаТоваров.Цена) Тогда
				ОбластьСтрока.Параметры.Цена = СтрокаТоваров.Цена;
			ИначеЕсли ЗначениеЗаполнено(СтрокаТоваров.Количество) Тогда
				ОбластьСтрока.Параметры.Цена = СтрокаТоваров.СуммаСписания / СтрокаТоваров.Количество;
			КонецЕсли;
			ОбластьСтрока.Параметры.Сумма = СтрокаТоваров.СуммаСписания;
			ТабличныйДокумент.Вывести(ОбластьСтрока);
		КонецЦикла;
		
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		ОбластьПодвал.Параметры.ОтправительМОЛ = Объект.СотрудникОтправитель;
		ОбластьПодвал.Параметры.ПолучательМОЛ = Объект.СотрудникПолучатель;
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции
	
#КонецОбласти

&ИзменениеИКонтроль("Печать")
Процедура АЛФ_Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)

	// Проверяем, нужно ли для макета Накладная формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Накладная", "Перемещение товаров", 
		ПечатьПеремещениеТоваров(МассивОбъектов, ОбъектыПечати), , "Документ.ПеремещениеТоваров.ПФ_MXL_Накладная");
	КонецЕсли;  
	
#Вставка     
	//+ИИТ_Аксеньюшкин
	// Проверяем, нужно ли для макета Накладная формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М11") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "М11", "Перемещение товаров М-11", 
			ПечатьПеремещениеТоваровМ11(МассивОбъектов, ОбъектыПечати), , "Документ.ПеремещениеТоваров.НакладнаяМ11");
	КонецЕсли;	
	//-ИИТ_Аксеньюшкин
#КонецВставки

	// Проверяем, нужно ли для макета ТОРГ13 формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТОРГ13") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТОРГ13", "ТОРГ-13 (Накладная на внутреннее перемещение)", 
		ПечатьТОРГ13(МассивОбъектов, ОбъектыПечати), , "ОбщийМакет.ПФ_MXL_ТОРГ13");
	КонецЕсли;

	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	

КонецПроцедуры      

&ИзменениеИКонтроль("ДобавитьКомандыПечати")
Процедура АЛФ_ДобавитьКомандыПечати(КомандыПечати)

	// Перемещение товаров
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная";
	КомандаПечати.Представление = НСтр("ru = 'Перемещение товаров'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";

	// Накладная на внутреннее перемещение (ТОРГ-13)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТОРГ13";
	КомандаПечати.Представление = НСтр("ru = 'Накладная на внутреннее перемещение (ТОРГ-13)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";

#Вставка
	//+ИИТ_Аксеньюшкин
	// Накладная на внутреннее перемещение М-11
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "М11";
	КомандаПечати.Представление = НСтр("ru = 'Накладная на внутреннее перемещение (М-11)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	//-ИИТ_Аксеньюшкин
#КонецВставки
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Перемещение товаров""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;


КонецПроцедуры
