
#Область Перенос_доработок

//ИИТ_Аксеньюшкин
Функция ПолучитьКоличествоВсегоПоПозиции(Номенклатура, Конфигурация, Цвет, Размер, Дата_)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИИТ_ОстаткиТМЦОстатки.КоличествоОстаток КАК КоличествоВсего
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	РегистрНакопления.ИИТ_ОстаткиТМЦ.Остатки(
	|			&Дата,
	|			МОЛ = &МОЛ
	|				И Склад = &Склад
	|				И Номенклатура = &Номенклатуры
	|				И Конфигурация = &Конфигурация
	|				И Размер = &Размер
	|				И Цвет = &Цвет) КАК ИИТ_ОстаткиТМЦОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.ИИТ_ОстаткиОсновныхСредствИИнструмента.Остатки(
	|			&Дата,
	|			МОЛ = &МОЛ
	|				И Склад = &Склад
	|				И ОсновноеСредство = &Номенклатуры
	|				И Конфигурация = &Конфигурация
	|				И Размер = &Размер
	|				И Цвет = &Цвет) КАК ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ.КоличествоВсего) КАК КоличествоВсего
	|ИЗ
	|	ВТ КАК ВТ";
	Запрос.УстановитьПараметр("Дата", Дата_);
	Запрос.УстановитьПараметр("Склад", СкладОтправитель);
	Запрос.УстановитьПараметр("МОЛ", СотрудникОтправитель);
	Запрос.УстановитьПараметр("Номенклатуры", Номенклатура);
	Запрос.УстановитьПараметр("Конфигурация", Конфигурация);
	Запрос.УстановитьПараметр("Цвет", Цвет);
	Запрос.УстановитьПараметр("Размер", Размер);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат ?(ЗначениеЗаполнено(Выборка.КоличествоВсего), Выборка.КоличествоВсего, 0);
	Иначе
		Возврат 0;
	КонецЕсли;

КонецФункции
	
#КонецОбласти

&После("ОбработкаЗаполнения")
Процедура АЛФ_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	//+ИИТ_Аксеньюшкин
	Если ДанныеЗаполнения <> неопределено Тогда

		ИИТ_ДокументОснование = ДанныеЗаполнения.Ссылка;

		Если ТипЗнч(ИИТ_ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
			
			ОбъектОтправитель = Справочники.Проекты.НайтиПоНаименованию(Строка(ИИТ_ДокументОснование.Склад));
			ОбъектОтправитель = ИИТ_ДокументОснование.ОбъектСтроительства;
			СотрудникОтправитель = ИИТ_ДокументОснование.МОЛ;
			ИИТ_ОтветственныйЗаОтправку = ОбъектОтправитель.ИИТ_ОтветственныйОператор;
			ИИТ_ОтветственныйЗаОтправку = ПараметрыСеанса.ТекущийПользователь;

			Товары.Очистить();
			Для Каждого СтрокаТоваров Из ИИТ_ДокументОснование.СопоставленныеТовары Цикл
				НоваяСтрока = Товары.Добавить();
				НоваяСтрока.БухгалтерскаяНоменклатура = СтрокаТоваров.НоменклатураБух;
				//НоваяСтрока.Номенклатура = ИИТ_ВспомогательныеФункцииСервер.ПолучитьПлоскуюНоменклатуру(СтрокаТоваров.Номенклатура,СтрокаТоваров.Конфигурация,СтрокаТоваров.ЦВет,СтрокаТоваров.Размер);
				НоваяСтрока.Номенклатура = СтрокаТоваров.Номенклатура;
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТоваров, "Конфигурация,Цвет,Размер");
				НоваяСтрока.Количество = СтрокаТоваров.Количество;
				НоваяСтрока.Цена = СтрокаТоваров.Цена;
				НоваяСтрока.СуммаСписания = СтрокаТоваров.Сумма;
				НоваяСтрока.СчетУчета = ПланыСчетов.Хозрасчетный.НайтиПоКоду("41.01");
				НоваяСтрока.НовыйСчетУчета = ПланыСчетов.Хозрасчетный.НайтиПоКоду("41.01");
			КонецЦикла;

		КонецЕсли;

	КонецЕсли;
	//-ИИТ_Аксеньюшкин

КонецПроцедуры

&После("ОбработкаПроведения")
Процедура АЛФ_ОбработкаПроведения(Отказ, РежимПроведения)
	
	//+ИИТ_Аксеньюшкин
	Если ЗначениеЗаполнено(ИИТ_ДокументОснование) И ТипЗнч(ИИТ_ДокументОснование) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		//Не делаем движение в этом случае
	Иначе
	
		Для Каждого СтрокаТЗ Из Товары Цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаТЗ.ИнвентарныйНомер) Тогда
				
				Движение = Движения.ИИТ_ОстаткиТМЦ.ДобавитьРасход();
				Движение.Период = Дата;
				Движение.МОЛ = СотрудникОтправитель;
				Движение.Склад = СкладОтправитель;
				Движение.Номенклатура = СтрокаТЗ.Номенклатура;
				Движение.БухгалтерскаяНоменклатура = СтрокаТЗ.БухгалтерскаяНоменклатура;
				Движение.Количество = СтрокаТЗ.Количество;
				Движение.Сумма = СтрокаТЗ.СуммаСписания;
				Движение.ОбъектСтроительства = ОбъектОтправитель;
				Движение.Конфигурация = СтрокаТЗ.Конфигурация;
				Движение.Цвет = СтрокаТЗ.Цвет;
				Движение.Размер = СтрокаТЗ.Размер;
				
				Движение = Движения.ИИТ_ОстаткиТМЦ.ДобавитьПриход();
				Движение.Период = Дата;
				Движение.МОЛ = СотрудникПолучатель;
				Движение.Склад = СкладПолучатель;
				Движение.Номенклатура = СтрокаТЗ.Номенклатура;
				Движение.БухгалтерскаяНоменклатура = СтрокаТЗ.БухгалтерскаяНоменклатура;
				Движение.Количество = СтрокаТЗ.Количество;
				Движение.Сумма = СтрокаТЗ.СуммаСписания;
				Движение.ОбъектСтроительства = ОбъектПолучатель;
				Движение.Конфигурация = СтрокаТЗ.Конфигурация;
				Движение.Цвет = СтрокаТЗ.Цвет;
				Движение.Размер = СтрокаТЗ.Размер;

			Иначе
				
				Движение = Движения.ИИТ_ОстаткиОсновныхСредствИИнструмента.ДобавитьРасход();
				Движение.Период = Дата;
				Движение.МОЛ = СотрудникОтправитель;
				Движение.Склад = СкладОтправитель;
				Движение.ОсновноеСредство = СтрокаТЗ.Номенклатура;
				Движение.ИнвентарныйНомер = СтрокаТЗ.ИнвентарныйНомер;
				Движение.СерийныйНомер = СтрокаТЗ.СерийныйНомер;
				Движение.Количество = СтрокаТЗ.Количество;
				Движение.БухгалтерскаяНоменклатура = СтрокаТЗ.БухгалтерскаяНоменклатура;
				//Если СтрокаТЗ.КоличествоПринятое > 0 Тогда
					Движение.Стоимость = СтрокаТЗ.СуммаСписания;
				//КонецЕсли;
				Движение.ОбъектСтроительства = ОбъектОтправитель;
				Движение.Конфигурация = СтрокаТЗ.Конфигурация;
				Движение.Цвет = СтрокаТЗ.Цвет;
				Движение.Размер = СтрокаТЗ.Размер;
				
				Движение = Движения.ИИТ_ОстаткиОсновныхСредствИИнструмента.ДобавитьПриход();
				Движение.Период = Дата;
				Движение.МОЛ = СотрудникПолучатель;
				Движение.Склад = СкладПолучатель;
				Движение.ОсновноеСредство = СтрокаТЗ.Номенклатура;
				Движение.ИнвентарныйНомер = СтрокаТЗ.ИнвентарныйНомер;
				Движение.СерийныйНомер = СтрокаТЗ.СерийныйНомер;
				Движение.Количество = СтрокаТЗ.Количество;
				Движение.БухгалтерскаяНоменклатура = СтрокаТЗ.БухгалтерскаяНоменклатура;
				//Если СтрокаТЗ.КоличествоПринятое > 0 Тогда
					Движение.Стоимость = СтрокаТЗ.СуммаСписания;
				//КонецЕсли;
				Движение.ОбъектСтроительства = ОбъектПолучатель;
				Движение.Конфигурация = СтрокаТЗ.Конфигурация;
				Движение.Цвет = СтрокаТЗ.Цвет;
				Движение.Размер = СтрокаТЗ.Размер;

			КонецЕсли;

		КонецЦикла;
		
	КонецЕсли;
	
	Движения.ИИТ_ОстаткиТМЦ.Записать();
	Движения.ИИТ_ОстаткиОсновныхСредствИИнструмента.Записать();

	//Удаляем текущее уведомление
	Если ЗначениеЗаполнено(ИИТ_ОтветственныйЗаПрием) И ИИТ_ОтветственныйЗаПрием = ПараметрыСеанса.ТекущийПользователь Тогда

		НаборЗаписей = РегистрыСведений.ИИТ_УведомленияПоПеремещениям.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ДокументПеремещения.Установить(Ссылка);
		НаборЗаписей.Прочитать();
		НаборЗаписей.Очистить();
		НаборЗаписей.Записать(Истина);

	КонецЕсли;
	
	//Создаем уведомление		
	Если ЗначениеЗаполнено(ИИТ_ОтветственныйЗаПрием) И ЗначениеЗаполнено(ИИТ_ОтветственныйЗаОтправку)
	И ИИТ_ОтветственныйЗаПрием <> ИИТ_ОтветственныйЗаОтправку И ИИТ_ОтветственныйЗаПрием <> ПараметрыСеанса.ТекущийПользователь Тогда

		НоваяЗапись = РегистрыСведений.ИИТ_УведомленияПоПеремещениям.СоздатьМенеджерЗаписи();
		НоваяЗапись.ДокументПеремещения = Ссылка;
		НоваяЗапись.Пользователь = ИИТ_ОтветственныйЗаПрием;
		НоваяЗапись.ДатаПостановкиЗадачи = ТекущаяДата();
		НоваяЗапись.СодержаниеУведомления = "Согласовать поступление ТМЦ";
		НоваяЗапись.Записать(Истина);

	ИначеЕсли Товары.Итог("КоличествоПринятое") <> Товары.Итог("Количество")
	И ИИТ_ОтветственныйЗаПрием <> ИИТ_ОтветственныйЗаОтправку И ИИТ_ОтветственныйЗаПрием = ПараметрыСеанса.ТекущийПользователь Тогда

		НоваяЗапись = РегистрыСведений.ИИТ_УведомленияПоПеремещениям.СоздатьМенеджерЗаписи();
		НоваяЗапись.ДокументПеремещения = Ссылка;
		НоваяЗапись.Пользователь = ИИТ_ОтветственныйЗаОтправку;
		НоваяЗапись.ДатаПостановкиЗадачи = ТекущаяДата();
		НоваяЗапись.СодержаниеУведомления = "Расхождение принятого и отправленного количества";
		НоваяЗапись.Записать(Истина);		

	КонецЕсли;

	//ПРОВЕРЯЕМ НА ОТРИЦАТЕЛЬНЫЕ ОСТАТКИ
	Для Каждого СтрокаТоваров Из Товары Цикл
		Превышение = СтрокаТоваров.Количество - ПолучитьКоличествоВсегоПоПозиции(СтрокаТоваров.Номенклатура, СтрокаТоваров.Конфигурация, СтрокаТоваров.Цвет, СтрокаТоваров.Размер, Дата - 1);
		Если Превышение > 0 Тогда
			Отказ = Истина;
			Сообщить("Превышен остаток по позиции: " + Строка(СтрокаТоваров.Номенклатура) + " (" + 
					Строка(СтрокаТоваров.Конфигурация) + "," + Строка(СтрокаТоваров.Размер) + ","  + Строка(СтрокаТоваров.Цвет) + "). Превышение: " + Строка(Превышение));
		КонецЕсли;											
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ИИТ_ДокументОснование) И ТипЗнч(ИИТ_ДокументОснование) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		
		//Остатки по ТМЦ
		НаборЗаписей = РегистрыНакопления.ИИТ_ОстаткиТМЦ.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ИИТ_ДокументОснование);
		НаборЗаписей.Прочитать();
		НаборЗаписей.Очистить();
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПеремещениеТоваровТовары.Номенклатура КАК Номенклатура,
		|	ПеремещениеТоваровТовары.Количество КАК Количество,
		|	ПеремещениеТоваровТовары.Цена КАК Цена,
		|	ПеремещениеТоваровТовары.БухгалтерскаяНоменклатура КАК БухгалтерскаяНоменклатура,
		|	ПеремещениеТоваровТовары.Конфигурация КАК Конфигурация,
		|	ПеремещениеТоваровТовары.Цвет КАК Цвет,
		|	ПеремещениеТоваровТовары.Размер КАК Размер
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
		|ГДЕ
		|	ПеремещениеТоваровТовары.Ссылка = &ПеремещениеОснование
		|	И ПеремещениеТоваровТовары.ИнвентарныйНомер = """"
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПеремещениеТоваровТовары.Номенклатура,
		|	-ПеремещениеТоваровТовары.КоличествоПринятое,
		|	ПеремещениеТоваровТовары.Цена,
		|	ПеремещениеТоваровТовары.БухгалтерскаяНоменклатура,
		|	ПеремещениеТоваровТовары.Конфигурация,
		|	ПеремещениеТоваровТовары.Цвет,
		|	ПеремещениеТоваровТовары.Размер
		|ИЗ
		|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
		|ГДЕ
		|	ПеремещениеТоваровТовары.Ссылка = &ДокСсылка
		|	И ПеремещениеТоваровТовары.ИнвентарныйНомер = """"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Номенклатура КАК Номенклатура,
		|	СУММА(ВТ.Количество) КАК Количество,
		|	ВТ.Цена КАК Цена,
		|	ВТ.БухгалтерскаяНоменклатура КАК БухгалтерскаяНоменклатура,
		|	ВТ.Конфигурация КАК Конфигурация,
		|	ВТ.Цвет КАК Цвет,
		|	ВТ.Размер КАК Размер
		|ИЗ
		|	ВТ КАК ВТ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ.Номенклатура,
		|	ВТ.Конфигурация,
		|	ВТ.БухгалтерскаяНоменклатура,
		|	ВТ.Цвет,
		|	ВТ.Размер,
		|	ВТ.Цена";
		Запрос.УстановитьПараметр("ПеремещениеОснование", ИИТ_ДокументОснование);
		Запрос.УстановитьПараметр("ДокСсылка", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = НаборЗаписей.ДобавитьРасход();
			НоваяСтрока.Период = ИИТ_ДокументОснование.Дата;
			НоваяСтрока.МОЛ = ИИТ_ДокументОснование.СотрудникОтправитель;
			НоваяСтрока.Склад = ИИТ_ДокументОснование.СкладОтправитель;
			НоваяСтрока.Номенклатура = Выборка.Номенклатура;
			НоваяСтрока.БухгалтерскаяНоменклатура = Выборка.БухгалтерскаяНоменклатура;
			НоваяСтрока.Количество = Выборка.Количество;
			НоваяСтрока.Сумма = Выборка.Количество * Выборка.Цена;
			НоваяСтрока.ОбъектСтроительства = ИИТ_ДокументОснование.ОбъектОтправитель;
			НоваяСтрока.Конфигурация = Выборка.Конфигурация;
			НоваяСтрока.Цвет = Выборка.Цвет;
			НоваяСтрока.Размер = Выборка.Размер;
			
			НоваяСтрока = НаборЗаписей.ДобавитьПриход();
			НоваяСтрока.Период = ИИТ_ДокументОснование.Дата;
			НоваяСтрока.МОЛ = ИИТ_ДокументОснование.СотрудникПолучатель;
			НоваяСтрока.Склад = ИИТ_ДокументОснование.СкладПолучатель;
			НоваяСтрока.Номенклатура = Выборка.Номенклатура;
			НоваяСтрока.БухгалтерскаяНоменклатура = Выборка.БухгалтерскаяНоменклатура;
			НоваяСтрока.Количество = Выборка.Количество;
			НоваяСтрока.Сумма = Выборка.Количество * Выборка.Цена;
			НоваяСтрока.ОбъектСтроительства = ИИТ_ДокументОснование.ОбъектПолучатель;
			НоваяСтрока.Конфигурация = Выборка.Конфигурация;
			НоваяСтрока.Цвет = Выборка.Цвет;
			НоваяСтрока.Размер = Выборка.Размер;

		КонецЦикла;
		
		НаборЗаписей.Записать(Истина);

		//Остатки по ОС
		НаборЗаписей = РегистрыНакопления.ИИТ_ОстаткиОсновныхСредствИИнструмента.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ИИТ_ДокументОснование);
		НаборЗаписей.Прочитать();
		НаборЗаписей.Очистить();
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПеремещениеТоваровТовары.Номенклатура КАК Номенклатура,
		|	ПеремещениеТоваровТовары.Количество КАК Количество,
		|	ПеремещениеТоваровТовары.Цена КАК Цена,
		|	ПеремещениеТоваровТовары.БухгалтерскаяНоменклатура КАК БухгалтерскаяНоменклатура,
		|	ПеремещениеТоваровТовары.Конфигурация КАК Конфигурация,
		|	ПеремещениеТоваровТовары.Цвет КАК Цвет,
		|	ПеремещениеТоваровТовары.Размер КАК Размер,
		|	ПеремещениеТоваровТовары.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ПеремещениеТоваровТовары.СерийныйНомер КАК СерийныйНомер
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
		|ГДЕ
		|	ПеремещениеТоваровТовары.Ссылка = &ПеремещениеОснование
		|	И ПеремещениеТоваровТовары.ИнвентарныйНомер <> """"
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПеремещениеТоваровТовары.Номенклатура,
		|	-ПеремещениеТоваровТовары.КоличествоПринятое,
		|	ПеремещениеТоваровТовары.Цена,
		|	ПеремещениеТоваровТовары.БухгалтерскаяНоменклатура,
		|	ПеремещениеТоваровТовары.Конфигурация,
		|	ПеремещениеТоваровТовары.Цвет,
		|	ПеремещениеТоваровТовары.Размер,
		|	ПеремещениеТоваровТовары.ИнвентарныйНомер,
		|	ПеремещениеТоваровТовары.СерийныйНомер
		|ИЗ
		|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
		|ГДЕ
		|	ПеремещениеТоваровТовары.Ссылка = &ДокСсылка
		|	И ПеремещениеТоваровТовары.ИнвентарныйНомер <> """"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Номенклатура КАК Номенклатура,
		|	СУММА(ВТ.Количество) КАК Количество,
		|	ВТ.Цена КАК Цена,
		|	ВТ.БухгалтерскаяНоменклатура КАК БухгалтерскаяНоменклатура,
		|	ВТ.Конфигурация КАК Конфигурация,
		|	ВТ.Цвет КАК Цвет,
		|	ВТ.Размер КАК Размер,
		|	ВТ.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ВТ.СерийныйНомер КАК СерийныйНомер
		|ИЗ
		|	ВТ КАК ВТ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ.Номенклатура,
		|	ВТ.Конфигурация,
		|	ВТ.БухгалтерскаяНоменклатура,
		|	ВТ.Цвет,
		|	ВТ.Размер,
		|	ВТ.Цена,
		|	ВТ.ИнвентарныйНомер,
		|	ВТ.СерийныйНомер";
		Запрос.УстановитьПараметр("ПеремещениеОснование", ИИТ_ДокументОснование);
		Запрос.УстановитьПараметр("ДокСсылка", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = НаборЗаписей.ДобавитьРасход();
			НоваяСтрока.Период = ИИТ_ДокументОснование.Дата;
			НоваяСтрока.МОЛ = ИИТ_ДокументОснование.СотрудникОтправитель;
			НоваяСтрока.Склад = ИИТ_ДокументОснование.СкладОтправитель;
			НоваяСтрока.ОсновноеСредство = Выборка.Номенклатура;
			НоваяСтрока.БухгалтерскаяНоменклатура = Выборка.БухгалтерскаяНоменклатура;
			НоваяСтрока.Количество = Выборка.Количество;
			НоваяСтрока.Стоимость = Выборка.Количество * Выборка.Цена;
			НоваяСтрока.ОбъектСтроительства = ИИТ_ДокументОснование.ОбъектОтправитель;
			НоваяСтрока.Конфигурация = Выборка.Конфигурация;
			НоваяСтрока.Цвет = Выборка.Цвет;
			НоваяСтрока.Размер = Выборка.Размер;
			НоваяСтрока.ИнвентарныйНомер = Выборка.ИнвентарныйНомер;
			НоваяСтрока.СерийныйНомер = Выборка.СерийныйНомер;
			
			НоваяСтрока = НаборЗаписей.ДобавитьПриход();
			НоваяСтрока.Период = ИИТ_ДокументОснование.Дата;
			НоваяСтрока.МОЛ = ИИТ_ДокументОснование.СотрудникПолучатель;
			НоваяСтрока.Склад = ИИТ_ДокументОснование.СкладПолучатель;
			НоваяСтрока.ОсновноеСредство = Выборка.Номенклатура;
			НоваяСтрока.БухгалтерскаяНоменклатура = Выборка.БухгалтерскаяНоменклатура;
			НоваяСтрока.Количество = Выборка.Количество;
			НоваяСтрока.Стоимость = Выборка.Количество * Выборка.Цена;
			НоваяСтрока.ОбъектСтроительства = ИИТ_ДокументОснование.ОбъектПолучатель;
			НоваяСтрока.Конфигурация = Выборка.Конфигурация;
			НоваяСтрока.Цвет = Выборка.Цвет;
			НоваяСтрока.Размер = Выборка.Размер;
			НоваяСтрока.ИнвентарныйНомер = Выборка.ИнвентарныйНомер;
			НоваяСтрока.СерийныйНомер = Выборка.СерийныйНомер;

		КонецЦикла;
		
		НаборЗаписей.Записать(Истина);

	КонецЕсли;
	//-ИИТ_Аксеньюшкин

КонецПроцедуры

&После("ПриЗаписи")
Процедура АЛФ_ПриЗаписи(Отказ)
	
	Если ЗначениеЗаполнено(ИИТ_ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	ДокументСоздан = Ложь;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПеремещениеТоваров.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|ГДЕ
	|	ПеремещениеТоваров.ИИТ_ДокументОснование = &ДокСсылка";
	Запрос.УстановитьПараметр("ДокСсылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДокОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументСоздан = Истина;
	Иначе
		ДокОбъект = Документы.ПеремещениеТоваров.СоздатьДокумент();
	КонецЕсли;
	ДокОбъект.Организация = Организация;
	ДокОбъект.Дата = Дата + 2;
	ДокОбъект.УстановитьНовыйНомер();
	ДокОбъект.МОЛОтправитель = МОЛПолучатель;
	ДокОбъект.МОЛПолучатель = МОЛОтправитель;
	ДокОбъект.ИИТ_ДокументОснование = Ссылка;
	ДокОбъект.ВидОперации = ВидОперации;

	ДокОбъект.СкладПолучатель = СкладОтправитель;
	ДокОбъект.СкладОтправитель = СкладПолучатель;
	ДокОбъект.СотрудникОтправитель = СотрудникПолучатель;
	ДокОбъект.СотрудникПолучатель = СотрудникОтправитель;
	ДокОбъект.МОЛОтправитель = МОЛПолучатель;
	ДокОбъект.МОЛПолучатель = МОЛОтправитель;
	ДокОбъект.ОбъектОтправитель = ОбъектПолучатель;
	ДокОбъект.ОбъектПолучатель = ОбъектОтправитель;
	ДокОбъект.ИИТ_ОтветственныйЗаОтправку = ИИТ_ОтветственныйЗаПрием;
	ДокОбъект.ИИТ_ОтветственныйЗаПрием = ИИТ_ОтветственныйЗаОтправку;
	
	ДокОбъект.Товары.Очистить();
	Для Каждого СтрокаТоваров Из Товары Цикл
		Если ЗначениеЗаполнено(СтрокаТоваров.КоличествоПринятое) И СтрокаТоваров.КоличествоПринятое <> СтрокаТоваров.Количество Тогда
			НоваяСтрока = ДокОбъект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТоваров);
			НоваяСтрока.КоличествоПринятое = 0;
			НоваяСтрока.Количество = СтрокаТоваров.Количество - СтрокаТоваров.КоличествоПринятое;
			НоваяСтрока.СуммаСписания = НоваяСтрока.Цена * НоваяСтрока.Количество;
		КонецЕсли;
	КонецЦикла;
	
	Если ДокОбъект.Товары.Количество() = 0 И Не ДокументСоздан Тогда
		Возврат;
	КонецЕсли;
	
	ДокОбъект.ОбменДанными.Загрузка = Истина;
	ДокОбъект.Записать();

КонецПроцедуры

&После("ПриКопировании")
Процедура АЛФ_ПриКопировании(ОбъектКопирования)
	
	//+ИИТ_Аксеньюшкин
	Для Каждого СтрокаТоваров Из Товары Цикл
		СтрокаТоваров.КоличествоПринятое = 0;
	КонецЦикла;
	//-ИИТ_Аксеньюшкин
	
КонецПроцедуры

&ИзменениеИКонтроль("ПередЗаписью")
Процедура АЛФ_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ВидОперации) Тогда
		ВидОперации = Перечисления.ВидыОперацийПеремещениеТоваров.ПередачаМеждуСкладами;
	КонецЕсли;

	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());

	Если ВидОперации = Перечисления.ВидыОперацийПеремещениеТоваров.ПередачаМеждуСкладами Тогда
#Удаление
		Материалы.Очистить();
#КонецУдаления		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПеремещениеТоваров.ПередачаМеждуСотрудниками Тогда
#Удаление
		Товары.Очистить();
		ТоварыНаКомиссии.Очистить();
		ВозвратнаяТара.Очистить();
		МатериалыЗаказчика.Очистить();
#КонецУдаления		
	КонецЕсли;

	УчетВПродажныхЦенах = УчетнаяПолитика.СпособОценкиТоваровВРознице(Организация, Дата)
	= Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости;

	ТипСкладаОтправителя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СкладОтправитель, "ТипСклада");
	ТипСкладаПолучателя  = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СкладПолучатель, "ТипСклада");

	Если ТипСкладаОтправителя <> Перечисления.ТипыСкладов.ОптовыйСклад 
		ИЛИ ТипСкладаПолучателя <> Перечисления.ТипыСкладов.ОптовыйСклад
		И МатериалыЗаказчика.Количество() > 0 Тогда
		МатериалыЗаказчика.Очистить();
	КонецЕсли;	

	Если УчетВПродажныхЦенах
		И (ТипСкладаОтправителя <> Перечисления.ТипыСкладов.ОптовыйСклад
		ИЛИ ТипСкладаПолучателя <> Перечисления.ТипыСкладов.ОптовыйСклад) Тогда

		Для каждого Строка Из Товары Цикл

			Документы.ПеремещениеТоваров.ЗаполнитьСчетУчетаТовараРозницаВПродажныхЦенах(
			Строка.СчетУчета, ТипСкладаОтправителя);
			Документы.ПеремещениеТоваров.ЗаполнитьСчетУчетаТовараРозницаВПродажныхЦенах(
			Строка.НовыйСчетУчета, ТипСкладаПолучателя);

		КонецЦикла;

	КонецЕсли;

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьВозвратнуюТару")
		И ВозвратнаяТара.Количество() > 0 Тогда
		ВозвратнаяТара.Очистить();
	КонецЕсли;

	Если УчетнаяПолитика.СпособОценкиМПЗ(Организация, Дата) <> Перечисления.СпособыОценки.ФИФО 
		И НЕ ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугКомитентов")
		И Товары.Количество() > 0 Тогда

		Товары.ЗагрузитьКолонку(Новый Массив(Товары.Количество()), "ДокументОприходования");

	КонецЕсли; 

	// При групповом перепроведении реквизиты документов не меняются,
	// поэтому обновление связанных данных выполнять не требуется.
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	СчетаУчетаВДокументах.ЗаполнитьПередЗаписью(ЭтотОбъект, РежимЗаписи);

#Вставка
	//+ИИТ_Аксеньюшкин
	СчетСписанияНДС = ПланыСчетов.Хозрасчетный.Продажи_НДС;
	Для Каждого СтрокаТоваров Из Товары Цикл
		СтрокаТоваров.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Списывается;
	КонецЦикла;
	//-ИИТ_Аксеньюшкин
#КонецВставки
	
КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаПроверкиЗаполнения")
Процедура АЛФ_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	МассивНепроверяемыхРеквизитов = Новый Массив;

	УчетВПродажныхЦенах	= (УчетнаяПолитика.СпособОценкиТоваровВРознице(Организация, Дата) =
	Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости);
	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Организация, Дата);	

	// Проверка заполнения шапки документа

	РаздельныйУчетНДСДо2014Года = УчетнаяПолитика.РаздельныйУчетНДСДо2014Года(Организация, Дата);

	Если НЕ РаздельныйУчетНДСДо2014Года Тогда
		МассивНепроверяемыхРеквизитов.Добавить("НДСвСтоимостиТоваров");
	КонецЕсли;

	// Проверка табличной части "Товары"

	Если РаздельныйУчетНДСНаСчете19 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("НДСвСтоимостиТоваров");
	КонецЕсли;

	Если ВидОперации = Перечисления.ВидыОперацийПеремещениеТоваров.ПередачаМеждуСотрудниками Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СкладОтправитель");
		МассивНепроверяемыхРеквизитов.Добавить("СкладПолучатель");
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("СотрудникОтправитель");
		МассивНепроверяемыхРеквизитов.Добавить("СотрудникПолучатель");
	КонецЕсли;

	Если УчетВПродажныхЦенах Тогда

		РеквизитыСкладаОтправителя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СкладОтправитель,
		"ТипСклада, ТипЦенРозничнойТорговли");
		ТипСкладаОтправителя		= РеквизитыСкладаОтправителя.ТипСклада;
		ТипЦенСкладаОтправителя	= РеквизитыСкладаОтправителя.ТипЦенРозничнойТорговли;

		РеквизитыСкладаПолучателя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СкладПолучатель,
		"ТипСклада, ТипЦенРозничнойТорговли");
		ТипСкладаПолучателя		= РеквизитыСкладаПолучателя.ТипСклада;
		ТипЦенСкладаПолучателя	= РеквизитыСкладаПолучателя.ТипЦенРозничнойТорговли;

		Если ТипСкладаОтправителя = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка
			И ТипСкладаПолучателя = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка Тогда
			МассивНепроверяемыхРеквизитов.Добавить("Товары.Номенклатура");
			МассивНепроверяемыхРеквизитов.Добавить("Товары.Количество");
		КонецЕсли;

		Если ТипСкладаОтправителя = Перечисления.ТипыСкладов.РозничныйМагазин
			ИЛИ ТипСкладаПолучателя = Перечисления.ТипыСкладов.РозничныйМагазин Тогда

			ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();

			ПорядокСубконто = Новый Массив();
			ПорядокСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
			ПорядокСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);

			ПодразделениеОстатков	= ?(ТипСкладаОтправителя = Перечисления.ТипыСкладов.РозничныйМагазин,
			ПодразделениеОтправитель, ПодразделениеПолучатель);

			Склад			= ?(ТипСкладаОтправителя = Перечисления.ТипыСкладов.РозничныйМагазин,
			СкладОтправитель, СкладПолучатель);
			ТипЦенСклада	= ?(ТипСкладаОтправителя = Перечисления.ТипыСкладов.РозничныйМагазин,
			ТипЦенСкладаОтправителя, ТипЦенСкладаПолучателя);

			МассивНоменклатуры	= ОбщегоНазначения.ВыгрузитьКолонку(Товары, "Номенклатура", Истина);

			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("КонецПериода",		Дата);
			Запрос.УстановитьПараметр("Организация",		Организация);
			Запрос.УстановитьПараметр("Подразделение",		ПодразделениеОстатков);
			Запрос.УстановитьПараметр("Склад",				Склад);
			Запрос.УстановитьПараметр("Счет",				ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ);
			Запрос.УстановитьПараметр("ПорядокСубконто",	ПорядокСубконто);
			Запрос.УстановитьПараметр("Номенклатура",		МассивНоменклатуры);

			Запрос.Текст =
			"ВЫБРАТЬ
			|	Остатки.Субконто1 КАК Номенклатура,
			|	Остатки.СуммаОстатокДт КАК СуммаОстаток,
			|	Остатки.КоличествоОстатокДт КАК КоличествоОстаток
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Остатки(
			|			&КонецПериода,
			|			Счет = &Счет,
			|			&ПорядокСубконто,
			|			Организация = &Организация
			|				И (Подразделение = &Подразделение
			|					ИЛИ Подразделение ЕСТЬ NULL )
			|				И Субконто1 В (&Номенклатура)
			|				И Субконто2 = &Склад) КАК Остатки
			|ГДЕ
			|	Остатки.КоличествоОстаток > 0";

			ТаблицаУчетныхЦен = Запрос.Выполнить().Выгрузить();
			ТаблицаУчетныхЦен.Индексы.Добавить("Номенклатура");

			ТаблицаРозничныхЦен	= Ценообразование.ПолучитьТаблицуЦенНоменклатуры(
			МассивНоменклатуры, ТипЦенСклада, Дата);

			Для каждого СтрокаТаблицы Из Товары Цикл

				Префикс		= "Товары[" + Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].";
				ИмяСписка	= НСтр("ru = 'Товары'");

				РозничнаяЦена	= 0;
				УчетнаяЦена	= 0;

				СтрокаТаблицыРозничныхЦен = ТаблицаРозничныхЦен.Найти(СтрокаТаблицы.Номенклатура, "Номенклатура");
				Если СтрокаТаблицыРозничныхЦен <> Неопределено Тогда
					РозничнаяЦена	= РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(
					СтрокаТаблицыРозничныхЦен.Цена, СтрокаТаблицыРозничныхЦен.Валюта, ВалютаРегламентированногоУчета, СтрокаТаблицыРозничныхЦен.Курс,
					1, СтрокаТаблицыРозничныхЦен.Кратность, 1);
				Иначе
					РозничнаяЦена	= 0;
				КонецЕсли;

				Если РозничнаяЦена = 0 Тогда
					ТекстСообщения	= НСтр("ru = 'Не установлена розничная цена.'");
					ТекстСообщения	= ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Корректность",
					НСтр("ru = 'Номенклатура'"), СтрокаТаблицы.НомерСтроки, ИмяСписка, ТекстСообщения);
					Поле = Префикс + "Номенклатура";
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
					Продолжить;
				КонецЕсли;

				СтрокаТаблицыУчетныхЦен = ТаблицаУчетныхЦен.Найти(СтрокаТаблицы.Номенклатура, "Номенклатура");
				Если СтрокаТаблицыУчетныхЦен <> Неопределено Тогда
					Если СтрокаТаблицыУчетныхЦен.КоличествоОстаток <> 0 Тогда
						УчетнаяЦена = Окр(СтрокаТаблицыУчетныхЦен.СуммаОстаток / СтрокаТаблицыУчетныхЦен.КоличествоОстаток, 2, 1);
					КонецЕсли;
				Иначе
					УчетнаяЦена	= 0;
				КонецЕсли;

				//Допустимое отклонение цены составляет цену минимальной единицы количества (0.001)
				ДопустимоеОтклонение = УчетнаяЦена * 0.001;

				Если УчетнаяЦена <> 0 
					И (РозничнаяЦена - УчетнаяЦена > ДопустимоеОтклонение
					ИЛИ РозничнаяЦена - УчетнаяЦена < - ДопустимоеОтклонение) Тогда
					ТекстСообщения	= НСтр("ru = 'Розничная цена (%1) не равна учетной цене (%2).
					|Измените розничную цену или проведите переоценку.'");
					ТекстСообщения	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, РозничнаяЦена, УчетнаяЦена);
					ТекстСообщения	= ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Корректность",
					НСтр("ru = 'Номенклатура'"), СтрокаТаблицы.НомерСтроки, ИмяСписка, ТекстСообщения);
					Поле = Префикс + "Номенклатура";
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				КонецЕсли;

			КонецЦикла;

		КонецЕсли;

	КонецЕсли;

	// Проверка табличной части "Возвратная тара"

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьВозвратнуюТару") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВозвратнаяТара.Номенклатура");
		МассивНепроверяемыхРеквизитов.Добавить("ВозвратнаяТара.Количество");
		МассивНепроверяемыхРеквизитов.Добавить("ВозвратнаяТара.СчетУчета");
		МассивНепроверяемыхРеквизитов.Добавить("ВозвратнаяТара.НовыйСчетУчета");
	КонецЕсли; 

	// Проверка табличной части "Материалы заказчика" 

	Если НЕ ПолучитьФункциональнуюОпцию("ВедетсяПроизводственнаяДеятельность") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("МатериалыЗаказчика.Номенклатура");
		МассивНепроверяемыхРеквизитов.Добавить("МатериалыЗаказчика.Количество");
		МассивНепроверяемыхРеквизитов.Добавить("МатериалыЗаказчика.СчетУчета");
		МассивНепроверяемыхРеквизитов.Добавить("МатериалыЗаказчика.Контрагент");
	КонецЕсли; 

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
#Удаление	
	СчетаУчетаВДокументах.ПроверитьЗаполнение(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
#КонецУдаления
КонецПроцедуры
