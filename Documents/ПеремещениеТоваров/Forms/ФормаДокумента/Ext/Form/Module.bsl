
#Область Перенос_доработок

//ИИТ_Аксеньюшкин
&НаСервере
Процедура ВидОперацииПриИзмененииНаСервере()
	Если Объект.ВидОперации = Перечисления.ВидыОперацийПеремещениеТоваров.ПередачаМеждуСотрудниками Тогда
		Объект.ОбъектПолучатель = Объект.ОбъектОтправитель;
		Объект.СкладПолучатель = Объект.СкладОтправитель;
		Объект.ИИТ_ОтветственныйЗаПрием = Объект.ОбъектПолучатель.ИИТ_ОтветственныйОператор;
	Иначе
	КонецЕсли;
КонецПроцедуры

//ИИТ_Аксеньюшкин
Функция ПолучитьКоличествоВсегоПоПозиции(Номенклатура, Конфигурация, Цвет, Размер, ИнвентарныйНомер, СерийныйНомер)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИИТ_ОстаткиТМЦОстатки.КоличествоОстаток КАК КоличествоВсего,
	|	ИИТ_ОстаткиТМЦОстатки.СуммаОстаток КАК СуммаОстаток
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	РегистрНакопления.ИИТ_ОстаткиТМЦ.Остатки(
	|			&Дата,
	|			МОЛ = &МОЛ
	|				И Склад = &Склад
	|				И Номенклатура = &Номенклатуры
	|				И Конфигурация = &Конфигурация
	|				И Размер = &Размер
	|				И Цвет = &Цвет) КАК ИИТ_ОстаткиТМЦОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.КоличествоОстаток,
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.СтоимостьОстаток
	|ИЗ
	|	РегистрНакопления.ИИТ_ОстаткиОсновныхСредствИИнструмента.Остатки(
	|			&Дата,
	|			МОЛ = &МОЛ
	|				И Склад = &Склад
	|				И ОсновноеСредство = &Номенклатуры
	|				И Конфигурация = &Конфигурация
	|				И Размер = &Размер
	|				И Цвет = &Цвет
	|				И ИнвентарныйНомер = &ИнвентарныйНомер
	|				И СерийныйНомер = &СерийныйНомер) КАК ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ.КоличествоВсего) КАК КоличествоВсего,
	|	СУММА(ВТ.СуммаОстаток) КАК СуммаОстаток
	|ИЗ
	|	ВТ КАК ВТ";
	Запрос.УстановитьПараметр("Дата", ?(Объект.Проведен, Объект.Дата - 1, ТекущаяДата()));
	Запрос.УстановитьПараметр("Склад", Объект.СкладОтправитель);
	Запрос.УстановитьПараметр("МОЛ", Объект.СотрудникОтправитель);
	Запрос.УстановитьПараметр("Номенклатуры", Номенклатура);
	Запрос.УстановитьПараметр("Конфигурация", Конфигурация);
	Запрос.УстановитьПараметр("Цвет", Цвет);
	Запрос.УстановитьПараметр("Размер", Размер);
	Запрос.УстановитьПараметр("ИнвентарныйНомер", ИнвентарныйНомер);
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтруктураРезультата = Новый Структура;
		СтруктураРезультата.Вставить("Количество", Выборка.КоличествоВсего);
		СтруктураРезультата.Вставить("Сумма", Выборка.СуммаОстаток);
		Возврат СтруктураРезультата;
	Иначе
		СтруктураРезультата = Новый Структура;
		СтруктураРезультата.Вставить("Количество", 0);
		СтруктураРезультата.Вставить("Сумма", 0);
		Возврат СтруктураРезультата;
	КонецЕсли;

КонецФункции

//ИИТ_Аксеньюшкин
&НаСервереБезКонтекста
Функция ПолучитьЦенуНоменклатуры(Номенклатура, СкладОтправитель, СотрудникОтправитель, Дата)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(ИИТ_ОстаткиТМЦОстатки.КоличествоОстаток) КАК КоличествоОстаток,
	|	СУММА(ИИТ_ОстаткиТМЦОстатки.СуммаОстаток) КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.ИИТ_ОстаткиТМЦ.Остатки(
	|			&Дата,
	|			Склад = &Склад
	|				И МОЛ = &МОЛ
	|				И Номенклатура = &Номенклатура) КАК ИИТ_ОстаткиТМЦОстатки";
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Склад", СкладОтправитель);
	Запрос.УстановитьПараметр("МОЛ", СотрудникОтправитель);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат ?(ЗначениеЗаполнено(Выборка.КоличествоОстаток), ?(ЗначениеЗаполнено(Выборка.СуммаОстаток), Выборка.СуммаОстаток, 0) / Выборка.КоличествоОстаток, 0);
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

//ИИТ_Аксеньюшкин
Функция ТекПользователь()
	Возврат ПараметрыСеанса.ТекущийПользователь;
КонецФункции

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаСервере
Процедура ЗаполнитьНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИИТ_ОстаткиТМЦОстатки.Номенклатура КАК Номенклатура,
	|	ИИТ_ОстаткиТМЦОстатки.КоличествоОстаток КАК КоличествоВсего,
	|	ИИТ_ОстаткиТМЦОстатки.СуммаОстаток КАК СуммаСписания,
	|	ИИТ_ОстаткиТМЦОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	1 КАК Коэффициент,
	|	&Счет КАК СчетУчета,
	|	&Счет КАК НовыйСчетУчета,
	|	"""" КАК ИнвентарныйНомер,
	|	"""" КАК СерийныйНомер,
	|	ИИТ_ОстаткиТМЦОстатки.БухгалтерскаяНоменклатура КАК БухгалтерскаяНоменклатура,
	|	ИИТ_ОстаткиТМЦОстатки.Конфигурация КАК Конфигурация,
	|	ИИТ_ОстаткиТМЦОстатки.Размер КАК Размер,
	|	ИИТ_ОстаткиТМЦОстатки.Цвет КАК Цвет
	|ИЗ
	|	РегистрНакопления.ИИТ_ОстаткиТМЦ.Остатки(
	|			&Дата,
	|			МОЛ = &МОЛ
	|				И Склад = &Склад
	|				И Номенклатура.ВидНоменклатуры = &ВидНоменклатуры) КАК ИИТ_ОстаткиТМЦОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.ОсновноеСредство,
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.КоличествоОстаток,
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.СтоимостьОстаток,
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.ОсновноеСредство.ЕдиницаИзмерения,
	|	1,
	|	&Счет,
	|	&Счет,
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.ИнвентарныйНомер,
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.СерийныйНомер,
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.БухгалтерскаяНоменклатура,
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.Конфигурация,
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.Размер,
	|	ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки.Цвет
	|ИЗ
	|	РегистрНакопления.ИИТ_ОстаткиОсновныхСредствИИнструмента.Остатки(
	|			&Дата,
	|			МОЛ = &МОЛ
	|				И Склад = &Склад
	|				И ОсновноеСредство.ВидНоменклатуры = &ВидНоменклатуры) КАК ИИТ_ОстаткиОсновныхСредствИИнструментаОстатки";
	Запрос.УстановитьПараметр("Дата", ?(Объект.Проведен, Объект.Дата - 1, ТекущаяДата()));
	Запрос.УстановитьПараметр("Склад", Объект.СкладОтправитель);
	Запрос.УстановитьПараметр("МОЛ", Объект.СотрудникОтправитель);
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.Хозрасчетный.ТоварыНаСкладах);
	Запрос.УстановитьПараметр("ВидНоменклатуры", Объект.ИИТ_ВидНоменклатуры);
	
	ТЗ = Запрос.Выполнить().Выгрузить();
	Объект.Товары.Очистить();
	
	Для Каждого СтрокаТЗ Из ТЗ Цикл
		НоваяСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
		НоваяСтрока.Цена = ?(ЗначениеЗаполнено(СтрокаТЗ.КоличествоВсего), СтрокаТЗ.СуммаСписания / СтрокаТЗ.КоличествоВсего, 0);
		НоваяСтрока.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Списывается;
	КонецЦикла;
	
	//Элементы.ТоварыПолностью.Видимость = Истина;
	//Элементы.ТоварыКоличествоВсего.Видимость = Истина;
	
КонецПроцедуры

//ИИТ_Аксеньюшкин
Процедура ЗаполнитьОстатки(ЗаполнятьЦену = Истина)

	Для Каждого СтрокаТоваров Из Объект.Товары Цикл

		СтруктураОстатков = ПолучитьКоличествоВсегоПоПозиции(СтрокаТоваров.Номенклатура, СтрокаТоваров.Конфигурация, 
																		СтрокаТоваров.Цвет, СтрокаТоваров.Размер, СтрокаТоваров.ИнвентарныйНомер, СтрокаТоваров.СерийныйНомер);

		СтрокаТоваров.КоличествоВсего = СтруктураОстатков.Количество;
		//СтрокаТоваров.СуммаСписания = СтруктураОстатков.Сумма;
		Если ЗаполнятьЦену Тогда
			СтрокаТоваров.Цена = ?(ЗначениеЗаполнено(СтрокаТоваров.КоличествоВсего), СтруктураОстатков.Сумма / СтрокаТоваров.КоличествоВсего, 0);
		КонецЕсли;
		СтрокаТоваров.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Списывается;

	КонецЦикла;

КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ОбъектОтправительПриИзменении(Элемент)
	ОбъектОтправительПриИзмененииНаСервере();
КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаСервере
Процедура ОбъектОтправительПриИзмененииНаСервере()
	Объект.ИИТ_ОтветственныйЗаОтправку = Объект.ОбъектОтправитель.ИИТ_ОтветственныйОператор;
КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ОбъектПолучательПриИзменении(Элемент)
	ОбъектПолучательПриИзмененииНаСервере();
КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаСервере
Процедура ОбъектПолучательПриИзмененииНаСервере()
	Объект.ИИТ_ОтветственныйЗаПрием = Объект.ОбъектПолучатель.ИИТ_ОтветственныйОператор;
КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ОтметитьВсе(Команда)
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		СтрокаТоваров.Полностью = Истина;
		СтрокаТоваров.Количество = СтрокаТоваров.КоличествоВсего;
	КонецЦикла;
КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура СнятьВсеПометки(Команда)
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		СтрокаТоваров.Полностью = Ложь;
		СтрокаТоваров.Количество = 0;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПринятоеПриИзменении(Элемент)

	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	Если ТекСтрока <> Неопределено Тогда
		ТекСтрока.СуммаСписания = ТекСтрока.Цена * ТекСтрока.Количество;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТоварыКонфигурацияПриИзменении(Элемент)
	ЗаполнитьОстатки();
КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ТоварыПередУдалением(Элемент, Отказ)
	
	Если Объект.ИИТ_ОтветственныйЗаОтправку <> Объект.ИИТ_ОтветственныйЗаПрием Тогда
		Если Параметры.Свойство("Принятие") ИЛИ Объект.ИИТ_ОтветственныйЗаПрием = ТекПользователь() Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ТоварыПолностьюПриИзменении(Элемент)

	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	Если ТекСтрока.Полностью Тогда
		ТекСтрока.Количество = ТекСтрока.КоличествоВсего;
	Иначе
		ТекСтрока.Количество = 0;
	КонецЕсли;
	ТекСтрока.СуммаСписания = ТекСтрока.Количество * ТекСтрока.Цена;

КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ТоварыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ЗаполнитьОстатки();
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура АЛФ_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)   
	
	//Команды формы
	КомандаФормы = Команды.Добавить("Заполнить");
	КомандаФормы.Заголовок = "Заполнить";
	КомандаФормы.Действие  = "Заполнить";
	
	КомандаФормы = Команды.Добавить("ПечатьМ11");
	КомандаФормы.Заголовок = "ПечатьМ11";
	КомандаФормы.Действие  = "ПечатьМ11";

	КомандаФормы = Команды.Добавить("ОтметитьВсе");
	КомандаФормы.Заголовок = "Отметить все";
	КомандаФормы.Действие  = "ОтметитьВсе";  

	КомандаФормы = Команды.Добавить("СнятьВсеПометки");
	КомандаФормы.Заголовок = "Снять все пометки";
	КомандаФормы.Действие  = "СнятьВсеПометки";  
	
	//Элементы
	Элементы.ГруппаНомерОрганизация.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ГруппаЛевоОсновнаяШапка = Элементы.Добавить("ГруппаЛевоОсновнаяШапка", Тип("ГруппаФормы"), Элементы.ГруппаНомерОрганизация);
	ГруппаЛевоОсновнаяШапка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаЛевоОсновнаяШапка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаЛевоОсновнаяШапка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаЛевоОсновнаяШапка.ОтображатьЗаголовок = Ложь;  	
	
	ГруппаПравоОсновнаяШапка = Элементы.Добавить("ГруппаПравоОсновнаяШапка", Тип("ГруппаФормы"), Элементы.ГруппаНомерОрганизация);
	ГруппаПравоОсновнаяШапка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПравоОсновнаяШапка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаПравоОсновнаяШапка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаПравоОсновнаяШапка.ОтображатьЗаголовок = Ложь;  	
	
	Элементы.Переместить(Элементы.ГруппаНомерДата, Элементы.ГруппаЛевоОсновнаяШапка);
	
	ПолеВидОперации = Элементы.ВидОперации;
	ПолеВидОперации.Вид = ВидПоляФормы.ПолеПереключателя;
	ПолеВидОперации.КоличествоКолонок = 2;
	ПолеВидОперации.СписокВыбора.Добавить(Перечисления.ВидыОперацийПеремещениеТоваров.ПередачаМеждуСкладами, "Передача между складами");
	ПолеВидОперации.СписокВыбора.Добавить(Перечисления.ВидыОперацийПеремещениеТоваров.ПередачаМеждуСотрудниками, "Передача между сотрудниками");
	Элементы.Переместить(ПолеВидОперации, ГруппаЛевоОсновнаяШапка);
	
	Элементы.Переместить(Элементы.Организация, ГруппаПравоОсновнаяШапка);

	ПолеВвода_АЛФ = Элементы.Добавить("ИИТ_ВидНоменклатуры", Тип("ПолеФормы"), ГруппаПравоОсновнаяШапка);
	ПолеВвода_АЛФ.Заголовок = "Вид номенклатуры";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ИИТ_ВидНоменклатуры";
	
	ПолеВвода_АЛФ = Элементы.Вставить("ОбъектОтправитель", Тип("ПолеФормы"), Элементы.ГруппаСкладЛевая, Элементы.СкладОтправитель);
	ПолеВвода_АЛФ.Заголовок = "Объект отправитель";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ОбъектОтправитель";
	ПолеВвода_АЛФ.УстановитьДействие("ПриИзменении", "ОбъектОтправительПриИзменении");

    Элементы.СкладОтправитель.Заголовок = "Склад отправителя";
	Элементы.ПодразделениеОтправитель.Видимость = Ложь;
	Элементы.СотрудникОтправитель.Заголовок = "МОЛ отправителя"; 
	
	ПолеВвода_АЛФ = Элементы.Добавить("ИИТ_ПодтверждениеОтправки", Тип("ПолеФормы"), Элементы.ГруппаСкладЛевая);
	ПолеВвода_АЛФ.Заголовок = "Подтверждение отправки";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеФлажка;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ИИТ_ПодтверждениеОтправки";
	ПолеВвода_АЛФ.Видимость = Ложь;
	
	ПолеВвода_АЛФ = Элементы.Добавить("КонтрагентОтправитель", Тип("ПолеФормы"), Элементы.ГруппаСкладЛевая);
	ПолеВвода_АЛФ.Заголовок = "Контрагент отправитель";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.КонтрагентОтправитель";
	
	ПолеВвода_АЛФ = Элементы.Вставить("ОбъектПолучатель", Тип("ПолеФормы"), Элементы.ГруппаСкладПравая, Элементы.СкладПолучатель);
	ПолеВвода_АЛФ.Заголовок = "Объект получатель";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ОбъектПолучатель";
	ПолеВвода_АЛФ.УстановитьДействие("ПриИзменении", "ОбъектПолучательПриИзменении");
	
    Элементы.СкладПолучатель.Заголовок = "Склад получателя";
	Элементы.ПодразделениеПолучатель.Видимость = Ложь;
    Элементы.СотрудникПолучатель.Заголовок = "МОЛ получателя";
	
	ПолеВвода_АЛФ = Элементы.Добавить("ИИТ_ПодтверждениеПолучения", Тип("ПолеФормы"), Элементы.ГруппаСкладПравая);
	ПолеВвода_АЛФ.Заголовок = "Подтверждение получения";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеФлажка;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ИИТ_ПодтверждениеПолучения";
	ПолеВвода_АЛФ.Видимость = Ложь;
	
	ПолеВвода_АЛФ = Элементы.Добавить("КонтрагентПолучатель", Тип("ПолеФормы"), Элементы.ГруппаСкладПравая);
	ПолеВвода_АЛФ.Заголовок = "Контрагент получатель";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.КонтрагентПолучатель";  
	
	//ТЧ товары
	Элементы.Товары.УстановитьДействие("ПриОкончанииРедактирования", "ТоварыПриОкончанииРедактирования");
	Элементы.Товары.УстановитьДействие("ПередУдалением", "ТоварыПередУдалением");
	
	КнопкаКоманднойПанели = Элементы.Добавить("ТоварыЗаполнить", Тип("КнопкаФормы"), Элементы.ТоварыКнопкиКоманднойПанели);
	КнопкаКоманднойПанели.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	КнопкаКоманднойПанели.Заголовок = "Заполнить";
	КнопкаКоманднойПанели.ИмяКоманды  = "Заполнить"; 
	
	КнопкаКоманднойПанели = Элементы.Добавить("ТоварыОтметитьВсе", Тип("КнопкаФормы"), Элементы.ТоварыКнопкиКоманднойПанели);
	КнопкаКоманднойПанели.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	КнопкаКоманднойПанели.Заголовок = "Отметить все";
	КнопкаКоманднойПанели.ИмяКоманды  = "ОтметитьВсе"; 
	КнопкаКоманднойПанели.Картинка = БиблиотекаКартинок.УстановитьФлажки;
	
	КнопкаКоманднойПанели = Элементы.Добавить("ТоварыСнятьВсеПометки", Тип("КнопкаФормы"), Элементы.ТоварыКнопкиКоманднойПанели);
	КнопкаКоманднойПанели.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	КнопкаКоманднойПанели.Заголовок = "Снять все";
	КнопкаКоманднойПанели.ИмяКоманды  = "СнятьВсеПометки"; 
	КнопкаКоманднойПанели.Картинка = БиблиотекаКартинок.СнятьФлажки;
	
	Элементы.ТоварыНомерСтроки.ФиксацияВТаблице = ФиксацияВТаблице.Нет;  
	//В исходной конфигурации у реквизита ТЧ изменен тип на составной
	//поэтому не доступны подчиненные реквизиты и нет пути к данным
	//Элементы.ТоварыНоменклатураКод.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	//Элементы.ТоварыНоменклатураАртикул.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	
	//Группы в ТЧ
	ГруппаКолонок_Н = Элементы.Вставить("ТоварыГруппа1", Тип("ГруппаФормы"), Элементы.Товары, Элементы.ТоварыКоличествоМест); 
	ГруппаКолонок_Н.Вид = ВидГруппыФормы.ГруппаКолонок;
	ГруппаКолонок_Н.Группировка = ГруппировкаКолонок.Вертикальная;
	ГруппаКолонок_Н.ОтображатьВШапке = Ложь;  
	
	Элементы.ТоварыНоменклатура.ФиксацияВТаблице = ФиксацияВТаблице.Нет;  
	Элементы.Переместить(Элементы.ТоварыНоменклатура, ГруппаКолонок_Н); 

	ЭлементТаблицыФормы = Элементы.Добавить("ТоварыБухгалтерскаяНоменклатура", Тип("ПолеФормы"), ГруппаКолонок_Н);
	ЭлементТаблицыФормы.Заголовок = "Бухгалтерская номенклатура"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.БухгалтерскаяНоменклатура";
	
	ЭлементТаблицыФормы = Элементы.Добавить("ТоварыНоменклатураТоварнаяКатегория", Тип("ПолеФормы"), ГруппаКолонок_Н);
	ЭлементТаблицыФормы.Заголовок = "Номенклатура снабжения"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.Видимость = Ложь;
	
	ГруппаКолонок_ИС = Элементы.Вставить("ТоварыГруппа2", Тип("ГруппаФормы"), Элементы.Товары, Элементы.ТоварыКоличествоМест); 
	ГруппаКолонок_ИС.Вид = ВидГруппыФормы.ГруппаКолонок;
	ГруппаКолонок_ИС.Группировка = ГруппировкаКолонок.Вертикальная;
	ГруппаКолонок_ИС.ОтображатьВШапке = Ложь;  

	ЭлементТаблицыФормы = Элементы.Добавить("ТоварыИнвентарныйНомер", Тип("ПолеФормы"), ГруппаКолонок_ИС);
	ЭлементТаблицыФормы.Заголовок = "Инвентарный номер"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.ИнвентарныйНомер";
	
	ЭлементТаблицыФормы = Элементы.Добавить("ТоварыСерийныйНомер", Тип("ПолеФормы"), ГруппаКолонок_ИС);
	ЭлементТаблицыФормы.Заголовок = "Серийный номер"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.СерийныйНомер";  
	
	ЭлементТаблицыФормы = Элементы.Вставить("ТоварыУХ_Характеристика", Тип("ПолеФормы"), Элементы.Товары, Элементы.ТоварыКоличествоМест);
	ЭлементТаблицыФормы.Заголовок = "Характеристика (УХ)"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.УХ_Характеристика";  
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	
	ГруппаКолонок_КЦР = Элементы.Вставить("ТоварыГруппа3", Тип("ГруппаФормы"), Элементы.Товары, Элементы.ТоварыКоличествоМест); 
	ГруппаКолонок_КЦР.Вид = ВидГруппыФормы.ГруппаКолонок;
	ГруппаКолонок_КЦР.Группировка = ГруппировкаКолонок.Вертикальная;
	ГруппаКолонок_КЦР.ОтображатьВШапке = Ложь;  

	ЭлементТаблицыФормы = Элементы.Добавить("ТоварыКонфигурация", Тип("ПолеФормы"), ГруппаКолонок_КЦР);
	ЭлементТаблицыФормы.Заголовок = "Конфигурация"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.Конфигурация";
	ЭлементТаблицыФормы.УстановитьДействие("ПриИзменении", "ТоварыКонфигурацияПриИзменении");
	
	ЭлементТаблицыФормы = Элементы.Добавить("ТоварыЦвет", Тип("ПолеФормы"), ГруппаКолонок_КЦР);
	ЭлементТаблицыФормы.Заголовок = "Цвет"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.Цвет";
	ЭлементТаблицыФормы.УстановитьДействие("ПриИзменении", "ТоварыЦветПриИзменении");

	ЭлементТаблицыФормы = Элементы.Добавить("ТоварыРазмер", Тип("ПолеФормы"), ГруппаКолонок_КЦР);
	ЭлементТаблицыФормы.Заголовок = "Размер"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.Размер";
	ЭлементТаблицыФормы.УстановитьДействие("ПриИзменении", "ТоварыРазмерПриИзменении");
	
	//Элементы.Переместить(Элементы.ТоварыНоменклатураЕдиницаИзмерения, Элементы.Товары, Элементы.ТоварыКоэффициент);
	
	ЭлементТаблицыФормы = Элементы.Вставить("ТоварыКоличествоПринятое", Тип("ПолеФормы"), Элементы.Товары, Элементы.ТоварыЦенаВРознице);
	ЭлементТаблицыФормы.Заголовок = "Кол-во принятое"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.КоличествоПринятое"; 
	ЭлементТаблицыФормы.Видимость = Ложь;
	
	ЭлементТаблицыФормы = Элементы.Вставить("ТоварыСуммаСписания", Тип("ПолеФормы"), Элементы.Товары, Элементы.ТоварыЦенаВРознице);
	ЭлементТаблицыФормы.Заголовок = "Сумма списания"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.СуммаСписания"; 

	ЭлементТаблицыФормы = Элементы.Вставить("ТоварыПолностью", Тип("ПолеФормы"), Элементы.Товары, Элементы.ТоварыЦенаВРознице);
	ЭлементТаблицыФормы.Заголовок = "Забрать все"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.Полностью"; 
	ЭлементТаблицыФормы.ШрифтЗаголовка = Новый Шрифт(ЭлементТаблицыФормы.ШрифтЗаголовка,,,Истина);	
	ЭлементТаблицыФормы.УстановитьДействие("ПриИзменении", "ТоварыПолностьюПриИзменении");
	
	ЭлементТаблицыФормы = Элементы.Вставить("ТоварыКоличествоВсего", Тип("ПолеФормы"), Элементы.Товары, Элементы.ТоварыЦенаВРознице);
	ЭлементТаблицыФормы.Заголовок = "Количество всего"; 
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.Товары.КоличествоВсего"; 
	ЭлементТаблицыФормы.ШрифтЗаголовка = Новый Шрифт(ЭлементТаблицыФормы.ШрифтЗаголовка,,,Истина);	
	
	//Подвал
	Элементы.Ответственный.Заголовок = "Автор";
	
	//Группа ГруппаОтветственные  
	ГруппаОтветственные = Элементы.Добавить("ГруппаОтветственные", Тип("ГруппаФормы"));
	ГруппаОтветственные.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаОтветственные.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаОтветственные.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаОтветственные.ОтображатьЗаголовок = Ложь;  	

	ПолеВвода_АЛФ = Элементы.Добавить("ИИТ_ОтветственныйЗаОтправку", Тип("ПолеФормы"), ГруппаОтветственные);
	ПолеВвода_АЛФ.Заголовок = "Ответственный за отправку";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ИИТ_ОтветственныйЗаОтправку";  
	
	ПолеВвода_АЛФ = Элементы.Добавить("ИИТ_ОтветственныйЗаПрием", Тип("ПолеФормы"), ГруппаОтветственные);
	ПолеВвода_АЛФ.Заголовок = "Ответственный за прием";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ИИТ_ОтветственныйЗаПрием";  

КонецПроцедуры

&НаСервере
Процедура АЛФ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)  
	
	//+ИИТ_Аксеньюшкин	
	Если Параметры.Свойство("Принятие") ИЛИ Объект.ИИТ_ОтветственныйЗаПрием = ПараметрыСеанса.ТекущийПользователь Тогда
		
		Элементы.ГруппаШапка.ТолькоПросмотр = Истина;
		Элементы.ТоварыСуммаСписания.ТолькоПросмотр = Истина;
		Элементы.ТоварыКоличество.ТолькоПросмотр = Истина;
		Элементы.ТоварыСерийныйНомер.ТолькоПросмотр = Истина;
		Элементы.ТоварыИнвентарныйНомер.ТолькоПросмотр = Истина;
		Элементы.ТоварыНоменклатура.ТолькоПросмотр = Истина;
		Элементы.ТоварыПолностью.ТолькоПросмотр = Истина;
		Элементы.ТоварыКоличествоВсего.ТолькоПросмотр = Истина;

		Элементы.ТоварыКоличествоПринятое.Видимость = Истина;
		Если Параметры.Свойство("Принятие") Тогда
			Для Каждого СтрокаТоваров Из Объект.Товары Цикл
				СтрокаТоваров.КоличествоПринятое = СтрокаТоваров.Количество;
			КонецЦикла;
		КонецЕсли;
		Элементы.ИИТ_ПодтверждениеПолучения.Видимость = Истина;
		Элементы.ИИТ_ПодтверждениеПолучения.Доступность = Истина;
		Элементы.ИИТ_ПодтверждениеПолучения.ТолькоПросмотр = Ложь;
		
		Элементы.ТоварыПолностью.Видимость = Ложь;

	КонецЕсли;
	
	Если Параметры.Свойство("Расхождение") ИЛИ (Объект.ИИТ_ОтветственныйЗаОтправку = ПараметрыСеанса.ТекущийПользователь И 
	Объект.ИИТ_ОтветственныйЗаПрием <> ПараметрыСеанса.ТекущийПользователь) Тогда
		Элементы.ТоварыКоличествоПринятое.Видимость = Истина;
		Элементы.ТоварыКоличествоПринятое.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	//Если Объект.Проведен Тогда
	//	Элементы.ТоварыПолностью.Видимость = Ложь;
	//	Элементы.ТоварыКоличествоВсего.Видимость = Ложь;
	//КонецЕсли;
	ЗаполнитьОстатки(Ложь);
	//-ИИТ_Аксеньюшкин   
	
КонецПроцедуры

&НаКлиенте
Процедура АЛФ_ПослеЗаписиПосле(ПараметрыЗаписи)

	//+ИИТ_Аксеньюшкин
	ЗаполнитьОстатки(Ложь);
	//-ИИТ_Аксеньюшкин

КонецПроцедуры

&НаСервере
Процедура АЛФ_ПослеЗаписиНаСервереПосле(ТекущийОбъект, ПараметрыЗаписи)
	
	//+ИИТ_Аксеньюшкин
	//Если Объект.Проведен Тогда
	//	Элементы.ТоварыПолностью.Видимость = Ложь;
	//	Элементы.ТоварыКоличествоВсего.Видимость = Ложь;
	//КонецЕсли;
	//-ИИТ_Аксеньюшкин

КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ВидОперацииПриИзменении")
Процедура АЛФ_ВидОперацииПриИзменении(Элемент)
#Удаление
	УправлениеФормой(ЭтаФорма);
#КонецУдаления
#Вставка    
	ВидОперацииПриИзмененииНаСервере();
#КонецВставки
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("СкладОтправительПриИзмененииНаСервере")
Процедура АЛФ_СкладОтправительПриИзмененииНаСервере()
#Удаление
	СвойстваСкладаОтправителя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
	Объект.СкладОтправитель, "ТипСклада, ТипЦенРозничнойТорговли");
	ТипЦенСкладОтправитель = СвойстваСкладаОтправителя.ТипЦенРозничнойТорговли;
	ТипСкладаОтправитель   = СвойстваСкладаОтправителя.ТипСклада;

	Объект.ПодразделениеОтправитель = ОбщегоНазначенияБПВызовСервера.ПолучитьПодразделение(
	Объект.Организация, Объект.СкладОтправитель);

	УстановитьНДСВСтоимостиТоваров();

	ЗаполнитьСтрокиТабличнойЧастиТоварыНаСервере();

	СчетаУчетаВДокументах.ЗаполнитьОбъектПриИзменении(НачатьЗаполнениеСчетовУчета("СкладОтправитель", Объект));

	УправлениеФормой(ЭтаФорма);
#КонецУдаления
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("СкладПолучательПриИзмененииНаСервере")
Процедура АЛФ_СкладПолучательПриИзмененииНаСервере()
#Удаление
	СвойстваСкладаПолучателя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
	Объект.СкладПолучатель, "ТипСклада, ТипЦенРозничнойТорговли");
	ТипЦенСкладПолучатель = СвойстваСкладаПолучателя.ТипЦенРозничнойТорговли;
	ТипСкладаПолучатель   = СвойстваСкладаПолучателя.ТипСклада;

	Объект.ПодразделениеПолучатель = ОбщегоНазначенияБПВызовСервера.ПолучитьПодразделение(
	Объект.Организация, Объект.СкладПолучатель);

	УстановитьНДСВСтоимостиТоваров();

	ЗаполнитьСтрокиТабличнойЧастиТоварыНаСервере();

	СчетаУчетаВДокументах.ЗаполнитьОбъектПриИзменении(НачатьЗаполнениеСчетовУчета("СкладПолучатель", Объект));

	УправлениеФормой(ЭтаФорма);
#КонецУдаления
КонецПроцедуры

&НаКлиенте
Процедура АЛФ_ТоварыКоличествоПриИзмененииПосле(Элемент)
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	//+ИИТ_Аксеньюшкин
	СтрокаТабличнойЧасти.СуммаСписания = СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество;
	//-ИИТ_Аксеньюшкин
КонецПроцедуры

&НаКлиенте
Процедура АЛФ_ТоварыНоменклатураПриИзмененииПосле(Элемент)
	ЗаполнитьОстатки();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыРазмерПриИзменении(Элемент)
	ЗаполнитьОстатки();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦветПриИзменении(Элемент)
	ЗаполнитьОстатки();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
&ИзменениеИКонтроль("УправлениеФормой")
Процедура АЛФ_УправлениеФормой(Форма)

	Объект		= Форма.Объект;
	Элементы	= Форма.Элементы;

	Если Объект.НДСвСтоимостиТоваров = ПредопределенноеЗначение("Перечисление.ДействиеНДСВСтоимостиТоваров.ВключитьВСтоимость") Тогда
		Элементы.ГруппаСчетИАналитикаСписанияНДС.Доступность = Истина;
	Иначе
		Элементы.ГруппаСчетИАналитикаСписанияНДС.Доступность = Ложь;
	КонецЕсли; 

	ПередачаМеждуСкладами = (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПеремещениеТоваров.ПередачаМеждуСкладами"));
	ПередачаМеждуСотрудниками = (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПеремещениеТоваров.ПередачаМеждуСотрудниками"));
#Удаление
	Элементы.СкладОтправитель.Видимость         = ПередачаМеждуСкладами;
	Элементы.СкладПолучатель.Видимость          = ПередачаМеждуСкладами;
	Элементы.ГруппаМатериалыЗаказчика.Видимость = ПередачаМеждуСкладами
	И Форма.ВедетсяПроизводственнаяДеятельность	
	И (Форма.ТипСкладаПолучатель  = ПредопределенноеЗначение("Перечисление.ТипыСкладов.ОптовыйСклад"))
	И (Форма.ТипСкладаОтправитель = ПредопределенноеЗначение("Перечисление.ТипыСкладов.ОптовыйСклад"));
	Элементы.СотрудникОтправитель.Видимость     = ПередачаМеждуСотрудниками;
	Элементы.СотрудникПолучатель.Видимость      = ПередачаМеждуСотрудниками;
	Элементы.ГруппаМатериалы.Видимость          = ПередачаМеждуСотрудниками;

	Если ПередачаМеждуСотрудниками И Элементы.ГруппаСтраницы.ТекущаяСтраница <> Элементы.ГруппаМатериалы Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаМатериалы;
	КонецЕсли;	
#КонецУдаления
	Элементы.ПодразделениеОтправитель.Доступность = ЗначениеЗаполнено(Объект.Организация);
	Элементы.ПодразделениеПолучатель.Доступность  = ЗначениеЗаполнено(Объект.Организация);

#Удаление
	Если НужноУбратьЗакладкиСтраниц(Объект) ИЛИ ПередачаМеждуСотрудниками Тогда
#КонецУдаления
#Вставка
	Если НужноУбратьЗакладкиСтраниц(Объект) ИЛИ Истина Тогда
#КонецВставки
		Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Иначе
		Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
	КонецЕсли;

#Вставка
	ПередачаМеждуСкладами = Ложь;
#КонецВставки
	Элементы.ТоварыИзменитьТовары.Видимость = ПередачаМеждуСкладами;
	Элементы.ФормаВыгрузитьВВЕТИС.Видимость = ПередачаМеждуСкладами; 
	КомандаВыгрузитьВЕГАИС = Элементы.Найти("ФормаВыгрузитьВЕГАИС");
	Если КомандаВыгрузитьВЕГАИС <> Неопределено Тогда
		КомандаВыгрузитьВЕГАИС.Видимость = ПередачаМеждуСкладами;
	КонецЕсли;

КонецПроцедуры


&НаСервере
&ИзменениеИКонтроль("ОбработкаВыбораПодборВставкаИзБуфераНаСервере")
Процедура АЛФ_ОбработкаВыбораПодборВставкаИзБуфераНаСервере(ВыбранноеЗначение, ИмяТаблицы)

	ЭтоВставкаИзБуфера = ВыбранноеЗначение.Свойство("ЭтоВставкаИзБуфера");
	СписокСвойств = Неопределено;
	Если ЭтоВставкаИзБуфера Тогда

		ТаблицаТоваров = ВыбранноеЗначение.Данные;
		СписокСвойств = ВыбранноеЗначение.СписокСвойств;

		Если ИмяТаблицы = "МатериалыЗаказчика" Тогда
			СписокСвойств = "Номенклатура, КоличествоМест, ЕдиницаИзмерения, Коэффициент, Количество, Контрагент"; 
		КонецЕсли;	

	Иначе

		ТаблицаТоваров = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресПодобраннойНоменклатурыВХранилище);

	КонецЕсли;

	СписокНоменклатуры	= ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТоваров, "Номенклатура", Истина);

	Если ИмяТаблицы = "МатериалыЗаказчика" Тогда
		Если ТаблицаТоваров.Колонки.Найти("Контрагент") = Неопределено Тогда
			ТаблицаТоваров.Колонки.Добавить("Контрагент", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
		КонецЕсли;

		КонтрагентыНоменклатуры = ПолучитьЕдинственныхЗаказчиковМатериалов(СписокНоменклатуры);
		Для Каждого КонтрагентНоменклатуры Из КонтрагентыНоменклатуры Цикл
			ПоискНоменклатуры = Новый Структура("Номенклатура", КонтрагентНоменклатуры.Ключ);
			НайденныеСтроки = ТаблицаТоваров.НайтиСтроки(ПоискНоменклатуры);
			Для Каждого НайденныеСтроки Из НайденныеСтроки Цикл
				НайденныеСтроки.Контрагент = КонтрагентНоменклатуры.Значение;	
			КонецЦикла;
		КонецЦикла;	

	КонецЕсли;	

	ДобавленныеСтроки = Новый Массив;

	ДанныеОбъекта = Новый Структура("Дата, Организация, Склад");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);

	ДанныеОбъекта.Склад	= Объект.СкладОтправитель;
	СоответствиеСведенийОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
	СписокНоменклатуры, ДанныеОбъекта, Ложь, Ложь);

	ДанныеОбъекта.Склад	= Объект.СкладПолучатель;
	СоответствиеНовыхСведенийОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
	СписокНоменклатуры, ДанныеОбъекта, Ложь, Ложь);

	ЕстьРеквизитЦена = ТаблицаТоваров.Колонки.Найти("Цена") <> Неопределено;

	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл

		СведенияОНоменклатуре = СоответствиеСведенийОНоменклатуре.Получить(СтрокаТовара.Номенклатура);
		Если ЭтоВставкаИзБуфера 
			И СведенияОНоменклатуре <> Неопределено
			И ЗначениеЗаполнено(СведенияОНоменклатуре.Услуга)
			И СведенияОНоменклатуре.Услуга Тогда

			Продолжить;

		КонецЕсли;
		СтрокаТабличнойЧасти	= Объект[ИмяТаблицы].Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаТовара, СписокСвойств);
		ДобавленныеСтроки.Добавить(СтрокаТабличнойЧасти);

		НовыеСведенияОНоменклатуре = СоответствиеНовыхСведенийОНоменклатуре.Получить(СтрокаТабличнойЧасти.Номенклатура);

		Если (ИмяТаблицы = "Товары" ИЛИ ИмяТаблицы = "ТоварыНаКомиссии" ИЛИ ИмяТаблицы = "МатериалыЗаказчика") 
			И СведенияОНоменклатуре <> Неопределено Тогда
			СтрокаТабличнойЧасти.ЕдиницаИзмерения	= СведенияОНоменклатуре.ЕдиницаИзмерения;
			СтрокаТабличнойЧасти.Коэффициент		= СведенияОНоменклатуре.Коэффициент;
		КонецЕсли;

		Если ИмяТаблицы = "Товары" Тогда

			Если ТипСкладаОтправитель = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка Тогда

				Если РазделятьПоСтавкамНДС И СведенияОНоменклатуре <> Неопределено Тогда
					СтрокаТабличнойЧасти.СтавкаНДСВРознице = СведенияОНоменклатуре.СтавкаНДСВРознице;
				КонецЕсли;

				Если ТипСкладаПолучатель = Перечисления.ТипыСкладов.ОптовыйСклад  Тогда
					ЦенаВРознице = ?(ЕстьРеквизитЦена, СтрокаТовара.Цена, НовыеСведенияОНоменклатуре.ЦенаВРознице);

					СтрокаТабличнойЧасти.ЦенаВРознице	= ЦенаВРознице;
					СтрокаТабличнойЧасти.СуммаВРознице	= ЦенаВРознице * СтрокаТабличнойЧасти.Количество;
				КонецЕсли;

			ИначеЕсли ТипСкладаПолучатель = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка Тогда

				Если РазделятьПоСтавкамНДС И НовыеСведенияОНоменклатуре <> Неопределено Тогда
					СтрокаТабличнойЧасти.СтавкаНДСВРознице = НовыеСведенияОНоменклатуре.СтавкаНДСВРознице;
				КонецЕсли;

				Если ТипСкладаОтправитель = Перечисления.ТипыСкладов.ОптовыйСклад Тогда
					ЦенаВРознице = ?(ЕстьРеквизитЦена, СтрокаТовара.Цена, НовыеСведенияОНоменклатуре.ЦенаВРознице);

					СтрокаТабличнойЧасти.ЦенаВРознице	= ЦенаВРознице;
					СтрокаТабличнойЧасти.СуммаВРознице	= ЦенаВРознице * СтрокаТабличнойЧасти.Количество;
				КонецЕсли;

			КонецЕсли;

		КонецЕсли;

#Вставка
		//+ИИТ_Аксеньюшкин
		СтрокаТабличнойЧасти.Цена = ПолучитьЦенуНоменклатуры(СтрокаТабличнойЧасти.Номенклатура, Объект.СкладОтправитель, Объект.СотрудникОтправитель, Объект.Дата);
		СтрокаТабличнойЧасти.СуммаСписания = СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество;
		//-ИИТ_Аксеньюшкин
#КонецВставки
	КонецЦикла;

	СчетаУчетаВДокументах.ЗаполнитьСтроки(ДобавленныеСтроки, ИмяТаблицы, Объект, Документы.ПеремещениеТоваров);

	Если ЭтоВставкаИзБуфера Тогда

		ВыбранноеЗначение.КоличествоДобавленныхСтрок = ДобавленныеСтроки.Количество();

	КонецЕсли;

КонецПроцедуры

