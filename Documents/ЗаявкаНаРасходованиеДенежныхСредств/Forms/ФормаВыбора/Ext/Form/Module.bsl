
&НаСервере
Процедура АЛФ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	//Элементы формы  
	ПолеВвода_АЛФ = Элементы.Добавить("ИИТ_ОтображатьЗаполненные", Тип("ПолеФормы"));
	ПолеВвода_АЛФ.Заголовок = "Не отображать заполненные";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеФлажка;	
	ПолеВвода_АЛФ.ПутьКДанным = "ИИТ_ОтображатьЗаполненные";
	ПолеВвода_АЛФ.УстановитьДействие("ПриИзменении", "ИИТ_ОтображатьЗаполненныеПриИзменении");
	ПолеВвода_АЛФ.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	
	ЭлементТаблицыФормы = Элементы.Добавить("ЗаказныеПозиции", Тип("ПолеФормы"), Элементы.Список);
	ЭлементТаблицыФормы.Заголовок = "Заказные позиции";
	ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеНадписи;
	ЭлементТаблицыФормы.ПутьКДанным = "Список.ЗаказныеПозиции";
	
	//+ИИТ_Аксеньюшкин
	МассивОС = ИИТ_ВспомогательныеФункцииСервер.ПолучитьМассивДоступныхПроектов();
	Список.Параметры.УстановитьЗначениеПараметра("МассивОС", МассивОС);
	Список.Параметры.УстановитьЗначениеПараметра("ДоступныВсеОС", РольДоступна("ПолныеПрава") ИЛИ РольДоступна("ИИТ_ПолноеЧтение") ИЛИ РольДоступна("ИИТ_Финслужба"));
	Для Каждого Элемент Из Элементы Цикл
		Если Найти(Элемент.Имя, "ОтборПредставление") > 0 Тогда
			Попытка
				Элемент.Видимость = Ложь;
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
	Если Параметры.Свойство("СкрытьЗакрытыеСписаниемЗаявки") Тогда
		Список.Параметры.УстановитьЗначениеПараметра("СкрытьЗакрытыеСписаниемЗаявки", Истина);
		Список.Параметры.УстановитьЗначениеПараметра("СкрытьВнутренниеЗаявки", Ложь);
		Элементы.ИИТ_ОтображатьЗаполненные.Видимость = Истина;
		ИИТ_ОтображатьЗаполненные = Истина;
	Иначе
		Список.Параметры.УстановитьЗначениеПараметра("СкрытьЗакрытыеСписаниемЗаявки", Ложь);
		Список.Параметры.УстановитьЗначениеПараметра("СкрытьВнутренниеЗаявки", Ложь);
		Элементы.ИИТ_ОтображатьЗаполненные.Видимость = Ложь;
	КонецЕсли;
	
	РежимПодбора = Параметры.Свойство("РежимПодбора");
	//-ИИТ_Аксеньюшкин

КонецПроцедуры

#Область Перенос_Доработок

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура ИИТ_ОтображатьЗаполненныеПриИзменении(Элемент)
	Список.Параметры.УстановитьЗначениеПараметра("СкрытьЗакрытыеСписаниемЗаявки", ИИТ_ОтображатьЗаполненные);
	Элементы.Список.Обновить();
КонецПроцедуры

//ИИТ_Аксеньюшкин
&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	Если РежимПодбора Тогда
		СтандартнаяОбработка = Ложь;
		Оповестить("ПодборЗаявкиВСписание", ВыбраннаяСтрока);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти