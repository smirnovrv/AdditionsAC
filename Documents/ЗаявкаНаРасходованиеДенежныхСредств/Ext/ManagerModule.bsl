
&ИзменениеИКонтроль("ПроверитьВозможностьУстановкиСостояния")
Функция АЛФ_ПроверитьВозможностьУстановкиСостояния(ОбъектСсылка, СостояниеОбъекта, ПротоколОшибок)

Если Не ЗначениеЗаполнено(ОбъектСсылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(ОбъектСсылка) <> Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств") Тогда
		ТекстОшибки =  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
			НСтр("ru = 'Объект %1 не является заявкой на расходование денежных средств.'"),
			ОбъектСсылка);
		УправлениеПроцессамиСогласованияУХКлиентСервер.ДобавитьОшибкуУстановкиСтатусаОбъекта(
			ПротоколОшибок, ОбъектСсылка, ТекстОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	ПроверкаПройдена = Истина;
	
	Если СостояниеОбъекта = Перечисления.СостоянияСогласования.Утверждена Тогда
		
		// Организация, контрагент, договор, счет контрагента, статьи ДДС, ЦФО, Период контроля
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаявкаНаОперацию.Ссылка КАК Ссылка,
		|	ЗаявкаНаОперацию.Организация КАК Организация,
		|	ЗаявкаНаОперацию.Контрагент КАК Контрагент,
		|	ЗаявкаНаОперацию.ЦФО КАК ЦФО,
		|	ЗаявкаНаОперацию.СчетКонтрагента КАК СчетКонтрагента,
		|	ЗаявкаНаОперацию.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ЗаявкаНаОперацию.ФормаОплаты КАК ФормаОплаты,
		|	ЗаявкаНаОперацию.СуммаДокумента КАК СуммаДокумента,
		|	ЗаявкаНаОперацию.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	ЗаявкаНаОперацию.ВидОперацииУХ КАК ВидОперацииУХ,
		|	ЗаявкаНаОперацию.БезакцептноеСписание КАК БезакцептноеСписание,
		|	ЗаявкаНаОперацию.СпособОплаты КАК СпособОплаты,
		|	ЗаявкаНаОперацию.ДвиженияОперации.(
		|		НомерСтроки КАК НомерСтроки,
		|		СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств
		|	) КАК ДвиженияОперации
		|ИЗ
		|	Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаОперацию
		|ГДЕ
		|	ЗаявкаНаОперацию.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ОбъектСсылка);
		
		ДанныеДокумента = Запрос.Выполнить().Выгрузить()[0];
		
		РеквизитыПроверяемыеНаЗаполнение = Новый СписокЗначений;
		РеквизитыПроверяемыеНаЗаполнение.Добавить("СуммаДокумента", 		НСтр("ru = 'Сумма документа'"));
		РеквизитыПроверяемыеНаЗаполнение.Добавить("ВалютаВзаиморасчетов", 	НСтр("ru = 'Валюта взаиморасчетов'"));
		#Удаление
		РеквизитыПроверяемыеНаЗаполнение.Добавить("ЦФО", 					НСтр("ru = 'ЦФО'")); 
		#КонецУдаления	
		РеквизитыПроверяемыеНаЗаполнение.Добавить("Организация", 			НСтр("ru = 'Организация'"));
		
		ЭтоЛицевыеСчетаСотрудников = ЭтоЛицевыеСчетаСотрудников(ДанныеДокумента.Ссылка);

		Если Не ВидыОперацийУХКлиентСерверПовтИсп.ЭтоПеремещениеСобственныхСредств(ДанныеДокумента.ВидОперацииУХ) 
			И НЕ ЭтоЛицевыеСчетаСотрудников Тогда
			Если ВидыОперацийУХКлиентСерверПовтИсп.ПеречислениеСотруднику(ДанныеДокумента.ВидОперацииУХ) Тогда  
				РеквизитыПроверяемыеНаЗаполнение.Добавить("Контрагент", НСтр("ru = 'Сотрудник'"));
			ИначеЕсли НЕ (ВидыОперацийУХВызовСервераПовтИсп.ЭтоПеречислениеЗаработнойПлатыПоВедомостям(ДанныеДокумента.ВидОперацииУХ)
							И ДанныеДокумента.ФормаОплаты = Перечисления.ФормыОплаты.Наличная) Тогда
				РеквизитыПроверяемыеНаЗаполнение.Добавить("Контрагент", НСтр("ru = 'Контрагент'"));  
			КонецЕсли;
		КонецЕсли;
		
		Если Не ВидыОперацийУХКлиентСерверПовтИсп.ЭтоПеремещениеВнутриОрганизации(ДанныеДокумента.ВидОперацииУХ)
			И ДанныеДокумента.ФормаОплаты = Перечисления.ФормыОплаты.Безналичная
			И Не ДанныеДокумента.БезакцептноеСписание
			И Не ЭтоЛицевыеСчетаСотрудников
			И НЕ ДанныеДокумента.СпособОплаты = Перечисления.СпособыОплатыПлатежей.ЧерезПлатежногоАгента Тогда 
			#Удаление
				РеквизитыПроверяемыеНаЗаполнение.Добавить("СчетКонтрагента", НСтр("ru = 'Счет контрагента'"));  
				#КонецУдаления
		КонецЕсли;

		
		Если НЕ ВидыОперацийУХКлиентСерверПовтИсп.ЭтоРасчетыБезДоговора(ДанныеДокумента.ВидОперацииУХ) Тогда
			РеквизитыПроверяемыеНаЗаполнение.Добавить("ДоговорКонтрагента", НСтр("ru = 'Договор контрагента'"));
		КонецЕсли;
		
		Для Каждого ПроверяемыйРеквизит из РеквизитыПроверяемыеНаЗаполнение Цикл
			Если Не ЗначениеЗаполнено(ДанныеДокумента[ПроверяемыйРеквизит.Значение]) Тогда
				
				ПроверкаПройдена = Ложь;
				
				ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле","Заполнение",ПроверяемыйРеквизит.Представление);
				УправлениеПроцессамиСогласованияУХКлиентСервер.ДобавитьОшибкуУстановкиСтатусаОбъекта(ПротоколОшибок, ОбъектСсылка, ТекстОшибки, "Объект." + ПроверяемыйРеквизит.Значение);
				
			КонецЕсли;
		КонецЦикла;
		
		Если Не ДанныеДокумента.ДвиженияОперации.Количество() Тогда
			
			ПроверкаПройдена = Ложь;
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Список","Заполнение", ,,НСтр("ru = 'Аналитики учета и планирования'"));
			УправлениеПроцессамиСогласованияУХКлиентСервер.ДобавитьОшибкуУстановкиСтатусаОбъекта(ПротоколОшибок, ОбъектСсылка, ТекстОшибки, "Объект.ДвиженияОперации");
			
		КонецЕсли;
		
		ПараметрыБюджета = ДвиженияБюджетированиеКлиентСерверУХ.ПараметрыБюджета(Перечисления.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияДенежныхСредств);
		
		Для Каждого ТекСтрокаРасшифровки Из ДанныеДокумента.ДвиженияОперации Цикл
			Если Не ЗначениеЗаполнено(ТекСтрокаРасшифровки[ПараметрыБюджета.КолонкаСтатья]) Тогда
				
				ПроверкаПройдена = Ложь;
				ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Колонка",
					"Заполнение", 
					ПараметрыБюджета.ЗаголовокКолонкиСтатья, 
					ТекСтрокаРасшифровки.НомерСтроки,
					НСтр("ru = 'Аналитики учета и планирования'"));
				ИмяПоля =  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Объект.ДвиженияОперации[%1].%2", ТекСтрокаРасшифровки.НомерСтроки - 1, ПараметрыБюджета.КолонкаСтатья);
				
				УправлениеПроцессамиСогласованияУХКлиентСервер.ДобавитьОшибкуУстановкиСтатусаОбъекта(ПротоколОшибок, ОбъектСсылка, ТекстОшибки, ИмяПоля);
				
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ОбъектСсылка.ПометкаУдаления И (СостояниеОбъекта = Перечисления.СостоянияСогласования.Утверждена 
		ИЛИ СостояниеОбъекта = Перечисления.СостоянияСогласования.Отклонена 
		ИЛИ СостояниеОбъекта = Перечисления.СостоянияСогласования.НаУтверждении) тогда
		
		ТекстОшибки = НСтр("ru = 'Установлена пометка удаления'");
		УправлениеПроцессамиСогласованияУХКлиентСервер.ДобавитьОшибкуУстановкиСтатусаОбъекта(
			ПротоколОшибок, ОбъектСсылка, ТекстОшибки);
		ПроверкаПройдена = Ложь;
	КонецЕсли;
	
	Возврат ПроверкаПройдена;

КонецФункции


&ИзменениеИКонтроль("Источник_РасходыПоДоговору")
Функция АЛФ_Источник_РасходыПоДоговору(Объект)

	РеквизитыЗаявки = ЗаявкиНаОперации.РеквизитыДокументаЗаявка(ТипЗнч(Объект.Ссылка));
	
	Договор = ЗаявкиНаОперации.РеквизитЗаявки(Объект, РеквизитыЗаявки.ДоговорКонтрагента);
	
	ВидыОпераций = Новый Массив;
	ВидыОпераций.Добавить(Справочники.ВидыОперацийУХ.ОплатаПоставщику);
	ВидыОпераций.Добавить(Справочники.ВидыОперацийУХ.УплатаНалогаЗаТретьихЛиц);
	
	Если Не ЗначениеЗаполнено(Объект.Контрагент) ИЛИ Не ЗначениеЗаполнено(Договор)
		 ИЛИ ВидыОпераций.Найти(Объект.ВидОперацииУХ) = Неопределено Тогда
		Возврат Неопределено; // Контроль не выполняется
	КонецЕсли; 
	
	СуммаРасходов = Объект.СуммаДокумента;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		РасходыПоДоговору = РасходыПоДоговору(Объект.Ссылка);
		СуммаРасходов = РасходыПоДоговору.СуммаИсполнения;
	КонецЕсли;

	Результат = КонтрольУХРасходыПоДоговору.СтруктураИсточник(Объект.Ссылка, Объект.Организация, Истина);
	Результат.Контрагент = Объект.Контрагент;
	Результат.Договор = Договор;
	Результат.Заявлено = СуммаРасходов;  
#Удаление	
	// Оплата
	Для Каждого СтрокаОперации Из Объект.ДвиженияОперации Цикл
		Строка = Результат.ДанныеДляПроверки.Добавить();
		Строка.Организация 	= Результат.Организация;
		Строка.Контрагент 	= Результат.Контрагент;
		Строка.Договор 		= Результат.Договор;
		Строка.Валюта 		= Объект.ВалютаВзаиморасчетов;
		Строка.ЭтоОплата 	= Истина;
		Строка.Сумма 		= СуммаРасходов;
	КонецЦикла;
#КонецУдаления	
	Возврат Результат;
	
КонецФункции

