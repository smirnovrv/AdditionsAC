
&После("ПриЗаписи")
Процедура АЛФ_ПриЗаписи(Отказ)

	//+ИИТ_Аксеньюшкин
	Если Владелец.ПометкаУдаления Тогда
		Отказ = Истина;
		Сообщить("Контрагент, по которому заключается договор, помечен на удаление.");
	КонецЕсли;
	//-ИИТ_Аксеньюшкин

КонецПроцедуры


&ИзменениеИКонтроль("ОбработкаПроверкиЗаполнения")
Процедура АЛФ_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	МассивНепроверяемыхРеквизитов = Новый Массив;

	Если ЗначениеЗаполнено(Дата) И ЗначениеЗаполнено(СрокДействия)
		И Дата > СрокДействия Тогда

		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность",
		НСтр("ru = 'Срок действия до'"), , , 
		НСтр("ru = 'Срок действия договора должен быть больше или равен дате договора.'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "СрокДействия", "Объект", Отказ);

	КонецЕсли;

	Если ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем
		ИЛИ ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.Прочее
		ИЛИ ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ЗаемПолученный
		ИЛИ ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СпособЗаполненияСтавкиНДС");
	Конецесли;

	ЭтоПлатежныйАгент = (НЕ ЭтоГруппа И ПлатежныйАгент);

	Если НЕ ЭтоПлатежныйАгент Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПризнакАгента");
	КонецЕсли;

	Если ЭтоПлатежныйАгент 
		И (ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.БанковскийПлатежныйАгент") 
		ИЛИ (ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.БанковскийПлатежныйСубагент")))
		И ЗначениеЗаполнено(ИННОператораПеревода) Тогда
		РезультатПроверки = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(ИННОператораПеревода, Истина);
		Если НЕ РезультатПроверки.СоответствуетТребованиям Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность","ИНН оператора перевода",,,РезультатПроверки.ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ИННОператораПеревода", "Объект", Отказ);
		КонецЕсли;
	КонецЕсли; 

	#Область УХ_Интеграция
#Удаление
	РасширениеБизнесЛогикиУХ.ОбработкаПроверкиЗаполненияДоговорыКонтрагентовУХ(ЭтотОбъект, Отказ, МассивНепроверяемыхРеквизитов);
#КонецУдаления
	#КонецОбласти

	Если ЗначениеЗаполнено(ВидОбеспеченияОбязательствИПлатежей) Тогда
		Если НЕ ЗначениеЗаполнено(ОбеспечениеПредоставил) Тогда
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение",
			НСтр("ru='Кто предоставил'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ЭтотОбъект.Ссылка, "ОбеспечениеПредоставил", , Отказ);
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(ОбеспечениеПолучил) Тогда
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение",
			НСтр("ru='В чью пользу'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ЭтотОбъект.Ссылка, "ОбеспечениеПолучил", , Отказ);
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(ОбеспечениеВыданоЗа) Тогда
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение",
			НСтр("ru='За кого'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ЭтотОбъект.Ссылка, "ОбеспечениеВыданоЗа", , Отказ);
		КонецЕсли;

	КонецЕсли;

	Если ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ЗаемПолученный
		И (ТипПроцентнойСтавки = Перечисления.ТипыПроцентныхСтавок.БезНачисленияПроцентов
		ИЛИ ТипПроцентнойСтавки = Перечисления.ТипыПроцентныхСтавок.СтавкаЦБ) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПроцентнаяСтавка");
	ИначеЕсли ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.ЗаемПолученный Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ТипПроцентнойСтавки");
		МассивНепроверяемыхРеквизитов.Добавить("ПроцентнаяСтавка");
	КонецЕсли; 

#Вставка
	МассивНепроверяемыхРеквизитов.Добавить("ВалютаВзаиморасчетов");
	МассивНепроверяемыхРеквизитов.Добавить("ВидДоговораУХ");
	МассивНепроверяемыхРеквизитов.Добавить("СпособЗаполненияСтавкиНДС");
	МассивНепроверяемыхРеквизитов.Добавить("СпособВыставленияДокументов");
	МассивНепроверяемыхРеквизитов.Добавить("БазовыйДоговор");
	МассивНепроверяемыхРеквизитов.Добавить("БанковскийСчетКонтрагента");
	МассивНепроверяемыхРеквизитов.Добавить("БанковскийСчет");
	МассивНепроверяемыхРеквизитов.Добавить("ВидФинансовогоИнструмента");
	МассивНепроверяемыхРеквизитов.Добавить("УникальныйНомерВалютногоКонтроля");
#КонецВставки
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

КонецПроцедуры


//&ИзменениеИКонтроль("ПередЗаписью")
Процедура АЛФ_ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если НЕ ЭтоГруппа Тогда
		ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
		Валютный      = (ВалютаВзаиморасчетов <> ВалютаРегламентированногоУчета);
		ОплатаВВалюте = (ВалютаВзаиморасчетов <> ВалютаРегламентированногоУчета) И НЕ РасчетыВУсловныхЕдиницах;

		Если ПустаяСтрока(Наименование) Тогда
			Наименование = ПечатьДоговоровКлиентСервер.РеквизитыДоговораСтрокой(Номер, Дата);
			Если ПустаяСтрока(Наименование) Тогда
				Наименование = ПечатьДоговоровКлиентСервер.НаименованиеПоУмолчаниюБезРеквизитов();
			КонецЕсли;
		КонецЕсли;

		Если ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.СПокупателем 
			И УчетАгентскогоНДСПокупателем = Истина Тогда 
			УчетАгентскогоНДСПокупателем  = Ложь;
		КонецЕсли;

		Если ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.ЗаемПолученный Тогда
			ТипПроцентнойСтавки = Неопределено;
			ПроцентнаяСтавка = 0;
			ИсторияПроцентныхСтавок.Очистить();
		КонецЕсли;

		Если ТипПроцентнойСтавки = Перечисления.ТипыПроцентныхСтавок.БезНачисленияПроцентов Тогда
			ИсторияПроцентныхСтавок.Очистить();
		КонецЕсли;

		Если НЕ БухгалтерскийУчетПереопределяемый.ИспользуетсяОбратноеНачислениеНДС()
			И ВидАгентскогоДоговора = Перечисления.ВидыАгентскихДоговоров.РеализацияТоваров Тогда

			ТекстСообщения = НСтр("ru = 'Вид агентского договора ""Реализация товаров (п.8 ст.161 НК РФ)"" недоступен в данной версии программы'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ВидАгентскогоДоговора", "Объект", Отказ);
			Возврат;
		КонецЕсли;

		НПД = Перечисления.СистемыНалогообложения.НалогНаПрофессиональныйДоход;
		Если Не ЭтоНовый() И НалогообложениеКонтрагента <> НПД Тогда
			ПрошлоеНалогообложение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "НалогообложениеКонтрагента");
			Если ПрошлоеНалогообложение = НПД Тогда
				ДополнительныеСвойства.Вставить(ИмяСвойстваПроверитьОпциюДоговорыССамозанятыми(), Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	#Область УХ_Интеграция
#Удаление	
	РасширениеБизнесЛогикиУХ.ПередЗаписьюДоговорыКонтрагентов(ЭтотОбъект, Отказ);
#КонецУдаления	
	#КонецОбласти

	ВыполнитьПроверкуКлючевыхРеквизитов(Отказ);

КонецПроцедуры

