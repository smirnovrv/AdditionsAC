
#Область перенесенные_Доработки

&НаКлиенте
Процедура ВидДоговораУХПриИзменении(Элемент)
	ВидДоговораУХПриИзмененииНаСервере();
КонецПроцедуры   

&НаСервере
Процедура ВидДоговораУХПриИзмененииНаСервере()
	Объект.ВидДоговора = Объект.ВидДоговораУХ.ВидДоговора;
КонецПроцедуры   

&НаСервере
Процедура ОбновитьКолонкиБюджета() 
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		
		Таб = ИИТ_ВспомогательныеФункцииСервер.ПолучитьДанныеАналитикиПоОбъектамСтроительства(Объект.Ссылка);   
		
		Если Не Таб.Итог("Бюджет") = Объект.Ссылка.ИИТ_АналитикаПоОбъектамСтроительства.Итог("Бюджет")
			Или  Не Таб.Итог("Сумма") = Объект.Ссылка.ИИТ_АналитикаПоОбъектамСтроительства.Итог("Сумма") Тогда 
			   Объект.ИИТ_АналитикаПоОбъектамСтроительства.Загрузить(Таб);
			   Модифицированность = Истина;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

//ИИТ_Аксеньюшкин
Процедура ОбновитьКолонкиБюджета_Удалить()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СУММА(СписаниеСРасчетногоСчетаРасшифровкаПлатежа.СуммаПлатежа) КАК Сумма,
		|	СписаниеСРасчетногоСчетаРасшифровкаПлатежа.ДокументПланирования.Ссылка КАК Заявка
		|ПОМЕСТИТЬ ВтСуммыОплат
		|ИЗ
		|	Документ.СписаниеСРасчетногоСчета.РасшифровкаПлатежа КАК СписаниеСРасчетногоСчетаРасшифровкаПлатежа
		|ГДЕ
		|	СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка.Проведен
		|	И СписаниеСРасчетногоСчетаРасшифровкаПлатежа.ДокументПланирования.ДоговорКонтрагента = &ДоговорКонтрагента
		|
		|СГРУППИРОВАТЬ ПО
		|	СписаниеСРасчетногоСчетаРасшифровкаПлатежа.ДокументПланирования.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтСуммыОплат.Заявка КАК Заявка,
		|	ВтСуммыОплат.Сумма / ВтСуммыОплат.Заявка.СуммаДокумента КАК КоэффициентОплаты
		|ПОМЕСТИТЬ ВтКОплаты
		|ИЗ
		|	ВтСуммыОплат КАК ВтСуммыОплат
		|ГДЕ
		|	ВтСуммыОплат.Заявка.Проведен
		|	И ВтСуммыОплат.Заявка.ДоговорКонтрагента = &ДоговорКонтрагента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Ссылка.ИИТ_ПроектСогласования КАК ОбъектСтроительства,
		|	ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Проект КАК ЭтапСтроительства,
		|	СУММА(ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Сумма * ЕСТЬNULL(ВтКОплаты.КоэффициентОплаты, 0)) КАК Сумма,
		|	СУММА(ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Сумма * ЕСТЬNULL(ВтКОплаты.КоэффициентОплаты, 0)) КАК Бюджет
		|ИЗ
		|	Документ.ЗаявкаНаРасходованиеДенежныхСредств.ДвиженияОперации КАК ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКОплаты КАК ВтКОплаты
		|		ПО ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Ссылка = ВтКОплаты.Заявка
		|ГДЕ
		|	ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Ссылка.ДоговорКонтрагента = &ДоговорКонтрагента
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Проект,
		|	ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Ссылка.ИИТ_ПроектСогласования";
		Запрос.УстановитьПараметр("ДоговорКонтрагента", Объект.Ссылка);
		
		ТЗ = Запрос.Выполнить().Выгрузить();
		
		Для Каждого СтрокаТЗ Из Объект.ИИТ_АналитикаПоОбъектамСтроительства Цикл
			
			Если СтрокаТЗ.НеОбновлятьСтроку Тогда
				СтрокаТЗ.БюджетДинамическая = СтрокаТЗ.Бюджет;
				Продолжить;
			КонецЕсли;
			
			НайденныяСтроки = ТЗ.НайтиСтроки(НОвый Структура("ОбъектСтроительства,ЭтапСтроительства", СтрокаТЗ.ОбъектСтроительства, СтрокаТЗ.ЭтапСтроительства));
			Если НайденныяСтроки.Количество() > 0 Тогда
				Если Объект.ИИТ_ДоговорУчета Тогда
					СтрокаТЗ.БюджетДинамическая = СтрокаТЗ.Бюджет;
				Иначе
					СтрокаТЗ.БюджетДинамическая = НайденныяСтроки[0].Бюджет;
				КонецЕсли;
				СтрокаТЗ.СуммаДинамическая = НайденныяСтроки[0].Бюджет;
			КонецЕсли;

		КонецЦикла;
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура АЛФ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)

	Элементы.ВидДоговора.Видимость = Ложь; 
	
	ПолеВвода_АЛФ = Элементы.Добавить("ВидДоговораУХ", Тип("ПолеФормы"), Элементы.ГруппаВидДоговора);
	ПолеВвода_АЛФ.Заголовок = "Вид договора";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеВвода;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ВидДоговораУХ";
    ПолеВвода_АЛФ.УстановитьДействие("ПриИзменении", "ВидДоговораУХПриИзменении");
	
	ПолеВвода_АЛФ = Элементы.Добавить("ИИТ_ДоговорУчета", Тип("ПолеФормы"), Элементы.ГруппаШапкаПравая);
	ПолеВвода_АЛФ.Заголовок = "Договор учета";
	ПолеВвода_АЛФ.Вид = ВидПоляФормы.ПолеФлажка;	
	ПолеВвода_АЛФ.ПутьКДанным = "Объект.ИИТ_ДоговорУчета";

	ТаблицаОбъекты = Элементы.Добавить("ИИТ_АналитикаПоОбъектамСтроительства", Тип("ТаблицаФормы"));
	ТаблицаОбъекты.ПутьКДанным = "Объект.ИИТ_АналитикаПоОбъектамСтроительства";
    ТаблицаОбъекты.КоманднаяПанель.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право; 
	ТаблицаОбъекты.КоманднаяПанель.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;

	ЭлементТаблицыФормы = Элементы.Добавить("ИИТ_АналитикаПоОбъектамСтроительстваНомерСтроки", Тип("ПолеФормы"), ТаблицаОбъекты);
	ЭлементТаблицыФормы.Заголовок 	= "N";
	ЭлементТаблицыФормы.Вид 		= ВидПоляФормы.ПолеНадписи;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.ИИТ_АналитикаПоОбъектамСтроительства.НомерСтроки";

	ЭлементТаблицыФормы = Элементы.Добавить("ИИТ_АналитикаПоОбъектамСтроительстваОбъектСтроительства", Тип("ПолеФормы"), ТаблицаОбъекты);
	ЭлементТаблицыФормы.Заголовок 	= "Объект строительства";
	ЭлементТаблицыФормы.Вид 		= ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.ИИТ_АналитикаПоОбъектамСтроительства.ОбъектСтроительства";

	ЭлементТаблицыФормы = Элементы.Добавить("ИИТ_АналитикаПоОбъектамСтроительстваЭтапСтроительства", Тип("ПолеФормы"), ТаблицаОбъекты);
	ЭлементТаблицыФормы.Заголовок 	= "Этап строительства";
	ЭлементТаблицыФормы.Вид 		= ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.ИИТ_АналитикаПоОбъектамСтроительства.ЭтапСтроительства";
	
	ЭлементТаблицыФормы = Элементы.Добавить("ИИТ_АналитикаПоОбъектамСтроительстваСуммаЗаказов", Тип("ПолеФормы"), ТаблицаОбъекты);	
	ЭлементТаблицыФормы.Заголовок 	= "Сумма заявок";
	ЭлементТаблицыФормы.Вид 		= ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.ИИТ_АналитикаПоОбъектамСтроительства.СуммаЗаказов";

	ЭлементТаблицыФормы = Элементы.Добавить("ИИТ_АналитикаПоОбъектамСтроительстваСумма", Тип("ПолеФормы"), ТаблицаОбъекты);
	ЭлементТаблицыФормы.Заголовок 	= "ФАКТ оплат";  
	ЭлементТаблицыФормы.Вид 		= ВидПоляФормы.ПолеВвода; 
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.ИИТ_АналитикаПоОбъектамСтроительства.Сумма";

	ЭлементТаблицыФормы = Элементы.Добавить("ИИТ_АналитикаПоОбъектамСтроительстваБюджет", Тип("ПолеФормы"), ТаблицаОбъекты);
	ЭлементТаблицыФормы.Заголовок 	= "Бюджет";
	ЭлементТаблицыФормы.Вид 		= ВидПоляФормы.ПолеВвода;
	ЭлементТаблицыФормы.ПутьКДанным = "Объект.ИИТ_АналитикаПоОбъектамСтроительства.Бюджет";	 	 
	
	ПолеВвода_АЛФ = Элементы.Добавить("АС_ДатаАктуальностиБюджет", Тип("ПолеФормы"),Элементы.ГруппаШапкаПравая);
	ПолеВвода_АЛФ.Заголовок 	= "Дата актуальности бюджета";
	ПолеВвода_АЛФ.Вид 			= ВидПоляФормы.ПолеНадписи;	  
	ПолеВвода_АЛФ.ПутьКДанным 	= "Объект.АС_ДатаАктуальностиБюджет";

	Команда = Команды.Добавить("Заполнить");
	Команда.Заголовок = "Обновить";
	Команда.Действие  = "ЗаполнитьАналитикаПоОбъектамСтроительства";   
	
	КнопкаФормы = Элементы.Добавить("ИИТ_АналитикаПоОбъектамСтроительстваЗаполнить", Тип("КнопкаФормы"), ТаблицаОбъекты.КоманднаяПанель);
	КнопкаФормы.ИмяКоманды 					= "Заполнить";	
	КнопкаФормы.ПоложениеВКоманднойПанели 	= ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели;
	КнопкаФормы.Вид 						= ВидКнопкиФормы.ОбычнаяКнопка; 
	
	//+ИИТ_Аксеньюшкин
	Элементы.ГруппаШапкаЛевая.ТолькоПросмотр = Ложь;
	Элементы.ГруппаВидДоговора.ТолькоПросмотр = Ложь;
	Элементы.ВидДоговора.ТолькоПросмотр = Ложь;
	Элементы.ГруппаНомерДата.ТолькоПросмотр = Ложь;
	Элементы.Номер.ТолькоПросмотр = Ложь;
	Элементы.Дата.ТолькоПросмотр = Ложь;
	Элементы.Наименование.ТолькоПросмотр = Ложь;
	
	Элементы.ГруппаШапкаЛевая.Доступность = Истина;
	Элементы.ГруппаВидДоговора.Доступность = Истина;
	Элементы.ВидДоговора.Доступность = Истина;
	Элементы.ГруппаНомерДата.Доступность = Истина;
	Элементы.Номер.Доступность = Истина;
	Элементы.Дата.Доступность = Истина;
	Элементы.Наименование.Доступность = Истина;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) И Не РольДоступна("ПолныеПрава") И Не РольДоступна("ИИТ_СозданиеДоговоров") Тогда
		Отказ = Истина;
	КонецЕсли;	

	ОбновитьКолонкиБюджета();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАналитикаПоОбъектамСтроительства(Команда)
	
	ЗаполнитьАналитикаПоОбъектамСтроительстваСервер();
	
	Объект.АС_ДатаАктуальностиБюджет = ТекущаяДата();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАналитикаПоОбъектамСтроительстваСервер()
	
	ВТ_ТЧ = Объект.Ссылка.ИИТ_АналитикаПоОбъектамСтроительства.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Проект КАК Проект,
		|	СУММА(ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Сумма) КАК СуммаЗаявки
		|ПОМЕСТИТЬ ВТ_Заявки
		|ИЗ
		|	Документ.ЗаявкаНаРасходованиеДенежныхСредств.ДвиженияОперации КАК ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации
		|ГДЕ
		|	ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Ссылка.Проведен
		|	И ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Ссылка.ДоговорКонтрагента = &ДоговорКонтрагента
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаявкаНаРасходованиеДенежныхСредствДвиженияОперации.Проект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Проект КАК Проект,
		|	СУММА(СписаниеСРасчетногоСчетаРасшифровкаПлатежа.СуммаПлатежа) КАК СуммаПлатежа
		|ПОМЕСТИТЬ ВТ_Оплаты
		|ИЗ
		|	Документ.СписаниеСРасчетногоСчета.РасшифровкаПлатежа КАК СписаниеСРасчетногоСчетаРасшифровкаПлатежа
		|ГДЕ
		|	СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка.Проведен
		|	И СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка.ДоговорКонтрагента = &ДоговорКонтрагента
		|
		|СГРУППИРОВАТЬ ПО
		|	СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Проект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТчДокумента.ОбъектСтроительства КАК ОбъектСтроительства,
		|	ВТ_ТчДокумента.ЭтапСтроительства КАК ЭтапСтроительства,
		|	ВТ_ТчДокумента.СуммаЗаказов КАК СуммаЗаказов,
		|	ВТ_ТчДокумента.Сумма КАК Сумма,
		|	ВТ_ТчДокумента.Бюджет КАК Бюджет,
		|	ВТ_ТчДокумента.НеОбновлятьСтроку КАК НеОбновлятьСтроку
		|ПОМЕСТИТЬ ВТ_ТЧ
		|ИЗ
		|	&ВТ_ТЧ КАК ВТ_ТчДокумента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ВТ_ТЧ.ОбъектСтроительства, ВТ_Оплаты.Проект.Родитель) КАК ОбъектСтроительства,
		|	ЕСТЬNULL(ВТ_ТЧ.ЭтапСтроительства, ВТ_Оплаты.Проект) КАК ЭтапСтроительства,
		|	ЕСТЬNULL(ВТ_ТЧ.СуммаЗаказов, 0) КАК СуммаЗаказов,
		|	ЕСТЬNULL(ВТ_ТЧ.Бюджет, 0) КАК Бюджет,
		|	ВТ_ТЧ.НеОбновлятьСтроку КАК НеОбновлятьСтроку,
		|	ЕСТЬNULL(ВТ_Оплаты.СуммаПлатежа, 0) КАК Сумма
		|ПОМЕСТИТЬ ВТ_ПодИтогОплаты
		|ИЗ
		|	ВТ_Оплаты КАК ВТ_Оплаты
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ТЧ КАК ВТ_ТЧ
		|		ПО (ВТ_ТЧ.ЭтапСтроительства = ВТ_Оплаты.Проект)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ВТ_ПодИтогОплаты.ОбъектСтроительства, ВТ_Заявки.Проект.Родитель) КАК ОбъектСтроительства,
		|	ЕСТЬNULL(ВТ_ПодИтогОплаты.ЭтапСтроительства, ВТ_Заявки.Проект) КАК ЭтапСтроительства,
		|	ЕСТЬNULL(ВТ_ПодИтогОплаты.Бюджет, 0) КАК Бюджет,
		|	ВТ_ПодИтогОплаты.НеОбновлятьСтроку КАК НеОбновлятьСтроку,
		|	ЕСТЬNULL(ВТ_ПодИтогОплаты.Сумма, 0) КАК Сумма,
		|	ВТ_Заявки.СуммаЗаявки КАК СуммаЗаказов
		|ИЗ
		|	ВТ_ПодИтогОплаты КАК ВТ_ПодИтогОплаты
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_Заявки КАК ВТ_Заявки
		|		ПО ВТ_ПодИтогОплаты.ЭтапСтроительства = ВТ_Заявки.Проект";
	
	Запрос.УстановитьПараметр("ДоговорКонтрагента"	, Объект.Ссылка);
	Запрос.УстановитьПараметр("ВТ_ТЧ"				, ВТ_ТЧ);
	
	РезультатЗапроса 	= Запрос.Выполнить();	
	табл 				= РезультатЗапроса.Выгрузить();
			
	Объект.ИИТ_АналитикаПоОбъектамСтроительства.Загрузить(табл);
	
КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ОбработкаОповещенияУХ")
Процедура АЛФ_ОбработкаОповещенияУХ(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_ДоговорыКонтрагентов" И Объект.Ссылка = Источник Тогда
		Если НЕ ЭтотОбъект.Модифицированность Тогда
			ЭтотОбъект.Прочитать();
			Если ЗначениеЗаполнено(Объект.ВерсияСоглашения) Тогда
#Удаление				
				// нужно открыть версию вместо договора
				Закрыть();
#КонецУдаления				
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура АЛФ_ПослеЗаписиНаСервереПосле(ТекущийОбъект, ПараметрыЗаписи)
	//+ИИТ_Аксеньюшкин
	//ОбновитьКолонкиБюджета();
	//-ИИТ_Аксеньюшкин
КонецПроцедуры

&НаКлиенте
Процедура АЛФ_ПриОткрытииПосле(Отказ)
	//+ИИТ_Аксеньюшкин
	Этаформа.ТолькоПросмотр = Ложь;
	//-ИИТ_Аксеньюшкин
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ОбработкаПроверкиЗаполненияНаСервере")
Процедура АЛФ_ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	Если ПредусмотреноОбеспечениеОбязательств Тогда
		Если НЕ ЗначениеЗаполнено(Объект.ВидОбеспеченияОбязательствИПлатежей) Тогда
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение",
			НСтр("ru='Вид обеспечения'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "ВидОбеспеченияОбязательствИПлатежей", "Объект", Отказ);
		КонецЕсли;
	КонецЕсли;

#Удаление
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
#КонецУдаления	

КонецПроцедуры
